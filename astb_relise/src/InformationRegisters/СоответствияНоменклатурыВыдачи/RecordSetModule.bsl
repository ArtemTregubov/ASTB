//+++АСТБ_Горюшин_Алексей_23718
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// проверка повторов
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СоответствияНоменклатурыВыдачи.НоменклатураНормыОрганизации КАК НоменклатураНормыОрганизации,
		|	СоответствияНоменклатурыВыдачи.Подразделение КАК Подразделение,
		|	СоответствияНоменклатурыВыдачи.Сотрудник КАК Сотрудник,
		|	СоответствияНоменклатурыВыдачи.Должность КАК Должность,
		|	СоответствияНоменклатурыВыдачи.Приоритет КАК Приоритет
		|ПОМЕСТИТЬ ВТ_ДвиженияДокумента
		|ИЗ
		|	РегистрСведений.СоответствияНоменклатурыВыдачи КАК СоответствияНоменклатурыВыдачи
		|ГДЕ
		|	СоответствияНоменклатурыВыдачи.Регистратор = &Регистратор
		|	И СоответствияНоменклатурыВыдачи.Использовать = ИСТИНА
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоответствияНоменклатурыВыдачиСрезПоследних.НоменклатураНормыОрганизации КАК НоменклатураНормыОрганизации,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Подразделение КАК Подразделение,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Сотрудник КАК Сотрудник,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Должность КАК Должность,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Приоритет КАК Приоритет
		|ПОМЕСТИТЬ ВТ_ДвиженияПоИзмерениям
		|ИЗ
		|	ВТ_ДвиженияДокумента КАК ВТ_ДвиженияДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствияНоменклатурыВыдачи.СрезПоследних(&Период, НЕ Регистратор = &Регистратор) КАК СоответствияНоменклатурыВыдачиСрезПоследних
		|		ПО (СоответствияНоменклатурыВыдачиСрезПоследних.НоменклатураНормыОрганизации = ВТ_ДвиженияДокумента.НоменклатураНормыОрганизации)
		|			И (СоответствияНоменклатурыВыдачиСрезПоследних.Подразделение = ВТ_ДвиженияДокумента.Подразделение)
		|			И (СоответствияНоменклатурыВыдачиСрезПоследних.Сотрудник = ВТ_ДвиженияДокумента.Сотрудник)
		|			И (СоответствияНоменклатурыВыдачиСрезПоследних.Должность = ВТ_ДвиженияДокумента.Должность)
		|			И (СоответствияНоменклатурыВыдачиСрезПоследних.Приоритет = ВТ_ДвиженияДокумента.Приоритет)
		|ГДЕ
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Использовать = ИСТИНА
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДвиженияДокумента.НоменклатураНормыОрганизации КАК НоменклатураНормыОрганизации,
		|	ВТ_ДвиженияДокумента.Подразделение КАК Подразделение,
		|	ВТ_ДвиженияДокумента.Сотрудник КАК Сотрудник,
		|	ВТ_ДвиженияДокумента.Должность КАК Должность,
		|	ВТ_ДвиженияДокумента.Приоритет КАК Приоритет
		|ПОМЕСТИТЬ ВТ_АнализируемыеДвижения
		|ИЗ
		|	ВТ_ДвиженияДокумента КАК ВТ_ДвиженияДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_ДвиженияПоИзмерениям.НоменклатураНормыОрганизации,
		|	ВТ_ДвиженияПоИзмерениям.Подразделение,
		|	ВТ_ДвиженияПоИзмерениям.Сотрудник,
		|	ВТ_ДвиженияПоИзмерениям.Должность,
		|	ВТ_ДвиженияПоИзмерениям.Приоритет
		|ИЗ
		|	ВТ_ДвиженияПоИзмерениям КАК ВТ_ДвиженияПоИзмерениям
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_АнализируемыеДвижения.НоменклатураНормыОрганизации КАК НоменклатураНормыОрганизации,
		|	ВТ_АнализируемыеДвижения.Подразделение КАК Подразделение,
		|	ВТ_АнализируемыеДвижения.Сотрудник КАК Сотрудник,
		|	ВТ_АнализируемыеДвижения.Должность КАК Должность,
		|	ВТ_АнализируемыеДвижения.Приоритет КАК Приоритет,
		|	СУММА(1) КАК КоличествоПовторов
		|ПОМЕСТИТЬ ВТ_Повторы
		|ИЗ
		|	ВТ_АнализируемыеДвижения КАК ВТ_АнализируемыеДвижения
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_АнализируемыеДвижения.НоменклатураНормыОрганизации,
		|	ВТ_АнализируемыеДвижения.Подразделение,
		|	ВТ_АнализируемыеДвижения.Сотрудник,
		|	ВТ_АнализируемыеДвижения.Должность,
		|	ВТ_АнализируемыеДвижения.Приоритет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Повторы.НоменклатураНормыОрганизации КАК НоменклатураНормыОрганизации,
		|	ВТ_Повторы.Подразделение КАК Подразделение,
		|	ВТ_Повторы.Сотрудник КАК Сотрудник,
		|	ВТ_Повторы.Должность КАК Должность,
		|	ВТ_Повторы.Приоритет КАК Приоритет,
		|	ВТ_Повторы.КоличествоПовторов КАК КоличествоПовторов
		|ИЗ
		|	ВТ_Повторы КАК ВТ_Повторы
		|ГДЕ
		|	ВТ_Повторы.КоличествоПовторов > 1";
	
	Запрос.УстановитьПараметр("Регистратор", ЭтотОбъект.Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("Период", ЭтотОбъект.Отбор.Регистратор.Значение.Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	//ШаблонСообщения = НСтр("ru='В регистре ""Соответствия номенклатуры выдачи"" или документе уже есть запись с приоритетом ""%Приоритет%"" с измерениями Номенклатура нормы ""%НоменклатураНормыОрганизации%"", Сотрудник ""%Сотрудник%"", Подразделение ""%Подразделение%"", Должность ""%Должность%"".'");
	ШаблонСообщения = НСтр("ru='Для номенклатуры нормы ""%НоменклатураНормыОрганизации%"", Сотрудник ""%Сотрудник%"", Подразделение ""%Подразделение%"", Должность ""%Должность%"" уже установлено соответствие приоритетом ""%Приоритет%"". Используйте другой приоритет.'");
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%Приоритет%", ВыборкаДетальныеЗаписи.Приоритет);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НоменклатураНормыОрганизации%", ВыборкаДетальныеЗаписи.НоменклатураНормыОрганизации);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Сотрудник%", ВыборкаДетальныеЗаписи.Сотрудник);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Подразделение%", ВыборкаДетальныеЗаписи.Подразделение);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Должность%", ВыборкаДетальныеЗаписи.Должность);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		
	КонецЦикла;
	
	// проверка нулей
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СоответствияНоменклатурыВыдачи.НоменклатураНормыОрганизации КАК НоменклатураНормыОрганизации,
		|	СоответствияНоменклатурыВыдачи.Подразделение КАК Подразделение,
		|	СоответствияНоменклатурыВыдачи.Сотрудник КАК Сотрудник,
		|	СоответствияНоменклатурыВыдачи.Должность КАК Должность,
		|	СоответствияНоменклатурыВыдачи.Приоритет КАК Приоритет
		|ИЗ
		|	РегистрСведений.СоответствияНоменклатурыВыдачи КАК СоответствияНоменклатурыВыдачи
		|ГДЕ
		|	СоответствияНоменклатурыВыдачи.Регистратор = &Регистратор
		|	И СоответствияНоменклатурыВыдачи.Приоритет = 0";
	
	Запрос.УстановитьПараметр("Регистратор", ЭтотОбъект.Отбор.Регистратор.Значение);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ШаблонСообщения = НСтр("ru='Для номенклатуры нормы ""%НоменклатураНормыОрганизации%"", Сотрудник ""%Сотрудник%"", Подразделение ""%Подразделение%"", Должность ""%Должность%"" указан приоритет ""%Приоритет%"". Используйте другой приоритет.'");
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%Приоритет%", ВыборкаДетальныеЗаписи.Приоритет);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НоменклатураНормыОрганизации%", ВыборкаДетальныеЗаписи.НоменклатураНормыОрганизации);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Сотрудник%", ВыборкаДетальныеЗаписи.Сотрудник);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Подразделение%", ВыборкаДетальныеЗаписи.Подразделение);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Должность%", ВыборкаДетальныеЗаписи.Должность);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		
	КонецЦикла;
	
КонецПроцедуры //---АСТБ_Горюшин_Алексей_23718