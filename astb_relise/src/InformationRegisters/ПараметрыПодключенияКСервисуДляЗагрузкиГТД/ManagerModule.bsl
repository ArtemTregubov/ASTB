#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ПолучитьПрокси(ТекстОшибки,ЗНАЧ Адрес,ЗНАЧ URI,ЗНАЧ Логин,ЗНАЧ Пароль) Экспорт
    
	ТекстОшибки = "";
	
	Если НЕ ЗначениеЗаполнено(Адрес) Тогда
	    Возврат Неопределено;
	КонецЕсли;
	
	Адрес = Адрес + "/ws/EISF.1cws?wsdl";
	
	Адрес = СтрЗаменить(Адрес,"//ws","/ws");
	
	//подключение без защищенного соединения
	ЕстьПодключение = Истина;
	Попытка
		Определение = Новый WSОпределения(Адрес,Логин,Пароль,,60);
		Прокси 		= Новый WSПрокси(Определение, URI, "EISF", "EISFSoap");
	Исключение
		ТекстОшибки = "Не удалось установить соединение с сервером." + Символы.ПС + ОписаниеОшибки();
		ЕстьПодключение = Ложь;
	КонецПопытки;

	Если НЕ ЕстьПодключение Тогда //подключение с защищенным соединением
		Попытка
			Определение = Новый WSОпределения(Адрес,Логин,Пароль,,60,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));
			Прокси 		= Новый WSПрокси(Определение, URI, "EISF", "EISFSoap",,,Новый ЗащищенноеСоединениеOpenSSL(Неопределено,Неопределено));
			ТекстОшибки = "";
		Исключение
			ТекстОшибки = "Не удалось установить соединение с сервером." + Символы.ПС + ОписаниеОшибки();
			Возврат Неопределено;
		КонецПопытки;
	КонецЕсли;	
	
	Прокси.Пользователь = Логин;
	Прокси.Пароль 		= Пароль;
	
	Возврат Прокси;
    
КонецФункции

Функция ЕстьНастройкиПодключения(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПараметрыПодключенияКСервисуДляЗагрузкиГТД.Организация КАК Организация,
	|	ПараметрыПодключенияКСервисуДляЗагрузкиГТД.Адрес КАК Адрес,
	|	ПараметрыПодключенияКСервисуДляЗагрузкиГТД.URI КАК URI,
	|	ПараметрыПодключенияКСервисуДляЗагрузкиГТД.Пользователь КАК Пользователь,
	|	ПараметрыПодключенияКСервисуДляЗагрузкиГТД.Пароль КАК Пароль
	|ИЗ
	|	РегистрСведений.ПараметрыПодключенияКСервисуДляЗагрузкиГТД КАК ПараметрыПодключенияКСервисуДляЗагрузкиГТД
	|ГДЕ
	|	ПараметрыПодключенияКСервисуДляЗагрузкиГТД.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация",Организация);
	
	Результат = Запрос.Выполнить();
	
	Возврат НЕ Результат.Пустой();
	
КонецФункции	

#КонецЕсли