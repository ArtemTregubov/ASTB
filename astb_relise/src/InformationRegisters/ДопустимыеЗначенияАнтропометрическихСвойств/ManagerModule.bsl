#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ВыполнитьЗагрузкуДопустимыхЗначенийАнтропометрии() Экспорт
	
	//очистка регистра сведений
	НаборЗаписей = РегистрыСведений.ДопустимыеЗначенияАнтропометрическихСвойств.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
	//чтение схемы из макета
	ВременныйФайлСхемы = ПолучитьИмяВременногоФайла();
	
	МакетСхемы = РегистрыСведений.ДопустимыеЗначенияАнтропометрическихСвойств.ПолучитьМакет("СхемаXML");
	МакетСхемы.Записать(ВременныйФайлСхемы);
	
	ЧтениеСхемы = Новый ЧтениеXML;
	ЧтениеСхемы.ОткрытьФайл(ВременныйФайлСхемы);

	Построитель_DOM = Новый ПостроительDOM;
	Документ_DOM 	= Построитель_DOM.Прочитать(ЧтениеСхемы);
	
	ПостроительСхем_XML = Новый ПостроительСхемXML;
	Схема_XML 			= ПостроительСхем_XML.СоздатьСхемуXML(Документ_DOM);
	
	НаборСхем = Новый НаборСхемXML;
	НаборСхем.Добавить(Схема_XML);
	
	НоваяФабрика = Новый ФабрикаXDTO(НаборСхем);
	
	//чтение данных из макета
	ВременныйФайлЗначений 	= ПолучитьИмяВременногоФайла();
	МакетДанных 			= РегистрыСведений.ДопустимыеЗначенияАнтропометрическихСвойств.ПолучитьМакет("ДанныеXML");
	МакетДанных.Записать(ВременныйФайлЗначений);
	
	Чтение = Новый ЧтениеXML;
	Чтение.ОткрытьФайл(ВременныйФайлЗначений);
	
	Попытка
		Данные = НоваяФабрика.ПрочитатьXML(Чтение);
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	Если Данные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из Данные.vas_item Цикл
		
		ВидСвойства = Справочники.ВидыАнтропометрическихСвойств[Элемент.vas_id];
		Для Каждого ТекущееЗначение Из Элемент.size_item Цикл
			
			Попытка 
				ДопустимоеЗначение = Число(ТекущееЗначение.matrix_name);
			Исключение
				ДопустимоеЗначение = ТекущееЗначение.matrix_name;
			КонецПопытки;
			
			НаборЗаписей = РегистрыСведений.ДопустимыеЗначенияАнтропометрическихСвойств.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ВидСвойства.Установить(ВидСвойства);
			НаборЗаписей.Отбор.ДопустимоеЗначение.Установить(ДопустимоеЗначение);
			НаборЗаписей.Прочитать();
			
			Если НаборЗаписей.Количество() = 0 Тогда
				
				НоваяЗапись 					= НаборЗаписей.Добавить();
				НоваяЗапись.ВидСвойства 		= ВидСвойства;
				НоваяЗапись.ДопустимоеЗначение 	= ДопустимоеЗначение;
			
			КонецЕсли;
 	
			НаборЗаписей.Записать();
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры	

#КонецЕсли