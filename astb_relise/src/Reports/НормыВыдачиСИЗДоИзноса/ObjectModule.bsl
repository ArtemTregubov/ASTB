
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; // отключаем стандартный вывод отчета - будем выводить программно 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();// Получаем настройки отчета
	
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных; // Создаем данные расшифровки 
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных; // Создаем компоновщик макета 

	//инициализируем настройки параметров отчета
	Параметр = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДатаАнализа"));
	Если не Параметр = Неопределено Тогда
		ДатаАнализа = КонецДня(Параметр.Значение.Дата);
		Параметр.Использование = Истина;
	Иначе
		Возврат;
	КонецЕсли;
	
	Параметр = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Организация"));
	Если не Параметр = Неопределено Тогда
		Организация = Параметр.Значение;
		Параметр.Использование = Истина;
	Иначе
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ЗанятыеРабочиеМестаОстатки.Сотрудник КАК Сотрудник
	|ИЗ
	|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(&ДатаАнализа, Организация = &Организация) КАК ЗанятыеРабочиеМестаОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗанятыеРабочиеМестаОстатки.Сотрудник.Наименование";
	
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("ДатаАнализа",ДатаАнализа);
	
	МассивСотрудников = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник");
	
	ГраницаАнализа = ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(Неопределено);
	
	ТаблицаЗанятыхРМ 			= ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуЗанятыхРабочихМестСУсловиями(МассивСотрудников,Организация,ГраницаАнализа);
	ТаблицаУстановленныхНорм 	= ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуУстановленныхНорм(Организация,ГраницаАнализа,ТаблицаЗанятыхРМ.ВыгрузитьКолонку("Подразделение"),ТаблицаЗанятыхРМ.ВыгрузитьКолонку("Должность"),ЛОЖЬ);
	
	ТаблицаСНормами 			= ПроцедурыРаботыСНормамиСервер.ПодобратьНормы(ТаблицаЗанятыхРМ,ТаблицаУстановленныхНорм,Организация,ГраницаАнализа);
	ТаблицаАнтропометрии 		= ПроцедурыРаботыСНормамиСервер.ПолучитьАнтропометриюСотрудников(МассивСотрудников,Организация);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаНормСотрудников.Сотрудник КАК Сотрудник,
	|	ТаблицаНормСотрудников.Подразделение КАК Подразделение,
	|	ТаблицаНормСотрудников.НормаВыдачи КАК НормаВыдачи,
	|	ТаблицаНормСотрудников.НоменклатураНормы КАК НоменклатураНормы
	|ПОМЕСТИТЬ ВТ_НормыСотрудников
	|ИЗ
	|	&ТаблицаНормСотрудников КАК ТаблицаНормСотрудников
	|ГДЕ
	|	ТаблицаНормСотрудников.Использовать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Сотрудники.Ссылка КАК Сотрудник,
	|	Сотрудники.ФизическоеЛицо.Пол КАК Пол,
	|	ВТ_НормыСотрудников.Подразделение КАК Подразделение,
	|	НормыВыдачиСИЗСоставНормы.НоменклатураНормы КАК НоменклатураНормы,
	|	НормыВыдачиСИЗСоставНормы.НоменклатураНормы.ВидСИЗ КАК ВидСИЗ,
	|	НормыВыдачиСИЗСоставНормы.НоменклатураНормы.ВидАнтропометрическогоСвойства КАК ВидАнтропометрическогоСвойства,
	|	НормыВыдачиСИЗСоставНормы.НоменклатураНормы.ИспользоватьРост КАК ИспользоватьРост
	|ПОМЕСТИТЬ ВТ_НоменклатураНормСотрудников
	|ИЗ
	|	ВТ_НормыСотрудников КАК ВТ_НормыСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НормыВыдачиСИЗ.СоставНормы КАК НормыВыдачиСИЗСоставНормы
	|		ПО ВТ_НормыСотрудников.НормаВыдачи = НормыВыдачиСИЗСоставНормы.Ссылка
	|			И ВТ_НормыСотрудников.НоменклатураНормы = НормыВыдачиСИЗСоставНормы.НоменклатураНормы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО ВТ_НормыСотрудников.Сотрудник = Сотрудники.Ссылка
	|ГДЕ
	|	НормыВыдачиСИЗСоставНормы.Ссылка.ВидРасчета = ЗНАЧЕНИЕ(Перечисление.ВидыРасчетаНорм.ДоИзноса)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаАнтропометрии.Сотрудник КАК Сотрудник,
	|	ТаблицаАнтропометрии.ВидСвойства КАК ВидСвойства,
	|	ТаблицаАнтропометрии.ДополнительноеУсловие КАК ДополнительноеУсловие,
	|	ТаблицаАнтропометрии.Размер КАК Размер,
	|	ТаблицаАнтропометрии.Рост КАК Рост
	|ПОМЕСТИТЬ ВТ_ТаблицаАнтропометрии
	|ИЗ
	|	&ТаблицаАнтропометрии КАК ТаблицаАнтропометрии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ТаблицаАнтропометрии.Сотрудник КАК Сотрудник,
	|	ВТ_ТаблицаАнтропометрии.ВидСвойства КАК ВидСвойства,
	|	ВТ_ТаблицаАнтропометрии.ДополнительноеУсловие КАК ДополнительноеУсловие,
	|	ВТ_ТаблицаАнтропометрии.Размер КАК Размер,
	|	ВТ_ТаблицаАнтропометрии.Рост КАК Рост
	|ПОМЕСТИТЬ ВТ_СгруппированнаяАнтропометрия
	|ИЗ
	|	ВТ_ТаблицаАнтропометрии КАК ВТ_ТаблицаАнтропометрии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НоменклатураНормСотрудников.Сотрудник КАК Сотрудник,
	|	ВТ_НоменклатураНормСотрудников.Пол КАК Пол,
	|	ВТ_НоменклатураНормСотрудников.Подразделение КАК Подразделение,
	|	ВТ_НоменклатураНормСотрудников.НоменклатураНормы КАК НоменклатураНормы,
	|	ВЫБОР
	|		КОГДА ВТ_НоменклатураНормСотрудников.ВидАнтропометрическогоСвойства = ЗНАЧЕНИЕ(Справочник.ВидыАнтропометрическихСвойств.ПустаяСсылка)
	|			ТОГДА ""Не задано""
	|		ИНАЧЕ ВТ_НоменклатураНормСотрудников.ВидАнтропометрическогоСвойства
	|	КОНЕЦ КАК ВидАнтропометрии,
	|	ВЫБОР
	|		КОГДА ВТ_НоменклатураНормСотрудников.ВидАнтропометрическогоСвойства = ЗНАЧЕНИЕ(Справочник.ВидыАнтропометрическихСвойств.ПустаяСсылка)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ВТ_СгруппированнаяАнтропометрия.Размер, ""Не задан"")
	|	КОНЕЦ КАК Размер,
	|	ВЫБОР
	|		КОГДА ВТ_НоменклатураНормСотрудников.ВидАнтропометрическогоСвойства = ЗНАЧЕНИЕ(Справочник.ВидыАнтропометрическихСвойств.ПустаяСсылка)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ВТ_СгруппированнаяАнтропометрия.Рост, ""Не задан"")
	|	КОНЕЦ КАК Рост,
	|	1 КАК КоличествоСотрудников
	|ПОМЕСТИТЬ ВТ_Результат
	|ИЗ
	|	ВТ_НоменклатураНормСотрудников КАК ВТ_НоменклатураНормСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СгруппированнаяАнтропометрия КАК ВТ_СгруппированнаяАнтропометрия
	|		ПО (ВЫБОР
	|				КОГДА ВТ_НоменклатураНормСотрудников.ВидАнтропометрическогоСвойства = ЗНАЧЕНИЕ(Справочник.ВидыАнтропометрическихСвойств.ПустаяСсылка)
	|					ТОГДА ЛОЖЬ
	|				ИНАЧЕ ВТ_НоменклатураНормСотрудников.Сотрудник = ВТ_СгруппированнаяАнтропометрия.Сотрудник
	|			КОНЕЦ)
	|			И (ВЫБОР
	|				КОГДА ВТ_НоменклатураНормСотрудников.ВидАнтропометрическогоСвойства = ЗНАЧЕНИЕ(Справочник.ВидыАнтропометрическихСвойств.ПустаяСсылка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ВТ_НоменклатураНормСотрудников.ВидАнтропометрическогоСвойства = ВТ_СгруппированнаяАнтропометрия.ВидСвойства
	|			КОНЕЦ)
	|			И (ВЫБОР
	|				КОГДА ВТ_НоменклатураНормСотрудников.ВидАнтропометрическогоСвойства = ЗНАЧЕНИЕ(Справочник.ВидыАнтропометрическихСвойств.ПустаяСсылка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ВТ_НоменклатураНормСотрудников.ИспользоватьРост
	|							ТОГДА НЕ ВТ_СгруппированнаяАнтропометрия.Рост = 0
	|						ИНАЧЕ ВТ_СгруппированнаяАнтропометрия.Рост = 0
	|					КОНЕЦ
	|			КОНЕЦ)
	|			И (ВЫБОР
	|				КОГДА ВТ_НоменклатураНормСотрудников.ВидАнтропометрическогоСвойства = ЗНАЧЕНИЕ(Справочник.ВидыАнтропометрическихСвойств.ПустаяСсылка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ВЫБОР
	|						КОГДА НЕ ВТ_СгруппированнаяАнтропометрия.ДополнительноеУсловие = ЗНАЧЕНИЕ(Справочник.ВидыСИЗ.ПустаяСсылка)
	|							ТОГДА ВТ_НоменклатураНормСотрудников.ВидСИЗ = ВТ_СгруппированнаяАнтропометрия.ДополнительноеУсловие
	|						ИНАЧЕ ИСТИНА
	|					КОНЕЦ
	|			КОНЕЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Результат.Пол КАК Пол,
	|	ВТ_Результат.Подразделение КАК Подразделение,
	|	ВТ_Результат.НоменклатураНормы КАК НоменклатураНормы,
	|	ВТ_Результат.ВидАнтропометрии КАК ВидАнтропометрии,
	|	ВТ_Результат.Размер КАК Размер,
	|	ВТ_Результат.Рост КАК Рост,
	|	СУММА(ВТ_Результат.КоличествоСотрудников) КАК КоличествоСотрудников
	|ИЗ
	|	ВТ_Результат КАК ВТ_Результат
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Результат.Пол,
	|	ВТ_Результат.Подразделение,
	|	ВТ_Результат.НоменклатураНормы,
	|	ВТ_Результат.ВидАнтропометрии,
	|	ВТ_Результат.Размер,
	|	ВТ_Результат.Рост
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_Результат.НоменклатураНормы.Наименование,
	|	ВТ_Результат.Пол,
	|	ВТ_Результат.Подразделение,
	|	ВТ_Результат.ВидАнтропометрии";
	
	Запрос.УстановитьПараметр("ТаблицаНормСотрудников",	ТаблицаСНормами);
	Запрос.УстановитьПараметр("ТаблицаАнтропометрии",	ТаблицаАнтропометрии);
	
	ТаблицаДляВывода = Запрос.Выполнить().Выгрузить();
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	// Скомпонуем результат
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	
	НаборыВнешнихДанных = Новый Структура;
	НаборыВнешнихДанных.Вставить("ТаблицаДляВывода",ТаблицаДляВывода);
	
	//Инициализировать(<Макет>, <ВнешниеНаборыДанных>, <ДанныеРасшифровки>, <ВозможностьИспользованияВнешнихФункций>) 
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,НаборыВнешнихДанных , ДанныеРасшифровки);
	
	ДокументРезультат.Очистить();
	
	// Выводим результат в табличный документ
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);	
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры
