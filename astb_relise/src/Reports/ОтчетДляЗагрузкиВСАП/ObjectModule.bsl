
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; // отключаем стандартный вывод отчета - будем выводить программно 
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();// Получаем настройки отчета
	
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных; // Создаем данные расшифровки 
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных; // Создаем компоновщик макета 
	
	ПараметрОтображатьДатуВыдачи = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ОтображатьДатуВыдачи"));
	Если не ПараметрОтображатьДатуВыдачи = Неопределено Тогда
		ОтображатьДатуВыдачи = ПараметрОтображатьДатуВыдачи.Значение;
		ПараметрОтображатьДатуВыдачи.Использование = Истина;
	Иначе
		Возврат;
	КонецЕсли;
	
	ПараметрПериод = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	Если не ПараметрПериод = Неопределено Тогда
		ДатаНачала 		= ПараметрПериод.Значение.ДатаНачала;
		ДатаОкончания 	= ПараметрПериод.Значение.ДатаОкончания;
		ПараметрПериод.Использование = Истина;
	Иначе
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОсновноеМестоРаботыСотрудника.Период КАК Период,
	|	ОсновноеМестоРаботыСотрудника.Сотрудник КАК Сотрудник,
	|	ОсновноеМестоРаботыСотрудника.Подразделение КАК Подразделение,
	|	ОсновноеМестоРаботыСотрудника.МоментВремени КАК МоментВремени,
	|	ОсновноеМестоРаботыСотрудника.ОсновноеМестоРаботы КАК ОсновноеМестоРаботы,
	|	ОсновноеМестоРаботыСотрудника.Организация КАК Организация,
	|	ОсновноеМестоРаботыСотрудника.Должность КАК Должность
	|ПОМЕСТИТЬ ВТ_ОсновныеМестаРаботыСотрудников
	|ИЗ
	|	РегистрСведений.ОсновноеМестоРаботыСотрудника КАК ОсновноеМестоРаботыСотрудника
	|ГДЕ
	|	ОсновноеМестоРаботыСотрудника.Активность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВложенныйЗапрос.Сотрудник КАК Сотрудник,
	|	ВложенныйЗапрос.Подразделение КАК Подразделение,
	|	ВложенныйЗапрос.Период КАК НачалоПериода,
	|	МИНИМУМ(ЕСТЬNULL(ВТ_ОсновныеМестаРаботыСотрудников.Период, &КонецПериода)) КАК КонецПериода,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ЕСТЬNULL(МВЗСотрудников.МВЗ, ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка)) КАК МВЗ,
	|	ВложенныйЗапрос.Должность КАК Должность
	|ПОМЕСТИТЬ ВТ_ПериодыРаботыСотрудников
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_ОсновныеМестаРаботыСотрудников.Период КАК Период,
	|		ВТ_ОсновныеМестаРаботыСотрудников.Сотрудник КАК Сотрудник,
	|		ВТ_ОсновныеМестаРаботыСотрудников.Подразделение КАК Подразделение,
	|		ВТ_ОсновныеМестаРаботыСотрудников.МоментВремени КАК МоментВремени,
	|		ВТ_ОсновныеМестаРаботыСотрудников.Организация КАК Организация,
	|		ВТ_ОсновныеМестаРаботыСотрудников.Должность КАК Должность
	|	ИЗ
	|		ВТ_ОсновныеМестаРаботыСотрудников КАК ВТ_ОсновныеМестаРаботыСотрудников
	|	ГДЕ
	|		ВТ_ОсновныеМестаРаботыСотрудников.ОсновноеМестоРаботы) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОсновныеМестаРаботыСотрудников КАК ВТ_ОсновныеМестаРаботыСотрудников
	|		ПО ВложенныйЗапрос.Сотрудник = ВТ_ОсновныеМестаРаботыСотрудников.Сотрудник
	|			И ВложенныйЗапрос.МоментВремени < ВТ_ОсновныеМестаРаботыСотрудников.МоментВремени
	|			И ВложенныйЗапрос.Организация = ВТ_ОсновныеМестаРаботыСотрудников.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МВЗСотрудников КАК МВЗСотрудников
	|		ПО ВложенныйЗапрос.Организация = МВЗСотрудников.Организация
	|			И ВложенныйЗапрос.Сотрудник = МВЗСотрудников.Сотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Сотрудник,
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Организация,
	|	ЕСТЬNULL(МВЗСотрудников.МВЗ, ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка)),
	|	ВложенныйЗапрос.Должность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МВЗПодразделений.Период КАК Период,
	|	МВЗПодразделений.Подразделение КАК Подразделение,
	|	МВЗПодразделений.МВЗ КАК МВЗ,
	|	МВЗПодразделений.Использовать КАК Использовать,
	|	МВЗПодразделений.МоментВремени КАК МоментВремени,
	|	МВЗПодразделений.МВЗ.Владелец КАК Организация
	|ПОМЕСТИТЬ ВТ_МВЗПодразделений
	|ИЗ
	|	РегистрСведений.МВЗПодразделений КАК МВЗПодразделений
	|ГДЕ
	|	МВЗПодразделений.Активность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Подразделение КАК Подразделение,
	|	ВложенныйЗапрос.МВЗ КАК МВЗ,
	|	ВложенныйЗапрос.Период КАК НачалоПериода,
	|	МИНИМУМ(ЕСТЬNULL(ВТ_МВЗПодразделений.Период, &КонецПериода)) КАК КонецПериода,
	|	ВложенныйЗапрос.МоментВремени КАК МоментВремени,
	|	ВложенныйЗапрос.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_ПериодыМВЗ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_МВЗПодразделений.Период КАК Период,
	|		ВТ_МВЗПодразделений.Подразделение КАК Подразделение,
	|		ВТ_МВЗПодразделений.МВЗ КАК МВЗ,
	|		ВТ_МВЗПодразделений.МоментВремени КАК МоментВремени,
	|		ВТ_МВЗПодразделений.Организация КАК Организация
	|	ИЗ
	|		ВТ_МВЗПодразделений КАК ВТ_МВЗПодразделений
	|	ГДЕ
	|		ВТ_МВЗПодразделений.Использовать) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МВЗПодразделений КАК ВТ_МВЗПодразделений
	|		ПО ВложенныйЗапрос.Подразделение = ВТ_МВЗПодразделений.Подразделение
	|			И ВложенныйЗапрос.МоментВремени < ВТ_МВЗПодразделений.МоментВремени
	|			И ВложенныйЗапрос.Организация = ВТ_МВЗПодразделений.Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.МВЗ,
	|	ВложенныйЗапрос.МоментВремени,
	|	ВложенныйЗапрос.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МВЗДолжностей.Период КАК Период,
	|	МВЗДолжностей.Должность КАК Должность,
	|	МВЗДолжностей.МВЗ КАК МВЗ,
	|	МВЗДолжностей.Использовать КАК Использовать,
	|	МВЗДолжностей.МоментВремени КАК МоментВремени,
	|	МВЗДолжностей.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_МВЗДолжностей
	|ИЗ
	|	РегистрСведений.МВЗДолжностей КАК МВЗДолжностей
	|ГДЕ
	|	МВЗДолжностей.Активность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Должность КАК Должность,
	|	ВложенныйЗапрос.МВЗ КАК МВЗ,
	|	ВложенныйЗапрос.Период КАК НачалоПериода,
	|	МИНИМУМ(ЕСТЬNULL(ВТ_МВЗДолжностей.Период, &КонецПериода)) КАК КонецПериода,
	|	ВложенныйЗапрос.МоментВремени КАК МоментВремени,
	|	ВложенныйЗапрос.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_ПериодыМВЗДолжностей
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_МВЗДолжностей.Период КАК Период,
	|		ВТ_МВЗДолжностей.Должность КАК Должность,
	|		ВТ_МВЗДолжностей.МВЗ КАК МВЗ,
	|		ВТ_МВЗДолжностей.МоментВремени КАК МоментВремени,
	|		ВТ_МВЗДолжностей.Организация КАК Организация
	|	ИЗ
	|		ВТ_МВЗДолжностей КАК ВТ_МВЗДолжностей
	|	ГДЕ
	|		ВТ_МВЗДолжностей.Использовать) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МВЗДолжностей КАК ВТ_МВЗДолжностей
	|		ПО ВложенныйЗапрос.Должность = ВТ_МВЗДолжностей.Должность
	|			И ВложенныйЗапрос.МоментВремени < ВТ_МВЗДолжностей.МоментВремени
	|			И ВложенныйЗапрос.Организация = ВТ_МВЗДолжностей.Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Должность,
	|	ВложенныйЗапрос.МВЗ,
	|	ВложенныйЗапрос.МоментВремени,
	|	ВложенныйЗапрос.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыдачаСредствЗащитыСотрудникуТовары.Ссылка.Организация КАК Организация,
	|	ВыдачаСредствЗащитыСотрудникуТовары.Ссылка.МВЗ КАК МВЗ,
	|	ВыдачаСредствЗащитыСотрудникуТовары.Сотрудник КАК Сотрудник,
	|	ВыдачаСредствЗащитыСотрудникуТовары.Номенклатура КАК Номенклатура,
	|	ВыдачаСредствЗащитыСотрудникуТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ВыдачаСредствЗащитыСотрудникуТовары.КоличествоВКомплекте = 0
	|			ТОГДА ВыдачаСредствЗащитыСотрудникуТовары.Количество
	|		ИНАЧЕ ВыдачаСредствЗащитыСотрудникуТовары.Количество * ВыдачаСредствЗащитыСотрудникуТовары.КоличествоВКомплекте
	|	КОНЕЦ КАК Количество,
	|	ВыдачаСредствЗащитыСотрудникуТовары.Ссылка.Дата КАК ДатаВыдачи,
	|	ВыдачаСредствЗащитыСотрудникуТовары.Комплект КАК Комплект,
	|	ВыдачаСредствЗащитыСотрудникуТовары.Ссылка.Склад КАК Склад
	|ПОМЕСТИТЬ ВТ_ДокументыВыдачи_
	|ИЗ
	|	Документ.ВыдачаСредствЗащитыСотруднику.Товары КАК ВыдачаСредствЗащитыСотрудникуТовары
	|ГДЕ
	|	НЕ ВыдачаСредствЗащитыСотрудникуТовары.НеВыдано
	|	И ВыдачаСредствЗащитыСотрудникуТовары.Ссылка.Проведен
	|	И ВыдачаСредствЗащитыСотрудникуТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И НЕ ВыдачаСредствЗащитыСотрудникуТовары.Ссылка.ПредварительнаяСборка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Организация,
	|	ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка),
	|	ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Сотрудник,
	|	ВыдачаДежурныхСредствЗащитыТовары.Номенклатура,
	|	ВыдачаДежурныхСредствЗащитыТовары.ХарактеристикаНоменклатуры,
	|	ВыдачаДежурныхСредствЗащитыТовары.Количество,
	|	ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Дата,
	|	NULL,
	|	ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Склад
	|ИЗ
	|	Документ.ВыдачаДежурныхСредствЗащиты.Товары КАК ВыдачаДежурныхСредствЗащитыТовары
	|ГДЕ
	|	ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Проведен
	|	И ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И НЕ ВыдачаДежурныхСредствЗащитыТовары.Ссылка.ПредварительнаяСборка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДокументыВыдачи_.Организация КАК Организация,
	|	ВТ_ДокументыВыдачи_.Сотрудник КАК Сотрудник,
	|	ВТ_ДокументыВыдачи_.Номенклатура КАК Номенклатура,
	|	ВТ_ДокументыВыдачи_.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ВТ_ДокументыВыдачи_.Количество) КАК Количество,
	|	ВТ_ДокументыВыдачи_.ДатаВыдачи КАК ДатаВыдачи,
	|	ВТ_ПериодыРаботыСотрудников.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ВТ_ДокументыВыдачи_.МВЗ = ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(ВТ_ПериодыРаботыСотрудников.МВЗ, ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка))
	|		ИНАЧЕ ЕСТЬNULL(ВТ_ДокументыВыдачи_.МВЗ, ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка))
	|	КОНЕЦ КАК МВЗ,
	|	ВТ_ДокументыВыдачи_.Комплект КАК Комплект,
	|	ВТ_ДокументыВыдачи_.Склад КАК Склад,
	|	ВТ_ПериодыРаботыСотрудников.Должность КАК Должность
	|ПОМЕСТИТЬ ВТ_ВыдачаСПодразделениями_
	|ИЗ
	|	ВТ_ДокументыВыдачи_ КАК ВТ_ДокументыВыдачи_
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодыРаботыСотрудников КАК ВТ_ПериодыРаботыСотрудников
	|		ПО ВТ_ДокументыВыдачи_.Сотрудник = ВТ_ПериодыРаботыСотрудников.Сотрудник
	|			И ВТ_ДокументыВыдачи_.Организация = ВТ_ПериодыРаботыСотрудников.Организация
	|			И ВТ_ДокументыВыдачи_.ДатаВыдачи <= ВТ_ПериодыРаботыСотрудников.КонецПериода
	|			И ВТ_ДокументыВыдачи_.ДатаВыдачи > ВТ_ПериодыРаботыСотрудников.НачалоПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДокументыВыдачи_.Организация,
	|	ВТ_ДокументыВыдачи_.Сотрудник,
	|	ВТ_ДокументыВыдачи_.Номенклатура,
	|	ВТ_ДокументыВыдачи_.ХарактеристикаНоменклатуры,
	|	ВТ_ДокументыВыдачи_.ДатаВыдачи,
	|	ВТ_ПериодыРаботыСотрудников.Подразделение,
	|	ВЫБОР
	|		КОГДА ВТ_ДокументыВыдачи_.МВЗ = ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(ВТ_ПериодыРаботыСотрудников.МВЗ, ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка))
	|		ИНАЧЕ ЕСТЬNULL(ВТ_ДокументыВыдачи_.МВЗ, ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка))
	|	КОНЕЦ,
	|	ВТ_ДокументыВыдачи_.Комплект,
	|	ВТ_ДокументыВыдачи_.Склад,
	|	ВТ_ПериодыРаботыСотрудников.Должность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВыдачаСПодразделениями_.Организация КАК Организация,
	|	ВТ_ВыдачаСПодразделениями_.Номенклатура КАК Номенклатура,
	|	ВТ_ВыдачаСПодразделениями_.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ВТ_ВыдачаСПодразделениями_.Количество) КАК Количество,
	|	ВТ_ВыдачаСПодразделениями_.ДатаВыдачи КАК ДатаВыдачи,
	|	ВЫБОР
	|		КОГДА ВТ_ВыдачаСПодразделениями_.МВЗ = ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ПериодыМВЗ.МВЗ ЕСТЬ NULL
	|						ТОГДА ВЫБОР
	|								КОГДА ВТ_ПериодыМВЗДолжностей.МВЗ ЕСТЬ NULL
	|									ТОГДА ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка)
	|								ИНАЧЕ ВТ_ПериодыМВЗДолжностей.МВЗ
	|							КОНЕЦ
	|					ИНАЧЕ ВТ_ПериодыМВЗ.МВЗ
	|				КОНЕЦ
	|		ИНАЧЕ ЕСТЬNULL(ВТ_ВыдачаСПодразделениями_.МВЗ, ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка))
	|	КОНЕЦ КАК МВЗ,
	|	ВТ_ВыдачаСПодразделениями_.Комплект КАК Комплект,
	|	ВТ_ВыдачаСПодразделениями_.Склад КАК Склад
	|ПОМЕСТИТЬ ВТ_ВыдачаСМВЗ_
	|ИЗ
	|	ВТ_ВыдачаСПодразделениями_ КАК ВТ_ВыдачаСПодразделениями_
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодыМВЗ КАК ВТ_ПериодыМВЗ
	|		ПО ВТ_ВыдачаСПодразделениями_.Подразделение = ВТ_ПериодыМВЗ.Подразделение
	|			И ВТ_ВыдачаСПодразделениями_.Организация = ВТ_ПериодыМВЗ.Организация
	|			И ВТ_ВыдачаСПодразделениями_.ДатаВыдачи <= ВТ_ПериодыМВЗ.КонецПериода
	|			И ВТ_ВыдачаСПодразделениями_.ДатаВыдачи > ВТ_ПериодыМВЗ.НачалоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодыМВЗДолжностей КАК ВТ_ПериодыМВЗДолжностей
	|		ПО ВТ_ВыдачаСПодразделениями_.Организация = ВТ_ПериодыМВЗДолжностей.Организация
	|			И ВТ_ВыдачаСПодразделениями_.Должность = ВТ_ПериодыМВЗДолжностей.Должность
	|			И ВТ_ВыдачаСПодразделениями_.ДатаВыдачи <= ВТ_ПериодыМВЗДолжностей.КонецПериода
	|			И ВТ_ВыдачаСПодразделениями_.ДатаВыдачи > ВТ_ПериодыМВЗДолжностей.НачалоПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ВыдачаСПодразделениями_.Организация,
	|	ВТ_ВыдачаСПодразделениями_.Номенклатура,
	|	ВТ_ВыдачаСПодразделениями_.ХарактеристикаНоменклатуры,
	|	ВТ_ВыдачаСПодразделениями_.ДатаВыдачи,
	|	ВТ_ВыдачаСПодразделениями_.Комплект,
	|	ВТ_ВыдачаСПодразделениями_.Склад,
	|	ВЫБОР
	|		КОГДА ВТ_ВыдачаСПодразделениями_.МВЗ = ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ПериодыМВЗ.МВЗ ЕСТЬ NULL
	|						ТОГДА ВЫБОР
	|								КОГДА ВТ_ПериодыМВЗДолжностей.МВЗ ЕСТЬ NULL
	|									ТОГДА ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка)
	|								ИНАЧЕ ВТ_ПериодыМВЗДолжностей.МВЗ
	|							КОНЕЦ
	|					ИНАЧЕ ВТ_ПериодыМВЗ.МВЗ
	|				КОНЕЦ
	|		ИНАЧЕ ЕСТЬNULL(ВТ_ВыдачаСПодразделениями_.МВЗ, ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка))
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВыдачаСПодразделениями_.Организация КАК Организация,
	|	ВТ_ВыдачаСПодразделениями_.Номенклатура КАК Номенклатура,
	|	ВТ_ВыдачаСПодразделениями_.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ВТ_ВыдачаСПодразделениями_.Количество) КАК Количество,
	|	НАЧАЛОПЕРИОДА(ВТ_ВыдачаСПодразделениями_.ДатаВыдачи, ДЕНЬ) КАК ДатаВыдачи,
	|	ВЫБОР
	|		КОГДА ВТ_ВыдачаСПодразделениями_.МВЗ = ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(ВТ_ПериодыМВЗ.МВЗ, ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка))
	|		ИНАЧЕ ЕСТЬNULL(ВТ_ВыдачаСПодразделениями_.МВЗ, ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка))
	|	КОНЕЦ КАК МВЗ,
	|	ВТ_ВыдачаСПодразделениями_.Комплект КАК Комплект,
	|	ВТ_ВыдачаСПодразделениями_.Склад КАК Склад
	|ПОМЕСТИТЬ ВТ_ВыдачаСМВЗ_Сгруппированная
	|ИЗ
	|	ВТ_ВыдачаСПодразделениями_ КАК ВТ_ВыдачаСПодразделениями_
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодыМВЗ КАК ВТ_ПериодыМВЗ
	|		ПО ВТ_ВыдачаСПодразделениями_.Подразделение = ВТ_ПериодыМВЗ.Подразделение
	|			И ВТ_ВыдачаСПодразделениями_.Организация = ВТ_ПериодыМВЗ.Организация
	|			И ВТ_ВыдачаСПодразделениями_.ДатаВыдачи <= ВТ_ПериодыМВЗ.КонецПериода
	|			И ВТ_ВыдачаСПодразделениями_.ДатаВыдачи > ВТ_ПериодыМВЗ.НачалоПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ВыдачаСПодразделениями_.Организация,
	|	ВТ_ВыдачаСПодразделениями_.Номенклатура,
	|	ВТ_ВыдачаСПодразделениями_.ХарактеристикаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ВТ_ВыдачаСПодразделениями_.МВЗ = ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(ВТ_ПериодыМВЗ.МВЗ, ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка))
	|		ИНАЧЕ ЕСТЬNULL(ВТ_ВыдачаСПодразделениями_.МВЗ, ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка))
	|	КОНЕЦ,
	|	ВТ_ВыдачаСПодразделениями_.Комплект,
	|	НАЧАЛОПЕРИОДА(ВТ_ВыдачаСПодразделениями_.ДатаВыдачи, ДЕНЬ),
	|	ВТ_ВыдачаСПодразделениями_.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ВыдачаСМВЗ_Сгруппированная.ДатаВыдачи КАК ДатаВыдачи
	|ПОМЕСТИТЬ ВТ_ДатыВыдачи
	|ИЗ
	|	ВТ_ВыдачаСМВЗ_Сгруппированная КАК ВТ_ВыдачаСМВЗ_Сгруппированная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НоменклатураКомплектующие.Ссылка КАК Комплект,
	|	НоменклатураКомплектующие.Номенклатура КАК Номенклатура,
	|	НоменклатураКомплектующие.Количество КАК КоличествоВКомплекте,
	|	ВЫБОР
	|		КОГДА ХарактеристикиНоменклатуры.Ссылка ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|		ИНАЧЕ ХарактеристикиНоменклатуры.Ссылка
	|	КОНЕЦ КАК ХарактеристикаКомплекта
	|ПОМЕСТИТЬ ВТ_КомплектыХарактеристики
	|ИЗ
	|	Справочник.Номенклатура.Комплектующие КАК НоменклатураКомплектующие
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО НоменклатураКомплектующие.Ссылка = ХарактеристикиНоменклатуры.Владелец
	|ГДЕ
	|	НоменклатураКомплектующие.Ссылка.Комплект
	|	И НоменклатураКомплектующие.Ссылка.УчитыватьКакКомплектВЗаказеПоставщикуИОтчетеДляСАП
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Организация,
	|	ВТ_КомплектыХарактеристики.Комплект КАК Комплект,
	|	ВТ_КомплектыХарактеристики.Номенклатура КАК Номенклатура,
	|	ВТ_КомплектыХарактеристики.ХарактеристикаКомплекта КАК ХарактеристикаКомплекта,
	|	ВТ_КомплектыХарактеристики.КоличествоВКомплекте КАК КоличествоВКомплекте
	|ПОМЕСТИТЬ ВТ_КомплектыХарактеристикиОрганизации
	|ИЗ
	|	ВТ_КомплектыХарактеристики КАК ВТ_КомплектыХарактеристики,
	|	Справочник.Организации КАК Организации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_КомплектыХарактеристикиОрганизации.Комплект КАК Комплект,
	|	ВТ_КомплектыХарактеристикиОрганизации.Номенклатура КАК Номенклатура,
	|	ВТ_КомплектыХарактеристикиОрганизации.ХарактеристикаКомплекта КАК ХарактеристикаКомплекта,
	|	ВТ_КомплектыХарактеристикиОрганизации.КоличествоВКомплекте КАК КоличествоВКомплекте,
	|	ВТ_КомплектыХарактеристикиОрганизации.Организация КАК Организация,
	|	МВЗ.Ссылка КАК МВЗ
	|ПОМЕСТИТЬ ВТ_КомплектыХарактеристикиОрганизацииМВЗ
	|ИЗ
	|	ВТ_КомплектыХарактеристикиОрганизации КАК ВТ_КомплектыХарактеристикиОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.МВЗ КАК МВЗ
	|		ПО ВТ_КомплектыХарактеристикиОрганизации.Организация = МВЗ.Владелец
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_КомплектыХарактеристикиОрганизации.Комплект,
	|	ВТ_КомплектыХарактеристикиОрганизации.Номенклатура,
	|	ВТ_КомплектыХарактеристикиОрганизации.ХарактеристикаКомплекта,
	|	ВТ_КомплектыХарактеристикиОрганизации.КоличествоВКомплекте,
	|	ВТ_КомплектыХарактеристикиОрганизации.Организация,
	|	ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка)
	|ИЗ
	|	ВТ_КомплектыХарактеристикиОрганизации КАК ВТ_КомплектыХарактеристикиОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗ.Комплект КАК Комплект,
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗ.Номенклатура КАК Номенклатура,
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗ.ХарактеристикаКомплекта КАК ХарактеристикаКомплекта,
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗ.КоличествоВКомплекте КАК КоличествоВКомплекте,
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗ.Организация КАК Организация,
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗ.МВЗ КАК МВЗ,
	|	ВТ_ДатыВыдачи.ДатаВыдачи КАК ДатаВыдачи
	|ПОМЕСТИТЬ ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи
	|ИЗ
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗ КАК ВТ_КомплектыХарактеристикиОрганизацииМВЗ,
	|	ВТ_ДатыВыдачи КАК ВТ_ДатыВыдачи
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Комплект,
	|	Номенклатура,
	|	ХарактеристикаКомплекта,
	|	МВЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Организация КАК Организация,
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.МВЗ КАК МВЗ,
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ДатаВыдачи КАК ДатаВыдачи,
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Комплект КАК Комплект,
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ХарактеристикаКомплекта КАК ХарактеристикаКомплекта,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_ВыдачаСМВЗ_Сгруппированная.Количество ЕСТЬ NULL
	|				ТОГДА 0
	|			ИНАЧЕ ВТ_ВыдачаСМВЗ_Сгруппированная.Количество / ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.КоличествоВКомплекте
	|		КОНЕЦ) КАК КоличествоКомплектов,
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Номенклатура КАК Номенклатура,
	|	ВТ_ВыдачаСМВЗ_Сгруппированная.Склад КАК Склад
	|ПОМЕСТИТЬ ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи
	|ИЗ
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи КАК ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыдачаСМВЗ_Сгруппированная КАК ВТ_ВыдачаСМВЗ_Сгруппированная
	|		ПО ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Организация = ВТ_ВыдачаСМВЗ_Сгруппированная.Организация
	|			И ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.МВЗ = ВТ_ВыдачаСМВЗ_Сгруппированная.МВЗ
	|			И ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ДатаВыдачи = ВТ_ВыдачаСМВЗ_Сгруппированная.ДатаВыдачи
	|			И ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Номенклатура = ВТ_ВыдачаСМВЗ_Сгруппированная.Номенклатура
	|			И (ВЫБОР
	|				КОГДА ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ХарактеристикаКомплекта.Метрика ЕСТЬ NULL
	|					ТОГДА ВТ_ВыдачаСМВЗ_Сгруппированная.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|				ИНАЧЕ ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ХарактеристикаКомплекта.Метрика = ВТ_ВыдачаСМВЗ_Сгруппированная.ХарактеристикаНоменклатуры.Метрика
	|			КОНЕЦ)
	|			И ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Комплект = ВТ_ВыдачаСМВЗ_Сгруппированная.Комплект
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Организация,
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.МВЗ,
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ДатаВыдачи,
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Комплект,
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ХарактеристикаКомплекта,
	|	ВТ_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Номенклатура,
	|	ВТ_ВыдачаСМВЗ_Сгруппированная.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	МВЗ,
	|	Комплект,
	|	ХарактеристикаКомплекта,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Организация КАК Организация,
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.МВЗ КАК МВЗ,
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ДатаВыдачи КАК ДатаВыдачи,
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Комплект КАК Комплект,
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ХарактеристикаКомплекта КАК ХарактеристикаКомплекта,
	|	МАКСИМУМ(ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.КоличествоКомплектов) КАК МаксимальноеКоличествоКомплектов,
	|	МИНИМУМ(ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.КоличествоКомплектов) КАК МинимальноеКоличествоКомплектов,
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Склад КАК Склад
	|ПОМЕСТИТЬ ВТ_АнализВыдачи_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи
	|ИЗ
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи КАК ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Организация,
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.МВЗ,
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ДатаВыдачи,
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Комплект,
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ХарактеристикаКомплекта,
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	МВЗ,
	|	Комплект,
	|	ХарактеристикаКомплекта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Организация КАК Организация,
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.МВЗ КАК МВЗ,
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ДатаВыдачи КАК ДатаВыдачи,
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Комплект КАК Комплект,
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ХарактеристикаКомплекта КАК ХарактеристикаКомплекта,
	|	ВТ_АнализВыдачи_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.МаксимальноеКоличествоКомплектов КАК МаксимальноеКоличествоКомплектов,
	|	ВТ_АнализВыдачи_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.МинимальноеКоличествоКомплектов КАК МинимальноеКоличествоКомплектов,
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Склад КАК Склад
	|ПОМЕСТИТЬ ВТ_ВыдачаКомплектов_Итог
	|ИЗ
	|	ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи КАК ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АнализВыдачи_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи КАК ВТ_АнализВыдачи_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи
	|		ПО ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Организация = ВТ_АнализВыдачи_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Организация
	|			И ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.МВЗ = ВТ_АнализВыдачи_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.МВЗ
	|			И ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ДатаВыдачи = ВТ_АнализВыдачи_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ДатаВыдачи
	|			И ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Комплект = ВТ_АнализВыдачи_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Комплект
	|			И ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ХарактеристикаКомплекта = ВТ_АнализВыдачи_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.ХарактеристикаКомплекта
	|			И ВТ_Выдача_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Склад = ВТ_АнализВыдачи_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.Склад
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ВТ_АнализВыдачи_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.МаксимальноеКоличествоКомплектов = ВТ_АнализВыдачи_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.МинимальноеКоличествоКомплектов
	|				ТОГДА НЕ ВТ_АнализВыдачи_КомплектыХарактеристикиОрганизацииМВЗДатыВыдачи.МаксимальноеКоличествоКомплектов = 0
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	МВЗ,
	|	Комплект,
	|	ХарактеристикаКомплекта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_КомплектыХарактеристики.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ВТ_Комплектующие
	|ИЗ
	|	ВТ_КомплектыХарактеристики КАК ВТ_КомплектыХарактеристики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВыдачаСМВЗ_Сгруппированная.Организация КАК Организация,
	|	ВТ_ВыдачаСМВЗ_Сгруппированная.Номенклатура КАК Номенклатура,
	|	ВТ_ВыдачаСМВЗ_Сгруппированная.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ВыдачаСМВЗ_Сгруппированная.Количество КАК Количество,
	|	ВТ_ВыдачаСМВЗ_Сгруппированная.ДатаВыдачи КАК ДатаВыдачи,
	|	ВТ_ВыдачаСМВЗ_Сгруппированная.МВЗ КАК МВЗ,
	|	ВТ_ВыдачаСМВЗ_Сгруппированная.ХарактеристикаНоменклатуры.КодSAP КАК ХарактеристикаНоменклатурыКодSAP,
	|	ВТ_ВыдачаСМВЗ_Сгруппированная.Склад КАК Склад
	|ПОМЕСТИТЬ ВТ_ВыдачаБезКомплектующих
	|ИЗ
	|	ВТ_ВыдачаСМВЗ_Сгруппированная КАК ВТ_ВыдачаСМВЗ_Сгруппированная
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Комплектующие КАК ВТ_Комплектующие
	|		ПО ВТ_ВыдачаСМВЗ_Сгруппированная.Номенклатура = ВТ_Комплектующие.Номенклатура
	|ГДЕ
	|	ВТ_Комплектующие.Номенклатура ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ВыдачаКомплектов_Итог.Организация КАК Организация,
	|	ВТ_ВыдачаКомплектов_Итог.МВЗ КАК МВЗ,
	|	ВТ_ВыдачаКомплектов_Итог.ДатаВыдачи КАК ДатаВыдачи,
	|	ВТ_ВыдачаКомплектов_Итог.Комплект КАК Комплект,
	|	ВЫБОР
	|		КОГДА ВТ_ВыдачаКомплектов_Итог.МаксимальноеКоличествоКомплектов = ВТ_ВыдачаКомплектов_Итог.МинимальноеКоличествоКомплектов
	|			ТОГДА ВТ_ВыдачаКомплектов_Итог.ХарактеристикаКомплекта
	|		ИНАЧЕ ""Ошибка""
	|	КОНЕЦ КАК ХарактеристикаКомплекта,
	|	ВЫБОР
	|		КОГДА ВТ_ВыдачаКомплектов_Итог.МаксимальноеКоличествоКомплектов = ВТ_ВыдачаКомплектов_Итог.МинимальноеКоличествоКомплектов
	|			ТОГДА ВТ_ВыдачаКомплектов_Итог.ХарактеристикаКомплекта.КодSAP
	|		ИНАЧЕ ""Ошибка""
	|	КОНЕЦ КАК КодСАП,
	|	ВЫБОР
	|		КОГДА ВТ_ВыдачаКомплектов_Итог.МаксимальноеКоличествоКомплектов = ВТ_ВыдачаКомплектов_Итог.МинимальноеКоличествоКомплектов
	|			ТОГДА ВТ_ВыдачаКомплектов_Итог.МаксимальноеКоличествоКомплектов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Количество,
	|	ВТ_ВыдачаКомплектов_Итог.Склад КАК Склад
	|ПОМЕСТИТЬ ВТ_ВыдачаКомплектовРезультат
	|ИЗ
	|	ВТ_ВыдачаКомплектов_Итог КАК ВТ_ВыдачаКомплектов_Итог
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	МВЗ,
	|	Комплект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВыдачаБезКомплектующих.Организация КАК Организация,
	|	ВТ_ВыдачаБезКомплектующих.Номенклатура КАК Номенклатура,
	|	ВТ_ВыдачаБезКомплектующих.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ВыдачаБезКомплектующих.Количество КАК Количество,
	|	ВТ_ВыдачаБезКомплектующих.ДатаВыдачи КАК ДатаВыдачи,
	|	ВТ_ВыдачаБезКомплектующих.МВЗ КАК МВЗ,
	|	ВТ_ВыдачаБезКомплектующих.ХарактеристикаНоменклатурыКодSAP КАК КодSAP,
	|	ВТ_ВыдачаБезКомплектующих.Склад КАК Склад
	|ПОМЕСТИТЬ ВТ_Результат
	|ИЗ
	|	ВТ_ВыдачаБезКомплектующих КАК ВТ_ВыдачаБезКомплектующих
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ВыдачаКомплектовРезультат.Организация,
	|	ВТ_ВыдачаКомплектовРезультат.Комплект,
	|	ВТ_ВыдачаКомплектовРезультат.ХарактеристикаКомплекта,
	|	ВТ_ВыдачаКомплектовРезультат.Количество,
	|	ВТ_ВыдачаКомплектовРезультат.ДатаВыдачи,
	|	ВТ_ВыдачаКомплектовРезультат.МВЗ,
	|	ВТ_ВыдачаКомплектовРезультат.КодСАП,
	|	ВТ_ВыдачаКомплектовРезультат.Склад
	|ИЗ
	|	ВТ_ВыдачаКомплектовРезультат КАК ВТ_ВыдачаКомплектовРезультат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Результат.Организация КАК Организация,
	|	ВТ_Результат.Номенклатура.КодСинхронизации КАК НоменклатураКодСинхронизации,
	|	ВТ_Результат.КодSAP КАК КодSAP,
	|	СУММА(ВТ_Результат.Количество) КАК Количество,
	|	ВТ_Результат.Номенклатура КАК Номенклатура,
	|	ВТ_Результат.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &ОтображатьДатуВыдачи
	|			ТОГДА ВТ_Результат.ДатаВыдачи
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ДатаВыдачи,
	|	ВТ_Результат.МВЗ КАК МВЗ,
	|	ВТ_Результат.Склад КАК Склад
	|ИЗ
	|	ВТ_Результат КАК ВТ_Результат
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Результат.Организация,
	|	ВТ_Результат.Номенклатура,
	|	ВТ_Результат.ХарактеристикаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &ОтображатьДатуВыдачи
	|			ТОГДА ВТ_Результат.ДатаВыдачи
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ВТ_Результат.МВЗ,
	|	ВТ_Результат.КодSAP,
	|	ВТ_Результат.Номенклатура.КодСинхронизации,
	|	ВТ_Результат.Склад
	|
	|УПОРЯДОЧИТЬ ПО
	|	НоменклатураКодСинхронизации,
	|	КодSAP,
	|	ДатаВыдачи";
	
	Запрос.УстановитьПараметр("КонецПериода",			ТекущаяДата());
	Запрос.УстановитьПараметр("ДатаНачала",				ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания",			ДатаОкончания);
	Запрос.УстановитьПараметр("ОтображатьДатуВыдачи",	ОтображатьДатуВыдачи);
	
	ТаблицаДляВывода = Запрос.Выполнить().Выгрузить();
	
	НайденныеСтроки = ТаблицаДляВывода.НайтиСтроки(Новый Структура("КодSAP","Ошибка"));
	
	Если НЕ НайденныеСтроки.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'При формировании отчета обнаружены ошибки формирования комплектов.'"));
	КонецЕсли;
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	// Скомпонуем результат
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	
	НаборыВнешнихДанных = Новый Структура;
	НаборыВнешнихДанных.Вставить("ТаблицаДляВывода",ТаблицаДляВывода);
	
	//Инициализировать(<Макет>, <ВнешниеНаборыДанных>, <ДанныеРасшифровки>, <ВозможностьИспользованияВнешнихФункций>) 
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,НаборыВнешнихДанных , ДанныеРасшифровки);
	
	ДокументРезультат.Очистить();
	
	// Выводим результат в табличный документ
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);	
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры
