
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЖелаемаяДатаПолучения = ТекущаяДата();
	
	ИзСписка = Истина;
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	
	Элементы.МестоПолучения.Видимость 				= НЕ ИзСписка;
	Элементы.МестоПолученияИзСправочника.Видимость 	= ИзСписка;
	
КонецПроцедуры	

&НаСервере
Процедура ОтправитьНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);	
	
	ВнешнийПользователь = ВнешниеПользователи.ТекущийВнешнийПользователь();
	ЗагрузитьЗаявкуНаПолучение(ВнешнийПользователь.ОбъектАвторизации);
	
КонецПроцедуры

&НаКлиенте
Процедура Отправить(Команда)
	
	ОтправитьНаСервере();
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьЗаявкуНаПолучение(ФизическоеЛицо)
	
	Сотрудник = Справочники.Сотрудники.НайтиПоРеквизиту("ФизическоеЛицо",ФизическоеЛицо);
	
	Если ИзСписка Тогда
		ВыбранноеМестоПолучения = ?(ЗначениеЗаполнено(МестоПолученияИзСправочника),МестоПолученияИзСправочника.Наименование, "не определено");
	Иначе
		ВыбранноеМестоПолучения = ?(ЗначениеЗаполнено(МестоПолучения),МестоПолучения, "не определено");
	КонецЕсли;	
	
	НовыйДокумент = Документы.ОбращениеНаСклад.СоздатьДокумент();
	НовыйДокумент.Дата 					= ТекущаяДата();
	НовыйДокумент.КатегорияОбращения 	= Справочники.КатегорииОбращенияНаСклад.ЗаявкаНаПолучение;
	НовыйДокумент.Организация 			= Сотрудник.Владелец;
	НовыйДокумент.СодержаниеОбращения 	= "Желаемое место получения средств защиты: < " + ВыбранноеМестоПолучения + " >";
	НовыйДокумент.СоздательДокумента 	= ВнешниеПользователи.ТекущийВнешнийПользователь();
	НовыйДокумент.Сотрудник 			= Сотрудник;
	НовыйДокумент.СтатусОбращения 		= Перечисления.СтатусыОбращенийНаСклад.Открыто;
	НовыйДокумент.ДатаПолучения			= ЖелаемаяДатаПолучения;
	НовыйДокумент.МестоПолучения		= ?(ИзСписка,МестоПолученияИзСправочника,Справочники.МестаПолучения.ПустаяСсылка());
	НовыйДокумент.Склад					= ПроцедурыРаботыСНормамиСервер.ПолучитьСкладВыдачи(Справочники.Подразделения.ПустаяСсылка(),Сотрудник,Перечисления.ВидыВыдачиСИЗ.ПерсональнаяВыдача);
	Документы.ОбращениеНаСклад.ЗаполнитьТаблицуДокумента(НовыйДокумент);
	НовыйДокумент.УстановитьНовыйНомер();
	НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзСпискаПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры
