
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Документ = Неопределено Тогда
		ВызватьИсключение НСтр("ru='Предусмотрено открытие обработки только из документов.'");
	КонецЕсли;
	
	Объект.Организация = Параметры.Организация;
	
	ЗаполнитьДеревоПодразделений();
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	АдресРезультатаПодбораВХранилище = ПоместитьРезультатыПодбораВХранилище();
	
	Структура = Новый Структура("АдресРезультатаПодбораВХранилище", АдресРезультатаПодбораВХранилище);
	
	Закрыть(КодВозвратаДиалога.ОК);
	
	ОповеститьОВыборе(Структура);

КонецПроцедуры

&НаСервере
Функция ПоместитьРезультатыПодбораВХранилище()
	
	МассивПодразделений = Новый Массив;
	
	ДеревоДанных = РеквизитФормыВЗначение("ДеревоПодразделений");
	
	Для Каждого СтрокаДерева Из ДеревоДанных.Строки Цикл
		
		Если СтрокаДерева.Использовать = 0 Тогда
			Продолжить;
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(СтрокаДерева.Подразделение) Тогда
			
			МассивПодразделений.Добавить(СтрокаДерева.Подразделение);
			
		КонецЕсли;
		
		ОбойтиДеревоВниз(МассивПодразделений,СтрокаДерева);
		
	КонецЦикла;
	
	АдресРезультатаПодбораВХранилище = ПоместитьВоВременноеХранилище(МассивПодразделений, УникальныйИдентификатор);
	
	Возврат АдресРезультатаПодбораВХранилище;
	
КонецФункции

&НаСервере
Процедура ОбойтиДеревоВниз(МассивПодразделений,СтрокаДерева)
	
	Для Каждого СтрокаДерева Из СтрокаДерева.Строки Цикл
		
		Если СтрокаДерева.Использовать = 0 Тогда
			Продолжить;
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(СтрокаДерева.Подразделение) Тогда
			
			МассивПодразделений.Добавить(СтрокаДерева.Подразделение);
			
		КонецЕсли;
		
		ОбойтиДеревоВниз(МассивПодразделений,СтрокаДерева);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПодразделенийИспользоватьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоПодразделений.ТекущиеДанные;
	
	Если ТекущиеДанные.Использовать = 2 Тогда
		ТекущиеДанные.Использовать = 0;
	КонецЕсли;
	
	ПроцедурыРаботыСНормамиКлиент.ОбходДереваВверх(ТекущиеДанные);
	ПроцедурыРаботыСНормамиКлиент.ОбходДереваВниз(ТекущиеДанные);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоПодразделений()
	
	ДанныеДереваПодразделений = РеквизитФормыВЗначение("ДеревоПодразделений");
	ДанныеДереваПодразделений.Строки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ Разрешенные
	|	Подразделения.Ссылка КАК Подразделение
	|ИЗ
	|	Справочник.Подразделения КАК Подразделения
	|ГДЕ
	|	Подразделения.Владелец = &Владелец
	|
	|СГРУППИРОВАТЬ ПО
	|	Подразделения.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Подразделения.Наименование
	|ИТОГИ ПО
	|	Подразделение ИЕРАРХИЯ";
	
	Запрос.УстановитьПараметр("Владелец",Объект.Организация);
	
	ДеревоЗапроса = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	СтрокаВерхнегоУровня = ДанныеДереваПодразделений.Строки.Добавить();
	СтрокаВерхнегоУровня.Подразделение = "Все подразделения";
	
	Для Каждого СтрокаДерева Из ДеревоЗапроса.Строки Цикл
		
		НоваяСтрока 				= СтрокаВерхнегоУровня.Строки.Добавить();
		НоваяСтрока.Подразделение 	= СтрокаДерева.Подразделение;
		НоваяСтрока.Использовать 	= 1;
		
		ОбходДереваВверх(НоваяСтрока);
		
		ОбработатьДеревоЗапросаРекурсивно(СтрокаДерева,НоваяСтрока);
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДанныеДереваПодразделений,"ДеревоПодразделений");
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьДеревоЗапросаРекурсивно(СтрокаДереваЗапроса,НоваяСтрокаДереваПодразделений)
	
	Для Каждого СтрокаДерева Из СтрокаДереваЗапроса.Строки Цикл
		
		Если НоваяСтрокаДереваПодразделений.Подразделение = СтрокаДерева.Подразделение Тогда //запрос возвращает "лишние" ветки
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = НоваяСтрокаДереваПодразделений.Строки.Добавить();
		НоваяСтрока.Подразделение = СтрокаДерева.Подразделение;
		
		НоваяСтрока.Использовать = 1;
		ОбходДереваВверх(НоваяСтрока);
		
		ОбработатьДеревоЗапросаРекурсивно(СтрокаДерева,НоваяСтрока);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбходДереваВверх(ТекущиеДанные) Экспорт

	Родитель = ТекущиеДанные.Родитель;
	Если Родитель <> Неопределено Тогда // Верхний уровень
		
		ДочерниеСтроки = Родитель.Строки;
		КоличествоВыбранных = 0;
		ОбщееКоличество = 0;
		Для каждого Элемент Из ДочерниеСтроки Цикл
			Если Элемент.Использовать = 2 Тогда
				КоличествоВыбранных = КоличествоВыбранных + 0.5;
			ИначеЕсли Элемент.Использовать = 1 Тогда
				КоличествоВыбранных = КоличествоВыбранных + 1;
			КонецЕсли;
			ОбщееКоличество = ОбщееКоличество + 1;
		КонецЦикла;
		
		Если ОбщееКоличество = КоличествоВыбранных Тогда
			Родитель.Использовать = 1;
		ИначеЕсли КоличествоВыбранных = 0 Тогда
			Родитель.Использовать = 0;
		Иначе
			Родитель.Использовать = 2;
		КонецЕсли;
		
		ОбходДереваВверх(Родитель);
		
	КонецЕсли;
	
КонецПроцедуры
