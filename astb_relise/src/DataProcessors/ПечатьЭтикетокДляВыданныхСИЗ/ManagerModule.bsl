#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_Этикетка") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ПФ_MXL_Этикетка",
			"Этикетка",
			СформироватьПечатныеФормыЭтикеток(МассивОбъектов, ПараметрыПечати),,"Обработка.ПечатьЭтикетокДляВыданныхСИЗ.ПФ_MXL_Этикетка");
			
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатныеФормыЭтикеток(МассивОбъектов, ОбъектыПечати)
	
	Перем ИсходныеДанные;
	
	//ДанныеПечати = МассивОбъектов[0];
	ОбъектыПечати.Свойство("ДанныеДляПечати",ИсходныеДанные);
	ИсходныеДанные = ПолучитьИзВременногоХранилища(ИсходныеДанные);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ИмяПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_Этикетка";
	
    Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьЭтикетокДляВыданныхСИЗ.ПФ_MXL_Этикетка");
	
	Эталон = Обработки.ПечатьЭтикетокДляВыданныхСИЗ.ПолучитьМакет("Эталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
	
	Для Каждого СтрокаИсходныхДанных Из ИсходныеДанные Цикл
		
		Если НЕ ЗначениеЗаполнено(СокрЛП(СтрокаИсходныхДанных.Штрихкод)) Тогда
			Продолжить;
		КонецЕсли;
		
		Область = Макет.ПолучитьОбласть("ОбластьСтроки|ОбластьКолонки");
		Для каждого Рисунок Из Область.Рисунки Цикл
			
			ПараметрыШтрихкода = Новый Структура;
			ПараметрыШтрихкода.Вставить("Ширина",          Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
			ПараметрыШтрихкода.Вставить("Высота",          Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
			ПараметрыШтрихкода.Вставить("Штрихкод",        СокрЛП(СтрокаИсходныхДанных.Штрихкод));
			ПараметрыШтрихкода.Вставить("ТипКода",         "1"); //EAN13
							
			Рисунок.Картинка = ПроцедурыРаботыСНормамиСервер.ПолучитьКартинкуШтрихкода(ПараметрыШтрихкода);
			
		КонецЦикла;
		
		Область.Параметры.Организация 			= СокрЛП(СтрокаИсходныхДанных.Сотрудник.Владелец.Наименование);
		Область.Параметры.Сотрудник 			= ФизическиеЛица.ФамилияИнициалыФизЛица(СтрокаИсходныхДанных.Сотрудник.Наименование);
		Область.Параметры.МестоХраненияСИЗ 		= СтрокаИсходныхДанных.Сотрудник.МестоХраненияСИЗ.Код;
		Область.Параметры.ТабельныйНомер 		= СтрокаИсходныхДанных.Сотрудник.ТабельныйНомер;
		Если ЗначениеЗаполнено(СтрокаИсходныхДанных.ДатаОкончанияИспользования) Тогда
			Область.Параметры.ПериодИспользования	= "с " + Формат(СтрокаИсходныхДанных.ДатаВыдачи,"ДЛФ=D") + " по " + Формат(СтрокаИсходныхДанных.ДатаОкончанияИспользования,"ДЛФ=D");
		Иначе
			Область.Параметры.ПериодИспользования	= "дата выдачи: " + Формат(СтрокаИсходныхДанных.ДатаВыдачи,"ДЛФ=D");
		КонецЕсли;
		Область.Параметры.Номенклатура 			= СтрокаИсходныхДанных.Номенклатура.Наименование;
		Область.Параметры.Размер 				= СтрокаИсходныхДанных.ХарактеристикаНоменклатуры.Наименование;
		Область.Параметры.Артикул 				= СтрокаИсходныхДанных.Номенклатура.Артикул;
		
		ТабличныйДокумент.Вывести(Область);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции
	
#КонецЕсли
