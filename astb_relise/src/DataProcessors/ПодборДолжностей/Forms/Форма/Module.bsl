
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Документ = Неопределено Тогда
		ВызватьИсключение НСтр("ru='Предусмотрено открытие обработки только из документов.'");
	КонецЕсли;
	
	ЗаполнитьДеревоДолжностей();
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	АдресРезультатаПодбораВХранилище = ПоместитьРезультатыПодбораВХранилище();
	
	Структура = Новый Структура("АдресРезультатаПодбораВХранилище", АдресРезультатаПодбораВХранилище);
	
	Закрыть(КодВозвратаДиалога.ОК);
	
	ОповеститьОВыборе(Структура);

КонецПроцедуры

&НаСервере
Функция ПоместитьРезультатыПодбораВХранилище()
	
	МассивДолжностей = Новый Массив;
	
	ДеревоДанных = РеквизитФормыВЗначение("ДеревоДолжностей");
	
	Для Каждого СтрокаДерева Из ДеревоДанных.Строки Цикл
		
		Если СтрокаДерева.Использовать = 0 Тогда
			Продолжить;
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(СтрокаДерева.Должность) Тогда
			
			МассивДолжностей.Добавить(СтрокаДерева.Должность);
			
		КонецЕсли;
		
		ОбойтиДеревоВниз(МассивДолжностей,СтрокаДерева);
		
	КонецЦикла;
	
	АдресРезультатаПодбораВХранилище = ПоместитьВоВременноеХранилище(МассивДолжностей, УникальныйИдентификатор);
	
	Возврат АдресРезультатаПодбораВХранилище;
	
КонецФункции

&НаСервере
Процедура ОбойтиДеревоВниз(МассивДолжностей,СтрокаДерева)
	
	Для Каждого СтрокаДерева Из СтрокаДерева.Строки Цикл
		
		Если СтрокаДерева.Использовать = 0 Тогда
			Продолжить;
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(СтрокаДерева.Должность) Тогда
			
			МассивДолжностей.Добавить(СтрокаДерева.Должность);
			
		КонецЕсли;
		
		ОбойтиДеревоВниз(МассивДолжностей,СтрокаДерева);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоДолжностейИспользоватьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоДолжностей.ТекущиеДанные;
	
	Если ТекущиеДанные.Использовать = 2 Тогда
		ТекущиеДанные.Использовать = 0;
	КонецЕсли;
	
	ПроцедурыРаботыСНормамиКлиент.ОбходДереваВверх(ТекущиеДанные);
	ПроцедурыРаботыСНормамиКлиент.ОбходДереваВниз(ТекущиеДанные);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоДолжностей()
	
	ДанныеДерева = РеквизитФормыВЗначение("ДеревоДолжностей");
	ДанныеДерева.Строки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДолжностиИПрофессии.Ссылка КАК Должность
	|ИЗ
	|	Справочник.ДолжностиИПрофессии КАК ДолжностиИПрофессии
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДолжностиИПрофессии.Наименование"
	;
	
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	
	СтрокаВерхнегоУровня = ДанныеДерева.Строки.Добавить();
	СтрокаВерхнегоУровня.Использовать = 1;
	
	Для Каждого СтрокаТаблицыЗапроса Из ТаблицаЗапроса Цикл
		
		НоваяСтрока 			 = СтрокаВерхнегоУровня.Строки.Добавить();
		НоваяСтрока.Должность 	 = СтрокаТаблицыЗапроса.Должность;
		НоваяСтрока.Использовать = 1;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДанныеДерева,"ДеревоДолжностей");
	
КонецПроцедуры

