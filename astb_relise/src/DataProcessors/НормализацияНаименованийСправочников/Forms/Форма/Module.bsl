
&НаСервере
Процедура ВыполнитьНормализациюНаСервере()
	
	Для Каждого СтрокаТаблицы Из Объект.ТаблицаИзменений Цикл
		
		Если НЕ СтрокаТаблицы.Использовать Тогда
			Продолжить;
		КонецЕсли;	
		
		СправочникОбъект 				= СтрокаТаблицы.Ссылка.ПолучитьОбъект();
		СправочникОбъект.Наименование 	= СтрокаТаблицы.НовоеНаименование;
		СправочникОбъект.Записать();
		
	КонецЦикла;	
	
	ЗаполнитьТаблицу();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьНормализацию(Команда)
	
	ВыполнитьНормализациюНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ВидСправочникаПриИзмененииНаСервере()
	
	ЗаполнитьТаблицу();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидСправочникаПриИзменении(Элемент)
	
	ВидСправочникаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивСправочников = СправочникиИзМетаданных();
	
	ЭтаФорма.Элементы.ВидСправочника.СписокВыбора.ЗагрузитьЗначения(МассивСправочников);
	
	ВидСправочника = МассивСправочников[0];
	
	ЗаполнитьТаблицу();
	
КонецПроцедуры

&НаСервере
Функция СправочникиИзМетаданных()
	
	ТаблицаСправочников = Новый ТаблицаЗначений;
	ТаблицаСправочников.Колонки.Добавить("ИмяСправочника");	
	
	Для Каждого СправочникИзМетаданных Из Метаданные.Справочники Цикл
		
		НоваяСтрока 				= ТаблицаСправочников.Добавить();
		НоваяСтрока.ИмяСправочника 	= СправочникИзМетаданных.Имя;
		
	КонецЦикла;
	
	ТаблицаСправочников.Сортировать("ИмяСправочника");
	
	Возврат ТаблицаСправочников.ВыгрузитьКолонку("ИмяСправочника");
	
КонецФункции

&НаСервере
Процедура ИзменитьФлажки(ЗначениеФлажка)
	
	Для Каждого СтрокаТаблицы Из Объект.ТаблицаИзменений Цикл
		
		СтрокаТаблицы.Использовать = ЗначениеФлажка;
		
	КонецЦикла;	
	
КонецПроцедуры	

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	ИзменитьФлажки(Истина);	
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	ИзменитьФлажки(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицу()
	
	Объект.ТаблицаИзменений.Очистить();
	
	Выборка = Справочники[ВидСправочника].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтароеНаименование 	= СокрЛП(Выборка.Наименование);
		НовоеНаименование 	= ПроцедурыРаботыСНормамиСервер.НормализоватьНаименование(СокрЛП(Выборка.Наименование));
		
		Если СтароеНаименование = НовоеНаименование Тогда
			Продолжить;
		КонецЕсли;	
		
		НоваяСтрока 					= Объект.ТаблицаИзменений.Добавить();
		НоваяСтрока.Использовать 		= Истина;
		НоваяСтрока.Ссылка 				= Выборка.Ссылка;
		НоваяСтрока.СтароеНаименование 	= СтароеНаименование;
		НоваяСтрока.НовоеНаименование 	= НовоеНаименование;
		
	КонецЦикла;	
	
КонецПроцедуры	