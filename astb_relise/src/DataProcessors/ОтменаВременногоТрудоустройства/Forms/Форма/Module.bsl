&НаКлиенте
Процедура УстановитьМетки(Команда)
	
	Для Каждого СтрокаТаблицы Из Объект.ТаблицаВременногоТрудоустройства Цикл
		
		СтрокаТаблицы.Использовать = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьМетки(Команда)
	
	Для Каждого СтрокаТаблицы Из Объект.ТаблицаВременногоТрудоустройства Цикл
		
		СтрокаТаблицы.Использовать = Ложь;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.ДатаАнализа = ТекущаяДата();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУвольнениеНаСервере(ОбъектФормы)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаВременногоТрудоустройства.Использовать КАК Использовать,
	|	ТаблицаВременногоТрудоустройства.Сотрудник КАК Сотрудник,
	|	ТаблицаВременногоТрудоустройства.Подразделение КАК Подразделение,
	|	ТаблицаВременногоТрудоустройства.Должность КАК Должность,
	|	ТаблицаВременногоТрудоустройства.РабочееМесто КАК РабочееМесто,
	|	ТаблицаВременногоТрудоустройства.ДатаТрудоустройства КАК ДатаТрудоустройства,
	|	ТаблицаВременногоТрудоустройства.ДатаОкончания КАК ДатаОкончания,
	|	ТаблицаВременногоТрудоустройства.ДокументВременногоТрудоустройства КАК ДокументВременногоТрудоустройства
	|ПОМЕСТИТЬ ВТ_ИсходнаяТаблица
	|ИЗ
	|	&ТаблицаВременногоТрудоустройства КАК ТаблицаВременногоТрудоустройства
	|ГДЕ
	|	ТаблицаВременногоТрудоустройства.Использовать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КадровоеПеремещениеРаботники.Ссылка КАК КадровоеПеремещение,
	|	КадровоеПеремещениеРаботники.Сотрудник КАК Сотрудник,
	|	КадровоеПеремещениеРаботники.ПодразделениеСтарое КАК ПодразделениеСтарое,
	|	КадровоеПеремещениеРаботники.ДолжностьСтарая КАК ДолжностьСтарая,
	|	КадровоеПеремещениеРаботники.РабочееМестоСтарое КАК РабочееМестоСтарое,
	|	КадровоеПеремещениеРаботники.ЗанимаемыхСтавокСтарое КАК ЗанимаемыхСтавокСтарое,
	|	КадровоеПеремещениеРаботники.ЗанимаемыхСтавокНовое КАК ЗанимаемыхСтавокНовое,
	|	КадровоеПеремещениеРаботники.ПодразделениеНовое КАК ПодразделениеНовое,
	|	КадровоеПеремещениеРаботники.ДолжностьНовая КАК ДолжностьНовая,
	|	КадровоеПеремещениеРаботники.РабочееМестоНовое КАК РабочееМестоНовое
	|ПОМЕСТИТЬ ВТ_ДокументыПеремещения
	|ИЗ
	|	Документ.КадровоеПеремещение.Работники КАК КадровоеПеремещениеРаботники
	|ГДЕ
	|	КадровоеПеремещениеРаботники.Ссылка.Организация = &Организация
	|	И КадровоеПеремещениеРаботники.Ссылка.Проведен
	|	И НЕ КадровоеПеремещениеРаботники.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриемНаРаботуРаботники.Ссылка КАК ПриемНаРаботу,
	|	ПриемНаРаботуРаботники.Сотрудник КАК Сотрудник,
	|	ПриемНаРаботуРаботники.ЗанимаемыхСтавок КАК ЗанимаемыхСтавок,
	|	ПриемНаРаботуРаботники.Подразделение КАК Подразделение,
	|	ПриемНаРаботуРаботники.Должность КАК Должность,
	|	ПриемНаРаботуРаботники.РабочееМесто КАК РабочееМесто
	|ПОМЕСТИТЬ ВТ_ДокументыПриема
	|ИЗ
	|	Документ.ПриемНаРаботу.Работники КАК ПриемНаРаботуРаботники
	|ГДЕ
	|	ПриемНаРаботуРаботники.Ссылка.Организация = &Организация
	|	И НЕ ПриемНаРаботуРаботники.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	И ПриемНаРаботуРаботники.Ссылка.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ИсходнаяТаблица.Сотрудник КАК Сотрудник,
	|	ВТ_ИсходнаяТаблица.Подразделение КАК Подразделение,
	|	ВТ_ИсходнаяТаблица.Должность КАК Должность,
	|	ВТ_ИсходнаяТаблица.РабочееМесто КАК РабочееМесто,
	|	ВТ_ДокументыПеремещения.ЗанимаемыхСтавокНовое КАК ЗанимаемыхСтавок
	|ПОМЕСТИТЬ ВТ_РезультатПоПеремещениям
	|ИЗ
	|	ВТ_ИсходнаяТаблица КАК ВТ_ИсходнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДокументыПеремещения КАК ВТ_ДокументыПеремещения
	|		ПО ВТ_ИсходнаяТаблица.ДокументВременногоТрудоустройства = ВТ_ДокументыПеремещения.КадровоеПеремещение
	|			И ВТ_ИсходнаяТаблица.Сотрудник = ВТ_ДокументыПеремещения.Сотрудник
	|			И ВТ_ИсходнаяТаблица.Подразделение = ВТ_ДокументыПеремещения.ПодразделениеНовое
	|			И ВТ_ИсходнаяТаблица.Должность = ВТ_ДокументыПеремещения.ДолжностьНовая
	|			И ВТ_ИсходнаяТаблица.РабочееМесто = ВТ_ДокументыПеремещения.РабочееМестоНовое
	|ГДЕ
	|	НЕ ВТ_ДокументыПеремещения.Сотрудник ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ИсходнаяТаблица.Сотрудник КАК Сотрудник,
	|	ВТ_ИсходнаяТаблица.Подразделение КАК Подразделение,
	|	ВТ_ИсходнаяТаблица.Должность КАК Должность,
	|	ВТ_ИсходнаяТаблица.РабочееМесто КАК РабочееМесто,
	|	ВТ_ДокументыПриема.ЗанимаемыхСтавок КАК ЗанимаемыхСтавок
	|ПОМЕСТИТЬ ВТ_РезультатПоПриему
	|ИЗ
	|	ВТ_ИсходнаяТаблица КАК ВТ_ИсходнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДокументыПриема КАК ВТ_ДокументыПриема
	|		ПО ВТ_ИсходнаяТаблица.Сотрудник = ВТ_ДокументыПриема.Сотрудник
	|			И ВТ_ИсходнаяТаблица.ДокументВременногоТрудоустройства = ВТ_ДокументыПриема.ПриемНаРаботу
	|			И ВТ_ИсходнаяТаблица.Подразделение = ВТ_ДокументыПриема.Подразделение
	|			И ВТ_ИсходнаяТаблица.Должность = ВТ_ДокументыПриема.Должность
	|			И ВТ_ИсходнаяТаблица.РабочееМесто = ВТ_ДокументыПриема.РабочееМесто
	|ГДЕ
	|	НЕ ВТ_ДокументыПриема.Сотрудник ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РезультатПоПеремещениям.Сотрудник КАК Сотрудник,
	|	ВТ_РезультатПоПеремещениям.Подразделение КАК Подразделение,
	|	ВТ_РезультатПоПеремещениям.Должность КАК Должность,
	|	ВТ_РезультатПоПеремещениям.РабочееМесто КАК РабочееМесто,
	|	ВТ_РезультатПоПеремещениям.ЗанимаемыхСтавок КАК ЗанимаемыхСтавок
	|ИЗ
	|	ВТ_РезультатПоПеремещениям КАК ВТ_РезультатПоПеремещениям
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_РезультатПоПриему.Сотрудник,
	|	ВТ_РезультатПоПриему.Подразделение,
	|	ВТ_РезультатПоПриему.Должность,
	|	ВТ_РезультатПоПриему.РабочееМесто,
	|	ВТ_РезультатПоПриему.ЗанимаемыхСтавок
	|ИЗ
	|	ВТ_РезультатПоПриему КАК ВТ_РезультатПоПриему
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник";	
	
	Запрос.УстановитьПараметр("Организация",						Объект.Организация);
	Запрос.УстановитьПараметр("ТаблицаВременногоТрудоустройства",	Объект.ТаблицаВременногоТрудоустройства.Выгрузить());
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() ТОгда
		
		Сообщить("Нет данных для формирования документа.");
		Возврат;
		
	КонецЕсли;
	
	ДокументОбъект = ДанныеФормыВЗначение(ОбъектФормы, Тип("ДокументОбъект.Увольнение"));
	
	ДокументОбъект.Дата 				= ТекущаяДата();
	ДокументОбъект.Комментарий 			= "Сформирован автоматически";
	ДокументОбъект.Организация 			= Объект.Организация;
	ДокументОбъект.Ответственный 		= ПараметрыСеанса.ТекущийПользователь;
	ДокументОбъект.СоздательДокумента 	= ПараметрыСеанса.ТекущийПользователь;
	ДокументОбъект.Работники.Загрузить(Результат.Выгрузить());
	ДокументОбъект.УстановитьНовыйНомер();
	
	ЗначениеВДанныеФормы(ДокументОбъект, ОбъектФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьУвольнение(Команда)
	
	ФормаНовогоДокумента = ПолучитьФорму("Документ.Увольнение.ФормаОбъекта");
	
	ОбъектФормы = ФормаНовогоДокумента.Объект;
	
	ЗаполнитьУвольнениеНаСервере(ОбъектФормы);
	
	КопироватьДанныеФормы(ОбъектФормы, ФормаНовогоДокумента.Объект);

    ФормаНовогоДокумента.Модифицированность = Истина; 
    ФормаНовогоДокумента.Открыть();
	
	Объект.ТаблицаВременногоТрудоустройства.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКадровоеПеремещениеНаСервере(ОбъектФормы)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаВременногоТрудоустройства.Использовать КАК Использовать,
	|	ТаблицаВременногоТрудоустройства.Сотрудник КАК Сотрудник,
	|	ТаблицаВременногоТрудоустройства.Подразделение КАК Подразделение,
	|	ТаблицаВременногоТрудоустройства.Должность КАК Должность,
	|	ТаблицаВременногоТрудоустройства.РабочееМесто КАК РабочееМесто,
	|	ТаблицаВременногоТрудоустройства.ДатаТрудоустройства КАК ДатаТрудоустройства,
	|	ТаблицаВременногоТрудоустройства.ДатаОкончания КАК ДатаОкончания,
	|	ТаблицаВременногоТрудоустройства.ДокументВременногоТрудоустройства КАК ДокументВременногоТрудоустройства
	|ПОМЕСТИТЬ ВТ_ИсходнаяТаблица
	|ИЗ
	|	&ТаблицаВременногоТрудоустройства КАК ТаблицаВременногоТрудоустройства
	|ГДЕ
	|	ТаблицаВременногоТрудоустройства.Использовать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КадровоеПеремещениеРаботники.Ссылка КАК КадровоеПеремещение,
	|	КадровоеПеремещениеРаботники.Сотрудник КАК Сотрудник,
	|	КадровоеПеремещениеРаботники.ПодразделениеСтарое КАК ПодразделениеСтарое,
	|	КадровоеПеремещениеРаботники.ДолжностьСтарая КАК ДолжностьСтарая,
	|	КадровоеПеремещениеРаботники.РабочееМестоСтарое КАК РабочееМестоСтарое,
	|	КадровоеПеремещениеРаботники.ЗанимаемыхСтавокСтарое КАК ЗанимаемыхСтавокСтарое,
	|	КадровоеПеремещениеРаботники.ЗанимаемыхСтавокНовое КАК ЗанимаемыхСтавокНовое,
	|	КадровоеПеремещениеРаботники.ПодразделениеНовое КАК ПодразделениеНовое,
	|	КадровоеПеремещениеРаботники.ДолжностьНовая КАК ДолжностьНовая,
	|	КадровоеПеремещениеРаботники.РабочееМестоНовое КАК РабочееМестоНовое
	|ПОМЕСТИТЬ ВТ_ДокументыПеремещения
	|ИЗ
	|	Документ.КадровоеПеремещение.Работники КАК КадровоеПеремещениеРаботники
	|ГДЕ
	|	КадровоеПеремещениеРаботники.Ссылка.Организация = &Организация
	|	И КадровоеПеремещениеРаботники.Ссылка.Проведен
	|	И НЕ КадровоеПеремещениеРаботники.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриемНаРаботуРаботники.Ссылка КАК ПриемНаРаботу,
	|	ПриемНаРаботуРаботники.Сотрудник КАК Сотрудник,
	|	ПриемНаРаботуРаботники.ЗанимаемыхСтавок КАК ЗанимаемыхСтавок,
	|	ПриемНаРаботуРаботники.Подразделение КАК Подразделение,
	|	ПриемНаРаботуРаботники.Должность КАК Должность,
	|	ПриемНаРаботуРаботники.РабочееМесто КАК РабочееМесто
	|ПОМЕСТИТЬ ВТ_ДокументыПриема
	|ИЗ
	|	Документ.ПриемНаРаботу.Работники КАК ПриемНаРаботуРаботники
	|ГДЕ
	|	ПриемНаРаботуРаботники.Ссылка.Организация = &Организация
	|	И ПриемНаРаботуРаботники.Ссылка.Проведен
	|	И НЕ ПриемНаРаботуРаботники.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ИсходнаяТаблица.Сотрудник КАК Сотрудник,
	|	ВТ_ИсходнаяТаблица.Подразделение КАК ПодразделениеСтарое,
	|	ВТ_ИсходнаяТаблица.Должность КАК ДолжностьСтарая,
	|	ВТ_ИсходнаяТаблица.РабочееМесто КАК РабочееМестоСтарое,
	|	ВТ_ДокументыПеремещения.ЗанимаемыхСтавокНовое КАК ЗанимаемыхСтавокСтарое,
	|	ВТ_ДокументыПеремещения.ПодразделениеСтарое КАК ПодразделениеНовое,
	|	ВТ_ДокументыПеремещения.ДолжностьСтарая КАК ДолжностьНовая,
	|	ВТ_ДокументыПеремещения.РабочееМестоСтарое КАК РабочееМестоНовое,
	|	ВТ_ДокументыПеремещения.ЗанимаемыхСтавокСтарое КАК ЗанимаемыхСтавокНовое
	|ПОМЕСТИТЬ ВТ_РезультатПоПеремещениям
	|ИЗ
	|	ВТ_ИсходнаяТаблица КАК ВТ_ИсходнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДокументыПеремещения КАК ВТ_ДокументыПеремещения
	|		ПО ВТ_ИсходнаяТаблица.ДокументВременногоТрудоустройства = ВТ_ДокументыПеремещения.КадровоеПеремещение
	|			И ВТ_ИсходнаяТаблица.Сотрудник = ВТ_ДокументыПеремещения.Сотрудник
	|			И ВТ_ИсходнаяТаблица.Подразделение = ВТ_ДокументыПеремещения.ПодразделениеНовое
	|			И ВТ_ИсходнаяТаблица.Должность = ВТ_ДокументыПеремещения.ДолжностьНовая
	|			И ВТ_ИсходнаяТаблица.РабочееМесто = ВТ_ДокументыПеремещения.РабочееМестоНовое
	|ГДЕ
	|	НЕ ВТ_ДокументыПеремещения.Сотрудник ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ИсходнаяТаблица.Сотрудник КАК Сотрудник,
	|	ВТ_ИсходнаяТаблица.Подразделение КАК ПодразделениеСтарое,
	|	ВТ_ИсходнаяТаблица.Должность КАК ДолжностьСтарая,
	|	ВТ_ИсходнаяТаблица.РабочееМесто КАК РабочееМестоСтарое,
	|	ВТ_ДокументыПриема.ЗанимаемыхСтавок КАК ЗанимаемыхСтавокСтарое,
	|	ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка) КАК ПодразделениеНовое,
	|	ЗНАЧЕНИЕ(Справочник.ДолжностиИПрофессии.ПустаяСсылка) КАК ДолжностьНовая,
	|	ЗНАЧЕНИЕ(Справочник.РабочиеМестаАСТБ.ПустаяСсылка) КАК РабочееМестоНовое,
	|	1 КАК ЗанимаемыхСтавокНовое
	|ПОМЕСТИТЬ ВТ_РезультатПоПриему
	|ИЗ
	|	ВТ_ИсходнаяТаблица КАК ВТ_ИсходнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДокументыПриема КАК ВТ_ДокументыПриема
	|		ПО ВТ_ИсходнаяТаблица.Сотрудник = ВТ_ДокументыПриема.Сотрудник
	|			И ВТ_ИсходнаяТаблица.ДокументВременногоТрудоустройства = ВТ_ДокументыПриема.ПриемНаРаботу
	|			И ВТ_ИсходнаяТаблица.Подразделение = ВТ_ДокументыПриема.Подразделение
	|			И ВТ_ИсходнаяТаблица.Должность = ВТ_ДокументыПриема.Должность
	|			И ВТ_ИсходнаяТаблица.РабочееМесто = ВТ_ДокументыПриема.РабочееМесто
	|ГДЕ
	|	НЕ ВТ_ДокументыПриема.Сотрудник ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РезультатПоПеремещениям.Сотрудник КАК Сотрудник,
	|	ВТ_РезультатПоПеремещениям.ПодразделениеСтарое КАК ПодразделениеСтарое,
	|	ВТ_РезультатПоПеремещениям.ДолжностьСтарая КАК ДолжностьСтарая,
	|	ВТ_РезультатПоПеремещениям.РабочееМестоСтарое КАК РабочееМестоСтарое,
	|	ВТ_РезультатПоПеремещениям.ЗанимаемыхСтавокСтарое КАК ЗанимаемыхСтавокСтарое,
	|	ВТ_РезультатПоПеремещениям.ПодразделениеНовое КАК ПодразделениеНовое,
	|	ВТ_РезультатПоПеремещениям.ДолжностьНовая КАК ДолжностьНовая,
	|	ВТ_РезультатПоПеремещениям.РабочееМестоНовое КАК РабочееМестоНовое,
	|	ВТ_РезультатПоПеремещениям.ЗанимаемыхСтавокНовое КАК ЗанимаемыхСтавокНовое
	|ИЗ
	|	ВТ_РезультатПоПеремещениям КАК ВТ_РезультатПоПеремещениям
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_РезультатПоПриему.Сотрудник,
	|	ВТ_РезультатПоПриему.ПодразделениеСтарое,
	|	ВТ_РезультатПоПриему.ДолжностьСтарая,
	|	ВТ_РезультатПоПриему.РабочееМестоСтарое,
	|	ВТ_РезультатПоПриему.ЗанимаемыхСтавокСтарое,
	|	ВТ_РезультатПоПриему.ПодразделениеНовое,
	|	ВТ_РезультатПоПриему.ДолжностьНовая,
	|	ВТ_РезультатПоПриему.РабочееМестоНовое,
	|	ВТ_РезультатПоПриему.ЗанимаемыхСтавокНовое
	|ИЗ
	|	ВТ_РезультатПоПриему КАК ВТ_РезультатПоПриему
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник";
	
	Запрос.УстановитьПараметр("Организация",						Объект.Организация);
	Запрос.УстановитьПараметр("ТаблицаВременногоТрудоустройства",	Объект.ТаблицаВременногоТрудоустройства.Выгрузить());
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() ТОгда
		
		Сообщить("Нет данных для формирования документа.");
		Возврат;
		
	КонецЕсли;
	
	ДокументОбъект = ДанныеФормыВЗначение(ОбъектФормы, Тип("ДокументОбъект.КадровоеПеремещение"));
	
	ДокументОбъект.Дата 				= ТекущаяДата();
	ДокументОбъект.Комментарий 			= "Сформирован автоматически";
	ДокументОбъект.Организация 			= Объект.Организация;
	ДокументОбъект.Ответственный 		= ПараметрыСеанса.ТекущийПользователь;
	ДокументОбъект.СоздательДокумента 	= ПараметрыСеанса.ТекущийПользователь;
	ДокументОбъект.Работники.Загрузить(Результат.Выгрузить());
	ДокументОбъект.УстановитьНовыйНомер();
	
	ЗначениеВДанныеФормы(ДокументОбъект, ОбъектФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКадровоеПеремещение(Команда)
	
	ФормаНовогоДокумента = ПолучитьФорму("Документ.КадровоеПеремещение.ФормаОбъекта");
	
	ОбъектФормы = ФормаНовогоДокумента.Объект;
	
	ЗаполнитьКадровоеПеремещениеНаСервере(ОбъектФормы);
	
	КопироватьДанныеФормы(ОбъектФормы, ФормаНовогоДокумента.Объект);

    ФормаНовогоДокумента.Модифицированность = Истина; 
    ФормаНовогоДокумента.Открыть();
	
	Объект.ТаблицаВременногоТрудоустройства.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗанятыеРабочиеМестаОстатки.Сотрудник,
	|	ЗанятыеРабочиеМестаОстатки.Подразделение,
	|	ЗанятыеРабочиеМестаОстатки.Должность,
	|	ЗанятыеРабочиеМестаОстатки.РабочееМесто
	|ПОМЕСТИТЬ ВТ_ЗанятыеРабочиеМеста
	|ИЗ
	|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(, Организация = &Организация) КАК ЗанятыеРабочиеМестаОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Регистратор,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Сотрудник,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Подразделение,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Должность,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.РабочееМесто,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ОсновноеМестоРаботы
	|ИЗ
	|	РегистрСведений.ОсновноеМестоРаботыСотрудника.СрезПоследних(, Организация = &Организация) КАК ОсновноеМестоРаботыСотрудникаСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ЗанятыеРабочиеМеста.Сотрудник,
	|	ВТ_ЗанятыеРабочиеМеста.Подразделение,
	|	ВТ_ЗанятыеРабочиеМеста.Должность,
	|	ВТ_ЗанятыеРабочиеМеста.РабочееМесто,
	|	ВТ_ОсновноеМестоРаботы.Регистратор.Дата КАК ДатаТрудоустройства,
	|	ВТ_ОсновноеМестоРаботы.Регистратор КАК ДокументВременногоТрудоустройства,
	|	ВТ_ОсновноеМестоРаботы.ДатаОкончания КАК ДатаОкончания
	|ИЗ
	|	ВТ_ЗанятыеРабочиеМеста КАК ВТ_ЗанятыеРабочиеМеста
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОсновноеМестоРаботы КАК ВТ_ОсновноеМестоРаботы
	|		ПО ВТ_ЗанятыеРабочиеМеста.Сотрудник = ВТ_ОсновноеМестоРаботы.Сотрудник
	|			И ВТ_ЗанятыеРабочиеМеста.Подразделение = ВТ_ОсновноеМестоРаботы.Подразделение
	|			И ВТ_ЗанятыеРабочиеМеста.Должность = ВТ_ОсновноеМестоРаботы.Должность
	|			И ВТ_ЗанятыеРабочиеМеста.РабочееМесто = ВТ_ОсновноеМестоРаботы.РабочееМесто
	|ГДЕ
	|	ВТ_ОсновноеМестоРаботы.ДатаОкончания < &ДатаАнализа
	|	И НЕ ВТ_ОсновноеМестоРаботы.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_ЗанятыеРабочиеМеста.Сотрудник.Наименование,
	|	ВТ_ЗанятыеРабочиеМеста.Подразделение.Наименование,
	|	ВТ_ЗанятыеРабочиеМеста.Должность.Наименование,
	|	ВТ_ЗанятыеРабочиеМеста.РабочееМесто.Наименование,
	|	ДатаТрудоустройства,
	|	ДатаОкончания,
	|	ДокументВременногоТрудоустройства";
	
	Запрос.УстановитьПараметр("Организация",Объект.Организация);
	Запрос.УстановитьПараметр("ДатаАнализа",Объект.ДатаАнализа);
	
	Объект.ТаблицаВременногоТрудоустройства.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТаблицу(Команда)
	
	ЗаполнитьТаблицуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаАнализаПриИзменении(Элемент)
	
	ЗаполнитьТаблицуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗаполнитьТаблицуНаСервере();
	
КонецПроцедуры
