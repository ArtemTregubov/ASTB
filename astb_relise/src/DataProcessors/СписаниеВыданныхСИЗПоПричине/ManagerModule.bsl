#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ЗаполнитьСписокСотрудников(ТекущийОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаНормВыдачи.НормаВыдачи КАК НормаВыдачи,
	|	ТаблицаНормВыдачи.Использовать КАК Использовать
	|ПОМЕСТИТЬ ВТ_ТаблицаНормВыдачи
	|ИЗ
	|	&ТаблицаНормВыдачи КАК ТаблицаНормВыдачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатурыНорм.НоменклатураНорм КАК НоменклатураНорм,
	|	ТаблицаНоменклатурыНорм.Использовать КАК Использовать
	|ПОМЕСТИТЬ ВТ_ТаблицаНоменклатурыНорм
	|ИЗ
	|	&ТаблицаНоменклатурыНорм КАК ТаблицаНоменклатурыНорм
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ТаблицаНоменклатуры.Использовать КАК Использовать
	|ПОМЕСТИТЬ ВТ_ТаблицаНоменклатуры
	|ИЗ
	|	&ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|ГДЕ
	|	ТаблицаНоменклатуры.Использовать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаНормВыдачи.НормаВыдачи КАК НормаВыдачи
	|ИЗ
	|	ВТ_ТаблицаНормВыдачи КАК ВТ_ТаблицаНормВыдачи
	|ГДЕ
	|	ВТ_ТаблицаНормВыдачи.Использовать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаНоменклатурыНорм.НоменклатураНорм КАК НоменклатураНорм,
	|	ВТ_ТаблицаНоменклатурыНорм.Использовать КАК Использовать
	|ИЗ
	|	ВТ_ТаблицаНоменклатурыНорм КАК ВТ_ТаблицаНоменклатурыНорм
	|ГДЕ
	|	ВТ_ТаблицаНоменклатурыНорм.Использовать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицаНоменклатуры.Использовать КАК Использовать
	|ИЗ
	|	ВТ_ТаблицаНоменклатуры КАК ВТ_ТаблицаНоменклатуры
	|ГДЕ
	|	ВТ_ТаблицаНоменклатуры.Использовать";
	
	Запрос.УстановитьПараметр("ТаблицаНормВыдачи",		ТекущийОбъект.ТаблицаНормВыдачи.Выгрузить());
	Запрос.УстановитьПараметр("ТаблицаНоменклатурыНорм",ТекущийОбъект.ТаблицаНоменклатурыНорм.Выгрузить());
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры",	ТекущийОбъект.ТаблицаНоменклатуры.Выгрузить());
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТабНормВыдачи 		= Результат[3].Выгрузить();
	ТабНоменклатурыНорм = Результат[4].Выгрузить();
	ТабНоменклатуры 	= Результат[5].Выгрузить();
	
	МассивНормВыдачи 		= ТабНормВыдачи.ВыгрузитьКолонку("НормаВыдачи");
	МассивНоменклатурыНорм 	= ТабНоменклатурыНорм.ВыгрузитьКолонку("НоменклатураНорм");
	МассивНоменклатуры 		= ТабНоменклатуры.ВыгрузитьКолонку("Номенклатура");
	
	Если ТекущийОбъект.ПричинаСписания = Перечисления.ПричиныСписания.ОкончаниеСрокаНоски Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВыданныеСредстваЗащитыОстатки.Сотрудник КАК Сотрудник,
		|	ВыданныеСредстваЗащитыОстатки.НормаВыдачи КАК НормаВыдачи,
		|	ВыданныеСредстваЗащитыОстатки.НоменклатураНормы КАК НоменклатураНормы,
		|	ВыданныеСредстваЗащитыОстатки.Номенклатура КАК Номенклатура,
		|	ВыданныеСредстваЗащитыОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВыданныеСредстваЗащитыОстатки.ДатаВыдачи КАК ДатаВыдачи,
		|	СУММА(ВыданныеСредстваЗащитыОстатки.КоличествоОстаток) КАК КоличествоОстаток
		|ПОМЕСТИТЬ ВТ_ВыданныеСИЗ
		|ИЗ
		|	РегистрНакопления.ВыданныеСредстваЗащиты.Остатки(
		|			&ПериодРасчета,
		|			НЕ НормаВыдачи = ЗНАЧЕНИЕ(Справочник.НормыВыдачиСИЗ.ПустаяСсылка)
		|				И Организация = &Организация
		|				И НормаВыдачи.ВидВыдачиСИЗ = &ВидВыдачиСИЗ
		|				И НормаВыдачи В (&МассивНормВыдачи)
		|				И НоменклатураНормы В (&МассивНоменклатурыНорм)
		|				И Номенклатура В (&МассивНоменклатуры)) КАК ВыданныеСредстваЗащитыОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ВыданныеСредстваЗащитыОстатки.Сотрудник,
		|	ВыданныеСредстваЗащитыОстатки.НормаВыдачи,
		|	ВыданныеСредстваЗащитыОстатки.НоменклатураНормы,
		|	ВыданныеСредстваЗащитыОстатки.Номенклатура,
		|	ВыданныеСредстваЗащитыОстатки.ХарактеристикаНоменклатуры,
		|	ВыданныеСредстваЗащитыОстатки.ДатаВыдачи
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НормыВыдачиСИЗСоставНормы.Ссылка КАК Ссылка,
		|	НормыВыдачиСИЗСоставНормы.НоменклатураНормы КАК НоменклатураНормы,
		|	НормыВыдачиСИЗСоставНормы.ПериодичностьВыдачи.ТипПериода КАК ПериодичностьВыдачиТипПериода,
		|	НормыВыдачиСИЗСоставНормы.ПериодичностьВыдачи.КоличествоПериодов КАК ПериодичностьВыдачиКоличествоПериодов
		|ПОМЕСТИТЬ ВТ_СоставНормыВыдачи
		|ИЗ
		|	Справочник.НормыВыдачиСИЗ.СоставНормы КАК НормыВыдачиСИЗСоставНормы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВыдачаПоПотребностиОстатки.Сотрудник КАК Сотрудник,
		|	ВыдачаПоПотребностиОстатки.НормаВыдачи КАК НормаВыдачи,
		|	ВыдачаПоПотребностиОстатки.НоменклатураНормы КАК НоменклатураНормы,
		|	ВыдачаПоПотребностиОстатки.ДатаПотребности КАК ДатаПотребности,
		|	ВыдачаПоПотребностиОстатки.ДатаВыдачи КАК ДатаВыдачи
		|ПОМЕСТИТЬ ВТ_ВыдачаПоПотребности
		|ИЗ
		|	РегистрНакопления.ВыдачаПоПотребности.Остатки(
		|			&ПериодРасчета,
		|			НЕ НормаВыдачи = ЗНАЧЕНИЕ(Справочник.НормыВыдачиСИЗ.ПустаяСсылка)
		|				И Организация = &Организация
		|				И НормаВыдачи.ВидВыдачиСИЗ = &ВидВыдачиСИЗ) КАК ВыдачаПоПотребностиОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПотребностьВыдачиСИЗОстатки.Сотрудник КАК Сотрудник,
		|	ПотребностьВыдачиСИЗОстатки.НоменклатураНормы КАК НоменклатураНормы
		|ПОМЕСТИТЬ ВТ_Потребность
		|ИЗ
		|	РегистрНакопления.ПотребностьВыдачиСИЗ.Остатки(
		|			&ПериодРасчета,
		|			Организация = &Организация
		|				И НормаВыдачи.ВидВыдачиСИЗ = &ВидВыдачиСИЗ) КАК ПотребностьВыдачиСИЗОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВТ_ВыданныеСИЗ.Сотрудник КАК Сотрудник,
		|	ВТ_ВыданныеСИЗ.НормаВыдачи КАК НормаВыдачи,
		|	ВТ_ВыданныеСИЗ.НоменклатураНормы КАК НоменклатураНормы,
		|	ВТ_ВыданныеСИЗ.Номенклатура КАК Номенклатура,
		|	ВТ_ВыданныеСИЗ.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВЫБОР
		|		КОГДА ВТ_ВыдачаПоПотребности.ДатаПотребности ЕСТЬ NULL
		|				ИЛИ ВТ_ВыдачаПоПотребности.ДатаПотребности = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА ВЫБОР
		|					КОГДА ВТ_ВыдачаПоПотребности1.ДатаПотребности ЕСТЬ NULL
		|							ИЛИ ВТ_ВыдачаПоПотребности1.ДатаПотребности = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|						ТОГДА ВЫБОР
		|								КОГДА ВТ_СоставНормыВыдачи.ПериодичностьВыдачиТипПериода = ЗНАЧЕНИЕ(Перечисление.ДоступныеПериодыОтчета.Год)
		|									ТОГДА ДОБАВИТЬКДАТЕ(ВТ_ВыданныеСИЗ.ДатаВыдачи, МЕСЯЦ, 12 * ВТ_СоставНормыВыдачи.ПериодичностьВыдачиКоличествоПериодов)
		|								ИНАЧЕ ДОБАВИТЬКДАТЕ(ВТ_ВыданныеСИЗ.ДатаВыдачи, МЕСЯЦ, ВТ_СоставНормыВыдачи.ПериодичностьВыдачиКоличествоПериодов)
		|							КОНЕЦ
		|					ИНАЧЕ ВТ_ВыдачаПоПотребности1.ДатаПотребности
		|				КОНЕЦ
		|		ИНАЧЕ ВТ_ВыдачаПоПотребности.ДатаПотребности
		|	КОНЕЦ КАК ДатаСледующейВыдачи
		|ПОМЕСТИТЬ ВТ_Результат
		|ИЗ
		|	ВТ_ВыданныеСИЗ КАК ВТ_ВыданныеСИЗ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СоставНормыВыдачи КАК ВТ_СоставНормыВыдачи
		|		ПО ВТ_ВыданныеСИЗ.НормаВыдачи = ВТ_СоставНормыВыдачи.Ссылка
		|			И ВТ_ВыданныеСИЗ.НоменклатураНормы = ВТ_СоставНормыВыдачи.НоменклатураНормы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Потребность КАК ВТ_Потребность
		|		ПО ВТ_ВыданныеСИЗ.Сотрудник = ВТ_Потребность.Сотрудник
		|			И ВТ_ВыданныеСИЗ.НоменклатураНормы = ВТ_Потребность.НоменклатураНормы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыдачаПоПотребности КАК ВТ_ВыдачаПоПотребности
		|		ПО ВТ_ВыданныеСИЗ.Сотрудник = ВТ_ВыдачаПоПотребности.Сотрудник
		|			И ВТ_ВыданныеСИЗ.НормаВыдачи = ВТ_ВыдачаПоПотребности.НормаВыдачи
		|			И ВТ_ВыданныеСИЗ.НоменклатураНормы = ВТ_ВыдачаПоПотребности.НоменклатураНормы
		|			И ВТ_ВыданныеСИЗ.ДатаВыдачи = ВТ_ВыдачаПоПотребности.ДатаВыдачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыдачаПоПотребности КАК ВТ_ВыдачаПоПотребности1
		|		ПО ВТ_ВыданныеСИЗ.Сотрудник = ВТ_ВыдачаПоПотребности1.Сотрудник
		|			И ВТ_ВыданныеСИЗ.НоменклатураНормы = ВТ_ВыдачаПоПотребности1.НоменклатураНормы
		|			И ВТ_ВыданныеСИЗ.ДатаВыдачи = ВТ_ВыдачаПоПотребности1.ДатаВыдачи
		|ГДЕ
		|	НЕ ВТ_Потребность.НоменклатураНормы ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗанятыеРабочиеМестаОстатки.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ВТ_ЗРМ
		|ИЗ
		|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
		|			&ПериодРасчета,
		|			Организация = &Организация
		|				И (Подразделение = &Подразделение
		|					ИЛИ &Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка))
		|				И (Должность = &Должность
		|					ИЛИ &Должность = ЗНАЧЕНИЕ(Справочник.ДолжностиИПрофессии.ПустаяСсылка))
		|				И (РабочееМесто = &РабочееМесто
		|					ИЛИ &РабочееМесто = ЗНАЧЕНИЕ(Справочник.РабочиеМестаАСТБ.ПустаяСсылка))) КАК ЗанятыеРабочиеМестаОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИСТИНА КАК Использовать,
		|	ВТ_Результат.Сотрудник КАК Сотрудник
		|ИЗ
		|	ВТ_Результат КАК ВТ_Результат
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗРМ КАК ВТ_ЗРМ
		|		ПО ВТ_Результат.Сотрудник = ВТ_ЗРМ.Сотрудник
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(ВТ_Результат.ДатаСледующейВыдачи, МЕСЯЦ) <= НАЧАЛОПЕРИОДА(&ПериодРасчета, МЕСЯЦ)
		|	И НЕ ВТ_ЗРМ.Сотрудник ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВТ_Результат.Сотрудник.Наименование";
		
		Запрос.УстановитьПараметр("ПериодРасчета",			ТекущийОбъект.ДатаАнализа);
		Запрос.УстановитьПараметр("Организация",			ТекущийОбъект.Организация);
		Запрос.УстановитьПараметр("Подразделение",			ТекущийОбъект.Подразделение);
		Запрос.УстановитьПараметр("Должность",				ТекущийОбъект.Должность);
		Запрос.УстановитьПараметр("РабочееМесто",			ТекущийОбъект.РабочееМесто);
		Запрос.УстановитьПараметр("МассивНормВыдачи",		МассивНормВыдачи);
		Запрос.УстановитьПараметр("МассивНоменклатурыНорм",	МассивНоменклатурыНорм);
		Запрос.УстановитьПараметр("МассивНоменклатуры",		МассивНоменклатуры);
		Запрос.УстановитьПараметр("ВидВыдачиСИЗ",			ТекущийОбъект.ВидВыдачиСИЗ);
		
	ИначеЕсли ТекущийОбъект.ПричинаСписания = Перечисления.ПричиныСписания.ОтменаНорм Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗанятыеРабочиеМестаОстатки.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ВТ_ЗРМ
		|ИЗ
		|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
		|			&ПериодРасчета,
		|			Организация = &Организация
		|				И (Подразделение = &Подразделение
		|					ИЛИ &Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка))
		|				И (Должность = &Должность
		|					ИЛИ &Должность = ЗНАЧЕНИЕ(Справочник.ДолжностиИПрофессии.ПустаяСсылка))
		|				И (РабочееМесто = &РабочееМесто
		|					ИЛИ &РабочееМесто = ЗНАЧЕНИЕ(Справочник.РабочиеМестаАСТБ.ПустаяСсылка))) КАК ЗанятыеРабочиеМестаОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВыданныеСредстваЗащитыОстатки.Сотрудник КАК Сотрудник,
		|	ИСТИНА КАК Использовать
		|ИЗ
		|	РегистрНакопления.ВыданныеСредстваЗащиты.Остатки(
		|			&ПериодРасчета,
		|			Организация = &Организация
		|				И НормаВыдачи.ВидВыдачиСИЗ = &ВидВыдачиСИЗ
		|				И НормаВыдачи В (&МассивНормВыдачи)
		|				И НоменклатураНормы В (&МассивНоменклатурыНорм)
		|				И Номенклатура В (&МассивНоменклатуры)) КАК ВыданныеСредстваЗащитыОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПотребностьВыдачиСИЗ.Остатки(
		|				&ПериодРасчета,
		|				Организация = &Организация
		|					И НормаВыдачи.ВидВыдачиСИЗ = &ВидВыдачиСИЗ
		|					И НормаВыдачи В (&МассивНормВыдачи)
		|					И НоменклатураНормы В (&МассивНоменклатурыНорм)) КАК ПотребностьВыдачиСИЗОстатки
		|		ПО ВыданныеСредстваЗащитыОстатки.Сотрудник = ПотребностьВыдачиСИЗОстатки.Сотрудник
		|			И ВыданныеСредстваЗащитыОстатки.НоменклатураНормы = ПотребностьВыдачиСИЗОстатки.НоменклатураНормы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗРМ КАК ВТ_ЗРМ
		|		ПО ВыданныеСредстваЗащитыОстатки.Сотрудник = ВТ_ЗРМ.Сотрудник
		|ГДЕ
		|	ПотребностьВыдачиСИЗОстатки.ДатаПотребности ЕСТЬ NULL
		|	И НЕ ВТ_ЗРМ.Сотрудник ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВыданныеСредстваЗащитыОстатки.Сотрудник.Наименование";
		
		Запрос.УстановитьПараметр("ПериодРасчета",			ТекущийОбъект.ДатаАнализа);
		Запрос.УстановитьПараметр("Организация",			ТекущийОбъект.Организация);
		Запрос.УстановитьПараметр("Подразделение",			ТекущийОбъект.Подразделение);
		Запрос.УстановитьПараметр("Должность",				ТекущийОбъект.Должность);
		Запрос.УстановитьПараметр("РабочееМесто",			ТекущийОбъект.РабочееМесто);
		Запрос.УстановитьПараметр("МассивНормВыдачи",		МассивНормВыдачи);
		Запрос.УстановитьПараметр("МассивНоменклатурыНорм",	МассивНоменклатурыНорм);
		Запрос.УстановитьПараметр("МассивНоменклатуры",		МассивНоменклатуры);
		Запрос.УстановитьПараметр("ВидВыдачиСИЗ",			ТекущийОбъект.ВидВыдачиСИЗ);
		
	ИначеЕсли ТекущийОбъект.ПричинаСписания = Перечисления.ПричиныСписания.Увольнение Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВыданныеСредстваЗащитыОстатки.Сотрудник КАК Сотрудник,
		|	ИСТИНА КАК Использовать
		|ИЗ
		|	РегистрНакопления.ВыданныеСредстваЗащиты.Остатки(
		|			&ПериодРасчета,
		|			Организация = &Организация
		|				И ВЫБОР
		|					КОГДА &ВидВыдачиСИЗ = ЗНАЧЕНИЕ(Перечисление.ВидыВыдачиСИЗ.ПроизвольнаяВыдача)
		|						ТОГДА НормаВыдачи = ЗНАЧЕНИЕ(Справочник.НормыВыдачиСИЗ.ПустаяСсылка)
		|					ИНАЧЕ НормаВыдачи.ВидВыдачиСИЗ = &ВидВыдачиСИЗ
		|							И НормаВыдачи В (&МассивНормВыдачи)
		|							И НоменклатураНормы В (&МассивНоменклатурыНорм)
		|				КОНЕЦ
		|				И Номенклатура В (&МассивНоменклатуры)) КАК ВыданныеСредстваЗащитыОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(&ПериодРасчета, Организация = &Организация) КАК ЗанятыеРабочиеМестаОстатки
		|		ПО ВыданныеСредстваЗащитыОстатки.Сотрудник = ЗанятыеРабочиеМестаОстатки.Сотрудник
		|ГДЕ
		|	ЗанятыеРабочиеМестаОстатки.Подразделение ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВыданныеСредстваЗащитыОстатки.Сотрудник.Наименование";		
		
		Запрос.УстановитьПараметр("ПериодРасчета",			ТекущийОбъект.ДатаАнализа);
		Запрос.УстановитьПараметр("Организация",			ТекущийОбъект.Организация);
		Запрос.УстановитьПараметр("МассивНормВыдачи",		МассивНормВыдачи);
		Запрос.УстановитьПараметр("МассивНоменклатурыНорм",	МассивНоменклатурыНорм);
		Запрос.УстановитьПараметр("МассивНоменклатуры",		МассивНоменклатуры);
		Запрос.УстановитьПараметр("ВидВыдачиСИЗ",			ТекущийОбъект.ВидВыдачиСИЗ);
		
	ИначеЕсли ТекущийОбъект.ПричинаСписания = Перечисления.ПричиныСписания.ТехническоеСписание Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗанятыеРабочиеМестаОстатки.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ВТ_ЗРМ
		|ИЗ
		|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
		|			&ПериодРасчета,
		|			Организация = &Организация
		|				И (Подразделение = &Подразделение
		|					ИЛИ &Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка))
		|				И (Должность = &Должность
		|					ИЛИ &Должность = ЗНАЧЕНИЕ(Справочник.ДолжностиИПрофессии.ПустаяСсылка))
		|				И (РабочееМесто = &РабочееМесто
		|					ИЛИ &РабочееМесто = ЗНАЧЕНИЕ(Справочник.РабочиеМестаАСТБ.ПустаяСсылка))) КАК ЗанятыеРабочиеМестаОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВыданныеСредстваЗащитыОстатки.Сотрудник КАК Сотрудник,
		|	ИСТИНА КАК Использовать
		|ИЗ
		|	РегистрНакопления.ВыданныеСредстваЗащиты.Остатки(
		|			&ПериодРасчета,
		|			Организация = &Организация
		|				И ВЫБОР
		|					КОГДА &ВидВыдачиСИЗ = ЗНАЧЕНИЕ(Перечисление.ВидыВыдачиСИЗ.ПроизвольнаяВыдача)
		|						ТОГДА НормаВыдачи = ЗНАЧЕНИЕ(Справочник.НормыВыдачиСИЗ.ПустаяСсылка)
		|					ИНАЧЕ НормаВыдачи.ВидВыдачиСИЗ = &ВидВыдачиСИЗ
		|							И НормаВыдачи В (&МассивНормВыдачи)
		|							И НоменклатураНормы В (&МассивНоменклатурыНорм)
		|				КОНЕЦ
		|				И Номенклатура В (&МассивНоменклатуры)) КАК ВыданныеСредстваЗащитыОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗРМ КАК ВТ_ЗРМ
		|		ПО ВыданныеСредстваЗащитыОстатки.Сотрудник = ВТ_ЗРМ.Сотрудник
		|ГДЕ
		|	НЕ ВТ_ЗРМ.Сотрудник ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВыданныеСредстваЗащитыОстатки.Сотрудник.Наименование";
		
		Запрос.УстановитьПараметр("ПериодРасчета",			ТекущийОбъект.ДатаАнализа);
		Запрос.УстановитьПараметр("Организация",			ТекущийОбъект.Организация);
		Запрос.УстановитьПараметр("Подразделение",			ТекущийОбъект.Подразделение);
		Запрос.УстановитьПараметр("Должность",				ТекущийОбъект.Должность);
		Запрос.УстановитьПараметр("РабочееМесто",			ТекущийОбъект.РабочееМесто);
		Запрос.УстановитьПараметр("МассивНормВыдачи",		МассивНормВыдачи);
		Запрос.УстановитьПараметр("МассивНоменклатурыНорм",	МассивНоменклатурыНорм);
		Запрос.УстановитьПараметр("МассивНоменклатуры",		МассивНоменклатуры);
		Запрос.УстановитьПараметр("ВидВыдачиСИЗ",			ТекущийОбъект.ВидВыдачиСИЗ);
		
	КонецЕсли;
	
	ТекущийОбъект.СписокСотрудников.Загрузить(Запрос.Выполнить().Выгрузить()); 
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуНормВыдачи(ТекущийОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИСТИНА КАК Использовать,
	|	НормыВыдачиСИЗ.Ссылка КАК НормаВыдачи
	|ИЗ
	|	Справочник.НормыВыдачиСИЗ КАК НормыВыдачиСИЗ
	|ГДЕ
	|	НормыВыдачиСИЗ.Владелец = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	НормыВыдачиСИЗ.Наименование";
	
	Запрос.УстановитьПараметр("Организация",ТекущийОбъект.Организация);
	
	ТекущийОбъект.ТаблицаНормВыдачи.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуНоменклатурыНорм(ТекущийОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИСТИНА КАК Использовать,
	|	НоменклатураНормОрганизации.Ссылка КАК НоменклатураНорм
	|ИЗ
	|	Справочник.НоменклатураНормОрганизации КАК НоменклатураНормОрганизации
	|ГДЕ
	|	НоменклатураНормОрганизации.Владелец = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	НоменклатураНормОрганизации.Наименование";
	
	Запрос.УстановитьПараметр("Организация",ТекущийОбъект.Организация);
	
	ТекущийОбъект.ТаблицаНоменклатурыНорм.Загрузить(Запрос.Выполнить().Выгрузить());	
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуНоменклатуры(ТекущийОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИСТИНА КАК Использовать,
	|	Номенклатура.Ссылка КАК Номенклатура
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	НЕ Номенклатура.ЭтоГруппа
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура.Наименование";
	
	ТекущийОбъект.ТаблицаНоменклатуры.Загрузить(Запрос.Выполнить().Выгрузить());	
	
КонецПроцедуры

Процедура СформироватьДокументыСписанияСИЗ(ТекущийОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаНормВыдачи.НормаВыдачи КАК НормаВыдачи,
	|	ТаблицаНормВыдачи.Использовать КАК Использовать
	|ПОМЕСТИТЬ ВТ_ТаблицаНормВыдачи
	|ИЗ
	|	&ТаблицаНормВыдачи КАК ТаблицаНормВыдачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатурыНорм.НоменклатураНорм КАК НоменклатураНорм,
	|	ТаблицаНоменклатурыНорм.Использовать КАК Использовать
	|ПОМЕСТИТЬ ВТ_ТаблицаНоменклатурыНорм
	|ИЗ
	|	&ТаблицаНоменклатурыНорм КАК ТаблицаНоменклатурыНорм
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ТаблицаНоменклатуры.Использовать КАК Использовать
	|ПОМЕСТИТЬ ВТ_ТаблицаНоменклатуры
	|ИЗ
	|	&ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|ГДЕ
	|	ТаблицаНоменклатуры.Использовать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаНормВыдачи.НормаВыдачи КАК НормаВыдачи
	|ИЗ
	|	ВТ_ТаблицаНормВыдачи КАК ВТ_ТаблицаНормВыдачи
	|ГДЕ
	|	ВТ_ТаблицаНормВыдачи.Использовать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаНоменклатурыНорм.НоменклатураНорм КАК НоменклатураНорм,
	|	ВТ_ТаблицаНоменклатурыНорм.Использовать КАК Использовать
	|ИЗ
	|	ВТ_ТаблицаНоменклатурыНорм КАК ВТ_ТаблицаНоменклатурыНорм
	|ГДЕ
	|	ВТ_ТаблицаНоменклатурыНорм.Использовать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицаНоменклатуры.Использовать КАК Использовать
	|ИЗ
	|	ВТ_ТаблицаНоменклатуры КАК ВТ_ТаблицаНоменклатуры
	|ГДЕ
	|	ВТ_ТаблицаНоменклатуры.Использовать";
	
	Запрос.УстановитьПараметр("ТаблицаНормВыдачи",		ТекущийОбъект.ТаблицаНормВыдачи.Выгрузить());
	Запрос.УстановитьПараметр("ТаблицаНоменклатурыНорм",ТекущийОбъект.ТаблицаНоменклатурыНорм.Выгрузить());
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры",	ТекущийОбъект.ТаблицаНоменклатуры.Выгрузить());
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТабНормВыдачи 		= Результат[3].Выгрузить();
	ТабНоменклатурыНорм = Результат[4].Выгрузить();
	ТабНоменклатуры 	= Результат[5].Выгрузить();
	
	МассивНормВыдачи 		= ТабНормВыдачи.ВыгрузитьКолонку("НормаВыдачи");
	МассивНоменклатурыНорм 	= ТабНоменклатурыНорм.ВыгрузитьКолонку("НоменклатураНорм");
	МассивНоменклатуры 		= ТабНоменклатуры.ВыгрузитьКолонку("Номенклатура");
	
	Для Каждого СтрокаСпискаСотрудников Из ТекущийОбъект.СписокСотрудников Цикл
		
		Если НЕ СтрокаСпискаСотрудников.Использовать Тогда
			Продолжить;
		КонецЕсли;
		
		НовыйДокумент 					= Документы.СписаниеСредствЗащитыСотрудника.СоздатьДокумент();
		НовыйДокумент.Организация 		= ТекущийОбъект.Организация;
		НовыйДокумент.Сотрудник 		= СтрокаСпискаСотрудников.Сотрудник;
		НовыйДокумент.ПричинаСписания 	= ТекущийОбъект.ПричинаСписания;
		НовыйДокумент.Комментарий 		= "Сформирован автоматически";
		НовыйДокумент.Дата 				= ТекущийОбъект.ДатаАнализа-1;
		НовыйДокумент.Ответственный		= ПараметрыСеанса.ТекущийПользователь;
		НовыйДокумент.СоздательДокумента= ПараметрыСеанса.ТекущийПользователь;
		НовыйДокумент.ВидВыдачиСИЗ		= ТекущийОбъект.ВидВыдачиСИЗ;
		
		Документы.СписаниеСредствЗащитыСотрудника.ЗаполнитьДокумент(СтрокаСпискаСотрудников.Сотрудник,НовыйДокумент);
		
		//применяем фильтры по нормам и номенклатуре
		ТаблицаДляАнализа = НовыйДокумент.Товары.Выгрузить();
		
		НовыйДокумент.Товары.Очистить();
		
		Для Каждого СтрокаТаблицыДляАнализа Из ТаблицаДляАнализа Цикл
			
			Если ЗначениеЗаполнено(СтрокаТаблицыДляАнализа.НормаВыдачи) Тогда //ищем норму выдачи, номенклатуру нормы и номенклатуру
				
				Если МассивНормВыдачи.Найти(СтрокаТаблицыДляАнализа.НормаВыдачи) = Неопределено 
					ИЛИ МассивНоменклатурыНорм.Найти(СтрокаТаблицыДляАнализа.НоменклатураНормы) = Неопределено
					ИЛИ МассивНоменклатуры.Найти(СтрокаТаблицыДляАнализа.Номенклатура) = Неопределено Тогда
					Продолжить;
				КонецЕсли;	
				
			Иначе //ищем номенклатуру нормы и номенклатуру				
				
				Если ЗначениеЗаполнено(СтрокаТаблицыДляАнализа.НоменклатураНормы) Тогда //ищем номенклатуру нормы и номенклатуру
					Если МассивНоменклатурыНорм.Найти(СтрокаТаблицыДляАнализа.НоменклатураНормы) = Неопределено
						ИЛИ МассивНоменклатуры.Найти(СтрокаТаблицыДляАнализа.Номенклатура) = Неопределено Тогда
						Продолжить;
					КонецЕсли;
				Иначе
					Если МассивНоменклатуры.Найти(СтрокаТаблицыДляАнализа.Номенклатура) = Неопределено Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;	
				
			КонецЕсли;	
			
			ЗаполнитьЗначенияСвойств(НовыйДокумент.Товары.Добавить(),СтрокаТаблицыДляАнализа);
			
		КонецЦикла;	
		
		Если НовыйДокумент.Товары.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НовыйДокумент.УстановитьНовыйНомер();
		
		Попытка
			НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
		    Сообщить("Создан документ: " + НовыйДокумент.Ссылка);
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли