///АСТБ_Горюшин_Алексей_19561

&НаСервере
Процедура ПрочитатьЗаписиНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВыданныеСредстваЗащиты.Период КАК Период,
		|	ВыданныеСредстваЗащиты.Регистратор КАК Регистратор,
		|	ВыданныеСредстваЗащиты.НомерСтроки КАК НомерСтрокиДвижения,
		|	ВыданныеСредстваЗащиты.Активность КАК Активность,
		|	ВЫБОР
		|		КОГДА ВыданныеСредстваЗащиты.ВидДвижения = &ВидДвиженияПриход
		|			ТОГДА ""Приход""
		|		ИНАЧЕ ""Расход""
		|	КОНЕЦ КАК ВидДвижения,
		|	ВыданныеСредстваЗащиты.Организация КАК Организация,
		|	ВыданныеСредстваЗащиты.Сотрудник КАК Сотрудник,
		|	ВыданныеСредстваЗащиты.НормаВыдачи КАК НормаВыдачи,
		|	ВыданныеСредстваЗащиты.НоменклатураНормы КАК НоменклатураНормы,
		|	ВыданныеСредстваЗащиты.Номенклатура КАК Номенклатура,
		|	ВыданныеСредстваЗащиты.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ВыданныеСредстваЗащиты.ДатаВыдачи КАК ДатаВыдачи,
		|	ВыданныеСредстваЗащиты.Количество КАК Количество,
		|	ВыданныеСредстваЗащиты.НеВыводитьВыдачуВ_ЛК КАК НеВыводитьВыдачуВ_ЛК
		|ИЗ
		|	РегистрНакопления.ВыданныеСредстваЗащиты КАК ВыданныеСредстваЗащиты
		|ГДЕ
		|	ВыданныеСредстваЗащиты.Организация = &Организация
		|	И ВыданныеСредстваЗащиты.Сотрудник = &Сотрудник
		|	И ВыданныеСредстваЗащиты.ВидДвижения = &ВидДвиженияПриход";
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Сотрудник", Объект.Сотрудник);
	Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
	
	ТЗ_Результат = Запрос.Выполнить().Выгрузить();
	
	Объект.ТаблицаДвижений.Загрузить(ТЗ_Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьЗаписи(Команда)
	ПрочитатьЗаписиНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДвиженийНеВыводитьВ_ЛКПриИзменении(Элемент)
	
	Элементы.ТаблицаДвижений.ТекущиеДанные.ОбрабатыватьЗапись = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьДвиженияНаСервере()
	
	БылиОшибки = Ложь;
	
	Для Каждого Строка ИЗ Объект.ТаблицаДвижений Цикл
		
		Если Строка.ОбрабатыватьЗапись Тогда
			
			НаборЗаписей = РегистрыНакопления.ВыданныеСредстваЗащиты.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Строка.Регистратор);
 			НаборЗаписей.Прочитать();
			
			КоличествоЗаписей = НаборЗаписей.Количество();
			ПродолжатьОбработку = Истина;
			НомерТекущейЗаписи = 0;
			
			Пока НомерТекущейЗаписи < КоличествоЗаписей И ПродолжатьОбработку Цикл
				
				Запись = НаборЗаписей[НомерТекущейЗаписи];
			
				Если Запись.Организация = Объект.Организация
					И Запись.Сотрудник = Объект.Сотрудник
					И Запись.НомерСтроки = Строка.НомерСтрокиДвижения
					Тогда
				
					Запись.НеВыводитьВыдачуВ_ЛК = Строка.НеВыводитьВыдачуВ_ЛК;
				    ПродолжатьОбработку = Ложь;
					
				КонецЕсли;
				
				НомерТекущейЗаписи = НомерТекущейЗаписи + 1;
			
			КонецЦикла;
			
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			Попытка
				НаборЗаписей.Записать();
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
				БылиОшибки = Истина;
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если БылиОшибки Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("При записи возникли ошибки!");
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Запись движений успешно завершена!");
	КонецЕсли;
	
	ПрочитатьЗаписиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДвижения(Команда)
	ЗаписатьДвиженияНаСервере();
КонецПроцедуры
