&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ФормаДлительнойОперации;

&НаКлиенте
Перем ТекущаяСтрокаДерева;

&НаКлиенте
Перем ЗапомнитьТекущуюСтрокуДерева;

&НаКлиенте
Перем СмещениеИндексов; //хранит количество строк в дереве номенклатуры для правильного определения текущей строки после обработки, т.к. индексы дерева не сбрасываются

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Объект.ТипИсточника 					= Перечисления.ТипыИсточниковДляЗагрузки.ЛокальныйРесурс;
	Объект.ФайлСертификатовНоменклатуры 	= "certificate2.zip";   // старый файл "certificate.zip"
	
	ВосстановитьНастройкиПодключенияFTPНаСервере();
	
	УправлениеВидимостьюЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСертификаты(Команда)
	
	ЗапомнитьТекущуюСтрокуДерева = Ложь;
	
	Состояние(НСтр("ru = 'Выполняется загрузка сертификатов'"));
	
	Результат = ВыполнитьЗагрузкуСертификатовНоменклатурыНаСервере();
	
	Если НЕ Результат.ЗаданиеВыполнено Тогда
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		АдресХранилища       = Результат.АдресХранилища;
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания_Загрузка", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
	Иначе
		Если Результат.УспешнаяЗагрузка Тогда
			//ЭтаФорма.Закрыть();
			ОповеститьОбИзменении(Тип("СправочникСсылка.Номенклатура"));
		Иначе
			Сообщить("При загрузке сертификатов номенклатуры произошли ошибки.");
		КонецЕсли;
		Если СмещениеИндексов = 0 Тогда
			СмещениеИндексов = ЭтаФорма.ДеревоНоменклатуры.ПолучитьЭлементы()[0].ПолучитьИдентификатор();
		КонецЕсли;
		Оповестить( , , ЭтаФорма);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ВыполнитьЗагрузкуСертификатовНоменклатурыНаСервере()
	
	ПараметрыЗагрузки = Новый Структура(
		"КаталогОбменаИнформацией, ТипИсточника, ФайлСертификатовНоменклатуры, FTPСоединениеАдрес, FTPСоединениеПароль, FTPСоединениеПассивноеСоединение, FTPСоединениеПользователь, FTPСоединениеПорт, FTPСоединениеПуть, FTPСоединениеЗащищенноеСоединение, ДеревоДляЗагрузки, ",
		Объект.КаталогОбменаИнформацией, Объект.ТипИсточника, Объект.ФайлСертификатовНоменклатуры, Объект.FTPСоединениеАдрес, Объект.FTPСоединениеПароль, Объект.FTPСоединениеПассивноеСоединение, Объект.FTPСоединениеПользователь, Объект.FTPСоединениеПорт, Объект.FTPСоединениеПуть, Объект.FTPСоединениеЗащищенноеСоединение, РеквизитФормыВЗначение("ДеревоНоменклатуры"));
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		Обработки.ЗагрузкаСертификатовНоменклатурыВостокСервис.ВыполнитьЗагрузкуСертификатовНоменклатуры(ПараметрыЗагрузки,АдресХранилища);
		Результат = Новый Структура("ЗаданиеВыполнено", Истина);
		
	Иначе
		НаименованиеЗадания = НСтр("ru = 'Загрузка номенклатуры '");
		
		Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор,
			"Обработки.ЗагрузкаСертификатовНоменклатурыВостокСервис.ВыполнитьЗагрузкуСертификатовНоменклатуры", 
			ПараметрыЗагрузки,
			НаименованиеЗадания);
			
		АдресХранилища = Результат.АдресХранилища;
		
	КонецЕсли;
	
	УспешнаяЗагрузка = Ложь;
	
	Если Результат.ЗаданиеВыполнено Тогда
		УспешнаяЗагрузка = ЗагрузитьРезультатЗагрузки();
	КонецЕсли;

	Результат.Вставить("УспешнаяЗагрузка", УспешнаяЗагрузка);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания_Загрузка()
	
	Попытка
		Если ФормаДлительнойОперации.Открыта() 
			И ФормаДлительнойОперации.ИдентификаторЗадания = ИдентификаторЗадания Тогда
			Если ПроверитьВыполнениеФоновогоЗаданияНаСервере(ИдентификаторЗадания) Тогда 
				УспешнаяЗагрузка = ЗагрузитьРезультатЗагрузки();
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
				Если УспешнаяЗагрузка Тогда
					//ЭтаФорма.Закрыть();
					ОповеститьОбИзменении(Тип("СправочникСсылка.Номенклатура"));
				Иначе
					Сообщить("При загрузке сертификатов номенклатуры произошли ошибки.");
				КонецЕсли;
				Если СмещениеИндексов = 0 Тогда
					СмещениеИндексов = ЭтаФорма.ДеревоНоменклатуры.ПолучитьЭлементы()[0].ПолучитьИдентификатор();
				КонецЕсли;
				Оповестить( , , ЭтаФорма);
			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				ПодключитьОбработчикОжидания(
					"Подключаемый_ПроверитьВыполнениеЗадания_Загрузка", 
					ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
					Истина);
			КонецЕсли;
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры

&НаСервере
Функция ЗагрузитьРезультатЗагрузки()
	
	Результат = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	ЗначениеВРеквизитФормы(Результат.ДеревоДляЗагрузки,"ДеревоНоменклатуры");
	
	Возврат Результат.УспешнаяЗагрузка;
	
КонецФункции

&НаКлиенте
Процедура ТипИсточникаПриИзменении(Элемент)
	
	УправлениеВидимостьюЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюЭлементовФормы()
	
	Если Объект.ТипИсточника = Перечисления.ТипыИсточниковДляЗагрузки.ЛокальныйРесурс Тогда
		Элементы.ГруппаФТПРесурс.Видимость 			= Ложь;
		Элементы.НастройкиПодключенияFTP.Видимость	= Ложь;
		Элементы.ГруппаЛокальныйРесурс.Видимость 	= Истина;
	Иначе
		Элементы.ГруппаФТПРесурс.Видимость 			= Истина;
		Элементы.НастройкиПодключенияFTP.Видимость	= Истина;
		Элементы.ГруппаЛокальныйРесурс.Видимость 	= Ложь;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьФайлСертификатов(Команда)
	
	//Элементы.ДеревоНоменклатуры.КоманднаяПанель.ПодчиненныеЭлементы.ФормаЗагрузитьСертификаты.Видимость = Ложь;
	
	Состояние(НСтр("ru = 'Выполняется чтение и анализ файла сертификатов номенклатуры'"));
	
	Результат = ВыполнитьЧтениеФайлаНаСервере();
	
	Если НЕ Результат.ЗаданиеВыполнено Тогда
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		АдресХранилища       = Результат.АдресХранилища;
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания_Чтение", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
	Иначе
		//Если Результат.МожноЗагружатьСертификаты Тогда
		//	Элементы.ДеревоНоменклатуры.КоманднаяПанель.ПодчиненныеЭлементы.ФормаЗагрузитьСертификаты.Видимость = Истина;
		//КонецЕсли;
		Если СмещениеИндексов = 0 Тогда
			СмещениеИндексов = ЭтаФорма.ДеревоНоменклатуры.ПолучитьЭлементы()[0].ПолучитьИдентификатор();
		КонецЕсли;
		//Оповестить( , , ЭтаФорма);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания_Чтение()
	
	Попытка
		Если ФормаДлительнойОперации.Открыта() 
			И ФормаДлительнойОперации.ИдентификаторЗадания = ИдентификаторЗадания Тогда
			Если ПроверитьВыполнениеФоновогоЗаданияНаСервере(ИдентификаторЗадания) Тогда 
				МожноЗагружатьСертификаты = ЗагрузитьРезультатЧтения();
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
				Если МожноЗагружатьСертификаты Тогда
					Элементы.ДеревоНоменклатуры.КоманднаяПанель.ПодчиненныеЭлементы.ФормаПрочитатьФайлСертификатов.Видимость = Истина;
				КонецЕсли;
				Если СмещениеИндексов = 0 Тогда
					СмещениеИндексов = ЭтаФорма.ДеревоНоменклатуры.ПолучитьЭлементы()[0].ПолучитьИдентификатор();
				КонецЕсли;
				Оповестить( , , ЭтаФорма);
 			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				ПодключитьОбработчикОжидания(
					"Подключаемый_ПроверитьВыполнениеЗадания_Чтение", 
					ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
					Истина);
			КонецЕсли;
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры	

&НаСервереБезКонтекста
Функция ПроверитьВыполнениеФоновогоЗаданияНаСервере(ФоновоеЗаданиеИдентификатор)
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ФоновоеЗаданиеИдентификатор);
КонецФункции

&НаСервере
Функция ВыполнитьЧтениеФайлаНаСервере()
	
	ПараметрыЗагрузки = Новый Структура(
		"КаталогОбменаИнформацией, ТипИсточника, ФайлСертификатовНоменклатуры, FTPСоединениеАдрес, FTPСоединениеПароль, FTPСоединениеПассивноеСоединение, FTPСоединениеПользователь, FTPСоединениеПорт, FTPСоединениеПуть, FTPСоединениеЗащищенноеСоединение",
		Объект.КаталогОбменаИнформацией, Объект.ТипИсточника, Объект.ФайлСертификатовНоменклатуры, Объект.FTPСоединениеАдрес, Объект.FTPСоединениеПароль, Объект.FTPСоединениеПассивноеСоединение, Объект.FTPСоединениеПользователь, Объект.FTPСоединениеПорт, Объект.FTPСоединениеПуть, Объект.FTPСоединениеЗащищенноеСоединение);
		
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		Обработки.ЗагрузкаСертификатовНоменклатурыВостокСервис.ВыполнитьЧтениеФайлаСертификатовНоменклатуры(ПараметрыЗагрузки,АдресХранилища);
		Результат = Новый Структура("ЗаданиеВыполнено", Истина);
		
	Иначе
		НаименованиеЗадания = НСтр("ru = 'Чтение файла сертификатов номенклатуры '");
		
		Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор,
			"Обработки.ЗагрузкаСертификатовНоменклатурыВостокСервис.ВыполнитьЧтениеФайлаСертификатовНоменклатуры", 
			ПараметрыЗагрузки,
			НаименованиеЗадания);
			
		АдресХранилища = Результат.АдресХранилища;
		
	КонецЕсли;
	
	МожноЗагружатьСертификаты = Ложь;
	
	Если Результат.ЗаданиеВыполнено Тогда
		МожноЗагружатьСертификаты = ЗагрузитьРезультатЧтения();
	КонецЕсли;

	Результат.Вставить("МожноЗагружатьСертификаты", МожноЗагружатьСертификаты);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЗагрузитьРезультатЧтения()
	
	Результат = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(Результат) = Тип("Строка") Тогда //это ошибка
		Сообщить(Результат);
		МожноЗагружатьСертификаты = Ложь;
	Иначе 
		ЗначениеВРеквизитФормы(Результат.ДеревоДляЗагрузки,	"ДеревоНоменклатуры");
		МожноЗагружатьСертификаты = Истина;
	КонецЕсли;
	
	Возврат МожноЗагружатьСертификаты;
	
КонецФункции

&НаКлиенте
Процедура УстановитьFTPКаталогПоУмолчанию(Команда)
	
	Объект.FTPСоединениеПуть = "help-docs/certificate";
	
	Если Объект.ТипИсточника = ПредопределенноеЗначение("Перечисление.ТипыИсточниковДляЗагрузки.FTPРесурс") Тогда
		
		Объект.FTPСоединениеАдрес 					= "ftp.vostok.ru";
		Объект.FTPСоединениеПароль 					= "mB3kU6na";
		Объект.FTPСоединениеПассивноеСоединение 	= Истина;
		Объект.FTPСоединениеПользователь 			= "astb-1c";
		Объект.FTPСоединениеПорт					= 990;
		Объект.FTPСоединениеЗащищенноеСоединение 	= Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогОбменаИнформациейНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ОбработчикВыбораФайловогоКаталога(Объект, "КаталогОбменаИнформацией", СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогОбменаИнформациейОткрытие(Элемент, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ОбработчикОткрытияФайлаИлиКаталога(Объект, "КаталогОбменаИнформацией", СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодключениеFTP(Команда)
	
	ПроверитьПодключениеFTPНаСервере();
		
КонецПроцедуры

&НаСервере
Процедура ПроверитьПодключениеFTPНаСервере()
	
	ПараметрыЗагрузки = Новый Структура(
		"КаталогОбменаИнформацией, ТипИсточника, ФайлСертификатовНоменклатуры, FTPСоединениеАдрес, FTPСоединениеПароль, FTPСоединениеПассивноеСоединение, FTPСоединениеПользователь, FTPСоединениеПорт, FTPСоединениеПуть, FTPСоединениеЗащищенноеСоединение",
		Объект.КаталогОбменаИнформацией, Объект.ТипИсточника, Объект.ФайлСертификатовНоменклатуры, Объект.FTPСоединениеАдрес, Объект.FTPСоединениеПароль, Объект.FTPСоединениеПассивноеСоединение, Объект.FTPСоединениеПользователь, Объект.FTPСоединениеПорт, Объект.FTPСоединениеПуть, Объект.FTPСоединениеЗащищенноеСоединение);
	ЗаполнитьЗначенияСвойств(ПараметрыЗагрузки, ЭтаФорма);
	
	Если Обработки.ЗагрузкаСертификатовНоменклатурыВостокСервис.УстановитьFTPСоединение(ПараметрыЗагрузки) = Неопределено Тогда
		Сообщить("Ошибка подключения к FTP!");
	Иначе
		Сообщить("Подключение к FTP выполнено успешно.");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиПодключенияFTP(Команда)
	
	СохранитьНастройкиПодключенияFTPНаСервере();
		
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиПодключенияFTPНаСервере() 
	
	ИдентификаторОбработки = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Обработка.ЗагрузкаСертификатовНоменклатурыВостокСервис");
	
	Если ЗначениеЗаполнено(ИдентификаторОбработки) Тогда
		
		СтруктураНастроек = Новый Структура;
		СтруктураНастроек.Вставить("FTPСоединениеАдрес",				Объект.FTPСоединениеАдрес);
		СтруктураНастроек.Вставить("FTPСоединениеПароль",				Объект.FTPСоединениеПароль);
		СтруктураНастроек.Вставить("FTPСоединениеПассивноеСоединение",	Объект.FTPСоединениеПассивноеСоединение);
		СтруктураНастроек.Вставить("FTPСоединениеПользователь",			Объект.FTPСоединениеПользователь);
		СтруктураНастроек.Вставить("FTPСоединениеПорт",					Объект.FTPСоединениеПорт);
		СтруктураНастроек.Вставить("FTPСоединениеПуть",					Объект.FTPСоединениеПуть);
		СтруктураНастроек.Вставить("FTPСоединениеЗащищенноеСоединение",	Объект.FTPСоединениеЗащищенноеСоединение);
		
		НаборЗаписей = РегистрыСведений.НастройкиПодключенияFTPДляДополнительныхОбработок.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИдентификаторОбъекта.Установить(ИдентификаторОбработки);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() = 0 Тогда
			НоваяЗапись 						= НаборЗаписей.Добавить();
			НоваяЗапись.ИдентификаторОбъекта 	= ИдентификаторОбработки;
			НоваяЗапись.НастройкиПодключенияFTP	= Новый ХранилищеЗначения(СтруктураНастроек);
		Иначе
			НаборЗаписей[0].ИдентификаторОбъекта	= ИдентификаторОбработки;
			НаборЗаписей[0].НастройкиПодключенияFTP	= Новый ХранилищеЗначения(СтруктураНастроек);
		КонецЕсли;
		
		НаборЗаписей.Записать();
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройкиПодключенияFTPНаСервере()
	
	ИдентификаторОбработки = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Обработка.ЗагрузкаСертификатовНоменклатурыВостокСервис");
	
	Если ЗначениеЗаполнено(ИдентификаторОбработки) Тогда
		
		НаборЗаписей = РегистрыСведений.НастройкиПодключенияFTPДляДополнительныхОбработок.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИдентификаторОбъекта.Установить(ИдентификаторОбработки);
		НаборЗаписей.Прочитать();
		
		Если НЕ НаборЗаписей.Количество() = 0 Тогда
			СтруктураНастроек 							= НаборЗаписей[0].НастройкиПодключенияFTP.Получить();
			Объект.FTPСоединениеАдрес 					= СтруктураНастроек.FTPСоединениеАдрес;
			Объект.FTPСоединениеПароль 					= СтруктураНастроек.FTPСоединениеПароль;
			Объект.FTPСоединениеПассивноеСоединение 	= СтруктураНастроек.FTPСоединениеПассивноеСоединение;
			Объект.FTPСоединениеПользователь 			= СтруктураНастроек.FTPСоединениеПользователь;
			Объект.FTPСоединениеПорт 					= СтруктураНастроек.FTPСоединениеПорт;
			Объект.FTPСоединениеПуть 					= СтруктураНастроек.FTPСоединениеПуть;
			Объект.FTPСоединениеЗащищенноеСоединение 	= ?(СтруктураНастроек.Свойство("FTPСоединениеЗащищенноеСоединение"),СтруктураНастроек.FTPСоединениеЗащищенноеСоединение,Ложь);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДеревоНоменклатурыИспользоватьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоНоменклатуры.ТекущиеДанные;
	
	Если ТекущиеДанные.Использовать = 2 Тогда
		ТекущиеДанные.Использовать = 0;
	КонецЕсли;
	
	ПроцедурыРаботыСНормамиКлиент.ОбходДереваВверх(ТекущиеДанные);
	ПроцедурыРаботыСНормамиКлиент.ОбходДереваВниз(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлСертификатовНоменклатурыВостокСервисПриИзменении(Элемент)
	
	Элементы.ДеревоНоменклатуры.КоманднаяПанель.ПодчиненныеЭлементы.ФормаЗагрузитьСертификаты.Видимость = Ложь;
	ОчиститьДеревоНоменклатуры();
	
КонецПроцедуры
 
&НаСервере
Процедура ОчиститьДеревоНоменклатуры()
	
	ДеревоДляЗагрузки = РеквизитФормыВЗначение("ДеревоНоменклатуры");
	ДеревоДляЗагрузки.Строки.Очистить();
	ЗначениеВРеквизитФормы(ДеревоДляЗагрузки,"ДеревоНоменклатуры");
	 
КонецПроцедуры

&НаКлиенте
Процедура ДеревоНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДеревоНоменклатуры.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда
		ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.Номенклатура);
		ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаЭлемента", ПараметрыФормы);
	Иначе
		Если ЗначениеЗаполнено(ТекущиеДанные.Сертификат) Тогда
			ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.Сертификат);
			ОткрытьФорму("Справочник.СертификатыНоменклатуры.Форма.ФормаЭлемента", ПараметрыФормы);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоНоменклатурыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоНоменклатуры.ТекущиеДанные;
	
	Если НЕ ТекущиеДанные = Неопределено Тогда
		Если ЗапомнитьТекущуюСтрокуДерева Тогда
			ТекущаяСтрокаДерева = Элементы.ДеревоНоменклатуры.ТекущаяСтрока;
		КонецЕсли;
	Иначе
		Если ЗапомнитьТекущуюСтрокуДерева Тогда
			ТекущаяСтрокаДерева = 0;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТекущаяСтрокаДерева 			= 0;
	ЗапомнитьТекущуюСтрокуДерева 	= Истина;
	СмещениеИндексов				= 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Элементы.ДеревоНоменклатуры.Развернуть(ЭтаФорма.ДеревоНоменклатуры.ПолучитьЭлементы()[0].ПолучитьИдентификатор(),Истина);
	Элементы.ДеревоНоменклатуры.ТекущаяСтрока = СмещениеИндексов + ТекущаяСтрокаДерева;
	ЗапомнитьТекущуюСтрокуДерева = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	
	Элементы_Уровень0 = ЭтаФорма.ДеревоНоменклатуры.ПолучитьЭлементы();
	Для Каждого Элемент_Уровень0 Из Элементы_Уровень0 Цикл
		Элемент_Уровень0.Использовать = Истина;
		Элементы_Уровень1 = Элемент_Уровень0.ПолучитьЭлементы();
		Для Каждого Элемент_Уровень1 Из Элементы_Уровень1 Цикл
			Элемент_Уровень1.Использовать = Истина;
			Элементы_Уровень2 = Элемент_Уровень1.ПолучитьЭлементы();
			Для Каждого Элемент_Уровень2 Из Элементы_Уровень2 Цикл
				Элемент_Уровень2.Использовать = Истина;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВсе(Команда)
	
	Элементы_Уровень0 = ЭтаФорма.ДеревоНоменклатуры.ПолучитьЭлементы();
	Для Каждого Элемент_Уровень0 Из Элементы_Уровень0 Цикл
		Элемент_Уровень0.Использовать = Ложь;
		Элементы_Уровень1 = Элемент_Уровень0.ПолучитьЭлементы();
		Для Каждого Элемент_Уровень1 Из Элементы_Уровень1 Цикл
			Элемент_Уровень1.Использовать = Ложь;
			Элементы_Уровень2 = Элемент_Уровень1.ПолучитьЭлементы();
			Для Каждого Элемент_Уровень2 Из Элементы_Уровень2 Цикл
				Элемент_Уровень2.Использовать = Ложь;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьНовые(Команда)
	
	Элементы_Уровень0 = ЭтаФорма.ДеревоНоменклатуры.ПолучитьЭлементы();
	Для Каждого Элемент_Уровень0 Из Элементы_Уровень0 Цикл
 		Элементы_Уровень1 = Элемент_Уровень0.ПолучитьЭлементы();
		Для Каждого Элемент_Уровень1 Из Элементы_Уровень1 Цикл
			Элементы_Уровень2 = Элемент_Уровень1.ПолучитьЭлементы();
			Для Каждого Элемент_Уровень2 Из Элементы_Уровень2 Цикл
				Если СокрЛП(Элемент_Уровень2.Примечание) = "Не загружен" Тогда
					Элемент_Уровень2.Использовать = Истина;
					ПроцедурыРаботыСНормамиКлиент.ОбходДереваВверх(Элемент_Уровень2);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры
