
&НаКлиенте
Процедура ФайлДанныхНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НастройкиДиалога = Новый Структура;
	НастройкиДиалога.Вставить("Фильтр", НСтр("ru = 'Документ XML(*.xml)|*.xml'"));
	НастройкиДиалога.Вставить("ПроверятьСуществованиеФайла", Истина);
	
	ОбменДаннымиКлиент.ОбработчикВыбораФайла(Объект,"ФайлДанных",СтандартнаяОбработка, НастройкиДиалога);
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлДанныхОткрытие(Элемент, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ОбработчикОткрытияФайлаИлиКаталога(Объект,"ФайлДанных",СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьФайл(Команда)
	
	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(НОВЫЙ ДвоичныеДанные(Объект.ФайлДанных),ЭтаФорма.УникальныйИдентификатор);
	
	ПрочитатьФайлНаСервереФорма(АдресВременногоХранилища);
	
	Если СтрДлина(Объект.ПротоколОшибок) > 0 Тогда  
	    ЭтаФорма.Элементы.ГруппаСтраницы.ТекущаяСтраница = ЭтаФорма.Элементы.СтраницаОшибки;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПрочитатьФайлНаСервереФорма(АдресВременногоХранилища)
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	ТекущийОбъект.ОчиститьТаблицы();
	ТекущийОбъект.ПрочитатьФайлНаСервере(АдресВременногоХранилища);
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеНаСервере()
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	ТекущийОбъект.ЗагрузитьДанные();
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеКлиент(Команда)
	
	ЗагрузитьДанныеНаСервере();
	
	Если Объект.ИсправлятьНаименованияСуществующихПодразделений Тогда
		//отображаем изменения в таблице подразделений
		КоличествоСтрок = Объект.ТаблицаПодразделений.Количество();
		Для Сч = 0 По КоличествоСтрок - 1 Цикл
			ЭтаФорма.ОтобразитьИзменениеДанных(Объект.ТаблицаПодразделений[Сч].Подразделение,ВидИзмененияДанных.Изменение);
		КонецЦикла;	
	КонецЕсли;
	
	Если Объект.ИсправлятьНаименованияСуществующихДолжностей Тогда
		//отображаем изменения в таблице должностей
		КоличествоСтрок = Объект.ТаблицаДолжностей.Количество();
		Для Сч = 0 По КоличествоСтрок - 1 Цикл
			ЭтаФорма.ОтобразитьИзменениеДанных(Объект.ТаблицаДолжностей[Сч].Должность,ВидИзмененияДанных.Изменение);
		КонецЦикла;
	КонецЕсли;
	
	Если Объект.ИсправлятьНаименованияСуществующихРабочихМест Тогда
		//отображаем изменения в таблице рабочих мест
		КоличествоСтрок = Объект.ТаблицаРабочихМест.Количество();
		Для Сч = 0 По КоличествоСтрок - 1 Цикл
			ЭтаФорма.ОтобразитьИзменениеДанных(Объект.ТаблицаРабочихМест[Сч].РабочееМесто,ВидИзмененияДанных.Изменение);
		КонецЦикла;
	КонецЕсли;
	
	Если СтрДлина(Объект.ПротоколОшибок) > 0 Тогда  
	    ЭтаФорма.Элементы.ГруппаСтраницы.ТекущаяСтраница = ЭтаФорма.Элементы.СтраницаОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = ЭтаФорма.Элементы.ТаблицаДокументов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		ТекущийТипДокумента 	= "";
		ТекущийНомерДокумента 	= "";
		ТекущаяДатаДокумента 	= Дата('00010101');
	Иначе
		ТекущийТипДокумента 	= ТекущиеДанные.document_type;
		ТекущийНомерДокумента 	= ТекущиеДанные.document_number;
		ТекущаяДатаДокумента 	= ТекущиеДанные.document_date;
	КонецЕсли;
	
	ЭтаФорма.Элементы.ТаблицаСоставаДокументов.ОтборСтрок = Новый ФиксированнаяСтруктура("document_type,document_date,document_number",ТекущийТипДокумента,ТекущаяДатаДокумента,ТекущийНомерДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСоставаДокументовПриАктивизацииСтроки(Элемент)
	
	Если ЭтаФорма.Элементы.ТаблицаСоставаДокументов.ТекущиеДанные = Неопределено Тогда
		ТекущийКодСинхронизации	= "";
	Иначе
		ТекущийКодСинхронизации = ЭтаФорма.Элементы.ТаблицаСоставаДокументов.ТекущиеДанные.document_worker_snils;
	КонецЕсли;	
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ЭтаФорма.ЗанятыеРабочиеМеста.Отбор, "Организация", 	 Объект.Организация,	  ВидСравненияКомпоновкиДанных.Равно, "Организация", 	  Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ЭтаФорма.ЗанятыеРабочиеМеста.Отбор, "КодСинхронизации", ТекущийКодСинхронизации, ВидСравненияКомпоновкиДанных.Равно, "КодСинхронизации", Истина);
	
КонецПроцедуры
