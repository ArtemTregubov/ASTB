//процедура выполняет отправку push-уведомления
//
//параметры:
// Уведомление - объект, типа "ДоставляемоеУведомление"
// Пользователь - получатель уведомления (физическое лицо)
//
Процедура ОтправитьУведомление(Уведомление, Пользователь, ВидУведомления) Экспорт
	
	Если Пользователь.ИдентификаторПодписчикаДоставляемыхУведомлений <> Неопределено Тогда
		Идентификатор = Пользователь.ИдентификаторПодписчикаДоставляемыхУведомлений.Получить();
		Если Идентификатор <> Неопределено Тогда
			Уведомление.Получатели.Добавить(Идентификатор);
			СформироватьДокументУведомления(Пользователь,Уведомление.Текст,ВидУведомления);
		КонецЕсли;
	КонецЕсли;
	
	Если Уведомление.Получатели.Количество() > 0 Тогда
		
		ДанныеАутентификации = СокрЛП(Константы.КлючДоступаОтправителяУведомлений.Получить());
			
		УдаленныеТокены = Новый Массив;
		ОтправкаДоставляемыхУведомлений.Отправить(Уведомление, ДанныеАутентификации, УдаленныеТокены, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьДокументУведомления(ФизическоеЛицо,ТекстУведомления,ВидУведомления)
	
	Сотрудник = Справочники.Сотрудники.НайтиПоРеквизиту("ФизическоеЛицо",ФизическоеЛицо);
	
	Если Сотрудник.Пустая() Тогда
		Возврат;
	КонецЕсли;	
	
	НовыйДокумент = Документы.Уведомление.СоздатьДокумент();
	НовыйДокумент.Дата				= ТекущаяДата();
	НовыйДокумент.ВидУведомления 	= ВидУведомления;
	НовыйДокумент.Организация 		= Сотрудник.Владелец;
	НовыйДокумент.Отправлено 		= Истина;
	НовыйДокумент.Сотрудник 		= Сотрудник;
	НовыйДокумент.ТекстУведомления 	= ТекстУведомления;
	НовыйДокумент.УстановитьНовыйНомер();
	НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
	
КонецПроцедуры	

//функция определяет возможность отправки уведомлений
//
//параметры: 
// Пользователь - элемент справочника внешние пользователи
//
Функция МожноОтправлятьУведомления(Пользователь) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Константы.КлючДоступаОтправителяУведомлений.Получить()) Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	//уведомления отправляются только авторизованным внешним пользователям
	Если ТипЗнч(Пользователь) = Тип("СправочникСсылка.ВнешниеПользователи") Тогда
		Если НЕ ЗначениеЗаполнено(Пользователь.ОбъектАвторизации) Тогда
			Возврат Ложь;		
		КонецЕсли;
		Если Пользователь.ПометкаУдаления Тогда
			Возврат Ложь;		
		КонецЕсли;
		Если Пользователь.Недействителен Тогда
			Возврат Ложь;		
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	ФизическоеЛицо = Пользователь.ОбъектАвторизации;
	
	//пользователь уже дожен был подключаться к базе
	Если НЕ ФизическоеЛицо.ИдентификаторПодписчикаДоставляемыхУведомлений = Неопределено Тогда
		Идентификатор = ФизическоеЛицо.ИдентификаторПодписчикаДоставляемыхУведомлений.Получить();
		Если НЕ Идентификатор = Неопределено Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;	
	
КонецФункции