
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОбратныйАдрес 	= ПолучитьАдресЭППользователя();
	Телефон			= ПолучитьТелефонПользователя();
	//АсТБ_Alexey_32345_********************************************************************
	ИнформацияДляПодписи = "<опишите, пожалуйста, свою проблему максимально подробно и с примерами>" 	
							+ Символы.ПС + "_________________________________________________________________________" 
							+ Символы.ПС + "Компьютер: " 		+ ИмяКомпьютера() 
							+ Символы.ПС + "Пользователь: " 	+ ПользователиКлиентСервер.ТекущийПользователь() 
							+ Символы.ПС + "База: " 			+ СтрокаСоединенияИнформационнойБазы()
							+ Символы.ПС + "Телефон: " 			+ Телефон
							+ Символы.ПС + "Обратный адрес: " 	+ ОбратныйАдрес;
	//АсТБ_Alexey_32345_********************************************************************
	ПараметрыПисьма = Новый Структура;
	ПараметрыПисьма.Вставить("УчетнаяЗапись", 	ПредопределенноеЗначение("Справочник.УчетныеЗаписиЭлектроннойПочты.СистемнаяУчетнаяЗаписьЭлектроннойПочты"));
	ПараметрыПисьма.Вставить("Кому", 			"astb_support@vostok.ru");
	ПараметрыПисьма.Вставить("Тело", 			ИнформацияДляПодписи);
	Если ЗначениеЗаполнено(ОбратныйАдрес) Тогда
		ПараметрыПисьма.Вставить("АдресОтвета", ОбратныйАдрес);
	КонецЕсли;
	ФормаСообщения = ПолучитьФорму("ОбщаяФорма.ОтправкаСообщения",ПараметрыПисьма);
	ФормаСообщения.Элементы.УчетнаяЗапись.ТолькоПросмотр = Истина;
	ФормаСообщения.Открыть();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресЭППользователя() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПользователиКонтактнаяИнформация.Ссылка КАК Ссылка,
	|	ПользователиКонтактнаяИнформация.АдресЭП КАК АдресЭП
	|ИЗ
	|	Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
	|ГДЕ
	|	ПользователиКонтактнаяИнформация.Ссылка = &Ссылка
	|	И НЕ ПользователиКонтактнаяИнформация.АдресЭП = """"";
	
	Запрос.УстановитьПараметр("Ссылка",ПользователиКлиентСервер.ТекущийПользователь());
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		
		Возврат "";
		
	Иначе
		
		Возврат Результат.Выгрузить()[0].АдресЭП;
		
	КонецЕсли;	
	
КонецФункции

&НаСервере
Функция ПолучитьТелефонПользователя() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПользователиКонтактнаяИнформация.Ссылка КАК Ссылка,
	|	ПользователиКонтактнаяИнформация.Представление КАК Представление,
	|	ПользователиКонтактнаяИнформация.НомерТелефона КАК НомерТелефона
	|ИЗ
	|	Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиКонтактнаяИнформация
	|ГДЕ
	|	ПользователиКонтактнаяИнформация.Ссылка = &Ссылка
	|	И НЕ ПользователиКонтактнаяИнформация.НомерТелефона = """"";
	
	Запрос.УстановитьПараметр("Ссылка",ПользователиКлиентСервер.ТекущийПользователь());
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		
		Возврат "";
		
	Иначе
		
		Возврат Результат.Выгрузить()[0].Представление;
		
	КонецЕсли;	
	
КонецФункции