//АСТБ_Горюшин_Алексей_17125

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТребуетсяКонтроль = Истина;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

	// Текущее состояние набора помещается во временную таблицу "ДвиженияРеализованныеСИЗПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализованныеСИЗ.Организация КАК Организация,
	|	РеализованныеСИЗ.Номенклатура КАК Номенклатура,
	|	РеализованныеСИЗ.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВЫБОР
	|		КОГДА РеализованныеСИЗ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РеализованныеСИЗ.Количество
	|		ИНАЧЕ -РеализованныеСИЗ.Количество
	|	КОНЕЦ КАК КоличествоПередЗаписью,
	|	РеализованныеСИЗ.ВидВыдачиСИЗ КАК ВидВыдачиСИЗ,
	|	РеализованныеСИЗ.СкладВыдачи КАК СкладВыдачи,
	|	РеализованныеСИЗ.СкладОтправитель КАК СкладОтправитель,
	|	РеализованныеСИЗ.МВЗ КАК МВЗ,
	|	РеализованныеСИЗ.Поставщик КАК Поставщик,
	|	РеализованныеСИЗ.ДокументВыдачи КАК ДокументВыдачи,
	|	РеализованныеСИЗ.ПредварительныйПереходПраваСобственности КАК ПредварительныйПереходПраваСобственности
	|ПОМЕСТИТЬ ДвиженияРеализованныеСИЗПередЗаписью
	|ИЗ
	|	РегистрНакопления.РеализованныеСИЗ КАК РеализованныеСИЗ
	|ГДЕ
	|	РеализованныеСИЗ.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый";
	Запрос.Выполнить();
	
КонецПроцедуры	
	
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Организация КАК Организация,
	|	ТаблицаИзменений.Номенклатура КАК Номенклатура,
	|	ТаблицаИзменений.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) КАК КоличествоИзменение,
	|	ТаблицаИзменений.ВидВыдачиСИЗ КАК ВидВыдачиСИЗ,
	|	ТаблицаИзменений.СкладВыдачи КАК СкладВыдачи,
	|	ТаблицаИзменений.СкладОтправитель КАК СкладОтправитель,
	|	ТаблицаИзменений.МВЗ КАК МВЗ,
	|	ТаблицаИзменений.Поставщик КАК Поставщик,
	|	ТаблицаИзменений.ДокументВыдачи КАК ДокументВыдачи,
	|	ТаблицаИзменений.ПредварительныйПереходПраваСобственности КАК ПредварительныйПереходПраваСобственности
	|ПОМЕСТИТЬ ДвиженияРеализованныеСИЗИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Организация КАК Организация,
	|		Таблица.Номенклатура КАК Номенклатура,
	|		Таблица.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		Таблица.КоличествоПередЗаписью КАК КоличествоИзменение,
	|		Таблица.ВидВыдачиСИЗ КАК ВидВыдачиСИЗ,
	|		Таблица.СкладВыдачи КАК СкладВыдачи,
	|		Таблица.СкладОтправитель КАК СкладОтправитель,
	|		Таблица.МВЗ КАК МВЗ,
	|		Таблица.Поставщик КАК Поставщик,
	|		Таблица.ДокументВыдачи КАК ДокументВыдачи,
	|		Таблица.ПредварительныйПереходПраваСобственности КАК ПредварительныйПереходПраваСобственности
	|	ИЗ
	|		ДвиженияРеализованныеСИЗПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Организация,
	|		Таблица.Номенклатура,
	|		Таблица.ХарактеристикаНоменклатуры,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.Количество
	|			ИНАЧЕ Таблица.Количество
	|		КОНЕЦ,
	|		Таблица.ВидВыдачиСИЗ,
	|		Таблица.СкладВыдачи,
	|		Таблица.СкладОтправитель,
	|		Таблица.МВЗ,
	|		Таблица.Поставщик,
	|		Таблица.ДокументВыдачи,
	|		Таблица.ПредварительныйПереходПраваСобственности
	|	ИЗ
	|		РегистрНакопления.РеализованныеСИЗ КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.ХарактеристикаНоменклатуры,
	|	ТаблицаИзменений.ВидВыдачиСИЗ,
	|	ТаблицаИзменений.СкладВыдачи,
	|	ТаблицаИзменений.СкладОтправитель,
	|	ТаблицаИзменений.МВЗ,
	|	ТаблицаИзменений.Поставщик,
	|	ТаблицаИзменений.ДокументВыдачи,
	|	ТаблицаИзменений.ПредварительныйПереходПраваСобственности
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияРеализованныеСИЗПередЗаписью";
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияРеализованныеСИЗИзменение", Выборка.Количество > 0);
	
КонецПроцедуры