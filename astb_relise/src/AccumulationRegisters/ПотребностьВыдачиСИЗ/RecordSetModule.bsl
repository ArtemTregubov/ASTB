Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//если есть движения по потребности в будущем, не записываем набор записей
	Если Не РольДоступна("АдминистраторСистемы") И ПроведениеСервер.ЕстьДвиженияПоПотребностиВБудущем(ЭтотОбъект) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
	    Возврат;
	КонецЕсли;
	
	ТребуетсяКонтроль = Истина;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

	// Текущее состояние набора помещается во временную таблицу "ДвиженияПотребностьВыдачиСИЗПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Организация КАК Организация,
	|	Таблица.Сотрудник КАК Сотрудник,
	|	Таблица.НоменклатураНормы КАК НоменклатураНормы,
	|	Таблица.НормаВыдачи КАК НормаВыдачи,
	|	Таблица.ДатаПотребности КАК ДатаПотребности,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.Количество
	|		ИНАЧЕ -Таблица.Количество
	|	КОНЕЦ КАК КоличествоПередЗаписью
	|ПОМЕСТИТЬ ДвиженияПотребностьВыдачиСИЗПередЗаписью
	|ИЗ
	|	РегистрНакопления.ПотребностьВыдачиСИЗ КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И (НЕ &ЭтоНовый)";
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;

	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Организация КАК Организация,
	|	ТаблицаИзменений.Сотрудник КАК Сотрудник,
	|	ТаблицаИзменений.НоменклатураНормы КАК НоменклатураНормы,
	|	ТаблицаИзменений.НормаВыдачи КАК НормаВыдачи,
	|	ТаблицаИзменений.ДатаПотребности КАК ДатаПотребности,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) КАК КоличествоИзменение
	|ПОМЕСТИТЬ ДвиженияПотребностьВыдачиСИЗИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Организация КАК Организация,
	|		Таблица.Сотрудник КАК Сотрудник,
	|		Таблица.НоменклатураНормы КАК НоменклатураНормы,
	|		Таблица.НормаВыдачи КАК НормаВыдачи,
	|		Таблица.ДатаПотребности КАК ДатаПотребности,
	|		Таблица.КоличествоПередЗаписью КАК КоличествоИзменение	
	|	ИЗ
	|		ДвиженияПотребностьВыдачиСИЗПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Организация,
	|		Таблица.Сотрудник,
	|		Таблица.НоменклатураНормы,
	|		Таблица.НормаВыдачи,
	|		Таблица.ДатаПотребности,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.Количество
	|			ИНАЧЕ Таблица.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ПотребностьВыдачиСИЗ КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Сотрудник,
	|	ТаблицаИзменений.НоменклатураНормы,
	|	ТаблицаИзменений.НормаВыдачи,
	|	ТаблицаИзменений.ДатаПотребности
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ТаблицаИзменений.КоличествоИзменение) > 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияПотребностьВыдачиСИЗПередЗаписью";
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияПотребностьВыдачиСИЗИзменение", Выборка.Количество > 0);

КонецПроцедуры
