Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТребуетсяКонтроль = Истина;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

	// Текущее состояние набора помещается во временную таблицу "ДвиженияВыданныеСредстваЗащитыПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Организация КАК Организация,
	|	Таблица.Сотрудник КАК Сотрудник,
	|	Таблица.НормаВыдачи КАК НормаВыдачи,
	|	Таблица.НоменклатураНормы КАК НоменклатураНормы,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	Таблица.ДатаВыдачи,
	|	Таблица.ПроцентИзноса КАК ПроцентИзноса,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.Количество
	|		ИНАЧЕ -Таблица.Количество
	|	КОНЕЦ КАК КоличествоПередЗаписью
	|ПОМЕСТИТЬ ДвиженияВыданныеСредстваЗащитыПередЗаписью
	|ИЗ
	|	РегистрНакопления.ВыданныеСредстваЗащиты КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый";
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;

	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Организация КАК Организация,
	|	ТаблицаИзменений.Сотрудник КАК Сотрудник,
	|	ТаблицаИзменений.НормаВыдачи КАК НормаВыдачи,
	|	ТаблицаИзменений.НоменклатураНормы КАК НоменклатураНормы,
	|	ТаблицаИзменений.Номенклатура КАК Номенклатура,
	|	ТаблицаИзменений.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТаблицаИзменений.ДатаВыдачи,
	|	ТаблицаИзменений.ПроцентИзноса,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) КАК КоличествоИзменение
	|ПОМЕСТИТЬ ДвиженияВыданныеСредстваЗащитыИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Организация КАК Организация,
	|		Таблица.Сотрудник КАК Сотрудник,
	|		Таблица.НормаВыдачи КАК НормаВыдачи,
	|		Таблица.НоменклатураНормы КАК НоменклатураНормы,
	|		Таблица.Номенклатура КАК Номенклатура,
	|		Таблица.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|		Таблица.ДатаВыдачи КАК ДатаВыдачи,
	|		Таблица.ПроцентИзноса КАК ПроцентИзноса,
	|		Таблица.КоличествоПередЗаписью КАК КоличествоИзменение
	|	ИЗ
	|		ДвиженияВыданныеСредстваЗащитыПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Организация,
	|		Таблица.Сотрудник,
	|		Таблица.НормаВыдачи,
	|		Таблица.НоменклатураНормы,
	|		Таблица.Номенклатура,
	|		Таблица.ХарактеристикаНоменклатуры,
	|		Таблица.ДатаВыдачи,
	|		Таблица.ПроцентИзноса,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.Количество
	|			ИНАЧЕ Таблица.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ВыданныеСредстваЗащиты КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Сотрудник,
	|	ТаблицаИзменений.НормаВыдачи,
	|	ТаблицаИзменений.НоменклатураНормы,
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.ХарактеристикаНоменклатуры,
	|	ТаблицаИзменений.ДатаВыдачи,
	|	ТаблицаИзменений.ПроцентИзноса
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияВыданныеСредстваЗащитыПередЗаписью";
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияВыданныеСредстваЗащитыИзменение", Выборка.Количество > 0);

КонецПроцедуры
