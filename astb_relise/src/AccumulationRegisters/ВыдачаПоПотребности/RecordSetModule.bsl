Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТребуетсяКонтроль = Истина;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

	// Текущее состояние набора помещается во временную таблицу "ДвиженияВыдачаПоПотребностиПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Организация КАК Организация,
	|	Таблица.Сотрудник КАК Сотрудник,
	|	Таблица.НормаВыдачи КАК НормаВыдачи,
	|	Таблица.НоменклатураНормы КАК НоменклатураНормы,
	|	Таблица.ДатаПотребности КАК ДатаПотребности,
	|	Таблица.ПроцентИзноса КАК ПроцентИзноса,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.Количество
	|		ИНАЧЕ -Таблица.Количество
	|	КОНЕЦ КАК КоличествоПередЗаписью,
	|	Таблица.ДатаВыдачи
	|ПОМЕСТИТЬ ДвиженияВыдачаПоПотребностиПередЗаписью
	|ИЗ
	|	РегистрНакопления.ВыдачаПоПотребности КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый";
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;

	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Организация КАК Организация,
	|	ТаблицаИзменений.Сотрудник КАК Сотрудник,
	|	ТаблицаИзменений.НормаВыдачи КАК НормаВыдачи,
	|	ТаблицаИзменений.НоменклатураНормы КАК НоменклатураНормы,
	|	ТаблицаИзменений.ДатаПотребности КАК ДатаПотребности,
	|	ТаблицаИзменений.ПроцентИзноса КАК ПроцентИзноса,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) КАК КоличествоИзменение,
	|	ТаблицаИзменений.ДатаВыдачи
	|ПОМЕСТИТЬ ДвиженияВыдачаПоПотребностиИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Организация КАК Организация,
	|		Таблица.Сотрудник КАК Сотрудник,
	|		Таблица.НормаВыдачи КАК НормаВыдачи,
	|		Таблица.НоменклатураНормы КАК НоменклатураНормы,
	|		Таблица.ДатаПотребности КАК ДатаПотребности,
	|		Таблица.ПроцентИзноса КАК ПроцентИзноса,
	|		Таблица.КоличествоПередЗаписью КАК КоличествоИзменение,
	|		Таблица.ДатаВыдачи КАК ДатаВыдачи
	|	ИЗ
	|		ДвиженияВыдачаПоПотребностиПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Организация,
	|		Таблица.Сотрудник,
	|		Таблица.НормаВыдачи,
	|		Таблица.НоменклатураНормы,
	|		Таблица.ДатаПотребности,
	|		Таблица.ПроцентИзноса,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.Количество
	|			ИНАЧЕ Таблица.Количество
	|		КОНЕЦ,
	|		Таблица.ДатаВыдачи
	|	ИЗ
	|		РегистрНакопления.ВыдачаПоПотребности КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Сотрудник,
	|	ТаблицаИзменений.НормаВыдачи,
	|	ТаблицаИзменений.НоменклатураНормы,
	|	ТаблицаИзменений.ДатаПотребности,
	|	ТаблицаИзменений.ПроцентИзноса,
	|	ТаблицаИзменений.ДатаВыдачи
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияВыдачаПоПотребностиПередЗаписью";
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияВыдачаПоПотребностиИзменение", Выборка.Количество > 0);

КонецПроцедуры
