&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация", Объект.Организация));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуДокументаНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Документы.УстановкаУсловийРаботыСотрудника.ЗаполнитьТабличнуюЧасть(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Объект.УсловияРаботы.Количество() > 0 Тогда
		
		Текст = "ru = ""Табличная часть будет очищена! Продолжить выполнение операции?"";";
		
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтаФорма, Параметры);
		
		ПоказатьВопрос(Оповещение,НСтр(Текст),РежимДиалогаВопрос.ДаНет);
		
	Иначе	
		
		ЗаполнитьТаблицуДокументаНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

    Объект.УсловияРаботы.Очистить();

	ЗаполнитьТаблицуДокументаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	ИзменитьФлажкиНаСервере(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	ИзменитьФлажкиНаСервере(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьФлажкиНаСервере(ЗначениеФлажка)
	
	Документ = РеквизитФормыВЗначение("Объект");
	
	ТаблицаДокумента = Документ.УсловияРаботы.Выгрузить();
	ТаблицаДокумента.ЗаполнитьЗначения(ЗначениеФлажка, "Использовать");
	Документ.УсловияРаботы.Загрузить(ТаблицаДокумента);
	
	ЗначениеВРеквизитФормы(Документ,"Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЭтаФорма.УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация", Объект.Организация));
	
КонецПроцедуры


