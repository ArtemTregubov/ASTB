#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктВыполненныхРабот") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"АктВыполненныхРабот",
			"Акт выполненных работ",
			СформироватьПечатнуюФормуАктаВыполненныхРабот(МассивОбъектов, ОбъектыПечати),,"Документ.ВыполненныеРаботыПоРемонту.ПФ_MXL_АктВыполненныхРабот");
				
	КонецЕсли;	
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуАктаВыполненныхРабот(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ВыполненныеРаботыПоРемонту_АктВыполненныхРабот";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыполненныеРаботыПоРемонту.Ссылка КАК Ссылка,
	|	ВыполненныеРаботыПоРемонту.Номер КАК Номер,
	|	ВыполненныеРаботыПоРемонту.Дата КАК Дата,
	|	ВыполненныеРаботыПоРемонту.Организация.НаименованиеПолное КАК ПредставлениеЗаказчика,
	|	ВыполненныеРаботыПоРемонту.Организация.Префикс КАК Префикс,
	|	ВыполненныеРаботыПоРемонту.Контрагент.НаименованиеПолное КАК ПредставлениеИспольнителя,
	|	ВыполненныеРаботыПоРемонту.СуммаДокумента КАК СуммаДокумента,
	|	ВыполненныеРаботыПоРемонту.НачалоПериода КАК НачалоПериода,
	|	ВыполненныеРаботыПоРемонту.КонецПериода КАК КонецПериода
	|ИЗ
	|	Документ.ВыполненныеРаботыПоРемонту КАК ВыполненныеРаботыПоРемонту
	|ГДЕ
	|	ВыполненныеРаботыПоРемонту.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыполненныеРаботыПоРемонтуДокументы.Ссылка КАК Ссылка,
	|	РемонтСредствЗащитыРемонт.ВидРемонта КАК ВидРемонта,
	|	СУММА(РемонтСредствЗащитыРемонт.Количество) КАК Количество,
	|	РемонтСредствЗащитыРемонт.Цена КАК Цена,
	|	СУММА(РемонтСредствЗащитыРемонт.Сумма) КАК Сумма
	|ИЗ
	|	Документ.ВыполненныеРаботыПоРемонту.Документы КАК ВыполненныеРаботыПоРемонтуДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РемонтСредствЗащиты.Ремонт КАК РемонтСредствЗащитыРемонт
	|		ПО ВыполненныеРаботыПоРемонтуДокументы.ДокументРемонта = РемонтСредствЗащитыРемонт.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыполненныеРаботыПоРемонтуДокументы.Ссылка,
	|	РемонтСредствЗащитыРемонт.ВидРемонта,
	|	РемонтСредствЗащитыРемонт.Цена
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	РемонтСредствЗащитыРемонт.ВидРемонта.Наименование
	|ИТОГИ ПО
	|	Ссылка";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВыполненныеРаботыПоРемонту.ПФ_MXL_АктВыполненныхРабот");
	
	ПакетЗапросов 		= Запрос.ВыполнитьПакет();
	ДанныеПечати 		= ПакетЗапросов[0].Выбрать();
	ВыборкаПоДокументам = ПакетЗапросов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если НЕ ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Ссылка", ДанныеПечати.Ссылка)) Тогда
			Продолжить;
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьМакета, ДанныеПечати.Ссылка);
		
		ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
		
		ОбластьМакета.Параметры.ТекстЗаголовка = ПроцедурыРаботыСНормамиСервер.СформироватьЗаголовокДокумента(Новый Структура("Номер, Дата, Префикс", ДанныеПечати.Номер, ДанныеПечати.Дата, ДанныеПечати.Префикс), "Акт о приемке выполненных работ") + " за период с " + Формат(ДанныеПечати.НачалоПериода,"ДЛФ=DD") + " по " + Формат(ДанныеПечати.КонецПериода,"ДЛФ=DD");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		
		Сч = 1;
		
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			ОбластьМакета = Макет.ПолучитьОбласть("Строка");
			ОбластьМакета.Параметры.Заполнить(ВыборкаПоТоварам);
			ОбластьМакета.Параметры.НомерСтроки = Сч;
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			Сч = Сч + 1;
			
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ТекстИтоговойСтроки = НСтр("ru = 'Всего наименований %ВсегоНаименований% на сумму %ВсегоСумма%'");
		ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%ВсегоНаименований%", Сч - 1);
		ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%ВсегоСумма%", Формат(ДанныеПечати.СуммаДокумента,"ЧЦ=15; ЧДЦ=2; ЧГ=0"));
		ОбластьМакета.Параметры.ИтоговаяСтрока = ТекстИтоговойСтроки;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
	
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//Акт выполненных работ
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ВыполненныеРаботыПоРемонту";
	КомандаПечати.Идентификатор = "АктВыполненныхРабот";
	КомандаПечати.Представление = НСтр("ru = 'Акт выполненных работ'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуДокумента(ТекущийОбъект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВыполненныеРаботыПоРемонтуДокументы.ДокументРемонта КАК ДокументРемонта
	|ПОМЕСТИТЬ ВТ_ЗарегистрированныеРемонты
	|ИЗ
	|	Документ.ВыполненныеРаботыПоРемонту.Документы КАК ВыполненныеРаботыПоРемонтуДокументы
	|ГДЕ
	|	НЕ ВыполненныеРаботыПоРемонтуДокументы.Ссылка = &Ссылка
	|	И ВыполненныеРаботыПоРемонтуДокументы.ДокументРемонта.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ВыполненныеРаботыПоРемонтуДокументы.ДокументРемонта.Организация = &Организация
	|	И ВыполненныеРаботыПоРемонтуДокументы.ДокументРемонта.Контрагент = &Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РемонтСредствЗащиты.Ссылка КАК ДокументРемонта,
	|	РемонтСредствЗащиты.СуммаДокумента КАК Сумма
	|ИЗ
	|	Документ.РемонтСредствЗащиты КАК РемонтСредствЗащиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗарегистрированныеРемонты КАК ВТ_ЗарегистрированныеРемонты
	|		ПО РемонтСредствЗащиты.Ссылка = ВТ_ЗарегистрированныеРемонты.ДокументРемонта
	|ГДЕ
	|	РемонтСредствЗащиты.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И РемонтСредствЗащиты.Организация = &Организация
	|	И РемонтСредствЗащиты.Контрагент = &Контрагент
	|	И ВТ_ЗарегистрированныеРемонты.ДокументРемонта ЕСТЬ NULL
	|	И НЕ РемонтСредствЗащиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРемонтаСИЗ.ПередачаВРемонт)
	|	И РемонтСредствЗащиты.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	РемонтСредствЗащиты.Дата";
	
	Запрос.УстановитьПараметр("Ссылка",			ТекущийОбъект.Ссылка);
	Запрос.УстановитьПараметр("Организация",	ТекущийОбъект.Организация);
	Запрос.УстановитьПараметр("ДатаНачала",		НачалоДня(ТекущийОбъект.НачалоПериода));
	Запрос.УстановитьПараметр("ДатаОкончания",	КонецДня(ТекущийОбъект.КонецПериода));
	Запрос.УстановитьПараметр("Контрагент",		ТекущийОбъект.Контрагент);
	
	ТекущийОбъект.Документы.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

#КонецЕсли