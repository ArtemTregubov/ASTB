#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	СуммаДокумента = Товары.Итог("Сумма");
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.ПроверитьДатыПодчиненныхДокументовУстановкиУсловий(Ссылка,ДополнительныеСвойства,Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	   	НачалоЗамера = ОценкаПроизводительности.НачатьЗамерВремени();	

	
	УстановитьПривилегированныйРежим(Истина);
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	Документы.ВыдачаСредствЗащитыСотруднику.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ЗапасыСерверПереопределяемый.ОтразитьОстаткиНоменклатуры(ДополнительныеСвойства, Движения, Отказ);
	
	Если НЕ ПредварительнаяСборка Тогда
		
		ЗапасыСерверПереопределяемый.ОтразитьИсторияОбращенийНаСклад(ДополнительныеСвойства, Движения, Отказ);
		Если НЕ ВидОперации = Перечисления.ВидыОперацийВыдачиСИЗ.ПредварительнаяЗаявка Тогда
			ЗапасыСерверПереопределяемый.ОтразитьВыданныеСредстваЗащиты(ДополнительныеСвойства, Движения, Отказ);
			ЗапасыСерверПереопределяемый.ОтразитьШтрихкодыНоменклатурыДляУчетаЧистки(ДополнительныеСвойства, Движения, Отказ);
			ЗапасыСерверПереопределяемый.ОтразитьКодыМаркировкиВыданныхСредствЗащиты(ДополнительныеСвойства, Движения, Отказ);
			Если НЕ (ВидВыдачиСИЗ = Перечисления.ВидыВыдачиСИЗ.ПроизвольнаяВыдача ИЛИ ВидВыдачиСИЗ = Перечисления.ВидыВыдачиСИЗ.УпрощеннаяВыдача) Тогда
				Если НЕ Организация.ИспользоватьГруппыИЛИ Тогда
					ПроведениеСервер.ПроверитьСоответствиеВыдачиПотребности(ЭтотОбъект,Отказ,"Товары");
				КонецЕсли;
				ЗапасыСерверПереопределяемый.ОтразитьПотребностьВыдачиСИЗ(ДополнительныеСвойства, Движения, Отказ);
				ЗапасыСерверПереопределяемый.ОтразитьВыдачуПоПотребности(ДополнительныеСвойства, Движения, Отказ);
				//АсТБ_Alexey_79639_********************************************************************
				ЗапасыСерверПереопределяемый.ОтразитьСредстваЗащитыКПолучению(ДополнительныеСвойства, Движения, Отказ);
				//АсТБ_Alexey_79639_********************************************************************
			КонецЕсли;
			Если ВидВыдачиСИЗ = Перечисления.ВидыВыдачиСИЗ.УпрощеннаяВыдача Тогда
				ЗапасыСерверПереопределяемый.ОтразитьВыданныеСредстваЗащиты(ДополнительныеСвойства, Движения, Отказ);
				ЗапасыСерверПереопределяемый.ОтразитьСрокиНоскиПоУпрощеннойВыдаче(ДополнительныеСвойства, Движения, Отказ);
			КонецЕсли;	
		КонецЕсли;
		
	КонецЕсли;

	СформироватьСписокРегистровДляКонтроля();
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	Если НЕ Отказ Тогда 
		ОценкаПроизводительности.ЗакончитьЗамерВремени("Док_ВыдачаСредствЗащитыСотруднику_ПроведениеСервер",НачалоЗамера);
	КонецЕсли;

КонецПроцедуры

Процедура СформироватьСписокРегистровДляКонтроля()

	Массив = Новый Массив;
	//Контроль выполняется при проведении\отмене проведения не нового документа.
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Если НЕ Склад.РазрешитьОтрицательныеОстатки Тогда 
			Массив.Добавить(Движения.ОстаткиНоменклатуры);
		КонецЕсли;
		Если НЕ (ВидВыдачиСИЗ = Перечисления.ВидыВыдачиСИЗ.ПроизвольнаяВыдача ИЛИ ВидВыдачиСИЗ = Перечисления.ВидыВыдачиСИЗ.УпрощеннаяВыдача) Тогда
			Массив.Добавить(Движения.ВыдачаПоПотребности);
			Массив.Добавить(Движения.ПотребностьВыдачиСИЗ);
		КонецЕсли;
	КонецЕсли;

	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроведениеСервер.ПроверитьЗаполнениеНоменклатуры(ЭтотОбъект,Отказ,"Товары");
	ПроведениеСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект,Отказ,"Товары");
	ПроведениеСервер.ПроверитьНулевоеКоличество(ЭтотОбъект,Отказ,"Товары");
	
	Если НЕ ЭтотОбъект.ПредварительнаяСборка И ПолучитьФункциональнуюОпцию("ИспользоватьМаркировку") Тогда
		Если НЕ (ЗначениеЗаполнено(ДокументОснование) И ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ВыдачаСредствЗащитыСотруднику")) Тогда
			ПроведениеСервер.ПроверитьЗаполнениеКодовМаркировки(ЭтотОбъект,Отказ);
		КонецЕсли;
	КонецЕсли;	
	
	Если ВидВыдачиСИЗ = Перечисления.ВидыВыдачиСИЗ.ПерсональнаяВыдача Тогда
		ПроведениеСервер.ПроверитьЗаполнениеКоличества(ЭтотОбъект,Отказ,"Товары");
		ПроведениеСервер.ПроверитьЗаполнениеПричиныНевыдачи(ЭтотОбъект,Отказ,"Товары");
	ИначеЕсли ВидВыдачиСИЗ = Перечисления.ВидыВыдачиСИЗ.ПроизвольнаяВыдача Тогда
		ПроведениеСервер.ПроверитьЗаполнениеПричиныНевыдачи(ЭтотОбъект,Отказ,"Товары");
		ПроверяемыеРеквизиты.Добавить("ДокументОснование");
		//проверка убрана 07.09.2018 по просьбе Щербакова Григория (Карелия-Восток-Сервис)
		//ПроведениеСервер.ПроверитьСоответствиеКоличестваЗаявкеНаПроизвольнуюВыдачу(ЭтотОбъект,Отказ);
	ИначеЕсли ВидВыдачиСИЗ = Перечисления.ВидыВыдачиСИЗ.КоллективнаяВыдача Тогда
		ПроведениеСервер.ПроверитьЗаполнениеПричиныНевыдачи(ЭтотОбъект,Отказ,"Товары");
		Если ЭтотОбъект.Организация.ИспользоватьПредварительныеЗаявкиНаКоллективнуюВыдачу Тогда
			Если ЭтотОбъект.ВидОперации = Перечисления.ВидыОперацийВыдачиСИЗ.ПредварительнаяЗаявка Тогда
				ПроверяемыеРеквизиты.Добавить("СкладОтправитель");
			КонецЕсли;
		Иначе
			Если НЕ Склад.АвтоматизированнаяВыдача Тогда
				ПроверяемыеРеквизиты.Добавить("СкладОтправитель");
				//+++АСТБ_Горюшин_Алексей_20872
				ПроверяемыеРеквизиты.Добавить("МОЛСкладаОтправителя");
				Если НЕ ЗначениеЗаполнено( ЭтотОбъект.МОЛСкладаОтправителя) Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Заполните МОЛ у склада-отправителя!");
				КонецЕсли;
				//---АСТБ_Горюшин_Алексей_20872
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ВидВыдачиСИЗ = Перечисления.ВидыВыдачиСИЗ.УпрощеннаяВыдача Тогда
		ПроведениеСервер.ПроверитьЗаполнениеНоменклатурыНормы(ЭтотОбъект,Отказ,"Товары");
		ПроведениеСервер.ПроверитьЗаполнениеПериодичностиВыдачи(ЭтотОбъект,Отказ,"Товары");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		
		Документы.ВыдачаСредствЗащитыСотруднику.ЗаполнитьДокумент(ДанныеЗаполнения,ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли

