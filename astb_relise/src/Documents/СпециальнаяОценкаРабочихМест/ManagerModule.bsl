#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СпециальнаяОценкаРабочихМестВредныеИОпасныеПроизводственныеФакторы.Фактор КАК Фактор,
	|	""Ф"" КАК ФакторИлиРабота,
	|	СпециальнаяОценкаРабочихМестВредныеИОпасныеПроизводственныеФакторы.Значение КАК Значение
	|ПОМЕСТИТЬ ВТ_Факторы
	|ИЗ
	|	Документ.СпециальнаяОценкаРабочихМест.ВредныеИОпасныеПроизводственныеФакторы КАК СпециальнаяОценкаРабочихМестВредныеИОпасныеПроизводственныеФакторы
	|ГДЕ
	|	СпециальнаяОценкаРабочихМестВредныеИОпасныеПроизводственныеФакторы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпециальнаяОценкаРабочихМестВредныеИОпасныеПроизводственныеРаботы.Работа КАК Работа,
	|	""Р"" КАК ФакторИлиРабота,
	|	СпециальнаяОценкаРабочихМестВредныеИОпасныеПроизводственныеРаботы.Значение КАК Значение
	|ПОМЕСТИТЬ ВТ_Работы
	|ИЗ
	|	Документ.СпециальнаяОценкаРабочихМест.ВредныеИОпасныеПроизводственныеРаботы КАК СпециальнаяОценкаРабочихМестВредныеИОпасныеПроизводственныеРаботы
	|ГДЕ
	|	СпециальнаяОценкаРабочихМестВредныеИОпасныеПроизводственныеРаботы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СпециальнаяОценкаРабочихМестРабочиеМеста.Ссылка.Дата КАК Период,
	|	СпециальнаяОценкаРабочихМестРабочиеМеста.Ссылка.Организация КАК Организация,
	|	СпециальнаяОценкаРабочихМестРабочиеМеста.Подразделение КАК Подразделение,
	|	СпециальнаяОценкаРабочихМестРабочиеМеста.Должность КАК Должность,
	|	СпециальнаяОценкаРабочихМестРабочиеМеста.РабочееМесто КАК РабочееМесто,
	|	0 КАК СлужебноеИзмерение,
	|	ВТ_Работы.ФакторИлиРабота КАК ФакторИлиРабота,
	|	ВТ_Работы.Работа КАК ФакторРабота,
	|	ВТ_Работы.Значение КАК Значение
	|ПОМЕСТИТЬ ВТ_Результат
	|ИЗ
	|	Документ.СпециальнаяОценкаРабочихМест.РабочиеМеста КАК СпециальнаяОценкаРабочихМестРабочиеМеста,
	|	ВТ_Работы КАК ВТ_Работы
	|ГДЕ
	|	СпециальнаяОценкаРабочихМестРабочиеМеста.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СпециальнаяОценкаРабочихМестРабочиеМеста.Ссылка.Дата,
	|	СпециальнаяОценкаРабочихМестРабочиеМеста.Ссылка.Организация,
	|	СпециальнаяОценкаРабочихМестРабочиеМеста.Подразделение,
	|	СпециальнаяОценкаРабочихМестРабочиеМеста.Должность,
	|	СпециальнаяОценкаРабочихМестРабочиеМеста.РабочееМесто,
	|	0,
	|	ВТ_Факторы.ФакторИлиРабота,
	|	ВТ_Факторы.Фактор,
	|	ВТ_Факторы.Значение
	|ИЗ
	|	Документ.СпециальнаяОценкаРабочихМест.РабочиеМеста КАК СпециальнаяОценкаРабочихМестРабочиеМеста,
	|	ВТ_Факторы КАК ВТ_Факторы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Результат.Период КАК Период,
	|	ВТ_Результат.Организация КАК Организация,
	|	ВТ_Результат.Подразделение КАК Подразделение,
	|	ВТ_Результат.Должность КАК Должность,
	|	ВТ_Результат.РабочееМесто КАК РабочееМесто,
	|	ВТ_Результат.СлужебноеИзмерение КАК СлужебноеИзмерение,
	|	ВТ_Результат.ФакторИлиРабота КАК ФакторИлиРабота,
	|	ВТ_Результат.ФакторРабота КАК ФакторРабота,
	|	ВТ_Результат.Значение КАК Значение
	|ИЗ
	|	ВТ_Результат КАК ВТ_Результат
	|
	|УПОРЯДОЧИТЬ ПО
	|	Подразделение,
	|	Должность,
	|	РабочееМесто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Результат.Подразделение КАК Подразделение,
	|	ВТ_Результат.Должность КАК Должность,
	|	ВТ_Результат.РабочееМесто КАК РабочееМесто
	|ИЗ
	|	ВТ_Результат КАК ВТ_Результат
	|
	|УПОРЯДОЧИТЬ ПО
	|	Подразделение,
	|	Должность,
	|	РабочееМесто";
	
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаДляДвижений = Результат[3].Выгрузить();
	ТаблицаДляСчетчика = Результат[4].Выгрузить();
	
	Для Каждого СтрокаТаблицыДляСчетчика Из ТаблицаДляСчетчика Цикл
		
		Сч = 1;
		
		СтруктураПоиска = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаТаблицыДляСчетчика);
		
		НайденныеСтроки = ТаблицаДляДвижений.НайтиСтроки(СтруктураПоиска);
		
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			
			НайденнаяСтрока.СлужебноеИзмерение = Сч;
			
			Сч = Сч + 1;
			
		КонецЦикла;	
			
	КонецЦикла;
	
	ТаблицыДляДвижений.Вставить("ТаблицаВредныеИОпасныеФакторыИРаботыНаРабочихМестах", ТаблицаДляДвижений);
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоВреднымИОпаснымФакторамИРаботам(ТекущийОбъект) Экспорт
	
	МассивВредныхФакторов 	= ТекущийОбъект.ВредныеИОпасныеПроизводственныеФакторы.ВыгрузитьКолонку("Фактор");
	МассивВредныхРабот 		= ТекущийОбъект.ВредныеИОпасныеПроизводственныеРаботы.ВыгрузитьКолонку("Работа");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПериодичностьВыдачиСИЗ.Ссылка КАК Периодичность,
	|	ПериодичностьВыдачиСИЗ.Приоритет КАК Приоритет
	|ПОМЕСТИТЬ ВТ_Периодичность
	|ИЗ
	|	Справочник.ПериодичностьВыдачиСИЗ КАК ПериодичностьВыдачиСИЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВредныеИОпасныеПроизводственныеРаботыВрачи.Врач КАК Врач,
	|	ВредныеИОпасныеПроизводственныеРаботыВрачи.ПоРекомендации КАК ПоРекомендации,
	|	ВредныеИОпасныеПроизводственныеРаботыВрачи.Периодичность.Приоритет КАК ПриоритетПериодичности
	|ПОМЕСТИТЬ ВТ_Врачи
	|ИЗ
	|	Справочник.ВредныеИОпасныеПроизводственныеРаботы.Врачи КАК ВредныеИОпасныеПроизводственныеРаботыВрачи
	|ГДЕ
	|	ВредныеИОпасныеПроизводственныеРаботыВрачи.Ссылка В(&МассивРабот)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВредныеИОпасныеПроизводственныеФакторыВрачи.Врач,
	|	ВредныеИОпасныеПроизводственныеФакторыВрачи.ПоРекомендации,
	|	ВредныеИОпасныеПроизводственныеФакторыВрачи.Периодичность.Приоритет
	|ИЗ
	|	Справочник.ВредныеИОпасныеПроизводственныеФакторы.Врачи КАК ВредныеИОпасныеПроизводственныеФакторыВрачи
	|ГДЕ
	|	ВредныеИОпасныеПроизводственныеФакторыВрачи.Ссылка В(&МассивФакторов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВредныеИОпасныеПроизводственныеРаботыЛабораторныеИФункциональныеИсследования.Исследование КАК Исследование,
	|	ВредныеИОпасныеПроизводственныеРаботыЛабораторныеИФункциональныеИсследования.ПоРекомендации КАК ПоРекомендации,
	|	ВредныеИОпасныеПроизводственныеРаботыЛабораторныеИФункциональныеИсследования.Периодичность.Приоритет КАК ПриоритетПериодичности
	|ПОМЕСТИТЬ ВТ_Исследования
	|ИЗ
	|	Справочник.ВредныеИОпасныеПроизводственныеРаботы.ЛабораторныеИФункциональныеИсследования КАК ВредныеИОпасныеПроизводственныеРаботыЛабораторныеИФункциональныеИсследования
	|ГДЕ
	|	ВредныеИОпасныеПроизводственныеРаботыЛабораторныеИФункциональныеИсследования.Ссылка В(&МассивРабот)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВредныеИОпасныеПроизводственныеФакторыЛабораторныеИФункциональныеИсследования.Исследование,
	|	ВредныеИОпасныеПроизводственныеФакторыЛабораторныеИФункциональныеИсследования.ПоРекомендации,
	|	ВредныеИОпасныеПроизводственныеФакторыЛабораторныеИФункциональныеИсследования.Периодичность.Приоритет
	|ИЗ
	|	Справочник.ВредныеИОпасныеПроизводственныеФакторы.ЛабораторныеИФункциональныеИсследования КАК ВредныеИОпасныеПроизводственныеФакторыЛабораторныеИФункциональныеИсследования
	|ГДЕ
	|	ВредныеИОпасныеПроизводственныеФакторыЛабораторныеИФункциональныеИсследования.Ссылка В(&МассивФакторов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВредныеИОпасныеПроизводственныеРаботыДополнительныеМедицинскиеПротивопоказания.Противопоказание КАК Противопоказание
	|ПОМЕСТИТЬ ВТ_Противопоказания
	|ИЗ
	|	Справочник.ВредныеИОпасныеПроизводственныеРаботы.ДополнительныеМедицинскиеПротивопоказания КАК ВредныеИОпасныеПроизводственныеРаботыДополнительныеМедицинскиеПротивопоказания
	|ГДЕ
	|	ВредныеИОпасныеПроизводственныеРаботыДополнительныеМедицинскиеПротивопоказания.Ссылка В(&МассивРабот)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВредныеИОпасныеПроизводственныеФакторыДополнительныеМедицинскиеПротивопоказания.Противопоказание
	|ИЗ
	|	Справочник.ВредныеИОпасныеПроизводственныеФакторы.ДополнительныеМедицинскиеПротивопоказания КАК ВредныеИОпасныеПроизводственныеФакторыДополнительныеМедицинскиеПротивопоказания
	|ГДЕ
	|	ВредныеИОпасныеПроизводственныеФакторыДополнительныеМедицинскиеПротивопоказания.Ссылка В(&МассивФакторов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Врачи.Врач КАК Врач,
	|	МИНИМУМ(ВТ_Врачи.ПоРекомендации) КАК ПоРекомендации,
	|	МАКСИМУМ(ВТ_Врачи.ПриоритетПериодичности) КАК ПриоритетПериодичности
	|ПОМЕСТИТЬ ВТ_Врачи_Результат
	|ИЗ
	|	ВТ_Врачи КАК ВТ_Врачи
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Врачи.Врач
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Исследования.Исследование КАК Исследование,
	|	МИНИМУМ(ВТ_Исследования.ПоРекомендации) КАК ПоРекомендации,
	|	МАКСИМУМ(ВТ_Исследования.ПриоритетПериодичности) КАК ПриоритетПериодичности
	|ПОМЕСТИТЬ ВТ_ИсследованияРезультат
	|ИЗ
	|	ВТ_Исследования КАК ВТ_Исследования
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Исследования.Исследование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ВТ_Врачи_Результат.Врач КАК Врач,
	|	ВТ_Врачи_Результат.ПоРекомендации КАК ПоРекомендации,
	|	ВТ_Периодичность.Периодичность КАК Периодичность
	|ИЗ
	|	ВТ_Врачи_Результат КАК ВТ_Врачи_Результат
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Периодичность КАК ВТ_Периодичность
	|		ПО ВТ_Врачи_Результат.ПриоритетПериодичности = ВТ_Периодичность.Приоритет
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_Врачи_Результат.Врач.Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ВТ_ИсследованияРезультат.Исследование КАК Исследование,
	|	ВТ_ИсследованияРезультат.ПоРекомендации КАК ПоРекомендации,
	|	ВТ_Периодичность.Периодичность КАК Периодичность
	|ИЗ
	|	ВТ_ИсследованияРезультат КАК ВТ_ИсследованияРезультат
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Периодичность КАК ВТ_Периодичность
	|		ПО ВТ_ИсследованияРезультат.ПриоритетПериодичности = ВТ_Периодичность.Приоритет
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_ИсследованияРезультат.Исследование.Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ВТ_Противопоказания.Противопоказание КАК Противопоказание
	|ИЗ
	|	ВТ_Противопоказания КАК ВТ_Противопоказания
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_Противопоказания.Противопоказание.Наименование";	
	
	Запрос.УстановитьПараметр("МассивРабот",	МассивВредныхРабот);
	Запрос.УстановитьПараметр("МассивФакторов",	МассивВредныхФакторов);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТекущийОбъект.Врачи.Загрузить(Результат[6].Выгрузить());
	ТекущийОбъект.ЛабораторныеИФункциональныеИсследования.Загрузить(Результат[7].Выгрузить());
	ТекущийОбъект.ДополнительныеМедицинскиеПротивопоказания.Загрузить(Результат[8].Выгрузить());	
	
КонецПроцедуры	

//процедура заполняет табличную часть "Рабочие места"
//Параметры:
//ТекущийОбъект - текущий документ-объект для заполнения
//МассивЗначений - массив выбранных значений
//ПараметрЗаполнения - строка, может принимать значения:
//1. "ПШР" 		- заполнение по подразделеням согласно штатному расписанию
//2. "ПЗРМ" 	- заполнение по подразделеням согласно занятым рабочим местам
//3. "ДШР" 		- заполнение по должностям согласно штатному расписанию
//4. "ДЗРМ" 	- заполнение по должностям согласно занятым рабочим местам
//5. "РМЗРМ" 	- заполнение по рабочим местам согласно занятым рабочим местам
Процедура ЗаполнитьРабочиеМеста(ТекущийОбъект, МассивЗначений, ПараметрЗаполнения) Экспорт
	
	ДатаАнализа = ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаАнализа",	ДатаАнализа);
	Запрос.УстановитьПараметр("Организация",	ТекущийОбъект.Организация);	
	Запрос.УстановитьПараметр("МассивЗначений",	МассивЗначений);
		
	Если ПараметрЗаполнения = "ПШР" Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ШтатноеРасписаниеСрезПоследних.Подразделение КАК Подразделение,
		|	ШтатноеРасписаниеСрезПоследних.Должность КАК Должность,
		|	ЗНАЧЕНИЕ(Справочник.РабочиеМестаАСТБ.ПустаяСсылка) КАК РабочееМесто
		|ИЗ
		|	РегистрСведений.ШтатноеРасписание.СрезПоследних(
		|			&ДатаАнализа,
		|			Организация = &Организация
		|				И Подразделение В (&МассивЗначений)) КАК ШтатноеРасписаниеСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	ШтатноеРасписаниеСрезПоследних.Подразделение.Наименование,
		|	ШтатноеРасписаниеСрезПоследних.Должность.Наименование";
		
	ИначеЕсли ПараметрЗаполнения = "ПЗРМ" Тогда	
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗанятыеРабочиеМестаОстатки.Подразделение КАК Подразделение,
		|	ЗанятыеРабочиеМестаОстатки.Должность КАК Должность,
		|	ЗанятыеРабочиеМестаОстатки.РабочееМесто КАК РабочееМесто
		|ИЗ
		|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
		|			&ДатаАнализа,
		|			Организация = &Организация
		|				И Подразделение В (&МассивЗначений)) КАК ЗанятыеРабочиеМестаОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗанятыеРабочиеМестаОстатки.Подразделение.Наименование,
		|	ЗанятыеРабочиеМестаОстатки.Должность.Наименование,
		|	ЗанятыеРабочиеМестаОстатки.РабочееМесто.Наименование";
		
	ИначеЕсли ПараметрЗаполнения = "ДШР" Тогда	
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ШтатноеРасписаниеСрезПоследних.Подразделение КАК Подразделение,
		|	ШтатноеРасписаниеСрезПоследних.Должность КАК Должность,
		|	ЗНАЧЕНИЕ(Справочник.РабочиеМестаАСТБ.ПустаяСсылка) КАК РабочееМесто
		|ИЗ
		|	РегистрСведений.ШтатноеРасписание.СрезПоследних(
		|			&ДатаАнализа,
		|			Организация = &Организация
		|				И Должность В (&МассивЗначений)) КАК ШтатноеРасписаниеСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	ШтатноеРасписаниеСрезПоследних.Подразделение.Наименование,
		|	ШтатноеРасписаниеСрезПоследних.Должность.Наименование";
		
	ИначеЕсли ПараметрЗаполнения = "ДЗРМ" Тогда	
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗанятыеРабочиеМестаОстатки.Подразделение КАК Подразделение,
		|	ЗанятыеРабочиеМестаОстатки.Должность КАК Должность,
		|	ЗанятыеРабочиеМестаОстатки.РабочееМесто КАК РабочееМесто
		|ИЗ
		|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
		|			&ДатаАнализа,
		|			Организация = &Организация
		|				И Должность В (&МассивЗначений)) КАК ЗанятыеРабочиеМестаОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗанятыеРабочиеМестаОстатки.Подразделение.Наименование,
		|	ЗанятыеРабочиеМестаОстатки.Должность.Наименование,
		|	ЗанятыеРабочиеМестаОстатки.РабочееМесто.Наименование";
				
	ИначеЕсли ПараметрЗаполнения = "РМЗРМ" Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗанятыеРабочиеМестаОстатки.Подразделение КАК Подразделение,
		|	ЗанятыеРабочиеМестаОстатки.Должность КАК Должность,
		|	ЗанятыеРабочиеМестаОстатки.РабочееМесто КАК РабочееМесто
		|ИЗ
		|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
		|			&ДатаАнализа,
		|			Организация = &Организация
		|				И РабочееМесто В (&МассивЗначений)) КАК ЗанятыеРабочиеМестаОстатки
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗанятыеРабочиеМестаОстатки.Подразделение.Наименование,
		|	ЗанятыеРабочиеМестаОстатки.Должность.Наименование,
		|	ЗанятыеРабочиеМестаОстатки.РабочееМесто.Наименование";
		
	Иначе
		
		Возврат;
		
	КонецЕсли;	
		
	ТекущийОбъект.РабочиеМеста.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры	

#КонецЕсли