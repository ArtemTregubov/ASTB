#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеФакторы.Ссылка.Дата КАК Период,
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеФакторы.Ссылка.Организация КАК Организация,
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеФакторы.Ссылка.Сотрудник КАК Сотрудник,
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеФакторы.Ссылка.МедицинскаяОрганизация КАК МедицинскаяОрганизация,
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеФакторы.Ссылка.ВидМедосмотра КАК ВидМедосмотра,
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеФакторы.Ссылка.НаправлениеНаМедицинскийОсмотр КАК Направление,
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеФакторы.Фактор КАК ФакторРабота,
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеФакторы.Противопоказания КАК Противопоказания
	|ИЗ
	|	Документ.МедицинскийОсмотр.ВредныеИОпасныеПроизводственныеФакторы КАК МедицинскийОсмотрВредныеИОпасныеПроизводственныеФакторы
	|ГДЕ
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеФакторы.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеРаботы.Ссылка.Дата,
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеРаботы.Ссылка.Организация,
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеРаботы.Ссылка.Сотрудник,
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеРаботы.Ссылка.МедицинскаяОрганизация,
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеРаботы.Ссылка.ВидМедосмотра,
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеРаботы.Ссылка.НаправлениеНаМедицинскийОсмотр,
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеРаботы.Работа,
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеРаботы.Противопоказания
	|ИЗ
	|	Документ.МедицинскийОсмотр.ВредныеИОпасныеПроизводственныеРаботы КАК МедицинскийОсмотрВредныеИОпасныеПроизводственныеРаботы
	|ГДЕ
	|	МедицинскийОсмотрВредныеИОпасныеПроизводственныеРаботы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МедицинскийОсмотр.Дата КАК Период,
	|	МедицинскийОсмотр.Организация КАК Организация,
	|	МедицинскийОсмотр.Сотрудник КАК Сотрудник,
	|	МедицинскийОсмотр.МедицинскаяОрганизация КАК МедицинскаяОрганизация,
	|	МедицинскийОсмотр.ВидМедосмотра КАК ВидМедосмотра,
	|	МедицинскийОсмотр.НаправлениеНаМедицинскийОсмотр КАК НаправлениеНаМедицинскийОсмотр,
	|	ЗНАЧЕНИЕ(Справочник.ВредныеИОпасныеПроизводственныеФакторы.ПустаяСсылка) КАК ФакторРабота,
	|	МедицинскийОсмотр.Противопоказания КАК Противопоказания
	|ИЗ
	|	Документ.МедицинскийОсмотр КАК МедицинскийОсмотр
	|ГДЕ
	|	МедицинскийОсмотр.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	
	Результат = Запрос.ВыполнитьПакет();
	ФакторыИРаботы = Результат[0].Выгрузить();
	Если ФакторыИРаботы.Количество() = 0 Тогда
		ТаблицыДляДвижений.Вставить("ТаблицаМедицинскиеОсмотрыСотрудников", Результат[1].Выгрузить());
	Иначе
		ТаблицыДляДвижений.Вставить("ТаблицаМедицинскиеОсмотрыСотрудников", ФакторыИРаботы);
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ЗаполнитьТаблицыДокумента(ТекущийОбъект) Экспорт
	
	ДатаАнализа = ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВредныеИОпасныеФакторыИРаботыНаРабочихМестахСрезПоследних.ФакторИлиРабота КАК ФакторИлиРабота,
	|	ВредныеИОпасныеФакторыИРаботыНаРабочихМестахСрезПоследних.ФакторРабота КАК ФакторРабота
	|ПОМЕСТИТЬ ВТ_Результат
	|ИЗ
	|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
	|			&ДатаАнализа,
	|			Организация = &Организация
	|				И Сотрудник = &Сотрудник) КАК ЗанятыеРабочиеМестаОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВредныеИОпасныеФакторыИРаботыНаРабочихМестах.СрезПоследних(&ДатаАнализа, Организация = &Организация) КАК ВредныеИОпасныеФакторыИРаботыНаРабочихМестахСрезПоследних
	|		ПО (ВредныеИОпасныеФакторыИРаботыНаРабочихМестахСрезПоследних.Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|				ИЛИ ЗанятыеРабочиеМестаОстатки.Подразделение = ВредныеИОпасныеФакторыИРаботыНаРабочихМестахСрезПоследних.Подразделение)
	|			И (ВредныеИОпасныеФакторыИРаботыНаРабочихМестахСрезПоследних.Должность = ЗНАЧЕНИЕ(Справочник.ДолжностиИПрофессии.ПустаяСсылка)
	|				ИЛИ ЗанятыеРабочиеМестаОстатки.Должность = ВредныеИОпасныеФакторыИРаботыНаРабочихМестахСрезПоследних.Должность)
	|			И (ВредныеИОпасныеФакторыИРаботыНаРабочихМестахСрезПоследних.РабочееМесто = ЗНАЧЕНИЕ(Справочник.РабочиеМестаАСТБ.ПустаяСсылка)
	|				ИЛИ ЗанятыеРабочиеМестаОстатки.РабочееМесто = ВредныеИОпасныеФакторыИРаботыНаРабочихМестахСрезПоследних.РабочееМесто)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Результат.ФакторРабота КАК Фактор
	|ИЗ
	|	ВТ_Результат КАК ВТ_Результат
	|ГДЕ
	|	ВТ_Результат.ФакторИлиРабота = ""Ф""
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_Результат.ФакторРабота.Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Результат.ФакторРабота КАК Работа
	|ИЗ
	|	ВТ_Результат КАК ВТ_Результат
	|ГДЕ
	|	ВТ_Результат.ФакторИлиРабота = ""Р""
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_Результат.ФакторРабота.Наименование";	
	
	Запрос.УстановитьПараметр("ДатаАнализа",ДатаАнализа);
	Запрос.УстановитьПараметр("Организация",ТекущийОбъект.Организация);
	Запрос.УстановитьПараметр("Сотрудник",	ТекущийОбъект.Сотрудник);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТекущийОбъект.ВредныеИОпасныеПроизводственныеФакторы.Очистить();
	ТекущийОбъект.ВредныеИОпасныеПроизводственныеРаботы.Очистить();
	
	ТекущийОбъект.ВредныеИОпасныеПроизводственныеФакторы.Загрузить(Результат[1].Выгрузить());
	ТекущийОбъект.ВредныеИОпасныеПроизводственныеРаботы.Загрузить(Результат[2].Выгрузить());
	
КонецПроцедуры	
	
#КонецЕсли