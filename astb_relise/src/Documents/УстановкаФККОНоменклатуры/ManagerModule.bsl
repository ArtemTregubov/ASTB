#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УстановкаФККОНоменклатурыТовары.Ссылка.Дата КАК Период,
	|	УстановкаФККОНоменклатурыТовары.Ссылка.Организация КАК Организация,
	|	УстановкаФККОНоменклатурыТовары.Номенклатура КАК Номенклатура,
	|	УстановкаФККОНоменклатурыТовары.ФККО КАК ФККО,
	|	УстановкаФККОНоменклатурыТовары.Использовать КАК Использовать
	|ИЗ
	|	Документ.УстановкаФККОНоменклатуры.Товары КАК УстановкаФККОНоменклатурыТовары
	|ГДЕ
	|	УстановкаФККОНоменклатурыТовары.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",	ДокументСсылка);
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	//по регистру "ФККО номенклатуры"
	ТаблицыДляДвижений.Вставить("ТаблицаФККОНоменклатуры", Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуДокумента(ПоТекущимДанным,ТекущийОбъект) Экспорт
	
	ТекущийОбъект.Товары.Очистить();
	
	Запрос = Новый Запрос;
	
	Если ПоТекущимДанным Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФККОНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
		|	ФККОНоменклатурыСрезПоследних.ФККО КАК ФККО,
		|	ФККОНоменклатурыСрезПоследних.Использовать КАК Использовать
		|ИЗ
		|	РегистрСведений.ФККОНоменклатуры.СрезПоследних(&ДатаАнализа, Организация = &Организация) КАК ФККОНоменклатурыСрезПоследних
		|ГДЕ
		|	ФККОНоменклатурыСрезПоследних.Использовать
		|
		|УПОРЯДОЧИТЬ ПО
		|	ФККОНоменклатурыСрезПоследних.Номенклатура.Наименование,
		|	ФККОНоменклатурыСрезПоследних.ФККО.Код";
		
		Запрос.УстановитьПараметр("ДатаАнализа",ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка));
		Запрос.УстановитьПараметр("Организация",ТекущийОбъект.Организация);
		
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Номенклатура
		|ПОМЕСТИТЬ ВТ_Номенклатура
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.ПодлежитУтилизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФККОНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
		|	ФККОНоменклатурыСрезПоследних.ФККО КАК ФККО
		|ПОМЕСТИТЬ ВТ_УстановленныеФККО
		|ИЗ
		|	РегистрСведений.ФККОНоменклатуры.СрезПоследних(&ДатаАнализа, Организация = &Организация) КАК ФККОНоменклатурыСрезПоследних
		|ГДЕ
		|	ФККОНоменклатурыСрезПоследних.Использовать
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Номенклатура.Номенклатура КАК Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.ФККО.ПустаяСсылка) КАК ФККО,
		|	ИСТИНА КАК Использовать
		|ИЗ
		|	ВТ_Номенклатура КАК ВТ_Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_УстановленныеФККО КАК ВТ_УстановленныеФККО
		|		ПО ВТ_Номенклатура.Номенклатура = ВТ_УстановленныеФККО.Номенклатура
		|ГДЕ
		|	ВТ_УстановленныеФККО.Номенклатура ЕСТЬ NULL";
		
		Запрос.УстановитьПараметр("ДатаАнализа",ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка));
		Запрос.УстановитьПараметр("Организация",ТекущийОбъект.Организация);
		
	КонецЕсли;
	
	ТекущийОбъект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

#КонецЕсли