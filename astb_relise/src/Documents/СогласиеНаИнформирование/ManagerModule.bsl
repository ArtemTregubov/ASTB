#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СогласиеНаИнформированиеРаботники.Ссылка.Дата КАК Период,
	|	СогласиеНаИнформированиеРаботники.Ссылка.Организация КАК Организация,
	|	СогласиеНаИнформированиеРаботники.Сотрудник КАК Сотрудник,
	|	СогласиеНаИнформированиеРаботники.SMS КАК SMS,
	|	СогласиеНаИнформированиеРаботники.Email КАК Email
	|ИЗ
	|	Документ.СогласиеНаИнформирование.Работники КАК СогласиеНаИнформированиеРаботники
	|ГДЕ
	|	СогласиеНаИнформированиеРаботники.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	//по регистру "Согласия на информирование"
	ТаблицыДляДвижений.Вставить("ТаблицаСогласияНаИнформирование", Запрос.Выполнить().Выгрузить());
		
КонецПроцедуры

Процедура ЗаполнитьТаблицуДокумента(ТекущийОбъект,ПоСуществующимДанным,МассивСотрудников = НЕОПРЕДЕЛЕНО) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗанятыеРабочиеМестаОстатки.Сотрудник КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА ФизическиеЛицаКонтактнаяИнформация.НомерТелефона ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ НЕ ФизическиеЛицаКонтактнаяИнформация.НомерТелефона = """"
	|	КОНЕЦ КАК ЕстьТелефон,
	|	ВЫБОР
	|		КОГДА ФизическиеЛицаКонтактнаяИнформация.АдресЭП ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ НЕ ФизическиеЛицаКонтактнаяИнформация.АдресЭП = """"
	|	КОНЕЦ КАК ЕстьЭП
	|ПОМЕСТИТЬ ВТ_ЗРМ
	|ИЗ
	|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
	|			&ПериодАнализа,
	|			Организация = &Организация
	|				И ВЫБОР
	|					КОГДА &ИспользоватьМассивСотрудников
	|						ТОГДА Сотрудник В (&МассивСотрудников)
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ) КАК ЗанятыеРабочиеМестаОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
	|		ПО ЗанятыеРабочиеМестаОстатки.Сотрудник.ФизическоеЛицо = ФизическиеЛицаКонтактнаяИнформация.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СогласияНаИнформированиеСрезПоследних.Организация КАК Организация,
	|	СогласияНаИнформированиеСрезПоследних.Сотрудник КАК Сотрудник,
	|	СогласияНаИнформированиеСрезПоследних.SMS КАК SMS,
	|	СогласияНаИнформированиеСрезПоследних.Email КАК Email
	|ПОМЕСТИТЬ ВТ_СогласияНаИнформирование
	|ИЗ
	|	РегистрСведений.СогласияНаИнформирование.СрезПоследних(&ПериодАнализа, Организация = &Организация) КАК СогласияНаИнформированиеСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сотрудники.Ссылка КАК Сотрудник,
	|	ВТ_СогласияНаИнформирование.SMS КАК SMS,
	|	ВТ_СогласияНаИнформирование.Email КАК Email
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СогласияНаИнформирование КАК ВТ_СогласияНаИнформирование
	|		ПО Сотрудники.Ссылка = ВТ_СогласияНаИнформирование.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗРМ КАК ВТ_ЗРМ
	|		ПО Сотрудники.Ссылка = ВТ_ЗРМ.Сотрудник
	|ГДЕ
	|	Сотрудники.Владелец = &Организация
	|	И ВЫБОР
	|			КОГДА &ИспользоватьМассивСотрудников
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ВЫБОР
	|					КОГДА &ПоСуществующимДанным
	|						ТОГДА НЕ ВТ_СогласияНаИнформирование.Сотрудник ЕСТЬ NULL
	|					ИНАЧЕ ВТ_СогласияНаИнформирование.Сотрудник ЕСТЬ NULL
	|				КОНЕЦ
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &ИспользоватьМассивСотрудников
	|				ТОГДА Сотрудники.Ссылка В (&МассивСотрудников)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА ВТ_ЗРМ.Сотрудник ЕСТЬ NULL
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ВТ_ЗРМ.ЕстьТелефон
	|					ИЛИ ВТ_ЗРМ.ЕстьЭП
	|		КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудники.Наименование";
	
	Запрос.УстановитьПараметр("ПериодАнализа",					ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка));
	Запрос.УстановитьПараметр("ПоСуществующимДанным",			ПоСуществующимДанным);
	Запрос.УстановитьПараметр("Организация",					ТекущийОбъект.Организация);
	Запрос.УстановитьПараметр("МассивСотрудников",				МассивСотрудников);
	Запрос.УстановитьПараметр("ИспользоватьМассивСотрудников",	НЕ МассивСотрудников = НЕОПРЕДЕЛЕНО);
	
	ТекущийОбъект.Работники.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры	

Процедура ЗаполнитьДокумент(ДанныеЗаполнения,ЭтотОбъект) Экспорт
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("Сотрудник") Тогда
			Основание = ДанныеЗаполнения.Сотрудник;
		Иначе
			Возврат;
		КонецЕсли;
		
	Иначе
		
		Основание = ДанныеЗаполнения.Ссылка;
		
	КонецЕсли;
	
	Если ТипЗнч(Основание) = Тип("СправочникСсылка.Сотрудники") Тогда
		
		ЭтотОбъект.Организация 	= Основание.Владелец;
		ЭтотОбъект.Дата			= ТекущаяДата();

		МассивСотрудников = Новый Массив;
		МассивСотрудников.Добавить(Основание);
		
		ЗаполнитьТаблицуДокумента(ЭтотОбъект,Истина,МассивСотрудников);
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.СогласиеНаИнформирование";
	КомандаПечати.Идентификатор = "СогласиеНаИнформирование";
	КомандаПечати.Представление = НСтр("ru = 'Согласие на информирование'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СогласиеНаИнформирование") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм, 
		"СогласиеНаИнформирование",
		"Согласие на информирование",
		СформироватьПечатнуюФормуСогласияНаИнформирование(МассивОбъектов, ОбъектыПечати),,"Документ.СогласиеНаИнформирование.ПФ_MXL_СогласиеНаИнформирование");
		
	КонецЕсли;
				
КонецПроцедуры

Функция СформироватьПечатнуюФормуСогласияНаИнформирование(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СогласиеНаИнформирование_СогласиеНаИнформирование";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФИОФизическихЛицСрезПоследних.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ФИОФизическихЛицСрезПоследних.Фамилия КАК Фамилия,
	|	ФИОФизическихЛицСрезПоследних.Имя КАК Имя,
	|	ФИОФизическихЛицСрезПоследних.Отчество КАК Отчество
	|ПОМЕСТИТЬ ВТ_ФИОФизЛиц
	|ИЗ
	|	РегистрСведений.ФИОФизическихЛиц.СрезПоследних КАК ФИОФизическихЛицСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФизическиеЛицаКонтактнаяИнформация.Ссылка КАК ФизическоеЛицо,
	|	ФизическиеЛицаКонтактнаяИнформация.Представление КАК АдресЭП
	|ПОМЕСТИТЬ ВТ_ПочтаФизЛиц
	|ИЗ
	|	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
	|ГДЕ
	|	ФизическиеЛицаКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailФизическиеЛица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФизическиеЛицаКонтактнаяИнформация.Ссылка КАК ФизическоеЛицо,
	|	ФизическиеЛицаКонтактнаяИнформация.Представление КАК Телефон
	|ПОМЕСТИТЬ ВТ_ТелефоныФизЛиц
	|ИЗ
	|	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
	|ГДЕ
	|	ФизическиеЛицаКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонФизическиеЛица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонтрагентыКонтактнаяИнформация.Ссылка КАК Контрагент,
	|	КонтрагентыКонтактнаяИнформация.Представление КАК АдресКонтрагента
	|ПОМЕСТИТЬ ВТ_ПочтовыеАдресаКонтрагентов
	|ИЗ
	|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
	|ГДЕ
	|	КонтрагентыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ПочтовыйАдресКонтрагента)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонтрагентыКонтактнаяИнформация.Ссылка КАК Контрагент,
	|	КонтрагентыКонтактнаяИнформация.Представление КАК ЮрАдресКонтрагента
	|ПОМЕСТИТЬ ВТ_ЮрАдресаКонтрагента
	|ИЗ
	|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
	|ГДЕ
	|	КонтрагентыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресКонтрагента)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонтрагентыКонтактнаяИнформация.Ссылка КАК Контрагент,
	|	КонтрагентыКонтактнаяИнформация.Представление КАК ПочтаКонтрагента
	|ПОМЕСТИТЬ ВТ_ПочтаКонтрагентов
	|ИЗ
	|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
	|ГДЕ
	|	КонтрагентыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailКонтрагента)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СогласиеНаИнформированиеРаботники.Ссылка КАК Ссылка,
	|	СогласиеНаИнформированиеРаботники.Сотрудник КАК Сотрудник,
	|	СогласиеНаИнформированиеРаботники.SMS КАК SMS,
	|	СогласиеНаИнформированиеРаботники.Email КАК Email,
	|	СогласиеНаИнформированиеРаботники.Ссылка.Контрагент КАК Контрагент,
	|	СогласиеНаИнформированиеРаботники.Ссылка.Дата КАК ДатаДокумента,
	|	ВТ_ФИОФизЛиц.Фамилия КАК Фамилия,
	|	ВТ_ФИОФизЛиц.Имя КАК Имя,
	|	ВТ_ФИОФизЛиц.Отчество КАК Отчество,
	|	ЕСТЬNULL(ВТ_ПочтаФизЛиц.АдресЭП, """") КАК АдресЭП,
	|	ЕСТЬNULL(ВТ_ТелефоныФизЛиц.Телефон, """") КАК Телефон,
	|	ЕСТЬNULL(ВТ_ПочтовыеАдресаКонтрагентов.АдресКонтрагента, """") КАК АдресКонтрагента,
	|	ЕСТЬNULL(ВТ_ЮрАдресаКонтрагента.ЮрАдресКонтрагента, """") КАК ЮрАдресКонтрагента,
	|	ЕСТЬNULL(ВТ_ПочтаКонтрагентов.ПочтаКонтрагента, """") КАК ПочтаКонтрагента
	|ИЗ
	|	Документ.СогласиеНаИнформирование.Работники КАК СогласиеНаИнформированиеРаботники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ФИОФизЛиц КАК ВТ_ФИОФизЛиц
	|		ПО СогласиеНаИнформированиеРаботники.Сотрудник.ФизическоеЛицо = ВТ_ФИОФизЛиц.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПочтаФизЛиц КАК ВТ_ПочтаФизЛиц
	|		ПО СогласиеНаИнформированиеРаботники.Сотрудник.ФизическоеЛицо = ВТ_ПочтаФизЛиц.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТелефоныФизЛиц КАК ВТ_ТелефоныФизЛиц
	|		ПО СогласиеНаИнформированиеРаботники.Сотрудник.ФизическоеЛицо = ВТ_ТелефоныФизЛиц.ФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПочтовыеАдресаКонтрагентов КАК ВТ_ПочтовыеАдресаКонтрагентов
	|		ПО СогласиеНаИнформированиеРаботники.Ссылка.Контрагент = ВТ_ПочтовыеАдресаКонтрагентов.Контрагент
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЮрАдресаКонтрагента КАК ВТ_ЮрАдресаКонтрагента
	|		ПО СогласиеНаИнформированиеРаботники.Ссылка.Контрагент = ВТ_ЮрАдресаКонтрагента.Контрагент
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПочтаКонтрагентов КАК ВТ_ПочтаКонтрагентов
	|		ПО СогласиеНаИнформированиеРаботники.Ссылка.Контрагент = ВТ_ПочтаКонтрагентов.Контрагент
	|ГДЕ
	|	СогласиеНаИнформированиеРаботники.Ссылка В(&МассивОбъектов)
	|	И (СогласиеНаИнформированиеРаботники.SMS
	|			ИЛИ СогласиеНаИнформированиеРаботники.Email)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СогласиеНаИнформирование.ПФ_MXL_СогласиеНаИнформирование");
	
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	
	ПервыйДокумент = Истина;
	
	Для Каждого СтрокаТаблицыЗапроса Из ТаблицаЗапроса Цикл
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
			
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.ПредставлениеКонтрагента 	= СокрЛП(СтрокаТаблицыЗапроса.Контрагент.Наименование);
		Область.Параметры.ЮрАдресКонтрагента 		= СтрокаТаблицыЗапроса.ЮрАдресКонтрагента;
		Область.Параметры.Фамилия 					= СтрокаТаблицыЗапроса.Фамилия;
		Область.Параметры.Имя 						= СтрокаТаблицыЗапроса.Имя;
		Область.Параметры.Отчество 					= СтрокаТаблицыЗапроса.Отчество;
		ТабличныйДокумент.Вывести(Область);
		
		Если СтрокаТаблицыЗапроса.SMS Тогда
			Область = Макет.ПолучитьОбласть("ШапкаТелефон");
			Область.Параметры.Телефон = СтрокаТаблицыЗапроса.Телефон;
			ТабличныйДокумент.Вывести(Область);
		КонецЕсли;
		
		Если СтрокаТаблицыЗапроса.Email Тогда
			Область = Макет.ПолучитьОбласть("ШапкаПочта");
			Область.Параметры.АдресЭП = СтрокаТаблицыЗапроса.АдресЭП;
			ТабличныйДокумент.Вывести(Область);
		КонецЕсли;
		
		Область = Макет.ПолучитьОбласть("Цели");
		ТабличныйДокумент.Вывести(Область);
		
		Если СтрокаТаблицыЗапроса.SMS Тогда
			Область = Макет.ПолучитьОбласть("ЦелиSMS");
			ТабличныйДокумент.Вывести(Область);
		КонецЕсли;
		
		Если СтрокаТаблицыЗапроса.Email Тогда
			Область = Макет.ПолучитьОбласть("ЦелиEmail");
			ТабличныйДокумент.Вывести(Область);
		КонецЕсли;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		Область.Параметры.АдресКонтрагента 		= СтрокаТаблицыЗапроса.АдресКонтрагента;
		Область.Параметры.ПочтаКонтрагента 		= СтрокаТаблицыЗапроса.ПочтаКонтрагента;
		Область.Параметры.ПредставлениеДаты 	= Формат(СтрокаТаблицыЗапроса.ДатаДокумента,"ДЛФ=DD");
		Область.Параметры.РасшифровкаПодписи 	= СтрокаТаблицыЗапроса.Фамилия + " " + Лев(СтрокаТаблицыЗапроса.Имя,1) + ". " + Лев(СтрокаТаблицыЗапроса.Отчество,1);
		
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, СтрокаТаблицыЗапроса.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецЕсли