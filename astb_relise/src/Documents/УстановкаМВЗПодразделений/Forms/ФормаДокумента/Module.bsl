&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация", Объект.Организация));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуДокументаНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Документы.УстановкаМВЗПодразделений.ЗаполнитьТабличнуюЧасть(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Объект.МВЗПодразделений.Количество() > 0 Тогда
	
		Текст = "ru = ""Табличная часть будет очищена! Продолжить выполнение операции?"";";
		
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтаФорма, Параметры);
		
		ПоказатьВопрос(Оповещение,НСтр(Текст),РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ЗаполнитьТаблицуДокументаНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

    Объект.МВЗПодразделений.Очистить();

	ЗаполнитьТаблицуДокументаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	ИзменитьФлажкиНаСервере(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	ИзменитьФлажкиНаСервере(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьФлажкиНаСервере(ЗначениеФлажка)
	
	Документ = РеквизитФормыВЗначение("Объект");
	
	ТаблицаДокумента = Документ.МВЗПодразделений.Выгрузить();
	ТаблицаДокумента.ЗаполнитьЗначения(ЗначениеФлажка, "Использовать");
	Документ.МВЗПодразделений.Загрузить(ТаблицаДокумента);
	
	ЗначениеВРеквизитФормы(Документ,"Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЭтаФорма.УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация", Объект.Организация));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоВсемПодразделениямНаСервере(ВыбранноеМВЗ)
	
	Документы.УстановкаМВЗПодразделений.ЗаполнитьПоВсемПодразделениям(Объект,ВыбранноеМВЗ);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоВсемПодразделениям(Команда)
	
	ПараметрыФормыВыбора = Новый Структура;
	ПараметрыФормыВыбора.Вставить("РежимОткрытияОкна",  РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ПараметрыФормыВыбора.Вставить("РежимВыбора",        Истина);
	ПараметрыФормыВыбора.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыФормыВыбора.Вставить("МножественныйВыбор", Ложь);
	ПараметрыФормыВыбора.Вставить("Отбор",              Новый Структура("Владелец", Объект.Организация));
	
	ОткрытьФорму("Справочник.МВЗ.ФормаВыбора",ПараметрыФормыВыбора,ЭтаФорма,УникальныйИдентификатор,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
			
		ЗаполнитьПоВсемПодразделениямНаСервере(ВыбранноеЗначение);
			
	КонецЕсли;
	
	Если Окно <> Неопределено Тогда
		Окно.Активизировать();
	КонецЕсли;

	Модифицированность = Истина;
	
КонецПроцедуры


