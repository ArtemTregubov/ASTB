#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УвольнениеРаботники.НомерСтроки,
	|	УвольнениеРаботники.Ссылка.Дата КАК Период,
	|	УвольнениеРаботники.Ссылка.Организация,
	|	УвольнениеРаботники.Подразделение КАК Подразделение,
	|	УвольнениеРаботники.Должность КАК Должность,
	|	УвольнениеРаботники.РабочееМесто КАК РабочееМесто,
	|	УвольнениеРаботники.Сотрудник,
	|	УвольнениеРаботники.ЗанимаемыхСтавок КАК Количество
	|ПОМЕСТИТЬ ВТ_ТаблицаДокумента
	|ИЗ
	|	Документ.Увольнение.Работники КАК УвольнениеРаботники
	|ГДЕ
	|	УвольнениеРаботники.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗанятыеРабочиеМестаОстатки.Подразделение,
	|	ЗанятыеРабочиеМестаОстатки.Должность,
	|	ЗанятыеРабочиеМестаОстатки.РабочееМесто,
	|	ЗанятыеРабочиеМестаОстатки.Сотрудник,
	|	СУММА(ЗанятыеРабочиеМестаОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ПОМЕСТИТЬ ВТ_ЗанятыеРабочиеМеста
	|ИЗ
	|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
	|			&МоментВремениДокумента,
	|			Организация = &Организация
	|				И Сотрудник В (&МассивСотрудников)) КАК ЗанятыеРабочиеМестаОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗанятыеРабочиеМестаОстатки.Подразделение,
	|	ЗанятыеРабочиеМестаОстатки.Должность,
	|	ЗанятыеРабочиеМестаОстатки.РабочееМесто,
	|	ЗанятыеРабочиеМестаОстатки.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Сотрудник,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Подразделение,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Должность,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.РабочееМесто,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.ОсновноеМестоРаботы,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ОсновноеМестоРаботы
	|ИЗ
	|	РегистрСведений.ОсновноеМестоРаботыСотрудника.СрезПоследних(
	|			&МоментВремениДокумента,
	|			Организация = &Организация
	|				И Сотрудник В (&МассивСотрудников)) КАК ОсновноеМестоРаботыСотрудникаСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗанятыеРабочиеМеста.Подразделение,
	|	ВТ_ЗанятыеРабочиеМеста.Должность,
	|	ВТ_ЗанятыеРабочиеМеста.РабочееМесто,
	|	ВТ_ЗанятыеРабочиеМеста.Сотрудник,
	|	ЕСТЬNULL(ВТ_ЗанятыеРабочиеМеста.Подразделение.Код, ""БезПодр"") + ЕСТЬNULL(ВТ_ЗанятыеРабочиеМеста.Должность.Код, ""БезДол"") + ЕСТЬNULL(ВТ_ЗанятыеРабочиеМеста.РабочееМесто.Код, ""БезРМ"") КАК КлючевоеПоле
	|ПОМЕСТИТЬ ВТ_ЗанятыеРабочиеМестаПослеУвольнения
	|ИЗ
	|	ВТ_ЗанятыеРабочиеМеста КАК ВТ_ЗанятыеРабочиеМеста
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|		ПО ВТ_ЗанятыеРабочиеМеста.Подразделение = ВТ_ТаблицаДокумента.Подразделение
	|			И ВТ_ЗанятыеРабочиеМеста.Должность = ВТ_ТаблицаДокумента.Должность
	|			И ВТ_ЗанятыеРабочиеМеста.РабочееМесто = ВТ_ТаблицаДокумента.РабочееМесто
	|			И ВТ_ЗанятыеРабочиеМеста.Сотрудник = ВТ_ТаблицаДокумента.Сотрудник
	|ГДЕ
	|	ВТ_ЗанятыеРабочиеМеста.КоличествоОстаток - ЕСТЬNULL(ВТ_ТаблицаДокумента.Количество, 0) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВТ_ТаблицаДокумента.НомерСтроки,
	|	ВТ_ТаблицаДокумента.Период,
	|	ВТ_ТаблицаДокумента.Организация,
	|	ВТ_ТаблицаДокумента.Подразделение,
	|	ВТ_ТаблицаДокумента.Должность,
	|	ВТ_ТаблицаДокумента.РабочееМесто,
	|	ВТ_ТаблицаДокумента.Сотрудник,
	|	ВТ_ТаблицаДокумента.Количество
	|ИЗ
	|	ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Сотрудник,
	|	ТаблицаДокумента.Подразделение,
	|	ТаблицаДокумента.Должность,
	|	ТаблицаДокумента.РабочееМесто,
	|	ДоступныеУсловияРаботыСотрудникаСрезПоследних.Условие,
	|	ЛОЖЬ КАК Использовать
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_ТаблицаДокумента.Подразделение КАК Подразделение,
	|		ВТ_ТаблицаДокумента.Должность КАК Должность,
	|		ВТ_ТаблицаДокумента.РабочееМесто КАК РабочееМесто,
	|		ВТ_ТаблицаДокумента.Сотрудник КАК Сотрудник,
	|		СУММА(ВТ_ТаблицаДокумента.Количество) КАК Количество,
	|		ВТ_ТаблицаДокумента.Период КАК Период
	|	ИЗ
	|		ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВТ_ТаблицаДокумента.Подразделение,
	|		ВТ_ТаблицаДокумента.Должность,
	|		ВТ_ТаблицаДокумента.РабочееМесто,
	|		ВТ_ТаблицаДокумента.Сотрудник,
	|		ВТ_ТаблицаДокумента.Период) КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗанятыеРабочиеМеста КАК ВТ_ЗанятыеРабочиеМеста
	|		ПО ТаблицаДокумента.Подразделение = ВТ_ЗанятыеРабочиеМеста.Подразделение
	|			И ТаблицаДокумента.Должность = ВТ_ЗанятыеРабочиеМеста.Должность
	|			И ТаблицаДокумента.РабочееМесто = ВТ_ЗанятыеРабочиеМеста.РабочееМесто
	|			И ТаблицаДокумента.Сотрудник = ВТ_ЗанятыеРабочиеМеста.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДоступныеУсловияРаботыСотрудника.СрезПоследних(&МоментВремениДокумента, Сотрудник В (&МассивСотрудников)) КАК ДоступныеУсловияРаботыСотрудникаСрезПоследних
	|		ПО ТаблицаДокумента.Должность = ДоступныеУсловияРаботыСотрудникаСрезПоследних.Должность
	|			И ТаблицаДокумента.Сотрудник = ДоступныеУсловияРаботыСотрудникаСрезПоследних.Сотрудник
	|ГДЕ
	|	НЕ ДоступныеУсловияРаботыСотрудникаСрезПоследних.Условие ЕСТЬ NULL 
	|	И ТаблицаДокумента.Количество - ЕСТЬNULL(ВТ_ЗанятыеРабочиеМеста.КоличествоОстаток, 0) = 0
	|	И ДоступныеУсловияРаботыСотрудникаСрезПоследних.Использовать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период,
	|	ВТ_ОсновноеМестоРаботы.Сотрудник.Владелец КАК Организация,
	|	ВТ_ОсновноеМестоРаботы.Сотрудник,
	|	ВЫБОР
	|		КОГДА ВТ_ЗанятыеРабочиеМестаПослеУвольнения.Сотрудник ЕСТЬ NULL 
	|			ТОГДА ВТ_ОсновноеМестоРаботы.Подразделение
	|		ИНАЧЕ ВТ_ЗанятыеРабочиеМестаПослеУвольнения.Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ВТ_ЗанятыеРабочиеМестаПослеУвольнения.Сотрудник ЕСТЬ NULL 
	|			ТОГДА ВТ_ОсновноеМестоРаботы.Должность
	|		ИНАЧЕ ВТ_ЗанятыеРабочиеМестаПослеУвольнения.Должность
	|	КОНЕЦ КАК Должность,
	|	ВЫБОР
	|		КОГДА ВТ_ЗанятыеРабочиеМестаПослеУвольнения.Сотрудник ЕСТЬ NULL 
	|			ТОГДА ВТ_ОсновноеМестоРаботы.РабочееМесто
	|		ИНАЧЕ ВТ_ЗанятыеРабочиеМестаПослеУвольнения.РабочееМесто
	|	КОНЕЦ КАК РабочееМесто,
	|	ВЫБОР
	|		КОГДА ВТ_ЗанятыеРабочиеМестаПослеУвольнения.Сотрудник ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОсновноеМестоРаботы,
	|	ВТ_ОсновноеМестоРаботы.ДатаОкончания
	|ИЗ
	|	ВТ_ОсновноеМестоРаботы КАК ВТ_ОсновноеМестоРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВТ_ЗанятыеРабочиеМестаПослеУвольнения.Сотрудник КАК Сотрудник,
	|			МАКСИМУМ(ВТ_ЗанятыеРабочиеМестаПослеУвольнения.КлючевоеПоле) КАК КлючевоеПоле
	|		ИЗ
	|			ВТ_ЗанятыеРабочиеМестаПослеУвольнения КАК ВТ_ЗанятыеРабочиеМестаПослеУвольнения
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ВТ_ЗанятыеРабочиеМестаПослеУвольнения.Сотрудник) КАК НовоеОсновноеМестоРаботы
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗанятыеРабочиеМестаПослеУвольнения КАК ВТ_ЗанятыеРабочиеМестаПослеУвольнения
	|			ПО НовоеОсновноеМестоРаботы.Сотрудник = ВТ_ЗанятыеРабочиеМестаПослеУвольнения.Сотрудник
	|				И НовоеОсновноеМестоРаботы.КлючевоеПоле = ВТ_ЗанятыеРабочиеМестаПослеУвольнения.КлючевоеПоле
	|		ПО ВТ_ОсновноеМестоРаботы.Сотрудник = НовоеОсновноеМестоРаботы.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|		ПО ВТ_ОсновноеМестоРаботы.Сотрудник = ВТ_ТаблицаДокумента.Сотрудник
	|			И ВТ_ОсновноеМестоРаботы.Подразделение = ВТ_ТаблицаДокумента.Подразделение
	|			И ВТ_ОсновноеМестоРаботы.Должность = ВТ_ТаблицаДокумента.Должность
	|			И ВТ_ОсновноеМестоРаботы.РабочееМесто = ВТ_ТаблицаДокумента.РабочееМесто
	|ГДЕ
	|	(ВТ_ЗанятыеРабочиеМестаПослеУвольнения.Сотрудник ЕСТЬ NULL 
	|			ИЛИ НЕ ВТ_ТаблицаДокумента.Сотрудник ЕСТЬ NULL )";
	
	Запрос.УстановитьПараметр("Ссылка",					ДокументСсылка);
	Запрос.УстановитьПараметр("МоментВремениДокумента", ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ДокументСсылка));
	Запрос.УстановитьПараметр("Период",					ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("Организация",			ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("МассивСотрудников", 		ДокументСсылка.Работники.ВЫгрузитьКолонку("Сотрудник"));
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	//по регистру "Занятые рабочие места"
	ТаблицыДляДвижений.Вставить("ТаблицаЗанятыеРабочиеМеста", Результат[4].Выгрузить());
	
	//по регистру "Доступные условия работы сотрудника"
	ТаблицыДляДвижений.Вставить("ТаблицаДоступныеУсловияРаботыСотрудника", Результат[5].Выгрузить());
	
	//по регистру "Основное место работы сотрудника"
	ТаблицыДляДвижений.Вставить("ТаблицаОсновноеМестоРаботыСотрудника", Результат[6].Выгрузить());
	
	//готовим пустую таблицу движений по регистру "Выдача по потребности"
	ТаблицаВыдачаПоПотребности = ДокументСсылка.ПолучитьОбъект().Движения.ВыдачаПоПотребности.Выгрузить().Скопировать(,"Период,ВидДвижения,Организация,Сотрудник,НормаВыдачи,НоменклатураНормы,ДатаПотребности,ДатаВыдачи,ПроцентИзноса,Количество");
	
	//по регистру "ПотребностьВыдачиСИЗ"
	ТаблицыДляДвижений.Вставить("ТаблицаПотребностьВыдачиСИЗ", ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуДвиженийПоПотребности(ДокументСсылка,,ТаблицаВыдачаПоПотребности));
	
	//по регистру "ВыдачаПоПотребности"
	ТаблицыДляДвижений.Вставить("ТаблицаВыдачаПоПотребности", ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуДвиженийВыдачиПоПотребности(ДокументСсылка,ТаблицыДляДвижений.ТаблицаПотребностьВыдачиСИЗ,,ТаблицаВыдачаПоПотребности));
	
КонецПроцедуры	

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
    Результат.Добавить("ВходящийНомер");
	Результат.Добавить("ДатаВходящегоДокумента");
    Возврат Результат;
	
КонецФункции

#КонецЕсли