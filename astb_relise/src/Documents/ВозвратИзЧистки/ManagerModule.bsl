#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	               |	ВозвратИзЧисткиТовары.Ссылка.Организация,
	               |	ВозвратИзЧисткиТовары.Ссылка КАК ВозвратИзЧистки,
	               |	ВозвратИзЧисткиТовары.Ссылка.Отправитель КАК Контрагент,
	               |	ВозвратИзЧисткиТовары.Сотрудник,
	               |	ВозвратИзЧисткиТовары.Номенклатура,
	               |	ВозвратИзЧисткиТовары.ХарактеристикаНоменклатуры,
	               |	ВозвратИзЧисткиТовары.Штрихкод КАК Штрихкод,
	               |	1 КАК Количество,
	               |	ВозвратИзЧисткиТовары.НомерСтроки,
	               |	ВозвратИзЧисткиТовары.Ссылка.Дата КАК Период
	               |ИЗ
	               |	Документ.ВозвратИзЧистки.Товары КАК ВозвратИзЧисткиТовары
	               |ГДЕ
	               |	ВозвратИзЧисткиТовары.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	ТаблицыДляДвижений.Вставить("ТаблицаСредстваЗащитыВЧистке", Результат);
	
КонецПроцедуры	
	
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктПриемаПередачи") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"АктПриемаПередачи",
			"Акт приема-передачи",
			СформироватьПечатнуюФормуАктаПриемаПередачи(МассивОбъектов, ОбъектыПечати),,"Документ.ВозвратИзЧистки.ПФ_MXL_АктПриемаПередачи");
				
	КонецЕсли;	
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуАктаПриемаПередачи(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ВозвратИзЧистки_ПФ_MXL_АктПриемаПередачи";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Документы.Ссылка КАК Ссылка,
	|	Документы.Отправитель.Наименование КАК Отправитель,
	|	Документы.Получатель.Наименование КАК Получатель,
	|	Документы.Номер КАК НомерДокумента,
	|	Документы.Дата КАК ДатаДокумента,
	|	Документы.НомерДоговора КАК НомерДоговора,
	|	Документы.ДатаДоговора КАК ДатаДоговора
	|ИЗ
	|	Документ.ВозвратИзЧистки КАК Документы
	|ГДЕ
	|	Документы.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВозвратИзЧисткиТовары.Ссылка КАК Ссылка,
	|	ВозвратИзЧисткиТовары.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	СУММА(ВозвратИзЧисткиТовары.Вес) КАК Вес,
	|	СУММА(1) КАК Количество,
	|	ВозвратИзЧисткиТовары.Номенклатура.Наименование КАК НаименованиеТовара
	|ИЗ
	|	Документ.ВозвратИзЧистки.Товары КАК ВозвратИзЧисткиТовары
	|ГДЕ
	|	ВозвратИзЧисткиТовары.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратИзЧисткиТовары.Ссылка,
	|	ВозвратИзЧисткиТовары.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	ВозвратИзЧисткиТовары.Номенклатура.Наименование
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|ИТОГИ ПО
	|	Ссылка");
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВозвратИзЧистки.ПФ_MXL_АктПриемаПередачи");
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ДанныеПечати = ПакетЗапросов[0].Выбрать();
	ВыборкаПоДокументам = ПакетЗапросов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если НЕ ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Ссылка", ДанныеПечати.Ссылка)) Тогда
			Продолжить;
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
			
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Заполнить(ДанныеПечати);
		НомерДокумента = ДанныеПечати.НомерДокумента;
		ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(НомерДокумента,Истина,Истина);
		Область.Параметры.НомерДокумента 	= НомерДокумента;
		Область.Параметры.АдресОтправителя 	= ФормированиеПечатныхФормПереопределяемый.ПолучитьАдресИзКонтактнойИнформации(ДанныеПечати.Ссылка.Отправитель,	"Фактический");
		Область.Параметры.АдресПолучателя 	= ФормированиеПечатныхФормПереопределяемый.ПолучитьАдресИзКонтактнойИнформации(ДанныеПечати.Ссылка.Получатель,	"Фактический");
		
		ТабличныйДокумент.Вывести(Область);
			
		СчетСтрок = 1;
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы");
			Область.Параметры.Заполнить(ВыборкаПоТоварам);
			
			Область.Параметры.НомерСтроки = СчетСтрок;
			
			ТабличныйДокумент.Вывести(Область);
			
			СчетСтрок = СчетСтрок + 1;
			
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		ТекстИтоговойСтроки = НСтр("ru = 'Всего наименований %ВсегоНаименований% на %ИтоговыйВес% кг'");
		ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%ВсегоНаименований%", СчетСтрок-1);
		ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%ИтоговыйВес%", Формат(ДанныеПечати.Ссылка.Товары.Итог("Вес"),"ЧЦ=15; ЧДЦ=3"));
		Область.Параметры.ИтоговаяСтрока = ТекстИтоговойСтроки;
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//заказ поставщику
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ВозвратИзЧистки";
	КомандаПечати.Идентификатор = "АктПриемаПередачи";
	КомандаПечати.Представление = НСтр("ru = 'Акт приема-передачи'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

Процедура ЗаполнитьПоОстаткам(ТекущийОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СредстваЗащитыВЧисткеОстатки.Сотрудник,
	|	СредстваЗащитыВЧисткеОстатки.Номенклатура,
	|	СредстваЗащитыВЧисткеОстатки.ХарактеристикаНоменклатуры,
	|	СредстваЗащитыВЧисткеОстатки.Штрихкод,
	|	СредстваЗащитыВЧисткеОстатки.Номенклатура.ВесДляУчетаЧистки КАК Вес
	|ИЗ
	|	РегистрНакопления.СредстваЗащитыВЧистке.Остатки(
	|			&Период,
	|			Организация = &Организация
	|				И Контрагент = &Контрагент) КАК СредстваЗащитыВЧисткеОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	СредстваЗащитыВЧисткеОстатки.Сотрудник.Наименование,
	|	СредстваЗащитыВЧисткеОстатки.Номенклатура.Наименование,
	|	СредстваЗащитыВЧисткеОстатки.ХарактеристикаНоменклатуры.КодSAP,
	|	СредстваЗащитыВЧисткеОстатки.Штрихкод";
		
	Запрос.УстановитьПараметр("Организация",ТекущийОбъект.Организация);
	Запрос.УстановитьПараметр("Контрагент",	ТекущийОбъект.Отправитель);
	Запрос.УстановитьПараметр("Период",		ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка));
	
	ТекущийОбъект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

#КонецЕсли