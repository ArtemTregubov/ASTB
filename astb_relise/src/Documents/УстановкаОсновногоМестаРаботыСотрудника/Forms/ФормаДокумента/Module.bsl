&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация", Объект.Организация));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуДокументаНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Документы.УстановкаОсновногоМестаРаботыСотрудника.ЗаполнитьТабличнуюЧасть(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Объект.ОсновноеМестоРаботы.Количество() > 0 Тогда
		
		Текст = "ru = ""Табличная часть будет очищена! Продолжить выполнение операции?"";";
		
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтаФорма, Параметры);
		
		ПоказатьВопрос(Оповещение,НСтр(Текст),РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ЗаполнитьТаблицуДокументаНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

    Объект.ОсновноеМестоРаботы.Очистить();

	ЗаполнитьТаблицуДокументаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЭтаФорма.УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация", Объект.Организация));
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновноеМестоРаботыОсновноеМестоРаботыПриИзменении(Элемент)
	
	Если Элементы.ОсновноеМестоРаботы.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ОсновноеМестоРаботы.ТекущиеДанные;
	
	Если Не ТекущиеДанные.ОсновноеМестоРаботы Тогда //снимать флажок нельзя
		ТекущиеДанные.ОсновноеМестоРаботы = Истина;
	Иначе
		ОсновноеМестоРаботыОсновноеМестоРаботыПриИзмененииНаСервере(ТекущиеДанные.НомерСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОсновноеМестоРаботыОсновноеМестоРаботыПриИзмененииНаСервере(НомерИзмененнойСтроки)
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	
	Для Каждого СтрокаТаблицыДокумента Из ТекущийОбъект.ОсновноеМестоРаботы Цикл
		
		Если СтрокаТаблицыДокумента.НомерСтроки = НомерИзмененнойСтроки Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицыДокумента.ОсновноеМестоРаботы = Ложь;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ТекущийОбъект,"Объект");
	
КонецПроцедуры




