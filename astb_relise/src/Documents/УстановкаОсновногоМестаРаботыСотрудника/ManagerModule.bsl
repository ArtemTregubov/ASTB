#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УстановкаОсновногоМестаРаботыСотрудникаОсновноеМестоРаботы.Ссылка.Дата КАК Период,
	|	УстановкаОсновногоМестаРаботыСотрудникаОсновноеМестоРаботы.Ссылка.Организация,
	|	УстановкаОсновногоМестаРаботыСотрудникаОсновноеМестоРаботы.Ссылка.Сотрудник,
	|	УстановкаОсновногоМестаРаботыСотрудникаОсновноеМестоРаботы.Подразделение,
	|	УстановкаОсновногоМестаРаботыСотрудникаОсновноеМестоРаботы.Должность,
	|	УстановкаОсновногоМестаРаботыСотрудникаОсновноеМестоРаботы.РабочееМесто,
	|	УстановкаОсновногоМестаРаботыСотрудникаОсновноеМестоРаботы.ОсновноеМестоРаботы
	|ПОМЕСТИТЬ ВТ_ТаблицаДокумента
	|ИЗ
	|	Документ.УстановкаОсновногоМестаРаботыСотрудника.ОсновноеМестоРаботы КАК УстановкаОсновногоМестаРаботыСотрудникаОсновноеМестоРаботы
	|ГДЕ
	|	УстановкаОсновногоМестаРаботыСотрудникаОсновноеМестоРаботы.Ссылка = &Ссылка
	|	И УстановкаОсновногоМестаРаботыСотрудникаОсновноеМестоРаботы.ОсновноеМестоРаботы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Подразделение,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Должность,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.РабочееМесто
	|ПОМЕСТИТЬ ВТ_ОсновноеМестоРаботы
	|ИЗ
	|	РегистрСведений.ОсновноеМестоРаботыСотрудника.СрезПоследних(
	|			&ПериодРасчета,
	|			Организация = &Организация
	|				И Сотрудник = &Сотрудник) КАК ОсновноеМестоРаботыСотрудникаСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаДокумента.Период,
	|	ВТ_ТаблицаДокумента.Организация,
	|	ВТ_ТаблицаДокумента.Сотрудник,
	|	ВТ_ТаблицаДокумента.Подразделение,
	|	ВТ_ТаблицаДокумента.Должность,
	|	ВТ_ТаблицаДокумента.РабочееМесто,
	|	ВТ_ТаблицаДокумента.ОсновноеМестоРаботы
	|ИЗ
	|	ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОсновноеМестоРаботы КАК ВТ_ОсновноеМестоРаботы
	|		ПО ВТ_ТаблицаДокумента.Подразделение = ВТ_ОсновноеМестоРаботы.Подразделение
	|			И ВТ_ТаблицаДокумента.Должность = ВТ_ОсновноеМестоРаботы.Должность
	|			И ВТ_ТаблицаДокумента.РабочееМесто = ВТ_ОсновноеМестоРаботы.РабочееМесто
	|ГДЕ
	|	ВТ_ОсновноеМестоРаботы.Подразделение ЕСТЬ NULL ";
	
	Запрос.УстановитьПараметр("Ссылка",			ДокументСсылка);
	Запрос.УстановитьПараметр("ПериодРасчета",	ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ДокументСсылка));
	Запрос.УстановитьПараметр("Организация",	ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("Сотрудник",		ДокументСсылка.Сотрудник);
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	//по регистру "Основное место работы сотрудника"
	ТаблицыДляДвижений.Вставить("ТаблицаОсновноеМестоРаботыСотрудника", Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧасть(ТекущийОбъект) Экспорт	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Подразделение,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Должность,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.РабочееМесто,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.ОсновноеМестоРаботы
	|ПОМЕСТИТЬ ВТ_ОсновноеМестоРаботы
	|ИЗ
	|	РегистрСведений.ОсновноеМестоРаботыСотрудника.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И Сотрудник = &Сотрудник) КАК ОсновноеМестоРаботыСотрудникаСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗанятыеРабочиеМестаОстатки.Подразделение,
	|	ЗанятыеРабочиеМестаОстатки.Должность,
	|	ЗанятыеРабочиеМестаОстатки.РабочееМесто
	|ПОМЕСТИТЬ ВТ_ЗанятыеРабочиеМеста
	|ИЗ
	|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
	|			&Период,
	|			Организация = &Организация
	|				И Сотрудник = &Сотрудник) КАК ЗанятыеРабочиеМестаОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗанятыеРабочиеМеста.Подразделение,
	|	ВТ_ЗанятыеРабочиеМеста.Должность,
	|	ВТ_ЗанятыеРабочиеМеста.РабочееМесто,
	|	ЕСТЬNULL(ВТ_ОсновноеМестоРаботы.ОсновноеМестоРаботы, ЛОЖЬ) КАК ОсновноеМестоРаботы
	|ИЗ
	|	ВТ_ЗанятыеРабочиеМеста КАК ВТ_ЗанятыеРабочиеМеста
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОсновноеМестоРаботы КАК ВТ_ОсновноеМестоРаботы
	|		ПО ВТ_ЗанятыеРабочиеМеста.Подразделение = ВТ_ОсновноеМестоРаботы.Подразделение
	|			И ВТ_ЗанятыеРабочиеМеста.Должность = ВТ_ОсновноеМестоРаботы.Должность
	|			И ВТ_ЗанятыеРабочиеМеста.РабочееМесто = ВТ_ОсновноеМестоРаботы.РабочееМесто";
	
	Запрос.УстановитьПараметр("Период",		ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка));
	Запрос.УстановитьПараметр("Организация",ТекущийОбъект.Организация);
	Запрос.УстановитьПараметр("Сотрудник",	ТекущийОбъект.Сотрудник);
	
	ТекущийОбъект.ОсновноеМестоРаботы.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры
	
#КонецЕсли