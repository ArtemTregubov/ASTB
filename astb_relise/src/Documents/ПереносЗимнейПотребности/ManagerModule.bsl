#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	//по регистру "ПотребностьВыдачиСИЗ"
	ТаблицыДляДвижений.Вставить("ТаблицаПотребностьВыдачиСИЗ", ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуДвиженийПоПотребности(ДокументСсылка));
	
	//по регистру "ВыдачаПоПотребности"
	ТаблицыДляДвижений.Вставить("ТаблицаВыдачаПоПотребности", ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуДвиженийВыдачиПоПотребности(ДокументСсылка,ТаблицыДляДвижений.ТаблицаПотребностьВыдачиСИЗ));
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧасть(ТекущийОбъект) Экспорт	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПериодыЗимыСрезПоследних.Подразделение,
	|	ПериодыЗимыСрезПоследних.НачалоЗимы,
	|	ПериодыЗимыСрезПоследних.КонецЗимы
	|ИЗ
	|	РегистрСведений.ПериодыЗимы.СрезПоследних(&Период, Организация = &Организация) КАК ПериодыЗимыСрезПоследних
	|ГДЕ
	|	ПериодыЗимыСрезПоследних.Использовать";
	
	Запрос.УстановитьПараметр("Период",		ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка));
	Запрос.УстановитьПараметр("Организация",ТекущийОбъект.Организация);
	
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТаблицыЗапроса Из ТаблицаЗапроса Цикл
		
		СтруктураПериодаЗимы = ПроцедурыРаботыСНормамиСервер.ПолучитьПериодЗимы(ТекущийОбъект.Дата,СтрокаТаблицыЗапроса.НачалоЗимы,СтрокаТаблицыЗапроса.КонецЗимы);
		
		НачальнаяДатаБудущейЗимы 	= СтруктураПериодаЗимы.НачалоЗимы;
		КонечнаяДатаБудущейЗимы 	= СтруктураПериодаЗимы.КонецЗимы;
		
		НоваяСтрока = ТекущийОбъект.ПериодыЗимы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицыЗапроса);
		НоваяСтрока.НачальнаяДатаБудущейЗимы   = НачальнаяДатаБудущейЗимы;
		НоваяСтрока.КонечнаяДатаБудущейЗимы    = КонечнаяДатаБудущейЗимы;
		
	КонецЦикла;

КонецПроцедуры
	
#КонецЕсли