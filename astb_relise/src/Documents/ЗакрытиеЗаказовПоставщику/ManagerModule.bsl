#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ЗакрытиеЗаказовПоставщикуТовары.Ссылка.Организация КАК Организация,
	|	ЗакрытиеЗаказовПоставщикуТовары.ЗаказПоставщику.Склад КАК Склад,
	|	ЗакрытиеЗаказовПоставщикуТовары.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ЗакрытиеЗаказовПоставщикуТовары.Номенклатура,
	|	ЗакрытиеЗаказовПоставщикуТовары.ХарактеристикаНоменклатуры,
	|	ЗакрытиеЗаказовПоставщикуТовары.ДокументВыдачи,
	|	ЗакрытиеЗаказовПоставщикуТовары.Количество,
	|	ЗакрытиеЗаказовПоставщикуТовары.НомерСтроки,
	|	ЗакрытиеЗаказовПоставщикуТовары.Ссылка.Дата КАК Период
	|ИЗ
	|	Документ.ЗакрытиеЗаказовПоставщику.Товары КАК ЗакрытиеЗаказовПоставщикуТовары
	|ГДЕ
	|	ЗакрытиеЗаказовПоставщикуТовары.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	ТаблицыДляДвижений.Вставить("ТаблицаЗаказыПоставщику", Результат);
	
КонецПроцедуры

//Танцюра А.Н. -- №138055 Доработка связки документов -- 15.11.2021 <<<

//Процедура ЗаполнитьДокумент(ТекущийОбъект) Экспорт
Процедура ЗаполнитьДокумент(Основание = Неопределено,ТекущийОбъект) Экспорт
	
	Если Основание = Неопределено Тогда
		Основание = Документы.ЗаказПоставщику.ПустаяСсылка();
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Основание) Тогда
		ТекущийОбъект.Организация 			= Основание.Организация; 
		ТекущийОбъект.ДатаЗакрытияЗаказов 	= КонецДня(Основание.Дата) + 1; 
		ТекущийОбъект.ДокументОснование		= Основание.Ссылка;
	КонецЕсли;	
	
//Танцюра А.Н. -- №138055 Доработка связки документов -- 15.11.2021 >>>

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = НОВЫЙ Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказыПоставщикуОстатки.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ЗаказыПоставщикуОстатки.Склад КАК Склад,
	|	ЗаказыПоставщикуОстатки.Номенклатура КАК Номенклатура,
	|	ЗаказыПоставщикуОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ЗаказыПоставщикуОстатки.ДокументВыдачи КАК ДокументВыдачи,
	|	ЗаказыПоставщикуОстатки.КоличествоОстаток КАК Количество,
	|	ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) КАК Цена,
	|	ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) * ЗаказыПоставщикуОстатки.КоличествоОстаток КАК Сумма
	|ИЗ
	|	РегистрНакопления.ЗаказыПоставщику.Остатки(
	|			&ПериодРасчета,
	|			Организация = &Организация                                            
	//Танцюра А.Н. -- №138055 Доработка связки документов -- 15.11.2021 <<<
	|				И ЗаказПоставщику.НачалоПериодаВыдачи < НАЧАЛОПЕРИОДА(&ДатаЗакрытия, МЕСЯЦ)
	|				И (&Основание = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка) ИЛИ ЗаказПоставщику = &Основание)) КАК ЗаказыПоставщикуОстатки  
	//Танцюра А.Н. -- №138055 Доработка связки документов -- 15.11.2021 >>>
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	|			МАКСИМУМ(ЦеныНоменклатурыСрезПоследних.Цена) КАК Цена,
	|			ЦеныНоменклатурыСрезПоследних.Поставщик КАК Поставщик
	|		ИЗ
	|			РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ПериодРасчета, Организация = &Организация) КАК ЦеныНоменклатурыСрезПоследних
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ЦеныНоменклатурыСрезПоследних.Номенклатура,
	|			ЦеныНоменклатурыСрезПоследних.Поставщик) КАК ЦеныНоменклатуры
	|		ПО ЗаказыПоставщикуОстатки.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|			И ЗаказыПоставщикуОстатки.Номенклатура.Поставщик = ЦеныНоменклатуры.Поставщик
	|ГДЕ
	|	ЗаказыПоставщикуОстатки.КоличествоОстаток > 0";
	
	Запрос.УстановитьПараметр("ПериодРасчета",	ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка));
	Запрос.УстановитьПараметр("ДатаЗакрытия",	ТекущийОбъект.ДатаЗакрытияЗаказов);
	Запрос.УстановитьПараметр("Организация",	ТекущийОбъект.Организация);    
	//Танцюра А.Н. -- №138055 Доработка связки документов -- 15.11.2021 <<<
	Запрос.УстановитьПараметр("Основание",		Основание); 
	//Танцюра А.Н. -- №138055 Доработка связки документов -- 15.11.2021 >>>
	
	ТекущийОбъект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
	ТекущийОбъект.СуммаДокумента = ТекущийОбъект.Товары.Итог("Сумма");
	
КонецПроцедуры  



#КонецЕсли