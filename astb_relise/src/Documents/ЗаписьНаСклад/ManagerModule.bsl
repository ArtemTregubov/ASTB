#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	СтруктураИнформацииОСтаройЗаписи = ПолучитьИнформациюОСтаройЗаписи(ДокументСсылка);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаписьНаСклад.Дата КАК Период,
	|	ЗаписьНаСклад.Организация КАК Организация,
	|	ЗаписьНаСклад.Склад КАК Склад,
	|	&СтарыйДень КАК День,
	|	ЗаписьНаСклад.Сотрудник КАК Сотрудник,
	|	&СтароеВремя КАК Время,
	|	ЛОЖЬ КАК Использовать
	|ИЗ
	|	Документ.ЗаписьНаСклад КАК ЗаписьНаСклад
	|ГДЕ
	|	ЗаписьНаСклад.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаписьНаСклад.Дата КАК Период,
	|	ЗаписьНаСклад.Организация КАК Организация,
	|	ЗаписьНаСклад.Сотрудник КАК Сотрудник,
	|	ЗаписьНаСклад.Склад КАК Склад,
	|	ЗаписьНаСклад.День КАК День,
	|	ЗаписьНаСклад.Время КАК Время,
	|	ИСТИНА КАК Использовать
	|ИЗ
	|	Документ.ЗаписьНаСклад КАК ЗаписьНаСклад
	|ГДЕ
	|	ЗаписьНаСклад.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",			ДокументСсылка);
	Запрос.УстановитьПараметр("СтарыйДень",		СтруктураИнформацииОСтаройЗаписи.СтараяЗаписьДень);
	Запрос.УстановитьПараметр("СтароеВремя",	СтруктураИнформацииОСтаройЗаписи.СтараяЗаписьВремя);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаСтаройЗаписи = Результат[0].Выгрузить();
	ТаблицаНовойЗаписи 	= Результат[1].Выгрузить();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	Если ЗначениеЗаполнено(СтруктураИнформацииОСтаройЗаписи.СтараяЗаписьДень) Тогда
	 	Если ЗначениеЗаполнено(ДокументСсылка.День) И ЗначениеЗаполнено(ДокументСсылка.Время) Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаНовойЗаписи,ТаблицаСтаройЗаписи);
		КонецЕсли;	
		ТаблицыДляДвижений.Вставить("ТаблицаЗаписиНаСклад", ТаблицаСтаройЗаписи);
	Иначе
		Если ЗначениеЗаполнено(ДокументСсылка.День) И ЗначениеЗаполнено(ДокументСсылка.Время) Тогда 
			ТаблицыДляДвижений.Вставить("ТаблицаЗаписиНаСклад", ТаблицаНовойЗаписи);
		КонецЕсли;	
	КонецЕсли;	
	
КонецПроцедуры

Функция ПолучитьИнформациюОСтаройЗаписи(ТекущийОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ЗаписиНаСкладСрезПоследних.День) КАК День,
	|	МАКСИМУМ(ЗаписиНаСкладСрезПоследних.Время) КАК Время
	|ПОМЕСТИТЬ ВТ_Результат
	|ИЗ
	|	РегистрСведений.ЗаписиНаСклад.СрезПоследних(
	|			&ПериодАнализа,
	|			Организация = &Организация
	|				И Склад = &Склад
	|				И Сотрудник = &Сотрудник
	|				И День >= &День) КАК ЗаписиНаСкладСрезПоследних
	|ГДЕ
	|	ЗаписиНаСкладСрезПоследних.Использовать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Результат.День КАК День,
	|	ВТ_Результат.Время КАК Время
	|ИЗ
	|	ВТ_Результат КАК ВТ_Результат
	|ГДЕ
	|	НЕ ВТ_Результат.День ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ПериодАнализа", 	ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка));
	Запрос.УстановитьПараметр("Организация",	ТекущийОбъект.Организация);
	Запрос.УстановитьПараметр("Склад",			ТекущийОбъект.Склад);
	Запрос.УстановитьПараметр("Сотрудник",		ТекущийОбъект.Сотрудник);
	Запрос.УстановитьПараметр("День",			НачалоДня(ТекущийОбъект.Дата));
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		СтараяЗаписьДень 	= Дата(1,1,1,0,0,0);
		СтараяЗаписьВремя 	= Дата(1,1,1,0,0,0);
	Иначе
		ТаблицаЗапроса 		= Результат.Выгрузить();
		СтараяЗаписьДень 	= ТаблицаЗапроса[0].День;
		СтараяЗаписьВремя 	= ТаблицаЗапроса[0].Время;
	КонецЕсли;
	
	Возврат Новый Структура("СтараяЗаписьДень, СтараяЗаписьВремя", СтараяЗаписьДень, СтараяЗаписьВремя);
	
КонецФункции	

#КонецЕсли