#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Организация КАК Организация,
	|	ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Склад КАК Склад,
	|	ВыдачаДежурныхСредствЗащитыТовары.Номенклатура КАК Номенклатура,
	|	ВыдачаДежурныхСредствЗащитыТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВыдачаДежурныхСредствЗащитыТовары.Количество КАК Количество,
	|	ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Дата КАК Период,
	|	ВыдачаДежурныхСредствЗащитыТовары.НоменклатураНормы КАК НоменклатураНормы,
	|	ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Дата КАК ДатаВыдачи,
	|	ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Сотрудник КАК Сотрудник,
	|	ВыдачаДежурныхСредствЗащитыТовары.ПроцентИзноса КАК ПроцентИзноса
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	Документ.ВыдачаДежурныхСредствЗащиты.Товары КАК ВыдачаДежурныхСредствЗащитыТовары
	|ГДЕ
	|	ВыдачаДежурныхСредствЗащитыТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШтрихкодыНоменклатурыДляУчетаЧистки.Штрихкод КАК Штрихкод
	|ПОМЕСТИТЬ ВТ_Штрихкоды
	|ИЗ
	|	РегистрСведений.ШтрихкодыНоменклатурыДляУчетаЧистки КАК ШтрихкодыНоменклатурыДляУчетаЧистки
	|ГДЕ
	|	НЕ ШтрихкодыНоменклатурыДляУчетаЧистки.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ОрганизацияДляОстатков КАК Организация,
	|	ВТ_ДанныеДокумента.Склад КАК Склад,
	|	ВТ_ДанныеДокумента.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеДокумента.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ВТ_ДанныеДокумента.Количество) КАК Количество,
	|	ВТ_ДанныеДокумента.Период КАК Период,
	|	ВТ_ДанныеДокумента.ПроцентИзноса КАК ПроцентИзноса
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеДокумента.Организация,
	|	ВТ_ДанныеДокумента.Склад,
	|	ВТ_ДанныеДокумента.Номенклатура,
	|	ВТ_ДанныеДокумента.ХарактеристикаНоменклатуры,
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.ПроцентИзноса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Организация КАК Организация,
	|	ВТ_ДанныеДокумента.Сотрудник КАК Сотрудник,
	|	ВТ_ДанныеДокумента.НоменклатураНормы КАК НоменклатураНормы,
	|	ВТ_ДанныеДокумента.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеДокумента.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ДанныеДокумента.ДатаВыдачи КАК ДатаВыдачи,
	|	СУММА(ВТ_ДанныеДокумента.Количество) КАК Количество,
	|	ВТ_ДанныеДокумента.Период КАК Период,
	|	ВТ_ДанныеДокумента.ПроцентИзноса КАК ПроцентИзноса
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеДокумента.Организация,
	|	ВТ_ДанныеДокумента.Номенклатура,
	|	ВТ_ДанныеДокумента.ХарактеристикаНоменклатуры,
	|	ВТ_ДанныеДокумента.НоменклатураНормы,
	|	ВТ_ДанныеДокумента.ДатаВыдачи,
	|	ВТ_ДанныеДокумента.Сотрудник,
	|	ВТ_ДанныеДокумента.Период,
	|	ВТ_ДанныеДокумента.ПроцентИзноса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.Ссылка.Организация КАК Организация,
	|	ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
	|	ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.Ссылка.Сотрудник КАК Сотрудник,
	|	ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.ДатаВыдачи КАК ДатаВыдачи
	|ИЗ
	|	Документ.ВыдачаДежурныхСредствЗащиты.ШтрихкодыНоменклатуры КАК ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Штрихкоды КАК ВТ_Штрихкоды
	|		ПО ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.Штрихкод = ВТ_Штрихкоды.Штрихкод
	|ГДЕ
	|	ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.Ссылка = &Ссылка
	//Танцюра А.Н. -- №129655 BUG: одновременная установка штрихкодов -- 02.11.2021 <<<
	//|	И ВТ_Штрихкоды.Штрихкод ЕСТЬ NULL
	//Танцюра А.Н. -- №129655 BUG: одновременная установка штрихкодов -- 02.11.2021 >>>
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыдачаДежурныхСредствЗащиты.Дата КАК Период,
	|	ВыдачаДежурныхСредствЗащиты.Организация КАК Организация,
	|	ВыдачаДежурныхСредствЗащиты.Склад КАК Склад,
	|	ВыдачаДежурныхСредствЗащиты.Сотрудник КАК Сотрудник,
	|	ЗНАЧЕНИЕ(Справочник.КатегорииОбращенияНаСклад.ВыдачаДежурныхСредствЗащиты) КАК КатегорияОбращения,
	|	ВыдачаДежурныхСредствЗащиты.Комментарий КАК СодержаниеОбращения,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыОбращенийНаСклад.Закрыто) КАК СтатусОбращения
	|ИЗ
	|	Документ.ВыдачаДежурныхСредствЗащиты КАК ВыдачаДежурныхСредствЗащиты
	|ГДЕ
	|	ВыдачаДежурныхСредствЗащиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыдачаДежурныхСредствЗащитыКодыМаркировки.Ссылка.Организация КАК Организация,
	|	ВыдачаДежурныхСредствЗащитыКодыМаркировки.КодМаркировки КАК КодМаркировки,
	|	ВыдачаДежурныхСредствЗащитыКодыМаркировки.Номенклатура КАК Номенклатура,
	|	ВыдачаДежурныхСредствЗащитыКодыМаркировки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры
	|ИЗ
	|	Документ.ВыдачаДежурныхСредствЗащиты.КодыМаркировки КАК ВыдачаДежурныхСредствЗащитыКодыМаркировки
	|ГДЕ
	|	ВыдачаДежурныхСредствЗащитыКодыМаркировки.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ОрганизацияДляОстатков",	?(ПолучитьФункциональнуюОпцию("НеВестиУчетОстатковНоменклатурыПоОрганизации"),Справочники.Организации.ПустаяСсылка(),ДокументСсылка.Организация));
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаДвиженийПоОстаткам = Результат[2].Выгрузить();
	ТаблицаДвиженийПоОстаткам.Колонки.Добавить("ВидДвижения");
	ТаблицаДвиженийПоОстаткам.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход,"ВидДвижения");
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	Если НЕ ДокументСсылка.ПредварительнаяСборка Тогда
		
		ТаблицаДвиженийПоВыдаче = Результат[3].Выгрузить();
		ТаблицаДвиженийПоВыдаче.Колонки.Добавить("ВидДвижения");
		ТаблицаДвиженийПоВыдаче.ЗаполнитьЗначения(ВидДвиженияНакопления.Приход,"ВидДвижения");
		
		ТаблицаДвиженийПоШтрихкодам = Результат[4].Выгрузить();
		
		ТаблицаДвиженийПоМаркировке = Результат[6].Выгрузить();
		
		//по регистру "КодыМаркировкиВыданныхСредствЗащиты"
		ТаблицыДляДвижений.Вставить("ТаблицаКодыМаркировкиВыданныхСредствЗащиты", ТаблицаДвиженийПоМаркировке);
		
		//по регистру "ШтрихкодыНоменклатурыДляУчетаЧистки"
		ТаблицыДляДвижений.Вставить("ТаблицаШтрихкодыНоменклатурыДляУчетаЧистки", ТаблицаДвиженийПоШтрихкодам);
		
		//по регистру "ИсторияОбращенийНаСклад"
		ТаблицыДляДвижений.Вставить("ТаблицаИсторияОбращенийНаСклад", Результат[5].Выгрузить());
		
		//движения по регистру "Выданные средства защиты"
		ТаблицыДляДвижений.Вставить("ТаблицаВыданныеСредстваЗащиты", ТаблицаДвиженийПоВыдаче);
		
	КонецЕсли;
	
	//движения по регистру "Остатки номенклатуры"
	ТаблицыДляДвижений.Вставить("ТаблицаОстаткиНоменклатуры", ТаблицаДвиженийПоОстаткам);
			
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТоварнаяНакладная") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ТоварнаяНакладная",
			"Товарная накладная",
			СформироватьПечатнуюФормуТоварнойНакладной(МассивОбъектов, ОбъектыПечати),,"Документ.ВыдачаДежурныхСредствЗащиты.ПФ_MXL_ТоварнаяНакладная");
				
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаданиеНаСборку") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ЗаданиеНаСборку",
			"Задание на сборку",
			СформироватьПечатнуюФормуЗаданияНаСборку(МассивОбъектов, ОбъектыПечати),,"Документ.ВыдачаДежурныхСредствЗащиты.ПФ_MXL_ЗаданиеНаСборку");
				
	КонецЕсли;
		
КонецПроцедуры

Функция СформироватьПечатнуюФормуТоварнойНакладной(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ВыдачаДежурныхСредствЗащиты_ТоварнаяНакладная";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОстаткиНоменклатуры.Регистратор КАК Ссылка,
	|	ОстаткиНоменклатуры.Регистратор.Организация КАК Организация,
	|	ОстаткиНоменклатуры.Номенклатура КАК Номенклатура,
	|	ОстаткиНоменклатуры.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ОстаткиНоменклатуры.Количество КАК Количество
	|ПОМЕСТИТЬ ВТ_ДанныеРегистра
	|ИЗ
	|	РегистрНакопления.ОстаткиНоменклатуры КАК ОстаткиНоменклатуры
	|ГДЕ
	|	ОстаткиНоменклатуры.Регистратор В(&МассивОбъектов)
	|	И ОстаткиНоменклатуры.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документы.Ссылка КАК Ссылка,
	|	Документы.Организация.Наименование КАК ПредставлениеОрганизации,
	|	Документы.Номер КАК Номер,
	|	Документы.Дата КАК Дата,
	|	Документы.Склад.Наименование КАК ПредставлениеСклада,
	|	Документы.Организация.Префикс КАК Префикс,
	|	Документы.Ответственный.Наименование КАК Ответственный,
	|	Документы.МОЛ.ФизическоеЛицо КАК МОЛ
	|ИЗ
	|	Документ.ВыдачаДежурныхСредствЗащиты КАК Документы
	|ГДЕ
	|	Документы.Ссылка В(&МассивОбъектов)
	|	И НЕ Документы.ПредварительнаяСборка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеРегистра.Ссылка КАК Ссылка,
	|	ВТ_ДанныеРегистра.Количество КАК Количество,
	|	ВТ_ДанныеРегистра.Номенклатура.Наименование КАК НаименованиеТовара,
	|	ВЫБОР
	|		КОГДА ВТ_ДанныеРегистра.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Артикул)
	|			ТОГДА ВТ_ДанныеРегистра.Номенклатура.Артикул
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВТ_ДанныеРегистра.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Код)
	|					ТОГДА ВТ_ДанныеРегистра.Номенклатура.Код
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ВТ_ДанныеРегистра.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.КодСинхронизации)
	|							ТОГДА ВТ_ДанныеРегистра.Номенклатура.КодСинхронизации
	|						ИНАЧЕ ВЫБОР
	|								КОГДА ВТ_ДанныеРегистра.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.НоменклатурныйНомерОрганизации)
	|									ТОГДА ЕСТЬNULL(НоменклатурныеНомераОрганизаций.НоменклатурныйНомер, """")
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК НоменклатурныйНомер,
	|	ВТ_ДанныеРегистра.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	ВТ_ДанныеРегистра.ХарактеристикаНоменклатуры КАК Размер
	|ИЗ
	|	ВТ_ДанныеРегистра КАК ВТ_ДанныеРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатурныеНомераОрганизаций КАК НоменклатурныеНомераОрганизаций
	|		ПО ВТ_ДанныеРегистра.Организация = НоменклатурныеНомераОрганизаций.Организация
	|			И ВТ_ДанныеРегистра.Номенклатура = НоменклатурныеНомераОрганизаций.Номенклатура
	|			И ВТ_ДанныеРегистра.ХарактеристикаНоменклатуры = НоменклатурныеНомераОрганизаций.ХарактеристикаНоменклатуры
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|ИТОГИ ПО
	|	Ссылка");
		
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВыдачаДежурныхСредствЗащиты.ПФ_MXL_ТоварнаяНакладная");
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ДанныеПечати = ПакетЗапросов[1].Выбрать();
	ВыборкаПоДокументам = ПакетЗапросов[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если НЕ ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Ссылка", ДанныеПечати.Ссылка)) Тогда
			Продолжить;
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
			
		Область = Макет.ПолучитьОбласть("Шапка");
		
		Область.Параметры.ТекстЗаголовка 					= "Товарная накладная №" + ДанныеПечати.Номер + " от " + Формат(ДанныеПечати.Дата,"ДЛФ=DD");
		Область.Параметры.ПредставлениеОрганизации 			= ДанныеПечати.ПредставлениеОрганизации;
		Область.Параметры.ПредставлениеСклада 				= ДанныеПечати.ПредставлениеСклада;
		
		ПредставлениеПолучатель 	= ФизическиеЛица.ФамилияИнициалыФизЛица(ДанныеПечати.МОЛ);
		ПредставлениеОтветственного = ФизическиеЛица.ФамилияИнициалыФизЛица(ДанныеПечати.Ответственный);
		
		ТабличныйДокумент.Вывести(Область);
			
		СчетСтрок = 0;
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			СчетСтрок = СчетСтрок + 1;
			
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы");
			Область.Параметры.Заполнить(ВыборкаПоТоварам);
			Область.Параметры.НомерСтроки = СчетСтрок;
			ТабличныйДокумент.Вывести(Область);
			
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		Область.Параметры.ВсегоНаименований 			= СчетСтрок;
		Область.Параметры.ПредставлениеПолучатель 		= ПредставлениеПолучатель;
		Область.Параметры.ПредставлениеОтветственного 	= ПредставлениеОтветственного;
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция СформироватьПечатнуюФормуЗаданияНаСборку(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ВыдачаДежурныхСредствЗащиты_ЗаданиеНаСборку";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОстаткиНоменклатуры.Регистратор КАК Ссылка,
	|	ОстаткиНоменклатуры.Регистратор.Организация КАК Организация,
	|	ОстаткиНоменклатуры.Номенклатура КАК Номенклатура,
	|	ОстаткиНоменклатуры.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ОстаткиНоменклатуры.Количество КАК Количество
	|ПОМЕСТИТЬ ВТ_ДанныеРегистра
	|ИЗ
	|	РегистрНакопления.ОстаткиНоменклатуры КАК ОстаткиНоменклатуры
	|ГДЕ
	|	ОстаткиНоменклатуры.Регистратор В(&МассивОбъектов)
	|	И ОстаткиНоменклатуры.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документы.Ссылка КАК Ссылка,
	|	Документы.Организация.Наименование КАК ПредставлениеОрганизации,
	|	Документы.Номер КАК Номер,
	|	Документы.Дата КАК Дата,
	|	Документы.Склад.Наименование КАК ПредставлениеСклада,
	|	Документы.Организация.Префикс КАК Префикс,
	|	Документы.Ответственный.Наименование КАК Ответственный,
	|	Документы.МОЛ.ФизическоеЛицо КАК МОЛ
	|ИЗ
	|	Документ.ВыдачаДежурныхСредствЗащиты КАК Документы
	|ГДЕ
	|	Документы.Ссылка В(&МассивОбъектов)
	|	И НЕ Документы.ПредварительнаяСборка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеРегистра.Ссылка КАК Ссылка,
	|	ВТ_ДанныеРегистра.Количество КАК Количество,
	|	ВТ_ДанныеРегистра.Номенклатура.Наименование КАК НаименованиеТовара,
	|	ВЫБОР
	|		КОГДА ВТ_ДанныеРегистра.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Артикул)
	|			ТОГДА ВТ_ДанныеРегистра.Номенклатура.Артикул
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВТ_ДанныеРегистра.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Код)
	|					ТОГДА ВТ_ДанныеРегистра.Номенклатура.Код
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ВТ_ДанныеРегистра.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.КодСинхронизации)
	|							ТОГДА ВТ_ДанныеРегистра.Номенклатура.КодСинхронизации
	|						ИНАЧЕ ВЫБОР
	|								КОГДА ВТ_ДанныеРегистра.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.НоменклатурныйНомерОрганизации)
	|									ТОГДА ЕСТЬNULL(НоменклатурныеНомераОрганизаций.НоменклатурныйНомер, """")
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК НоменклатурныйНомер,
	|	ВТ_ДанныеРегистра.ХарактеристикаНоменклатуры КАК Размер
	|ИЗ
	|	ВТ_ДанныеРегистра КАК ВТ_ДанныеРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатурныеНомераОрганизаций КАК НоменклатурныеНомераОрганизаций
	|		ПО ВТ_ДанныеРегистра.Организация = НоменклатурныеНомераОрганизаций.Организация
	|			И ВТ_ДанныеРегистра.Номенклатура = НоменклатурныеНомераОрганизаций.Номенклатура
	|			И ВТ_ДанныеРегистра.ХарактеристикаНоменклатуры = НоменклатурныеНомераОрганизаций.ХарактеристикаНоменклатуры
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ДанныеРегистра.Номенклатура КАК Номенклатура
	|ИЗ
	|	ВТ_ДанныеРегистра КАК ВТ_ДанныеРегистра");
		
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ВыдачаДежурныхСредствЗащиты.ПФ_MXL_ЗаданиеНаСборку");
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ДанныеПечати = ПакетЗапросов[1].Выбрать();
	ВыборкаПоДокументам = ПакетЗапросов[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	МассивНоменклатуры = ПакетЗапросов[3].Выгрузить().ВыгрузитьКолонку("Номенклатура");
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если НЕ ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Ссылка", ДанныеПечати.Ссылка)) Тогда
			Продолжить;
		КонецЕсли;
		
		ТаблицаТочекХранения = ПроцедурыРаботыСНормамиСервер.ПолучитьТочкиХраненияНоменклатуры(МассивНоменклатуры,ДанныеПечати.Склад);
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
			
		Область = Макет.ПолучитьОбласть("Шапка");
		
		Область.Параметры.ТекстЗаголовка = "Задание на сборку №" + ДанныеПечати.Номер + " от " + Формат(ДанныеПечати.Дата,"ДЛФ=DD");
		
		ПредставлениеОтветственного = ФизическиеЛица.ФамилияИнициалыФизЛица(ДанныеПечати.Ответственный);
		
		ТабличныйДокумент.Вывести(Область);
			
		СчетСтрок = 0;
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			Если ТаблицаТочекХранения = Неопределено Тогда
			    ТочкаХранения = "";
			Иначе
				НайденныеСтроки = ТаблицаТочекХранения.НайтиСтроки(Новый Структура("Номенклатура",ВыборкаПоТоварам.Номенклатура));
				Если НайденныеСтроки.Количество() = 0 Тогда
					ТочкаХранения = "";
				Иначе
					ТочкаХранения = НайденныеСтроки[0].ТочкаХранения.Наименование;	
				КонецЕсли;
			КонецЕсли;
			
			СчетСтрок = СчетСтрок + 1;
			
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы");
			Область.Параметры.Заполнить(ВыборкаПоТоварам);
			Область.Параметры.НомерСтроки = СчетСтрок;
			Область.Параметры.ТочкаХранения = ТочкаХранения;
			ТабличныйДокумент.Вывести(Область);
			
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		Область.Параметры.ВсегоНаименований 			= СчетСтрок;
		Область.Параметры.ПредставлениеОтветственного 	= ПредставлениеОтветственного;
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ВыдачаДежурныхСредствЗащиты";
	КомандаПечати.Идентификатор = "ТоварнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Товарная накладная'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	//задание на сборку
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ВыдачаДежурныхСредствЗащиты";
	КомандаПечати.Идентификатор = "ЗаданиеНаСборку";
	КомандаПечати.Представление = НСтр("ru = 'Задание на сборку'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

КонецПроцедуры

// Функция помещает необходимые данные в структуру. Структура помещается во временное хранилище
//
// Возвращаемое значение:
//	Адрес   - адрес структуры данных во временном хранилище
//
Функция ПолучитьДанныеДляПечатиЭтикеток(МассивДокументов) Экспорт
	
	//Танцюра А.Н. -- №129655 BUG: одновременная установка штрихкодов -- 02.11.2021 <<<
	
	ТекстСообщения = "Документы: ";
	
	ЕстьНепроведенные = Ложь;
	
	Для Каждого ТекущийДокумент Из МассивДокументов Цикл
		
		Если НЕ ТекущийДокумент.Проведен Тогда
			
			ЕстьНепроведенные = Истина;
			
			ТекстСообщения = ТекстСообщения + Символы.ПС + ТекущийДокумент;
			
		КонецЕсли;	
		
	КонецЦикла;	
	
	ТекстСообщения = ТекстСообщения + Символы.ПС + "не проведены. Печать этикеток невозможна.";
	
	Если ЕстьНепроведенные Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = ТекстСообщения;
		СообщениеПользователю.Сообщить();
	КонецЕсли;	
	
	//Танцюра А.Н. -- №129655 BUG: одновременная установка штрихкодов -- 02.11.2021 >>>
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВыдачаДежурныхСредствЗащитыТовары.Номенклатура.Комплект
	|			ТОГДА НоменклатураКомплектующие.Номенклатура
	|		ИНАЧЕ ВыдачаДежурныхСредствЗащитыТовары.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Организация.ОтображатьДатуОкончанияСрокаНоскиНаЭтикетках
	|				ТОГДА ВЫБОР
	|						КОГДА НормыВыдачиСИЗСоставНормы.ПериодичностьВыдачи.ТипПериода = ЗНАЧЕНИЕ(Перечисление.ДоступныеПериодыОтчета.Год)
	|							ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Дата, ДЕНЬ), МЕСЯЦ, НормыВыдачиСИЗСоставНормы.ПериодичностьВыдачи.КоличествоПериодов * 12)
	|						ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Дата, ДЕНЬ), МЕСЯЦ, НормыВыдачиСИЗСоставНормы.ПериодичностьВыдачи.КоличествоПериодов)
	|					КОНЕЦ
	|			ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|		КОНЕЦ) КАК ДатаОкончанияИспользования
	|ПОМЕСТИТЬ ВТ_ДатаОкончанияИспользования
	|ИЗ
	|	Документ.ВыдачаДежурныхСредствЗащиты.Товары КАК ВыдачаДежурныхСредствЗащитыТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.Комплектующие КАК НоменклатураКомплектующие
	|		ПО ВыдачаДежурныхСредствЗащитыТовары.Номенклатура = НоменклатураКомплектующие.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НормыВыдачиСИЗ.СоставНормы КАК НормыВыдачиСИЗСоставНормы
	|		ПО ВыдачаДежурныхСредствЗащитыТовары.НормаВыдачи = НормыВыдачиСИЗСоставНормы.Ссылка
	|			И ВыдачаДежурныхСредствЗащитыТовары.НоменклатураНормы = НормыВыдачиСИЗСоставНормы.НоменклатураНормы
	|ГДЕ
	|	ВыдачаДежурныхСредствЗащитыТовары.Ссылка В(&МассивДокументов)
	//Танцюра А.Н. -- №129655 BUG: одновременная установка штрихкодов -- 02.11.2021 <<<
	|	И ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Проведен
	//Танцюра А.Н. -- №129655 BUG: одновременная установка штрихкодов -- 02.11.2021 >>>
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ВыдачаДежурныхСредствЗащитыТовары.Номенклатура.Комплект
	|			ТОГДА НоменклатураКомплектующие.Номенклатура
	|		ИНАЧЕ ВыдачаДежурныхСредствЗащитыТовары.Номенклатура
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.Ссылка.Сотрудник КАК Сотрудник,
	|	ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.Ссылка.Дата КАК ДатаВыдачи,
	|	ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
	|	ВТ_ДатаОкончанияИспользования.ДатаОкончанияИспользования КАК ДатаОкончанияИспользования
	|ИЗ
	|	Документ.ВыдачаДежурныхСредствЗащиты.ШтрихкодыНоменклатуры КАК ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДатаОкончанияИспользования КАК ВТ_ДатаОкончанияИспользования
	|		ПО ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.Номенклатура = ВТ_ДатаОкончанияИспользования.Номенклатура
	|ГДЕ
	|	ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.Ссылка В(&МассивДокументов)
	//Танцюра А.Н. -- №129655 BUG: одновременная установка штрихкодов -- 02.11.2021 <<<
	|	И ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.Ссылка.Проведен
	//Танцюра А.Н. -- №129655 BUG: одновременная установка штрихкодов -- 02.11.2021 >>>
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.Ссылка.Сотрудник.Наименование,
	|	ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.Номенклатура.Наименование,
	|	ВыдачаДежурныхСредствЗащитыШтрихкодыНоменклатуры.ХарактеристикаНоменклатуры.КодSAP";
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	СтруктураРезультат = Новый Структура;
	СтруктураРезультат.Вставить("ИсходныеДанные", Запрос.Выполнить().Выгрузить());
	
	Возврат ПоместитьВоВременноеХранилище(СтруктураРезультат);
	
КонецФункции 

Процедура ЗаполнитьШтрихкоды(ТекущийОбъект) Экспорт
	
	ТаблицаВыдачи = ТекущийОбъект.Товары.Выгрузить();
	ТаблицаВыдачи.Колонки.Добавить("ДатаВыдачи",Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.Дата)));
	ТаблицаВыдачи.ЗаполнитьЗначения(ТекущийОбъект.Дата,"ДатаВыдачи");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаВыдачи.Номенклатура КАК НоменклатураДокумента,
	|	ТаблицаВыдачи.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатурыДокумента,
	|	ТаблицаВыдачи.Количество КАК Количество,
	|	ТаблицаВыдачи.ДатаВыдачи КАК ДатаВыдачи
	|ПОМЕСТИТЬ ВТ_ТаблицаВыдачи
	|ИЗ
	|	&ТаблицаВыдачи КАК ТаблицаВыдачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	ЕСТЬNULL(ХарактеристикиНоменклатуры.Ссылка, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК ХарактеристикаНоменклатуры,
	|	ВТ_ТаблицаВыдачи.Количество КАК Количество,
	|	ВТ_ТаблицаВыдачи.ДатаВыдачи КАК ДатаВыдачи
	|ПОМЕСТИТЬ ВТ_ВыдачаСНоменклатурой
	|ИЗ
	|	ВТ_ТаблицаВыдачи КАК ВТ_ТаблицаВыдачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|		ПО ВТ_ТаблицаВыдачи.НоменклатураДокумента = Номенклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО ВТ_ТаблицаВыдачи.НоменклатураДокумента = ХарактеристикиНоменклатуры.Владелец
	|			И ВТ_ТаблицаВыдачи.ХарактеристикаНоменклатурыДокумента = ХарактеристикиНоменклатуры.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураКомплектующие.Ссылка КАК Комплект,
	|	НоменклатураКомплектующие.Номенклатура КАК Номенклатура,
	|	НоменклатураКомплектующие.Количество КАК Количество
	|ПОМЕСТИТЬ ВТ_Комплектующие
	|ИЗ
	|	Справочник.Номенклатура.Комплектующие КАК НоменклатураКомплектующие
	|ГДЕ
	|	НоменклатураКомплектующие.Номенклатура.ИспользоватьШтрихкод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВТ_ВыдачаСНоменклатурой.Номенклатура.Комплект
	|			ТОГДА ВТ_Комплектующие.Номенклатура
	|		ИНАЧЕ ВТ_ВыдачаСНоменклатурой.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	ВТ_ВыдачаСНоменклатурой.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ВТ_ВыдачаСНоменклатурой.Номенклатура.Комплект
	|			ТОГДА ВТ_ВыдачаСНоменклатурой.Количество * ЕСТЬNULL(ВТ_Комплектующие.Количество, 0)
	|		ИНАЧЕ ВТ_ВыдачаСНоменклатурой.Количество
	|	КОНЕЦ КАК Количество,
	|	ВТ_ВыдачаСНоменклатурой.ДатаВыдачи КАК ДатаВыдачи
	|ПОМЕСТИТЬ ВТ_ВыдачаСКомплектующими
	|ИЗ
	|	ВТ_ВыдачаСНоменклатурой КАК ВТ_ВыдачаСНоменклатурой
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Комплектующие КАК ВТ_Комплектующие
	|		ПО ВТ_ВыдачаСНоменклатурой.Номенклатура = ВТ_Комплектующие.Комплект
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ВТ_ВыдачаСНоменклатурой.Номенклатура.Комплект
	|				ТОГДА НЕ ВТ_Комплектующие.Номенклатура ЕСТЬ NULL
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаШтрихкодов.Штрихкод КАК Штрихкод,
	|	ТаблицаШтрихкодов.Номенклатура КАК Номенклатура,
	|	ТаблицаШтрихкодов.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТаблицаШтрихкодов.ДатаВыдачи КАК ДатаВыдачи
	|ПОМЕСТИТЬ ВТ_ТаблицаШтрихкодов
	|ИЗ
	|	&ТаблицаШтрихкодов КАК ТаблицаШтрихкодов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаШтрихкодов.Штрихкод КАК Штрихкод,
	|	ВТ_ТаблицаШтрихкодов.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицаШтрихкодов.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ТаблицаШтрихкодов.ДатаВыдачи КАК ДатаВыдачи
	|ИЗ
	|	ВТ_ТаблицаШтрихкодов КАК ВТ_ТаблицаШтрихкодов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВыдачаСКомплектующими.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(ВТ_ВыдачаСКомплектующими.ХарактеристикаНоменклатуры, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК ХарактеристикаНоменклатуры,
	|	ВТ_ВыдачаСКомплектующими.Количество КАК Количество,
	|	ВТ_ВыдачаСКомплектующими.ДатаВыдачи КАК ДатаВыдачи
	|ИЗ
	|	ВТ_ВыдачаСКомплектующими КАК ВТ_ВыдачаСКомплектующими
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО ВТ_ВыдачаСКомплектующими.Номенклатура = ХарактеристикиНоменклатуры.Владелец
	|			И ВТ_ВыдачаСКомплектующими.ХарактеристикаНоменклатуры.Метрика = ХарактеристикиНоменклатуры.Метрика
	|ГДЕ
	|	ВТ_ВыдачаСКомплектующими.Номенклатура.ИспользоватьШтрихкод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ШтрихкодыНоменклатурыДляУчетаЧистки.Номенклатура КАК Номенклатура,
	|	ШтрихкодыНоменклатурыДляУчетаЧистки.ДатаВыдачи КАК ДатаВыдачи,
	|	ШтрихкодыНоменклатурыДляУчетаЧистки.Штрихкод КАК Штрихкод,
	|	ШтрихкодыНоменклатурыДляУчетаЧистки.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.ШтрихкодыНоменклатурыДляУчетаЧистки КАК ШтрихкодыНоменклатурыДляУчетаЧистки
	|ГДЕ
	|	ШтрихкодыНоменклатурыДляУчетаЧистки.Организация = &Организация
	|	И ШтрихкодыНоменклатурыДляУчетаЧистки.Сотрудник = &Сотрудник
	|	И ШтрихкодыНоменклатурыДляУчетаЧистки.Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка)
	|	И НЕ ШтрихкодыНоменклатурыДляУчетаЧистки.Штрихкод В (&МассивШтрихкодов)";
	
	Запрос.УстановитьПараметр("ТаблицаВыдачи",		ТаблицаВыдачи);
	Запрос.УстановитьПараметр("ТаблицаШтрихкодов",	ТекущийОбъект.ШтрихкодыНоменклатуры.Выгрузить());
	Запрос.УстановитьПараметр("Организация",		ТекущийОбъект.Организация);
	Запрос.УстановитьПараметр("Сотрудник",			ТекущийОбъект.Сотрудник);
	Запрос.УстановитьПараметр("МассивШтрихкодов",	ТекущийОбъект.ШтрихкодыНоменклатуры.ВыгрузитьКолонку("Штрихкод"));
	
	Результат = Запрос.ВыполнитьПакет();
		
	ТаблицаШтрихкодов 		= Результат[5].Выгрузить();
	ТаблицаВыдачи 			= Результат[6].Выгрузить();
	ТаблицаСтарыхШтрихКодов = Результат[7].Выгрузить();
	
	Для Каждого СтрокаТаблицыВыдачи Из ТаблицаВыдачи Цикл
		
		СтруктураПоискаШтрихкодов = Новый Структура("Номенклатура, ДатаВыдачи", СтрокаТаблицыВыдачи.Номенклатура, НачалоДня(ТекущийОбъект.Дата));
		СтруктураПоискаСтарыхШтрихкодов = Новый Структура("Регистратор, Номенклатура, ДатаВыдачи", ТекущийОбъект.Ссылка, СтрокаТаблицыВыдачи.Номенклатура, НачалоДня(ТекущийОбъект.Дата));
		
		НайденныеСтрокиТаблицаШтрихкодов 		= ТаблицаШтрихкодов.НайтиСтроки(СтруктураПоискаШтрихкодов);
		НайденныеСтрокиТаблицаСтарыхШтрихКодов 	= ТаблицаСтарыхШтрихКодов.НайтиСтроки(СтруктураПоискаСтарыхШтрихкодов);
		
		КоличествоШтрихкодов 		= НайденныеСтрокиТаблицаШтрихкодов.Количество();
		КоличествоСтарыхШтрихкодов 	= НайденныеСтрокиТаблицаСтарыхШтрихКодов.Количество();
		ВсегоШтрихкодов 			= КоличествоСтарыхШтрихкодов + КоличествоШтрихкодов;
		
		Если ВсегоШтрихкодов >= СтрокаТаблицыВыдачи.Количество Тогда
			Продолжить;
		КонецЕсли;	
		
		КоличествоЭкземпляровДляШтрихкодов = СтрокаТаблицыВыдачи.Количество - ВсегоШтрихкодов;
		
		Пока КоличествоЭкземпляровДляШтрихкодов > 0 Цикл
			
			НоваяСтрока = ТекущийОбъект.ШтрихкодыНоменклатуры.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицыВыдачи);
			
			НоваяСтрока.ДатаВыдачи = НачалоДня(ТекущийОбъект.Дата);
			
			КоличествоЭкземпляровДляШтрихкодов = КоличествоЭкземпляровДляШтрихкодов - 1;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ЗаполнитьПустыеШтрихкоды(ТекущийОбъект);
		
КонецПроцедуры

Процедура ЗаполнитьПустыеШтрихкоды(ТекущийОбъект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МаксимальноеЗначениеШтрихкода = ТекущийОбъект.Организация.МаксимальноеЗначениеШтрихкода;
	
	Для Каждого СтрокаТаблицыШтрихкодов Из ТекущийОбъект.ШтрихкодыНоменклатуры Цикл
		
		Если ЗначениеЗаполнено(СтрокаТаблицыШтрихкодов.Штрихкод) Тогда
			Продолжить;
		КонецЕсли;	
		
		МаксимальноеЗначениеШтрихкода = МаксимальноеЗначениеШтрихкода + 1;
		
		СтрокаТаблицыШтрихкодов.Штрихкод = РегистрыСведений.ШтрихкодыНоменклатурыДляУчетаЧистки.ПолучитьШтрихкодПоКоду(МаксимальноеЗначениеШтрихкода,"2",ТекущийОбъект.Организация.ПрефиксШтрихкодаНоменклатуры);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуДокумента(ТекущийОбъект) Экспорт
	
	ДатаАнализа = ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗанятыеРабочиеМестаОстатки.Сотрудник
	|ИЗ
	|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
	|			&ПериодАнализа,
	|			Организация = &Организация
	|				И Сотрудник = &Сотрудник) КАК ЗанятыеРабочиеМестаОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗанятыеРабочиеМестаОстатки.Сотрудник.Наименование";
	
	Запрос.УстановитьПараметр("ПериодАнализа",	ДатаАнализа);
	Запрос.УстановитьПараметр("Организация",	ТекущийОбъект.Организация);
	Запрос.УстановитьПараметр("Сотрудник",		ТекущийОбъект.Сотрудник);
		
	МассивСотрудников = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник");
	
	ТаблицаЗанятыхРМ 			= ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуЗанятыхРабочихМестСУсловиями(МассивСотрудников,ТекущийОбъект.Организация,ДатаАнализа);
	ТаблицаУстановленныхНорм 	= ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуУстановленныхНорм(ТекущийОбъект.Организация,ДатаАнализа,ТаблицаЗанятыхРМ.ВыгрузитьКолонку("Подразделение"),ТаблицаЗанятыхРМ.ВыгрузитьКолонку("Должность"),ЛОЖЬ);
	ТаблицаСНормами 			= ПроцедурыРаботыСНормамиСервер.ПодобратьНормы(ТаблицаЗанятыхРМ,ТаблицаУстановленныхНорм,ТекущийОбъект.Организация,ДатаАнализа);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаНормСотрудников.Сотрудник КАК Сотрудник,
	|	ТаблицаНормСотрудников.НормаВыдачи КАК НормаВыдачи,
	|	ТаблицаНормСотрудников.НоменклатураНормы КАК НоменклатураНормы,
	|	ТаблицаНормСотрудников.ТипПериода КАК ТипПериода,
	//АсТБ_Alexey_97548_********************************************************************
	|	ТаблицаНормСотрудников.КоличествоПериодов КАК КоличествоПериодов,
	|	ТаблицаНормСотрудников.КоличествоВПериоде КАК КоличествоВПериоде
	//АсТБ_Alexey_97548_********************************************************************
	|ПОМЕСТИТЬ ВТ_НормыСотрудников
	|ИЗ
	|	&ТаблицаНормСотрудников КАК ТаблицаНормСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НормыВыдачиСИЗ.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_НормыВыдачи
	|ИЗ
	|	Справочник.НормыВыдачиСИЗ КАК НормыВыдачиСИЗ
	|ГДЕ
	|	НормыВыдачиСИЗ.Владелец = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НормыСотрудников.НормаВыдачи КАК НормаВыдачи,
	|	ВТ_НормыСотрудников.НоменклатураНормы КАК НоменклатураНормы,
	//АсТБ_Alexey_97548_********************************************************************
	|	ВЫБОР
	|		КОГДА ВТ_НормыСотрудников.ТипПериода = ЗНАЧЕНИЕ(Перечисление.ДоступныеПериодыОтчета.ПустаяСсылка)
	|			ТОГДА 0
	|		ИНАЧЕ ВТ_НормыСотрудников.КоличествоВПериоде
	|	КОНЕЦ КАК Количество,
	//АсТБ_Alexey_97548_********************************************************************
	|	ВТ_НормыСотрудников.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТ_Результат
	|ИЗ
	|	ВТ_НормыСотрудников КАК ВТ_НормыСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НормыВыдачи КАК ВТ_НормыВыдачи
	|		ПО ВТ_НормыСотрудников.НормаВыдачи = ВТ_НормыВыдачи.Ссылка
	|ГДЕ
	|	ВТ_НормыВыдачи.Ссылка.ВидРасчета = ЗНАЧЕНИЕ(Перечисление.ВидыРасчетаНорм.Дежурный)
	//+++АСТБ_Горюшин_Алексей_57264
	|	И НЕ ВТ_НормыВыдачи.Ссылка.СоставНормы.УчитыватьВПотребности
	//---АСТБ_Горюшин_Алексей_57264
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Результат.НормаВыдачи КАК НормаВыдачи,
	|	ВТ_Результат.НоменклатураНормы КАК НоменклатураНормы,
	|	ВТ_Результат.Количество КАК Количество,
	|	ВТ_Результат.Сотрудник КАК Сотрудник
	|ИЗ
	|	ВТ_Результат КАК ВТ_Результат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Результат.Сотрудник КАК Сотрудник,
	|	ВТ_Результат.НоменклатураНормы КАК НоменклатураНормы
	|ИЗ
	|	ВТ_Результат КАК ВТ_Результат";
	
	Запрос.УстановитьПараметр("ТаблицаНормСотрудников",	ТаблицаСНормами);
	Запрос.УстановитьПараметр("Организация",			ТекущийОбъект.Организация);
	Запрос.УстановитьПараметр("ПериодАнализа",			ДатаАнализа);
	Запрос.УстановитьПараметр("МассивСотрудников", 		МассивСотрудников);

	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаДляДокумента					= Результат[3].Выгрузить();
	ТаблицаНоменклатурыНормСотрудников 	= Результат[4].Выгрузить();
	
	//+++АСТБ_Горюшин_Алексей_57264
	// Корректировка фактической потребности
	Для Каждого СтрокаТаблицыДляДокумента ИЗ ТаблицаДляДокумента Цикл
		
		//+++АСТБ_Горюшин_Алексей_57264_2
		Если ЗначениеЗаполнено(ТекущийОбъект.Ссылка) Тогда
		//---АСТБ_Горюшин_Алексей_57264_2	
		ДатаОкончания = ДатаАнализа.Значение.Дата;
		//+++АСТБ_Горюшин_Алексей_57264_2
		Иначе
		    ДатаОкончания = ДатаАнализа.Значение;
		КонецЕсли;
		//---АСТБ_Горюшин_Алексей_57264_2
		Если СтрокаТаблицыДляДокумента.НормаВыдачи.СоставНормы[0].ПериодичностьВыдачи.ТипПериода = Перечисления.ДоступныеПериодыОтчета.Год Тогда
			//+++АСТБ_Горюшин_Алексей_66448
			ДатаНачала = НачалоМесяца(ДобавитьМесяц(ДатаОкончания, 1 + -1 * 12 * СтрокаТаблицыДляДокумента.НормаВыдачи.СоставНормы[0].ПериодичностьВыдачи.КоличествоПериодов));
			//было
			//ДатаНачала = ДобавитьМесяц(ДатаОкончания, -1 * 12 * СтрокаТаблицыДляДокумента.НормаВыдачи.СоставНормы[0].ПериодичностьВыдачи.КоличествоПериодов);
			//---АСТБ_Горюшин_Алексей_66448
		Иначе 
			//+++АСТБ_Горюшин_Алексей_66448
			ДатаНачала = НачалоМесяца(ДобавитьМесяц(ДатаОкончания, 1 + -1 * СтрокаТаблицыДляДокумента.НормаВыдачи.СоставНормы[0].ПериодичностьВыдачи.КоличествоПериодов));
			//было
			//ДатаНачала = ДобавитьМесяц(ДатаОкончания, -1 * СтрокаТаблицыДляДокумента.НормаВыдачи.СоставНормы[0].ПериодичностьВыдачи.КоличествоПериодов);
			//---АСТБ_Горюшин_Алексей_66448
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВыдачаДежурныхСредствЗащитыТовары.НормаВыдачи КАК НормаВыдачи,
		|	ВыдачаДежурныхСредствЗащитыТовары.НоменклатураНормы КАК НоменклатураНормы,
		|	СУММА(ВыдачаДежурныхСредствЗащитыТовары.Количество) КАК Количество,
		|	ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Сотрудник КАК Сотрудник
		|ИЗ
		|	Документ.ВыдачаДежурныхСредствЗащиты.Товары КАК ВыдачаДежурныхСредствЗащитыТовары
		|ГДЕ
		|	ВыдачаДежурныхСредствЗащитыТовары.НоменклатураНормы = &НоменклатураНормы
		|	И ВыдачаДежурныхСредствЗащитыТовары.НормаВыдачи = &НормаВыдачи
		|	И ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Проведен
		|	И ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Сотрудник = &Сотрудник
		|	И ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Дата >= &ДатаНачала
		|	И ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Дата <= &ДатаОкончания
		|
		|СГРУППИРОВАТЬ ПО
		|	ВыдачаДежурныхСредствЗащитыТовары.НормаВыдачи,
		|	ВыдачаДежурныхСредствЗащитыТовары.НоменклатураНормы,
		|	ВыдачаДежурныхСредствЗащитыТовары.Ссылка.Сотрудник";
		
		Запрос.УстановитьПараметр("ДатаНачала", 		ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончания", 		ДатаОкончания);
		Запрос.УстановитьПараметр("НоменклатураНормы", 	СтрокаТаблицыДляДокумента.НоменклатураНормы);
		Запрос.УстановитьПараметр("НормаВыдачи", 		СтрокаТаблицыДляДокумента.НормаВыдачи);
		Запрос.УстановитьПараметр("Сотрудник", 			СтрокаТаблицыДляДокумента.Сотрудник);
		
		ТаблицаРанееВыданных = Запрос.Выполнить().Выгрузить();

		Для Каждого Строка ИЗ ТаблицаРанееВыданных Цикл
			СтрокаТаблицыДляДокумента.Количество = СтрокаТаблицыДляДокумента.Количество - Строка.Количество;
		КонецЦикла;	
		
		СтрокаТаблицыДляДокумента.Количество = ?(СтрокаТаблицыДляДокумента.Количество < 0, 0, СтрокаТаблицыДляДокумента.Количество);
		
	КонецЦикла;
	//---АСТБ_Горюшин_Алексей_57264
	
	ТаблицаРазмеров = ПроцедурыРаботыСНормамиСервер.ПодобратьРазмерыДляСотрудников(ТаблицаНоменклатурыНормСотрудников,ДатаАнализа,ТекущийОбъект.Организация);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДляДокумента.Сотрудник КАК Сотрудник,
	|	ТаблицаДляДокумента.НормаВыдачи КАК НормаВыдачи,
	|	ТаблицаДляДокумента.НоменклатураНормы КАК НоменклатураНормы,
	|	ТаблицаДляДокумента.Количество КАК Количество
	|ПОМЕСТИТЬ ВТ_ТаблицаДляДокумента
	|ИЗ
	|	&ТаблицаДляДокумента КАК ТаблицаДляДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ЦеныНоменклатурыСрезПоследних.Цена) КАК Цена,
	|	ЦеныНоменклатурыСрезПоследних.Поставщик КАК Поставщик
	|ПОМЕСТИТЬ ВТ_ЦеныНоменклатуры
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ПериодАнализа, Организация = &Организация) КАК ЦеныНоменклатурыСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура,
	|	ЦеныНоменклатурыСрезПоследних.Поставщик
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРазмеров.Сотрудник КАК Сотрудник,
	|	ТаблицаРазмеров.НоменклатураНормы КАК НоменклатураНормы,
	|	ТаблицаРазмеров.Номенклатура КАК Номенклатура,
	|	ТаблицаРазмеров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТаблицаРазмеров.ПриоритетСоответствия КАК ПриоритетСоответствия,
	|	ТаблицаРазмеров.Приоритет КАК Приоритет,
	|	ТаблицаРазмеров.ПриоритетРазмера КАК ПриоритетРазмера
	|ПОМЕСТИТЬ ВТ_ТаблицаРазмеров
	|ИЗ
	|	&ТаблицаРазмеров КАК ТаблицаРазмеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Номенклатура
	|ПОМЕСТИТЬ ВТ_Номенклатура
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	НЕ Номенклатура.ЭтоГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
	|	ОстаткиНоменклатурыОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ОстаткиНоменклатурыОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ПОМЕСТИТЬ ВТ_ОстаткиНоменклатуры
	|ИЗ
	|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|			&ПериодАнализа,
	|			Организация = &ОрганизацияДляОстатков
	|				И Склад = &Склад) КАК ОстаткиНоменклатурыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиНоменклатурыОстатки.Номенклатура,
	|	ОстаткиНоменклатурыОстатки.ХарактеристикаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаРазмеров.Сотрудник КАК Сотрудник,
	|	ВТ_ТаблицаРазмеров.НоменклатураНормы КАК НоменклатураНормы,
	|	ВТ_Номенклатура.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицаРазмеров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ТаблицаРазмеров.ПриоритетСоответствия КАК ПриоритетСоответствия,
	|	ВТ_ТаблицаРазмеров.Приоритет КАК Приоритет,
	|	ВТ_ТаблицаРазмеров.ПриоритетРазмера КАК ПриоритетРазмера,
	|	ЕСТЬNULL(ВТ_ОстаткиНоменклатуры.КоличествоОстаток, 0) КАК Остаток
	|ПОМЕСТИТЬ ВТ_ОстаткиПодобранныхРазмеров
	|ИЗ
	|	ВТ_ТаблицаРазмеров КАК ВТ_ТаблицаРазмеров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиНоменклатуры КАК ВТ_ОстаткиНоменклатуры
	|		ПО ВТ_ТаблицаРазмеров.Номенклатура = ВТ_ОстаткиНоменклатуры.Номенклатура
	|			И ВТ_ТаблицаРазмеров.ХарактеристикаНоменклатуры = ВТ_ОстаткиНоменклатуры.ХарактеристикаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Номенклатура КАК ВТ_Номенклатура
	|		ПО ВТ_ТаблицаРазмеров.Номенклатура = ВТ_Номенклатура.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ТаблицаРазмеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОстаткиПодобранныхРазмеров.Сотрудник КАК Сотрудник,
	|	ВТ_ОстаткиПодобранныхРазмеров.НоменклатураНормы КАК НоменклатураНормы,
	|	ВТ_ОстаткиПодобранныхРазмеров.Номенклатура КАК Номенклатура,
	|	ВТ_ОстаткиПодобранныхРазмеров.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ОстаткиПодобранныхРазмеров.ПриоритетСоответствия КАК ПриоритетСоответствия,
	|	ВТ_ОстаткиПодобранныхРазмеров.Приоритет КАК Приоритет,
	|	ВТ_ОстаткиПодобранныхРазмеров.ПриоритетРазмера КАК ПриоритетРазмера,
	|	ЕСТЬNULL(ВТ_ЦеныНоменклатуры.Цена, 0) КАК Цена
	|ПОМЕСТИТЬ ВТ_ОстаткиПодобранныхРазмеровСЦенами
	|ИЗ
	|	ВТ_ОстаткиПодобранныхРазмеров КАК ВТ_ОстаткиПодобранныхРазмеров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЦеныНоменклатуры КАК ВТ_ЦеныНоменклатуры
	|		ПО ВТ_ОстаткиПодобранныхРазмеров.Номенклатура = ВТ_ЦеныНоменклатуры.Номенклатура
	|			И ВТ_ОстаткиПодобранныхРазмеров.Номенклатура.Поставщик = ВТ_ЦеныНоменклатуры.Поставщик
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ЦеныНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаДляДокумента.Сотрудник КАК Сотрудник,
	|	ВТ_ТаблицаДляДокумента.НормаВыдачи КАК НормаВыдачи,
	|	ВТ_ТаблицаДляДокумента.НоменклатураНормы КАК НоменклатураНормы,
	|	ВТ_ТаблицаДляДокумента.Количество КАК Количество,
	|	ВТ_ОстаткиПодобранныхРазмеровСЦенами.Номенклатура КАК Номенклатура,
	|	ВТ_ОстаткиПодобранныхРазмеровСЦенами.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ОстаткиПодобранныхРазмеровСЦенами.ПриоритетСоответствия КАК ПриоритетСоответствия,
	|	ВТ_ОстаткиПодобранныхРазмеровСЦенами.Приоритет КАК Приоритет,
	|	ВТ_ОстаткиПодобранныхРазмеровСЦенами.ПриоритетРазмера КАК ПриоритетРазмера,
	|	ВТ_ОстаткиПодобранныхРазмеровСЦенами.Цена КАК Цена
	|ИЗ
	|	ВТ_ТаблицаДляДокумента КАК ВТ_ТаблицаДляДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиПодобранныхРазмеровСЦенами КАК ВТ_ОстаткиПодобранныхРазмеровСЦенами
	|		ПО ВТ_ТаблицаДляДокумента.Сотрудник = ВТ_ОстаткиПодобранныхРазмеровСЦенами.Сотрудник
	|			И ВТ_ТаблицаДляДокумента.НоменклатураНормы = ВТ_ОстаткиПодобранныхРазмеровСЦенами.НоменклатураНормы
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник,
	|	НормаВыдачи,
	|	НоменклатураНормы,
	|	ПриоритетСоответствия,
	|	Приоритет,
	|	ПриоритетРазмера
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОстаткиНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВТ_ОстаткиНоменклатуры.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ОстаткиНоменклатуры.КоличествоОстаток КАК КоличествоОстаток
	|ИЗ
	|	ВТ_ОстаткиНоменклатуры КАК ВТ_ОстаткиНоменклатуры
	//АсТБ_Alexey_97548_********************************************************************
	|ГДЕ
	|	ВТ_ОстаткиНоменклатуры.КоличествоОстаток > 0";
	//АсТБ_Alexey_97548_********************************************************************
	
	Запрос.УстановитьПараметр("ТаблицаДляДокумента",	ТаблицаДляДокумента);
	Запрос.УстановитьПараметр("ТаблицаРазмеров",		ТаблицаРазмеров);
	Запрос.УстановитьПараметр("Организация",			ТекущийОбъект.Организация);
	Запрос.УстановитьПараметр("Склад",					ТекущийОбъект.Склад);
	Запрос.УстановитьПараметр("ПериодАнализа",			ДатаАнализа);
	Запрос.УстановитьПараметр("ОрганизацияДляОстатков",	?(ПолучитьФункциональнуюОпцию("НеВестиУчетОстатковНоменклатурыПоОрганизации"),Справочники.Организации.ПустаяСсылка(),ТекущийОбъект.Организация));
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаНоменклатуры	= Результат[9].Выгрузить();
	ТаблицаОстатков 	= Результат[10].Выгрузить();
	
	ТекущийОбъект.Товары.Очистить();
	
	Для Каждого СтрокаТаблицыДляДокумента Из ТаблицаДляДокумента Цикл
		
		СтруктураПоиска = Новый Структура("Сотрудник, НормаВыдачи, НоменклатураНормы", СтрокаТаблицыДляДокумента.Сотрудник, СтрокаТаблицыДляДокумента.НормаВыдачи, СтрокаТаблицыДляДокумента.НоменклатураНормы);
		
		НайденныеСтрокиТаблицыНоменклатуры = ТаблицаНоменклатуры.НайтиСтроки(СтруктураПоиска);
		
		//АсТБ_Alexey_97548_********************************************************************
		Если СтрокаТаблицыДляДокумента.Количество = 0 Тогда //выдано сверх нормы
			
			СтрокаЗаполнена = Ложь;
			
			//заполняем номенклатуру выадчи, которая есть на остатках без количества
			Для Каждого НайденнаяСтрокаТаблицыНоменклатуры Из НайденныеСтрокиТаблицыНоменклатуры Цикл
				
				Если СтрокаЗаполнена Тогда
					Прервать;
				КонецЕсли;	
				
				СтруктураПоискаОстатков = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", НайденнаяСтрокаТаблицыНоменклатуры.Номенклатура, НайденнаяСтрокаТаблицыНоменклатуры.ХарактеристикаНоменклатуры);
				
				НайденныеСтрокиТаблицыОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураПоискаОстатков);
				
				Для Каждого НайденнаяСтрокаТаблицыОстатков Из НайденныеСтрокиТаблицыОстатков Цикл
					
					НоваяСтрока = ТекущийОбъект.Товары.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока,НайденнаяСтрокаТаблицыНоменклатуры);
					НоваяСтрока.Количество 	= 0;
					СтрокаЗаполнена = Истина;
					
					Прервать;
					
				КонецЦикла;
				
			КонецЦикла;
			
			Если НЕ СтрокаЗаполнена Тогда //не нашли остатков - номенклатуру выдачи не заполняем
				
				НоваяСтрока = ТекущийОбъект.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицыДляДокумента);
				
			КонецЕсли;	
			
		Иначе
		//АсТБ_Alexey_97548_********************************************************************
		
			ОсталосьВыдать = СтрокаТаблицыДляДокумента.Количество;
		
			Для Каждого НайденнаяСтрокаТаблицыНоменклатуры Из НайденныеСтрокиТаблицыНоменклатуры Цикл
				
				СтруктураПоискаОстатков = Новый Структура("Номенклатура, ХарактеристикаНоменклатуры", НайденнаяСтрокаТаблицыНоменклатуры.Номенклатура, НайденнаяСтрокаТаблицыНоменклатуры.ХарактеристикаНоменклатуры);
				
				НайденныеСтрокиТаблицыОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураПоискаОстатков);
				
				Для Каждого НайденнаяСтрокаТаблицыОстатков Из НайденныеСтрокиТаблицыОстатков Цикл
					
					Если НайденнаяСтрокаТаблицыОстатков.КоличествоОстаток >= ОсталосьВыдать Тогда
						
						НайденнаяСтрокаТаблицыОстатков.КоличествоОстаток = НайденнаяСтрокаТаблицыОстатков.КоличествоОстаток - ОсталосьВыдать;
						
						НоваяСтрока = ТекущийОбъект.Товары.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока,НайденнаяСтрокаТаблицыНоменклатуры);
						НоваяСтрока.Количество 	= ОсталосьВыдать;
						НоваяСтрока.Сумма 		= НоваяСтрока.Количество * НоваяСтрока.Цена;
						
						ОсталосьВыдать = 0;
						
						Прервать;
						
					Иначе //остатков недостаточно
						
						//АсТБ_Alexey_97548_********************************************************************
						Если НЕ НайденнаяСтрокаТаблицыОстатков.КоличествоОстаток = 0 Тогда
							
							НоваяСтрока = ТекущийОбъект.Товары.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока,НайденнаяСтрокаТаблицыНоменклатуры);
							НоваяСтрока.Количество 	= НайденнаяСтрокаТаблицыОстатков.КоличествоОстаток;
							НоваяСтрока.Сумма 		= НоваяСтрока.Количество * НоваяСтрока.Цена;
							
							ОсталосьВыдать = ОсталосьВыдать - НайденнаяСтрокаТаблицыОстатков.КоличествоОстаток;
							
						КонецЕсли;
						//АсТБ_Alexey_97548_********************************************************************
						
					КонецЕсли;	
					
				КонецЦикла;
				
				Если ОсталосьВыдать = 0 Тогда
					Прервать;
				КонецЕсли;	
				
			КонецЦикла;
			
			Если НЕ ОсталосьВыдать = 0 Тогда
				
				НоваяСтрока = ТекущийОбъект.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицыДляДокумента);
				НоваяСтрока.Количество = ОсталосьВыдать;
				
			КонецЕсли;		
		
		КонецЕсли;	
		//АсТБ_Alexey_97548_********************************************************************
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли