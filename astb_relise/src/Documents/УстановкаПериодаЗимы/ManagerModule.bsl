#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ 
	|	УстановкаПериодаЗимыПериодыЗимы.Ссылка.Дата КАК Период,
	|	УстановкаПериодаЗимыПериодыЗимы.Ссылка.Организация,
	|	УстановкаПериодаЗимыПериодыЗимы.Подразделение,
	|	УстановкаПериодаЗимыПериодыЗимы.НачалоЗимы,
	|	УстановкаПериодаЗимыПериодыЗимы.КонецЗимы,
	|	УстановкаПериодаЗимыПериодыЗимы.Использовать
	|ИЗ
	|	Документ.УстановкаПериодаЗимы.ПериодыЗимы КАК УстановкаПериодаЗимыПериодыЗимы
	|ГДЕ
	|	УстановкаПериодаЗимыПериодыЗимы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	//по регистру "Периоды зимы"
	ТаблицыДляДвижений.Вставить("ТаблицаПериодыЗимы", Запрос.Выполнить().Выгрузить());
	
	//по регистру "ПотребностьВыдачиСИЗ"
	ТаблицыДляДвижений.Вставить("ТаблицаПотребностьВыдачиСИЗ", ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуДвиженийПоПотребности(ДокументСсылка));
	
	//по регистру "ВыдачаПоПотребности"
	ТаблицыДляДвижений.Вставить("ТаблицаВыдачаПоПотребности", ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуДвиженийВыдачиПоПотребности(ДокументСсылка,ТаблицыДляДвижений.ТаблицаПотребностьВыдачиСИЗ));
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧасть(ТекущийОбъект) Экспорт	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПериодыЗимыСрезПоследних.Подразделение,
	|	ПериодыЗимыСрезПоследних.НачалоЗимы,
	|	ПериодыЗимыСрезПоследних.КонецЗимы,
	|	ПериодыЗимыСрезПоследних.Использовать КАК Использовать
	|ИЗ
	|	РегистрСведений.ПериодыЗимы.СрезПоследних(&Период, Организация = &Организация) КАК ПериодыЗимыСрезПоследних";
	
	Запрос.УстановитьПараметр("Период",		ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка));
	Запрос.УстановитьПараметр("Организация",ТекущийОбъект.Организация);
	
	ТекущийОбъект.ПериодыЗимы.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры
	
#КонецЕсли