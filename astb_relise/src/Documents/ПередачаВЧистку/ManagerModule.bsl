#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
				   |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
				   |	ПередачаВЧисткуТовары.Ссылка.Организация,
				   |	ПередачаВЧисткуТовары.Ссылка КАК ПередачаВЧистку,
				   |	ПередачаВЧисткуТовары.Ссылка.Получатель КАК Контрагент,
				   |	ПередачаВЧисткуТовары.Сотрудник,
				   |	ПередачаВЧисткуТовары.Номенклатура,
				   |	ПередачаВЧисткуТовары.ХарактеристикаНоменклатуры,
				   |	ПередачаВЧисткуТовары.Штрихкод КАК Штрихкод,
				   |	1 КАК Количество,
				   |	ПередачаВЧисткуТовары.НомерСтроки,
				   |	ПередачаВЧисткуТовары.Ссылка.Дата КАК Период
				   |ИЗ
				   |	Документ.ПередачаВЧистку.Товары КАК ПередачаВЧисткуТовары
				   |ГДЕ
				   |	ПередачаВЧисткуТовары.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	ТаблицыДляДвижений.Вставить("ТаблицаСредстваЗащитыВЧистке", Результат);
	
КонецПроцедуры	
	
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктПриемаПередачи") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"АктПриемаПередачи",
			"Акт приема-передачи",
			СформироватьПечатнуюФормуАктаПриемаПередачи(МассивОбъектов, ОбъектыПечати),,"Документ.ПередачаВЧистку.ПФ_MXL_АктПриемаПередачи");
				
	КонецЕсли;	
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуАктаПриемаПередачи(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПередачаВЧистку_ПФ_MXL_АктПриемаПередачи";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Документы.Ссылка КАК Ссылка,
	|	Документы.Отправитель.Наименование КАК Отправитель,
	|	Документы.Получатель.Наименование КАК Получатель,
	|	Документы.Номер КАК НомерДокумента,
	|	Документы.Дата КАК ДатаДокумента,
	|	Документы.НомерДоговора КАК НомерДоговора,
	|	Документы.ДатаДоговора КАК ДатаДоговора
	|ИЗ
	|	Документ.ПередачаВЧистку КАК Документы
	|ГДЕ
	|	Документы.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПередачаВЧисткуТовары.Ссылка КАК Ссылка,
	|	ПередачаВЧисткуТовары.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	СУММА(ПередачаВЧисткуТовары.Вес) КАК Вес,
	|	СУММА(1) КАК Количество,
	|	ПередачаВЧисткуТовары.Номенклатура.Наименование КАК НаименованиеТовара
	|ИЗ
	|	Документ.ПередачаВЧистку.Товары КАК ПередачаВЧисткуТовары
	|ГДЕ
	|	ПередачаВЧисткуТовары.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПередачаВЧисткуТовары.Ссылка,
	|	ПередачаВЧисткуТовары.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	ПередачаВЧисткуТовары.Номенклатура.Наименование
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|ИТОГИ ПО
	|	Ссылка");
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПередачаВЧистку.ПФ_MXL_АктПриемаПередачи");
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ДанныеПечати = ПакетЗапросов[0].Выбрать();
	ВыборкаПоДокументам = ПакетЗапросов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если НЕ ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Ссылка", ДанныеПечати.Ссылка)) Тогда
			Продолжить;
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
			
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Заполнить(ДанныеПечати);
		НомерДокумента = ДанныеПечати.НомерДокумента;
		ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(НомерДокумента,Истина,Истина);
		Область.Параметры.НомерДокумента 	= НомерДокумента;
		Область.Параметры.АдресОтправителя 	= ФормированиеПечатныхФормПереопределяемый.ПолучитьАдресИзКонтактнойИнформации(ДанныеПечати.Ссылка.Отправитель,	"Фактический");
		Область.Параметры.АдресПолучателя 	= ФормированиеПечатныхФормПереопределяемый.ПолучитьАдресИзКонтактнойИнформации(ДанныеПечати.Ссылка.Получатель,	"Фактический");
		
		ТабличныйДокумент.Вывести(Область);
			
		СчетСтрок = 1;
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы");
			Область.Параметры.Заполнить(ВыборкаПоТоварам);
			
			Область.Параметры.НомерСтроки = СчетСтрок;
			
			ТабличныйДокумент.Вывести(Область);
			
			СчетСтрок = СчетСтрок + 1;
			
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		ТекстИтоговойСтроки = НСтр("ru = 'Всего наименований %ВсегоНаименований% на %ИтоговыйВес% кг'");
		ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%ВсегоНаименований%", СчетСтрок-1);
		ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%ИтоговыйВес%", Формат(ДанныеПечати.Ссылка.Товары.Итог("Вес"),"ЧЦ=15; ЧДЦ=3"));
		Область.Параметры.ИтоговаяСтрока = ТекстИтоговойСтроки;
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//заказ поставщику
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ПередачаВЧистку";
	КомандаПечати.Идентификатор = "АктПриемаПередачи";
	КомандаПечати.Представление = НСтр("ru = 'Акт приема-передачи'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

#КонецЕсли