#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УстановкаСоответствийНоменклатурыВыдачиСоответствия.Ссылка.Дата КАК Период,
	|	УстановкаСоответствийНоменклатурыВыдачиСоответствия.НоменклатураНормы КАК НоменклатураНормыОрганизации,
	|	УстановкаСоответствийНоменклатурыВыдачиСоответствия.Номенклатура,
	|	УстановкаСоответствийНоменклатурыВыдачиСоответствия.Сотрудник,
	|	УстановкаСоответствийНоменклатурыВыдачиСоответствия.Подразделение,
	|	УстановкаСоответствийНоменклатурыВыдачиСоответствия.Должность,
	|	УстановкаСоответствийНоменклатурыВыдачиСоответствия.УсловиеСоответствия,
	|	УстановкаСоответствийНоменклатурыВыдачиСоответствия.Приоритет,
	|	УстановкаСоответствийНоменклатурыВыдачиСоответствия.Использовать
	//+++АСТБ_Горюшин_Алексей_23718
	|ПОМЕСТИТЬ ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия
	//---АСТБ_Горюшин_Алексей_23718
	|ИЗ
	|	Документ.УстановкаСоответствийНоменклатурыВыдачи.Соответствия КАК УстановкаСоответствийНоменклатурыВыдачиСоответствия
	|ГДЕ
	|	УстановкаСоответствийНоменклатурыВыдачиСоответствия.Ссылка = &Ссылка
	//+++АСТБ_Горюшин_Алексей_23718
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоответствияНоменклатурыВыдачиСрезПоследних.НоменклатураНормыОрганизации КАК НоменклатураНормыОрганизации,
	|	СоответствияНоменклатурыВыдачиСрезПоследних.Номенклатура КАК Номенклатура,
	|	СоответствияНоменклатурыВыдачиСрезПоследних.Сотрудник КАК Сотрудник,
	|	СоответствияНоменклатурыВыдачиСрезПоследних.Подразделение КАК Подразделение,
	|	СоответствияНоменклатурыВыдачиСрезПоследних.Должность КАК Должность,
	|	СоответствияНоменклатурыВыдачиСрезПоследних.УсловиеСоответствия КАК УсловиеСоответствия,
	|	СоответствияНоменклатурыВыдачиСрезПоследних.Приоритет КАК Приоритет,
	|	СоответствияНоменклатурыВыдачиСрезПоследних.Использовать КАК Использовать,
	|	СоответствияНоменклатурыВыдачиСрезПоследних.Период КАК Период
	|ПОМЕСТИТЬ ВТ_СоответствияНоменклатурыВыдачиСрезПоследних
	|ИЗ
	|	РегистрСведений.СоответствияНоменклатурыВыдачи.СрезПоследних(&Период, НоменклатураНормыОрганизации.Владелец = &Организация) КАК СоответствияНоменклатурыВыдачиСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.Период КАК Период,
	|	ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.НоменклатураНормыОрганизации КАК НоменклатураНормыОрганизации,
	|	ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.Номенклатура КАК Номенклатура,
	|	ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.Сотрудник КАК Сотрудник,
	|	ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.Подразделение КАК Подразделение,
	|	ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.Должность КАК Должность,
	|	ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.УсловиеСоответствия КАК УсловиеСоответствия,
	|	ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.Приоритет КАК Приоритет,
	|	ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.Использовать КАК Использовать,
	|	ВТ_СоответствияНоменклатурыВыдачиСрезПоследних.Период КАК ПериодПроверки
	|ПОМЕСТИТЬ ВТ_Результат
	|ИЗ
	|	ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия КАК ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СоответствияНоменклатурыВыдачиСрезПоследних КАК ВТ_СоответствияНоменклатурыВыдачиСрезПоследних
	|		ПО ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.НоменклатураНормыОрганизации = ВТ_СоответствияНоменклатурыВыдачиСрезПоследних.НоменклатураНормыОрганизации
	|			И ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.Номенклатура = ВТ_СоответствияНоменклатурыВыдачиСрезПоследних.Номенклатура
	|			И ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.Сотрудник = ВТ_СоответствияНоменклатурыВыдачиСрезПоследних.Сотрудник
	|			И ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.Подразделение = ВТ_СоответствияНоменклатурыВыдачиСрезПоследних.Подразделение
	|			И ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.Должность = ВТ_СоответствияНоменклатурыВыдачиСрезПоследних.Должность
	|			И ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.УсловиеСоответствия = ВТ_СоответствияНоменклатурыВыдачиСрезПоследних.УсловиеСоответствия
	|			И ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.Приоритет = ВТ_СоответствияНоменклатурыВыдачиСрезПоследних.Приоритет
	|			И ВТ_УстановкаСоответствийНоменклатурыВыдачиСоответствия.Использовать = ВТ_СоответствияНоменклатурыВыдачиСрезПоследних.Использовать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Результат.Период КАК Период,
	|	ВТ_Результат.НоменклатураНормыОрганизации КАК НоменклатураНормыОрганизации,
	|	ВТ_Результат.Номенклатура КАК Номенклатура,
	|	ВТ_Результат.Сотрудник КАК Сотрудник,
	|	ВТ_Результат.Подразделение КАК Подразделение,
	|	ВТ_Результат.Должность КАК Должность,
	|	ВТ_Результат.УсловиеСоответствия КАК УсловиеСоответствия,
	|	ВТ_Результат.Приоритет КАК Приоритет,
	|	ВТ_Результат.Использовать КАК Использовать
	|ИЗ
	|	ВТ_Результат КАК ВТ_Результат
	|ГДЕ
	|	ВТ_Результат.ПериодПроверки ЕСТЬ NULL";
	//---АСТБ_Горюшин_Алексей_23718
	
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	//+++АСТБ_Горюшин_Алексей_23718
	Запрос.УстановитьПараметр("Период",	ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("Организация", ДокументСсылка.Организация);	
	//---АСТБ_Горюшин_Алексей_23718
	
	ТаблицыДляДвижений.Вставить("ТаблицаСоответствияНоменклатурыВыдачи", Запрос.Выполнить().Выгрузить());
		
КонецПроцедуры	

//+++АСТБ_Горюшин_Алексей_23718
Процедура ЗаполнитьТаблицуДокумента(ТекущийОбъект, ВариантЗаполнения, ЗначениеПараметраЗаполнения = Неопределено) Экспорт
//было	
//Процедура ЗаполнитьТаблицуДокумента(ТекущийОбъект,ВариантЗаполнения) Экспорт
//---АСТБ_Горюшин_Алексей_23718
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период",		?(ЗначениеЗаполнено(ТекущийОбъект.Ссылка),ТекущийОбъект.Дата,ТекущаяДата()));
	Запрос.УстановитьПараметр("Организация",ТекущийОбъект.Организация);
	
	Если ВариантЗаполнения = "ПоБазе" Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СоответствияНоменклатурыВыдачиСрезПоследних.НоменклатураНормыОрганизации КАК НоменклатураНормы,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Номенклатура КАК Номенклатура,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Сотрудник КАК Сотрудник,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Подразделение КАК Подразделение,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Должность КАК Должность,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.УсловиеСоответствия КАК УсловиеСоответствия,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Приоритет КАК Приоритет,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Использовать КАК Использовать
		|ИЗ
		|	РегистрСведений.СоответствияНоменклатурыВыдачи.СрезПоследних(&Период, НоменклатураНормыОрганизации.Владелец = &Организация) КАК СоответствияНоменклатурыВыдачиСрезПоследних
		//+++АСТБ_Горюшин_Алексей_23718
		|ГДЕ
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Использовать = Истина
		//---АСТБ_Горюшин_Алексей_23718
		|
		|УПОРЯДОЧИТЬ ПО
		|	СоответствияНоменклатурыВыдачиСрезПоследних.НоменклатураНормыОрганизации.Наименование,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Приоритет";
		
		ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
		
	ИначеЕсли ВариантЗаполнения = "ПоПустым" Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	НоменклатураНормОрганизации.Ссылка КАК НоменклатураНормы,
		|	0 КАК Приоритет
		|ИЗ
		|	Справочник.НоменклатураНормОрганизации КАК НоменклатураНормОрганизации
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			СоответствияНоменклатурыВыдачиСрезПоследних.НоменклатураНормыОрганизации КАК НоменклатураНормы
		|		ИЗ
		|			РегистрСведений.СоответствияНоменклатурыВыдачи.СрезПоследних(&Период, НоменклатураНормыОрганизации.Владелец = &Организация) КАК СоответствияНоменклатурыВыдачиСрезПоследних) КАК ВложенныйЗапрос
		|		ПО НоменклатураНормОрганизации.Ссылка = ВложенныйЗапрос.НоменклатураНормы
		|ГДЕ
		|	НоменклатураНормОрганизации.Владелец = &Организация
		|	И ВложенныйЗапрос.НоменклатураНормы ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	НоменклатураНормОрганизации.Наименование";
		
		ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	
		ПроставитьПриоритет(ТаблицаЗапроса);
		
	//+++АСТБ_Горюшин_Алексей_23718
	ИначеЕсли ВариантЗаполнения = "ПоНоменклатуреНормы" Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СоответствияНоменклатурыВыдачиСрезПоследних.НоменклатураНормыОрганизации КАК НоменклатураНормы,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Номенклатура КАК Номенклатура,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Сотрудник КАК Сотрудник,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Подразделение КАК Подразделение,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Должность КАК Должность,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.УсловиеСоответствия КАК УсловиеСоответствия,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Приоритет КАК Приоритет,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Использовать КАК Использовать
		|ИЗ
		|	РегистрСведений.СоответствияНоменклатурыВыдачи.СрезПоследних(&Период, НоменклатураНормыОрганизации.Владелец = &Организация И НоменклатураНормыОрганизации В (&СписокНоменклатурыНорм)) КАК СоответствияНоменклатурыВыдачиСрезПоследних
		|ГДЕ
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Использовать = Истина
		|
		|УПОРЯДОЧИТЬ ПО
		|	СоответствияНоменклатурыВыдачиСрезПоследних.НоменклатураНормыОрганизации.Наименование,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Приоритет";
		
		Запрос.УстановитьПараметр("СписокНоменклатурыНорм", ЗначениеПараметраЗаполнения);
		ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
		
	ИначеЕсли ВариантЗаполнения = "ПоНоменклатуреВыдачи" Тогда	
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СоответствияНоменклатурыВыдачиСрезПоследних.НоменклатураНормыОрганизации КАК НоменклатураНормы,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Номенклатура КАК Номенклатура,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Сотрудник КАК Сотрудник,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Подразделение КАК Подразделение,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Должность КАК Должность,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.УсловиеСоответствия КАК УсловиеСоответствия,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Приоритет КАК Приоритет,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Использовать КАК Использовать
		|ИЗ
		|	РегистрСведений.СоответствияНоменклатурыВыдачи.СрезПоследних(&Период, НоменклатураНормыОрганизации.Владелец = &Организация И Номенклатура В (&СписокНоменклатурыВыдачи)) КАК СоответствияНоменклатурыВыдачиСрезПоследних
		|ГДЕ
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Использовать = Истина
		|
		|УПОРЯДОЧИТЬ ПО
		|	СоответствияНоменклатурыВыдачиСрезПоследних.НоменклатураНормыОрганизации.Наименование,
		|	СоответствияНоменклатурыВыдачиСрезПоследних.Приоритет";
		
		Запрос.УстановитьПараметр("СписокНоменклатурыВыдачи", ЗначениеПараметраЗаполнения);
		ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	//---АСТБ_Горюшин_Алексей_23718
		
	КонецЕсли;
		
	ТекущийОбъект.Соответствия.Загрузить(ТаблицаЗапроса);
	
КонецПроцедуры

Процедура ПроставитьПриоритет(ТаблицаЗапроса) Экспорт
	
	ТекущаяНоменклатураНормы = "";
	
	Для Каждого СтрокаТаблицы Из ТаблицаЗапроса Цикл
		
		Если НЕ ТекущаяНоменклатураНормы = СтрокаТаблицы.НоменклатураНормы Тогда
			Сч = 1;
			ТекущаяНоменклатураНормы = СтрокаТаблицы.НоменклатураНормы;
		Иначе
			Сч = Сч + 1;
		КонецЕсли;
		
		СтрокаТаблицы.Приоритет = Сч;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПоОтсутствующейНоменклатуре(ТекущийОбъект, МассивПоставщиков) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
	|	СУММА(ОстаткиНоменклатурыОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ПОМЕСТИТЬ ВТ_Остатки
	|ИЗ
	|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|			&ПериодАнализа,
	|			Организация = &Организация
	|				ИЛИ Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК ОстаткиНоменклатурыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиНоменклатурыОстатки.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Номенклатура
	|ПОМЕСТИТЬ ВТ_НоменклатураПоставщиков
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Поставщик В(&МассивПоставщиков)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоответствияНоменклатурыВыдачиСрезПоследних.НоменклатураНормыОрганизации КАК НоменклатураНормыОрганизации,
	|	СоответствияНоменклатурыВыдачиСрезПоследних.Номенклатура КАК Номенклатура,
	|	СоответствияНоменклатурыВыдачиСрезПоследних.Сотрудник КАК Сотрудник,
	|	СоответствияНоменклатурыВыдачиСрезПоследних.Подразделение КАК Подразделение,
	|	СоответствияНоменклатурыВыдачиСрезПоследних.Должность КАК Должность,
	|	СоответствияНоменклатурыВыдачиСрезПоследних.УсловиеСоответствия КАК УсловиеСоответствия,
	|	СоответствияНоменклатурыВыдачиСрезПоследних.Приоритет КАК Приоритет
	|ПОМЕСТИТЬ ВТ_Маппинг
	|ИЗ
	|	РегистрСведений.СоответствияНоменклатурыВыдачи.СрезПоследних(&ПериодАнализа, НоменклатураНормыОрганизации.Владелец = &Организация) КАК СоответствияНоменклатурыВыдачиСрезПоследних
	|ГДЕ
	|	СоответствияНоменклатурыВыдачиСрезПоследних.Использовать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НоменклатураПоставщиков.Номенклатура КАК Номенклатура,
	|	ВТ_Маппинг.НоменклатураНормыОрганизации КАК НоменклатураНормы,
	|	ВТ_Маппинг.Сотрудник КАК Сотрудник,
	|	ВТ_Маппинг.Подразделение КАК Подразделение,
	|	ВТ_Маппинг.Должность КАК Должность,
	|	ВТ_Маппинг.УсловиеСоответствия КАК УсловиеСоответствия,
	|	ВТ_Маппинг.Приоритет КАК Приоритет
	|ИЗ
	|	ВТ_НоменклатураПоставщиков КАК ВТ_НоменклатураПоставщиков
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Остатки КАК ВТ_Остатки
	|		ПО ВТ_НоменклатураПоставщиков.Номенклатура = ВТ_Остатки.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Маппинг КАК ВТ_Маппинг
	|		ПО ВТ_НоменклатураПоставщиков.Номенклатура = ВТ_Маппинг.Номенклатура
	|ГДЕ
	|	ВТ_Остатки.КоличествоОстаток ЕСТЬ NULL
	|	И НЕ ВТ_Маппинг.НоменклатураНормыОрганизации ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ПериодАнализа",		ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка));
	Запрос.УстановитьПараметр("Организация",		ТекущийОбъект.Организация);
	Запрос.УстановитьПараметр("МассивПоставщиков",	МассивПоставщиков);
	
	ТекущийОбъект.Соответствия.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры	

#КонецЕсли