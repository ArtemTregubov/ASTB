#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	//движения по регистру "Остатки номенклатуры"
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ПередачаСИЗвУтилизациюТовары.Номенклатура КАК Номенклатура,
	|	ПередачаСИЗвУтилизациюТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ПередачаСИЗвУтилизациюТовары.Ссылка.Склад КАК Склад,
	|	&ОрганизацияДляОстатков КАК Организация,
	|	ПередачаСИЗвУтилизациюТовары.Ссылка.Дата КАК Период,
	|	ПередачаСИЗвУтилизациюТовары.ПроцентИзноса КАК ПроцентИзноса,
	|	1 КАК Количество,
	|	ПередачаСИЗвУтилизациюТовары.ФККО КАК ФККО,
	|	ПередачаСИЗвУтилизациюТовары.Вес КАК Вес,
	|	ПередачаСИЗвУтилизациюТовары.Ссылка.Контрагент КАК Контрагент
	|ПОМЕСТИТЬ ВТ_ТаблицаДокумента
	|ИЗ
	|	Документ.ПередачаСИЗвУтилизацию.Товары КАК ПередачаСИЗвУтилизациюТовары
	|ГДЕ
	|	ПередачаСИЗвУтилизациюТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаДокумента.ВидДвижения КАК ВидДвижения,
	|	ВТ_ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицаДокумента.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ТаблицаДокумента.Склад КАК Склад,
	|	ВТ_ТаблицаДокумента.Организация КАК Организация,
	|	ВТ_ТаблицаДокумента.Период КАК Период,
	|	ВТ_ТаблицаДокумента.ПроцентИзноса КАК ПроцентИзноса,
	|	СУММА(ВТ_ТаблицаДокумента.Количество) КАК Количество
	|ИЗ
	|	ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаДокумента.ВидДвижения,
	|	ВТ_ТаблицаДокумента.Номенклатура,
	|	ВТ_ТаблицаДокумента.ХарактеристикаНоменклатуры,
	|	ВТ_ТаблицаДокумента.Склад,
	|	ВТ_ТаблицаДокумента.Организация,
	|	ВТ_ТаблицаДокумента.Период,
	|	ВТ_ТаблицаДокумента.ПроцентИзноса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ВТ_ТаблицаДокумента.Контрагент КАК Контрагент,
	|	ВТ_ТаблицаДокумента.Склад КАК Склад,
	|	ВТ_ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицаДокумента.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ТаблицаДокумента.ФККО КАК ФККО,
	|	СУММА(ВТ_ТаблицаДокумента.Вес) КАК Вес,
	|	СУММА(ВТ_ТаблицаДокумента.Количество) КАК Количество,
	|	ВТ_ТаблицаДокумента.Период КАК Период
	|ИЗ
	|	ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаДокумента.Период,
	|	ВТ_ТаблицаДокумента.Организация,
	|	ВТ_ТаблицаДокумента.Контрагент,
	|	ВТ_ТаблицаДокумента.Склад,
	|	ВТ_ТаблицаДокумента.Номенклатура,
	|	ВТ_ТаблицаДокумента.ХарактеристикаНоменклатуры,
	|	ВТ_ТаблицаДокумента.ФККО";
	
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Запрос.УстановитьПараметр("ОрганизацияДляОстатков",?(ПолучитьФункциональнуюОпцию("НеВестиУчетОстатковНоменклатурыПоОрганизации"),Справочники.Организации.ПустаяСсылка(),ДокументСсылка.Организация));
	Запрос.УстановитьПараметр("Организация",ДокументСсылка.Организация);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	ТаблицыДляДвижений.Вставить("ТаблицаОстаткиНоменклатуры", 	Результат[1].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаУтилизированныеСИЗ", 	Результат[2].Выгрузить());
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//Акт (организация-контрагент)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ПередачаСИЗвУтилизацию";
	КомандаПечати.Идентификатор = "Акт_Организация_Контрагент";
	КомандаПечати.Представление = НСтр("ru = 'Акт (организация - контрагент)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	//списание с учетом % износа
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ПередачаСИЗвУтилизацию";
	КомандаПечати.Идентификатор = "Акт_Контрагент_Утилизатор";
	КомандаПечати.Представление = НСтр("ru = 'Акт (контрагент - утилизатор)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Акт_Организация_Контрагент") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"Акт_Организация_Контрагент",
			"Акт (организация - контрагент)",
			СформироватьПечатнуюФорму_Акт_Организация_Контрагент(МассивОбъектов, ОбъектыПечати),,"Документ.ПередачаСИЗвУтилизацию.ПФ_MXL_Акт_Организация_Контрагент");
				
	КонецЕсли;		
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Акт_Контрагент_Утилизатор") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"Акт_Контрагент_Утилизатор",
			"Акт (контрагент - утилизатор)",
			СформироватьПечатнуюФорму_Акт_Контрагент_Утилизатор(МассивОбъектов, ОбъектыПечати),,"Документ.ПередачаСИЗвУтилизацию.ПФ_MXL_Акт_Контрагент_Утилизатор");
				
	КонецЕсли;
		
КонецПроцедуры

Функция СформироватьПечатнуюФорму_Акт_Организация_Контрагент(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПередачаСИЗвУтилизацию_Акт_Организация_Контрагент";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Документы.Ссылка КАК Ссылка,
	|	Документы.Организация.НаименованиеПолное КАК ОрганизацияПолноеНаименование,
	|	Документы.Организация.Наименование КАК ОрганизацияНаименование,
	|	Документы.Номер КАК НомерДокумента,
	|	Документы.Дата КАК ДатаДокумента,
	|	Документы.Контрагент.НаименованиеПолное КАК КонтрагентПолноеНаименование,
	|	Документы.Контрагент.Наименование КАК КонтрагентНаименование,
	|	Документы.ОрганизацияКонтрагентНомерПриложения КАК НомерПриложения,
	|	Документы.ОрганизацияКонтрагентНомерДоговора КАК НомерДоговора,
	|	Документы.ОрганизацияКонтрагентДатаДоговора КАК ДатаДоговора,
	|	Документы.ОрганизацияКонтрагентПунктДоговора КАК ПунктДоговора,
	|	Документы.ОрганизацияВЛице КАК ОрганизацияВЛице,
	|	Документы.ОрганизацияФИО КАК ОрганизацияФИО,
	|	Документы.КонтрагентВЛице КАК КонтрагентВЛице,
	|	Документы.КонтрагентФИО КАК КонтрагентФИО
	|ИЗ
	|	Документ.ПередачаСИЗвУтилизацию КАК Документы
	|ГДЕ
	|	Документы.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПередачаСИЗвУтилизациюОбщийВес.Ссылка КАК Ссылка,
	|	СУММА(ПередачаСИЗвУтилизациюОбщийВес.Вес) КАК Вес,
	|	ПередачаСИЗвУтилизациюОбщийВес.ФККО.Код КАК Код,
	|	ПередачаСИЗвУтилизациюОбщийВес.ФККО.Описание КАК Описание,
	|	ПередачаСИЗвУтилизациюОбщийВес.ФККО.КлассОпасности КАК КлассОпасности
	|ИЗ
	|	Документ.ПередачаСИЗвУтилизацию.ОбщийВес КАК ПередачаСИЗвУтилизациюОбщийВес
	|ГДЕ
	|	ПередачаСИЗвУтилизациюОбщийВес.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПередачаСИЗвУтилизациюОбщийВес.Ссылка,
	|	ПередачаСИЗвУтилизациюОбщийВес.ФККО.Код,
	|	ПередачаСИЗвУтилизациюОбщийВес.ФККО.Описание,
	|	ПередачаСИЗвУтилизациюОбщийВес.ФККО.КлассОпасности
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Код
	|ИТОГИ ПО
	|	Ссылка");
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПередачаСИЗвУтилизацию.ПФ_MXL_Акт_Организация_Контрагент");
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	ДанныеПечати = ПакетЗапросов[0].Выбрать();
	
	ВыборкаПоДокументам = ПакетЗапросов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если НЕ ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Ссылка", ДанныеПечати.Ссылка)) Тогда
			Продолжить;
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
			
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Заполнить(ДанныеПечати);
		Область.Параметры.ДатаДокумента = Формат(ДанныеПечати.ДатаДокумента,"ДФ='dd MMMM yyyy'");
		Область.Параметры.ДатаДоговора = Формат(ДанныеПечати.ДатаДоговора,"ДФ='dd MMMM yyyy'");
		
		ТабличныйДокумент.Вывести(Область);
			
		СчетСтрок = 1;
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			Область = Макет.ПолучитьОбласть("Строка");
			Область.Параметры.Заполнить(ВыборкаПоТоварам);
			Область.Параметры.НомерПоПорядку = СчетСтрок;
			ТабличныйДокумент.Вывести(Область);
			
			СчетСтрок = СчетСтрок + 1;
			
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		Область.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция СформироватьПечатнуюФорму_Акт_Контрагент_Утилизатор(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПередачаСИЗвУтилизацию_Акт_Контрагент_Утилизатор";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Документы.Ссылка КАК Ссылка,
	|	Документы.Организация.НаименованиеПолное КАК ОрганизацияПолноеНаименование,
	|	Документы.Организация.Наименование КАК ОрганизацияНаименование,
	|	Документы.Утилизатор.НаименованиеПолное КАК УтилизаторПолноеНаименование,
	|	Документы.Утилизатор.Наименование КАК УтилизаторНаименование,
	|	Документы.Номер КАК НомерДокумента,
	|	Документы.Дата КАК ДатаДокумента,
	|	Документы.Контрагент.НаименованиеПолное КАК КонтрагентПолноеНаименование,
	|	Документы.Контрагент.Наименование КАК КонтрагентНаименование,
	|	Документы.КонтрагентУтилизаторНомерПриложения КАК НомерПриложения,
	|	Документы.КонтрагентУтилизаторНомерДоговора КАК НомерДоговора,
	|	Документы.КонтрагентУтилизаторДатаДоговора КАК ДатаДоговора,
	|	Документы.КонтрагентУтилизаторНомерДоверенности КАК НомерДоверенности,
	|	Документы.КонтрагентУтилизаторДатаДоверенности КАК ДатаДоверенности,
	|	Документы.УтилизаторВЛице КАК УтилизаторВЛице,
	|	Документы.УтилизаторФИО КАК УтилизаторФИО,
	|	Документы.УтилизаторНаОснованииЛицензии КАК УтилизаторНаОснованииЛицензии,
	|	Документы.КонтрагентВЛице КАК КонтрагентВЛице,
	|	Документы.КонтрагентФИО КАК КонтрагентФИО
	|ИЗ
	|	Документ.ПередачаСИЗвУтилизацию КАК Документы
	|ГДЕ
	|	Документы.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПередачаСИЗвУтилизациюОбщийВес.Ссылка КАК Ссылка,
	|	СУММА(ПередачаСИЗвУтилизациюОбщийВес.Вес) КАК Вес,
	|	ПередачаСИЗвУтилизациюОбщийВес.ФККО.Код КАК Код,
	|	ПередачаСИЗвУтилизациюОбщийВес.ФККО.Описание КАК Описание,
	|	ПередачаСИЗвУтилизациюОбщийВес.ФККО.КлассОпасности КАК КлассОпасности
	|ИЗ
	|	Документ.ПередачаСИЗвУтилизацию.ОбщийВес КАК ПередачаСИЗвУтилизациюОбщийВес
	|ГДЕ
	|	ПередачаСИЗвУтилизациюОбщийВес.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПередачаСИЗвУтилизациюОбщийВес.Ссылка,
	|	ПередачаСИЗвУтилизациюОбщийВес.ФККО.Код,
	|	ПередачаСИЗвУтилизациюОбщийВес.ФККО.Описание,
	|	ПередачаСИЗвУтилизациюОбщийВес.ФККО.КлассОпасности
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Код
	|ИТОГИ ПО
	|	Ссылка");
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПередачаСИЗвУтилизацию.ПФ_MXL_Акт_Контрагент_Утилизатор");
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	ДанныеПечати = ПакетЗапросов[0].Выбрать();
	
	ВыборкаПоДокументам = ПакетЗапросов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если НЕ ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Ссылка", ДанныеПечати.Ссылка)) Тогда
			Продолжить;
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
			
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Заполнить(ДанныеПечати);
		Область.Параметры.ДатаДокумента = Формат(ДанныеПечати.ДатаДокумента,"ДФ='dd MMMM yyyy'");
		Область.Параметры.ДатаДоговора = Формат(ДанныеПечати.ДатаДоговора,"ДФ='dd MMMM yyyy'");
		Область.Параметры.ДатаДоверенности = Формат(ДанныеПечати.ДатаДоверенности,"ДФ='dd MMMM yyyy'");
		
		ТабличныйДокумент.Вывести(Область);
			
		СчетСтрок = 1;
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			Область = Макет.ПолучитьОбласть("Строка");
			Область.Параметры.Заполнить(ВыборкаПоТоварам);
			Область.Параметры.НомерПоПорядку = СчетСтрок;
			ТабличныйДокумент.Вывести(Область);
			
			СчетСтрок = СчетСтрок + 1;
			
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		Область.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ЗаполнитьТаблицуТоваровДокумента(ТекущийОбъект) Экспорт
	
	ТекущийОбъект.Товары.Очистить();
	
	Запрос = НОВЫЙ Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
	|	ОстаткиНоменклатурыОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ОстаткиНоменклатурыОстатки.КоличествоОстаток) КАК Количество,
	|	ОстаткиНоменклатурыОстатки.Номенклатура.ВесДляУчетаЧистки КАК Вес,
	|	ОстаткиНоменклатурыОстатки.ПроцентИзноса КАК ПроцентИзноса
	|ПОМЕСТИТЬ ВТ_Остатки
	|ИЗ
	|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|			&ПериодРасчета,
	|			Организация = &ОрганизацияДляОстатков
	|				И Склад = &Склад) КАК ОстаткиНоменклатурыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиНоменклатурыОстатки.Номенклатура,
	|	ОстаткиНоменклатурыОстатки.ХарактеристикаНоменклатуры,
	|	ОстаткиНоменклатурыОстатки.Номенклатура.ВесДляУчетаЧистки,
	|	ОстаткиНоменклатурыОстатки.ПроцентИзноса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФККОНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ФККОНоменклатурыСрезПоследних.ФККО) КАК ФККО
	|ПОМЕСТИТЬ ВТ_ФККО
	|ИЗ
	|	РегистрСведений.ФККОНоменклатуры.СрезПоследних(&ПериодРасчета, Организация = &Организация) КАК ФККОНоменклатурыСрезПоследних
	|ГДЕ
	|	ФККОНоменклатурыСрезПоследних.Использовать
	|
	|СГРУППИРОВАТЬ ПО
	|	ФККОНоменклатурыСрезПоследних.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФККОНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ФККОНоменклатурыСрезПоследних.ФККО) КАК КоличествоФККО
	|ПОМЕСТИТЬ ВТ_КоличествоФККО
	|ИЗ
	|	РегистрСведений.ФККОНоменклатуры.СрезПоследних(&ПериодРасчета, Организация = &Организация) КАК ФККОНоменклатурыСрезПоследних
	|ГДЕ
	|	ФККОНоменклатурыСрезПоследних.Использовать
	|
	|СГРУППИРОВАТЬ ПО
	|	ФККОНоменклатурыСрезПоследних.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Остатки.Номенклатура КАК Номенклатура,
	|	ВТ_Остатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ВТ_Остатки.Количество) КАК Количество,
	|	ВТ_Остатки.Вес КАК Вес,
	|	ВТ_ФККО.ФККО КАК ФККО,
	|	ВТ_КоличествоФККО.КоличествоФККО КАК КоличествоФККО,
	|	ВТ_Остатки.ПроцентИзноса КАК ПроцентИзноса
	|ИЗ
	|	ВТ_Остатки КАК ВТ_Остатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ФККО КАК ВТ_ФККО
	|		ПО ВТ_Остатки.Номенклатура = ВТ_ФККО.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КоличествоФККО КАК ВТ_КоличествоФККО
	|		ПО ВТ_Остатки.Номенклатура = ВТ_КоличествоФККО.Номенклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Остатки.Номенклатура,
	|	ВТ_Остатки.ХарактеристикаНоменклатуры,
	|	ВТ_Остатки.Вес,
	|	ВТ_ФККО.ФККО,
	|	ВТ_КоличествоФККО.КоличествоФККО,
	|	ВТ_Остатки.ПроцентИзноса
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_Остатки.Номенклатура.Наименование,
	|	ВТ_Остатки.ХарактеристикаНоменклатуры.КодSAP";
	
	Запрос.УстановитьПараметр("ПериодРасчета",			ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка));
	Запрос.УстановитьПараметр("ОрганизацияДляОстатков",	?(ПолучитьФункциональнуюОпцию("НеВестиУчетОстатковНоменклатурыПоОрганизации"),Справочники.Организации.ПустаяСсылка(),ТекущийОбъект.Организация));
	Запрос.УстановитьПараметр("Организация",			ТекущийОбъект.Организация);
	Запрос.УстановитьПараметр("Склад",					ТекущийОбъект.Склад);
	
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТаблицыЗапроса Из ТаблицаЗапроса Цикл
		
		Для Сч = 1 По СтрокаТаблицыЗапроса.Количество Цикл
			
			ЗаполнитьЗначенияСвойств(ТекущийОбъект.Товары.Добавить(),СтрокаТаблицыЗапроса);
			
		КонецЦикла;	
		
	КонецЦикла;	
	
	ЗаполнитьТаблицуОбщегоВесаДокумента(ТекущийОбъект);
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуОбщегоВесаДокумента(ТекущийОбъект) Экспорт
	
	Запрос = НОВЫЙ Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваровДокумента.ФККО КАК ФККО,
	|	ТаблицаТоваровДокумента.Вес КАК Вес
	|ПОМЕСТИТЬ ВТ_ТаблицаТоваровДокумента
	|ИЗ
	|	&ТаблицаТоваровДокумента КАК ТаблицаТоваровДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаТоваровДокумента.ФККО КАК ФККО,
	|	СУММА(ВТ_ТаблицаТоваровДокумента.Вес) КАК Вес
	|ИЗ
	|	ВТ_ТаблицаТоваровДокумента КАК ВТ_ТаблицаТоваровДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаТоваровДокумента.ФККО";
	
	Запрос.УстановитьПараметр("ТаблицаТоваровДокумента",ТекущийОбъект.Товары.Выгрузить());
	
	ТекущийОбъект.ОбщийВес.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры	

#КонецЕсли