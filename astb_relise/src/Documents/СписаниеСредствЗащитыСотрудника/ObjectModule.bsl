#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	СуммаДокумента = Товары.Итог("Сумма");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	Документы.СписаниеСредствЗащитыСотрудника.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ЗапасыСерверПереопределяемый.ОтразитьВыданныеСредстваЗащиты(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСерверПереопределяемый.ОтразитьУточненныеПричиныСписания(ДополнительныеСвойства, Движения, Отказ);
	Если (НЕ (ПричинаСписания = Перечисления.ПричиныСписания.Увольнение 
		ИЛИ ПричинаСписания = Перечисления.ПричиныСписания.ОкончаниеСрокаНоски
		ИЛИ ПричинаСписания = Перечисления.ПричиныСписания.ОтменаНорм)) Тогда
		Если Не (ВидВыдачиСИЗ = Перечисления.ВидыВыдачиСИЗ.ПроизвольнаяВыдача ИЛИ ВидВыдачиСИЗ = Перечисления.ВидыВыдачиСИЗ.УпрощеннаяВыдача) Тогда
			ЗапасыСерверПереопределяемый.ОтразитьПотребностьВыдачиСИЗ(ДополнительныеСвойства, Движения, Отказ);
			ЗапасыСерверПереопределяемый.ОтразитьВыдачуПоПотребности(ДополнительныеСвойства, Движения, Отказ);
		КонецЕсли;
	КонецЕсли;
	СформироватьСписокРегистровДляКонтроля();
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура СформироватьСписокРегистровДляКонтроля()

	Массив = Новый Массив;
	// Контроль выполняется при проведении\отмене проведения не нового документа.
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Массив.Добавить(Движения.ВыданныеСредстваЗащиты);
		Массив.Добавить(Движения.ВыдачаПоПотребности);
		Если (НЕ (ПричинаСписания = Перечисления.ПричиныСписания.Увольнение ИЛИ ПричинаСписания = Перечисления.ПричиныСписания.ОкончаниеСрокаНоски))
			И (НЕ ВидВыдачиСИЗ = Перечисления.ВидыВыдачиСИЗ.КоллективнаяВыдача) Тогда
			Массив.Добавить(Движения.ПотребностьВыдачиСИЗ);
		КонецЕсли;
	КонецЕсли;

	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	//ПроведениеСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект,Отказ,"Товары");
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		
		Документы.СписаниеСредствЗащитыСотрудника.ЗаполнитьДокумент(ДанныеЗаполнения,ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли