#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ОбменДанными.Загрузка и Режим = РежимПроведенияДокумента.Оперативный И Организация.КонтролироватьКоличествоСтавокПоШтатномуРасписанию Тогда
		
		ТаблицаРаботников = Работники.Выгрузить(,"Подразделение,Должность,ЗанимаемыхСтавок");
		ТаблицаРаботников.Свернуть("Подразделение,Должность","ЗанимаемыхСтавок");
		ТаблицаРаботников.Колонки.Добавить("Организация",Новый ОписаниеТипов("СправочникСсылка.Организации"));
		ТаблицаРаботников.ЗаполнитьЗначения(Организация,"Организация");
		
		Запрос = Новый Запрос;
		МВТ = Новый МенеджерВременныхТаблиц;
		Запрос.МенеджерВременныхТаблиц = МВТ;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	ТЗ.Подразделение КАК Подразделение,
		               |	ТЗ.Должность КАК Должность,
		               |	ТЗ.Организация КАК Организация,
		               |	ТЗ.ЗанимаемыхСтавок КАК ЗанимаемыхСтавок
		               |ПОМЕСТИТЬ ТЗФайл
		               |ИЗ
		               |	&Таблица КАК ТЗ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ЗанятыеРабочиеМестаОстатки.Подразделение КАК Подразделение,
		               |	ЗанятыеРабочиеМестаОстатки.Должность КАК Должность,
		               |	СУММА(ЗанятыеРабочиеМестаОстатки.КоличествоОстаток) КАК КоличествоОстаток
		               |ПОМЕСТИТЬ ВТ_ЗРМ
		               |ИЗ
		               |	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(&ДатаАнализа, Организация = &Организация) КАК ЗанятыеРабочиеМестаОстатки
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ЗанятыеРабочиеМестаОстатки.Подразделение,
		               |	ЗанятыеРабочиеМестаОстатки.Должность
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	ТЗФайл.Организация КАК Организация,
		               |	ТЗФайл.Подразделение КАК Подразделение,
		               |	ТЗФайл.Должность КАК Должность,
		               |	СУММА((ЕСТЬNULL(ШтатноеРасписаниеСрезПоследних.КоличествоСтавок, 0) - ТЗФайл.ЗанимаемыхСтавок - ЕСТЬNULL(ВТ_ЗРМ.КоличествоОстаток, 0)) * -1) КАК Ставка
		               |ИЗ
		               |	ТЗФайл КАК ТЗФайл
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтатноеРасписание.СрезПоследних(&ДатаАнализа, Организация = &Организация) КАК ШтатноеРасписаниеСрезПоследних
		               |		ПО (ШтатноеРасписаниеСрезПоследних.Подразделение = ТЗФайл.Подразделение)
		               |			И (ШтатноеРасписаниеСрезПоследних.Должность = ТЗФайл.Должность)
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗРМ КАК ВТ_ЗРМ
		               |		ПО ТЗФайл.Подразделение = ВТ_ЗРМ.Подразделение
		               |			И ТЗФайл.Должность = ВТ_ЗРМ.Должность
		               |ГДЕ
		               |	ЕСТЬNULL(ШтатноеРасписаниеСрезПоследних.КоличествоСтавок, 0) - ТЗФайл.ЗанимаемыхСтавок - ЕСТЬNULL(ВТ_ЗРМ.КоличествоОстаток, 0) < 0
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ТЗФайл.Должность,
		               |	ТЗФайл.Подразделение,
		               |	ТЗФайл.Организация";
		Запрос.УстановитьПараметр("Таблица",	ТаблицаРаботников); 
		Запрос.УстановитьПараметр("Организация",Организация);
		Запрос.УстановитьПараметр("ДатаАнализа",ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(Ссылка));
		Результат = Запрос.Выполнить().Выгрузить();
		Если Результат.Количество() > 0 Тогда
			Отказ = Истина;
			Для каждого СтрокаРез из Результат Цикл
				Сообщить("В штатном расписании для организации " + Организация + ", подразделения " + СтрокаРез.Подразделение + ", должности "+ СтрокаРез.Должность 
				+ " указано меньшее количество ставок (на "+ СтрокаРез.Ставка + "шт.)");
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если не Отказ Тогда
		ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, Режим);
		Документы.ПриемНаРаботу.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
		ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
		ЗапасыСерверПереопределяемый.ОтразитьЗанятыеРабочиеМеста(ДополнительныеСвойства, Движения, Отказ);
		ЗапасыСерверПереопределяемый.ОтразитьДоступныеУсловияРаботыСотрудника(ДополнительныеСвойства, Движения, Отказ);
		ЗапасыСерверПереопределяемый.ОтразитьПотребностьВыдачиСИЗ(ДополнительныеСвойства, Движения, Отказ);
		ЗапасыСерверПереопределяемый.ОтразитьВыдачуПоПотребности(ДополнительныеСвойства, Движения, Отказ);
		ЗапасыСерверПереопределяемый.ОтразитьОсновноеМестоРаботыСотрудника(ДополнительныеСвойства, Движения, Отказ);
		СформироватьСписокРегистровДляКонтроля();
		ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
		ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
		ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроведениеСервер.ПроверитьЗаполнениеТабельныхНомеров(ЭтотОбъект,Отказ,"Работники");
	
КонецПроцедуры

Процедура СформироватьСписокРегистровДляКонтроля()

	Массив = Новый Массив;
	//Контроль выполняется при проведении\отмене проведения не нового документа.
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Массив.Добавить(Движения.ПотребностьВыдачиСИЗ);
		Массив.Добавить(Движения.ВыдачаПоПотребности);
	КонецЕсли;

	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);

КонецПроцедуры

#КонецЕсли




