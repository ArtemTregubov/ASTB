#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(УстановленныеНормыВыдачиСИЗСрезПоследних.Период) КАК Период,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.Подразделение КАК Подразделение,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.Должность КАК Должность,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.РабочееМесто КАК РабочееМесто,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.УсловиеНормы КАК УсловиеНормы,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.НормаВыдачи КАК НормаВыдачи
	|ПОМЕСТИТЬ ВТ_МаксимальныеДатыНорм
	|ИЗ
	|	РегистрСведений.УстановленныеНормыВыдачиСИЗ.СрезПоследних(
	|			&ПериодРасчета,
	|			Организация = &Организация
	|				И (Подразделение В (&МассивПодразделений)
	|					ИЛИ Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка))
	|				И Должность В (&МассивДолжностей)
	|				И (РабочееМесто В (&МассивРабочихМест)
	|					ИЛИ РабочееМесто = ЗНАЧЕНИЕ(Справочник.РабочиеМестаАСТБ.ПустаяСсылка))) КАК УстановленныеНормыВыдачиСИЗСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.Подразделение,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.Должность,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.РабочееМесто,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.УсловиеНормы,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.НормаВыдачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_МаксимальныеДатыНорм.Подразделение КАК Подразделение,
	|	ВТ_МаксимальныеДатыНорм.Должность КАК Должность,
	|	ВТ_МаксимальныеДатыНорм.РабочееМесто КАК РабочееМесто,
	|	ВТ_МаксимальныеДатыНорм.УсловиеНормы КАК УсловиеНормы,
	|	ВТ_МаксимальныеДатыНорм.НормаВыдачи КАК НормаВыдачи,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.Использовать КАК Использовать
	|ПОМЕСТИТЬ ВТ_ПоследниеНормы
	|ИЗ
	|	ВТ_МаксимальныеДатыНорм КАК ВТ_МаксимальныеДатыНорм
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УстановленныеНормыВыдачиСИЗ.СрезПоследних(
	|				&ПериодРасчета,
	|				Организация = &Организация
	|					И (Подразделение В (&МассивПодразделений)
	|						ИЛИ Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка))
	|					И Должность В (&МассивДолжностей)
	|					И (РабочееМесто В (&МассивРабочихМест)
	|						ИЛИ РабочееМесто = ЗНАЧЕНИЕ(Справочник.РабочиеМестаАСТБ.ПустаяСсылка))) КАК УстановленныеНормыВыдачиСИЗСрезПоследних
	|		ПО ВТ_МаксимальныеДатыНорм.Период = УстановленныеНормыВыдачиСИЗСрезПоследних.Период
	|			И ВТ_МаксимальныеДатыНорм.Подразделение = УстановленныеНормыВыдачиСИЗСрезПоследних.Подразделение
	|			И ВТ_МаксимальныеДатыНорм.Должность = УстановленныеНормыВыдачиСИЗСрезПоследних.Должность
	|			И ВТ_МаксимальныеДатыНорм.РабочееМесто = УстановленныеНормыВыдачиСИЗСрезПоследних.РабочееМесто
	|			И ВТ_МаксимальныеДатыНорм.УсловиеНормы = УстановленныеНормыВыдачиСИЗСрезПоследних.УсловиеНормы
	|			И ВТ_МаксимальныеДатыНорм.НормаВыдачи = УстановленныеНормыВыдачиСИЗСрезПоследних.НормаВыдачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ВТ_ПоследниеНормы.Подразделение КАК Подразделение,
	|	ВТ_ПоследниеНормы.Должность КАК Должность,
	|	ВТ_ПоследниеНормы.РабочееМесто КАК РабочееМесто,
	|	ВТ_ПоследниеНормы.УсловиеНормы КАК УсловиеНормы,
	|	ВТ_ПоследниеНормы.Использовать КАК Использовать
	|ПОМЕСТИТЬ ВТ_НормыСПериодом
	|ИЗ
	|	ВТ_ПоследниеНормы КАК ВТ_ПоследниеНормы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НормыСПериодом.Период КАК Период,
	|	ВТ_НормыСПериодом.Подразделение КАК Подразделение,
	|	ВТ_НормыСПериодом.Должность КАК Должность,
	|	ВТ_НормыСПериодом.РабочееМесто КАК РабочееМесто,
	|	ВТ_НормыСПериодом.УсловиеНормы КАК УсловиеНормы,
	|	МАКСИМУМ(ВТ_НормыСПериодом.Использовать) КАК Использовать
	|ИЗ
	|	ВТ_НормыСПериодом КАК ВТ_НормыСПериодом
	|ГДЕ
	|	ВТ_НормыСПериодом.Использовать
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_НормыСПериодом.Период,
	|	ВТ_НормыСПериодом.Подразделение,
	|	ВТ_НормыСПериодом.Должность,
	|	ВТ_НормыСПериодом.РабочееМесто,
	|	ВТ_НормыСПериодом.УсловиеНормы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПриемНаРаботуРаботники.Сотрудник КАК Сотрудник,
	|	ПриемНаРаботуРаботники.Подразделение КАК Подразделение,
	|	ПриемНаРаботуРаботники.Должность КАК Должность,
	|	ПриемНаРаботуРаботники.РабочееМесто КАК РабочееМесто
	|ИЗ
	|	Документ.ПриемНаРаботу.Работники КАК ПриемНаРаботуРаботники
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
	|				&ПериодРасчета,
	|				Организация = &Организация
	|					И Сотрудник В (&МассивСотрудников)) КАК ЗанятыеРабочиеМестаОстатки
	|		ПО ПриемНаРаботуРаботники.Сотрудник = ЗанятыеРабочиеМестаОстатки.Сотрудник
	|			И ПриемНаРаботуРаботники.Подразделение = ЗанятыеРабочиеМестаОстатки.Подразделение
	|			И ПриемНаРаботуРаботники.Должность = ЗанятыеРабочиеМестаОстатки.Должность
	|			И ПриемНаРаботуРаботники.РабочееМесто = ЗанятыеРабочиеМестаОстатки.РабочееМесто
	|ГДЕ
	|	ЗанятыеРабочиеМестаОстатки.КоличествоОстаток ЕСТЬ NULL
	|	И ПриемНаРаботуРаботники.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",				ДокументСсылка);
	Запрос.УстановитьПараметр("Период",				ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("ПериодРасчета",		ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ДокументСсылка));
	Запрос.УстановитьПараметр("Организация",		ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("МассивСотрудников", 	ДокументСсылка.Работники.ВЫгрузитьКолонку("Сотрудник"));
	Запрос.УстановитьПараметр("МассивДолжностей",	ДокументСсылка.Работники.ВЫгрузитьКолонку("Должность"));
	Запрос.УстановитьПараметр("МассивПодразделений",ДокументСсылка.Работники.ВЫгрузитьКолонку("Подразделение"));
	Запрос.УстановитьПараметр("МассивРабочихМест",	ДокументСсылка.Работники.ВЫгрузитьКолонку("РабочееМесто"));
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаУсловийРаботы = ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуУсловийРаботыСотрудников(Результат[3].Выгрузить(),Результат[4].Выгрузить());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПриемНаРаботуРаботники.НомерСтроки КАК НомерСтроки,
	|	ПриемНаРаботуРаботники.Ссылка.Дата КАК Период,
	|	ПриемНаРаботуРаботники.Ссылка.Организация КАК Организация,
	|	ПриемНаРаботуРаботники.Подразделение КАК Подразделение,
	|	ПриемНаРаботуРаботники.Должность КАК Должность,
	|	ПриемНаРаботуРаботники.РабочееМесто КАК РабочееМесто,
	|	ПриемНаРаботуРаботники.Сотрудник КАК Сотрудник,
	|	ПриемНаРаботуРаботники.ЗанимаемыхСтавок КАК Количество,
	|	ВЫБОР
	|		КОГДА ПриемНаРаботуРаботники.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ПриемНаРаботуРаботники.Ссылка.ДатаОкончания
	|		ИНАЧЕ ПриемНаРаботуРаботники.ДатаОкончания
	|	КОНЕЦ КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ТаблицаДокумента
	|ИЗ
	|	Документ.ПриемНаРаботу.Работники КАК ПриемНаРаботуРаботники
	|ГДЕ
	|	ПриемНаРаботуРаботники.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗанятыеРабочиеМестаОстатки.Подразделение КАК Подразделение,
	|	ЗанятыеРабочиеМестаОстатки.Должность КАК Должность,
	|	ЗанятыеРабочиеМестаОстатки.РабочееМесто КАК РабочееМесто,
	|	ЗанятыеРабочиеМестаОстатки.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТ_ЗанятыеРабочиеМеста
	|ИЗ
	|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
	|			&ПериодРасчета,
	|			Организация = &Организация
	|				И Сотрудник В (&МассивСотрудников)) КАК ЗанятыеРабочиеМестаОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Сотрудник КАК Сотрудник,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Подразделение КАК Подразделение,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Должность КАК Должность,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.РабочееМесто КАК РабочееМесто,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.ОсновноеМестоРаботы КАК ОсновноеМестоРаботы,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ОсновноеМестоРаботы
	|ИЗ
	|	РегистрСведений.ОсновноеМестоРаботыСотрудника.СрезПоследних(
	|			&ПериодРасчета,
	|			Организация = &Организация
	|				И Сотрудник В (&МассивСотрудников)) КАК ОсновноеМестоРаботыСотрудникаСрезПоследних
	|ГДЕ
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.ОсновноеМестоРаботы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаДокумента.Сотрудник КАК Сотрудник,
	|	ВТ_ТаблицаДокумента.Подразделение КАК Подразделение,
	|	ВТ_ТаблицаДокумента.Должность КАК Должность,
	|	ВТ_ТаблицаДокумента.РабочееМесто КАК РабочееМесто
	|ПОМЕСТИТЬ ВТ_ВсеЗанятыеРабочиеМеста
	|ИЗ
	|	ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ЗанятыеРабочиеМеста.Сотрудник,
	|	ВТ_ЗанятыеРабочиеМеста.Подразделение,
	|	ВТ_ЗанятыеРабочиеМеста.Должность,
	|	ВТ_ЗанятыеРабочиеМеста.РабочееМесто
	|ИЗ
	|	ВТ_ЗанятыеРабочиеМеста КАК ВТ_ЗанятыеРабочиеМеста
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВсеЗанятыеРабочиеМеста.Сотрудник КАК Сотрудник,
	|	ВТ_ВсеЗанятыеРабочиеМеста.Подразделение КАК Подразделение,
	|	ВТ_ВсеЗанятыеРабочиеМеста.Должность КАК Должность,
	|	ВТ_ВсеЗанятыеРабочиеМеста.РабочееМесто КАК РабочееМесто,
	|	ЕСТЬNULL(ВТ_ОсновноеМестоРаботы.ОсновноеМестоРаботы, ЛОЖЬ) КАК ОсновноеМестоРаботы,
	|	ЕСТЬNULL(ВТ_ОсновноеМестоРаботы.ДатаОкончания, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТ_РезультатПоОсновномуМестуРаботы
	|ИЗ
	|	ВТ_ВсеЗанятыеРабочиеМеста КАК ВТ_ВсеЗанятыеРабочиеМеста
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОсновноеМестоРаботы КАК ВТ_ОсновноеМестоРаботы
	|		ПО ВТ_ВсеЗанятыеРабочиеМеста.Сотрудник = ВТ_ОсновноеМестоРаботы.Сотрудник
	|			И ВТ_ВсеЗанятыеРабочиеМеста.Подразделение = ВТ_ОсновноеМестоРаботы.Подразделение
	|			И ВТ_ВсеЗанятыеРабочиеМеста.Должность = ВТ_ОсновноеМестоРаботы.Должность
	|			И ВТ_ВсеЗанятыеРабочиеМеста.РабочееМесто = ВТ_ОсновноеМестоРаботы.РабочееМесто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РезультатПоОсновномуМестуРаботы.Сотрудник КАК Сотрудник,
	|	МАКСИМУМ(ВТ_РезультатПоОсновномуМестуРаботы.ОсновноеМестоРаботы) КАК ОсновноеМестоРаботы
	|ПОМЕСТИТЬ ВТ_ЕстьОсновноеМестоРаботы
	|ИЗ
	|	ВТ_РезультатПоОсновномуМестуРаботы КАК ВТ_РезультатПоОсновномуМестуРаботы
	|ГДЕ
	|	ВТ_РезультатПоОсновномуМестуРаботы.ОсновноеМестоРаботы
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_РезультатПоОсновномуМестуРаботы.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВТ_ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ВТ_ТаблицаДокумента.Период КАК Период,
	|	ВТ_ТаблицаДокумента.Организация КАК Организация,
	|	ВТ_ТаблицаДокумента.Подразделение КАК Подразделение,
	|	ВТ_ТаблицаДокумента.Должность КАК Должность,
	|	ВТ_ТаблицаДокумента.РабочееМесто КАК РабочееМесто,
	|	ВТ_ТаблицаДокумента.Сотрудник КАК Сотрудник,
	|	ВТ_ТаблицаДокумента.Количество КАК Количество
	|ИЗ
	|	ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаДокумента.Период КАК Период,
	|	ВТ_ТаблицаДокумента.Организация КАК Организация,
	|	МАКСИМУМ(ВТ_ТаблицаДокумента.Подразделение) КАК Подразделение,
	|	МАКСИМУМ(ВТ_ТаблицаДокумента.Должность) КАК Должность,
	|	ВТ_ТаблицаДокумента.РабочееМесто КАК РабочееМесто,
	|	ВТ_ТаблицаДокумента.ДатаОкончания КАК ДатаОкончания,
	|	ВТ_ТаблицаДокумента.Сотрудник КАК Сотрудник,
	|	МАКСИМУМ(ИСТИНА) КАК ОсновноеМестоРаботы
	|ИЗ
	|	ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЕстьОсновноеМестоРаботы КАК ВТ_ЕстьОсновноеМестоРаботы
	|		ПО ВТ_ТаблицаДокумента.Сотрудник = ВТ_ЕстьОсновноеМестоРаботы.Сотрудник
	|ГДЕ
	|	ВТ_ЕстьОсновноеМестоРаботы.Сотрудник ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаДокумента.Период,
	|	ВТ_ТаблицаДокумента.Организация,
	|	ВТ_ТаблицаДокумента.РабочееМесто,
	|	ВТ_ТаблицаДокумента.Сотрудник,
	|	ВТ_ТаблицаДокумента.ДатаОкончания";
	
	Запрос.УстановитьПараметр("Ссылка",				ДокументСсылка);
	Запрос.УстановитьПараметр("ПериодРасчета",		ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ДокументСсылка));
	Запрос.УстановитьПараметр("Организация",		ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("МассивСотрудников",	ДокументСсылка.Работники.ВыгрузитьКолонку("Сотрудник"));
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	//по регистру "Занятые рабочие места"
	ТаблицыДляДвижений.Вставить("ТаблицаЗанятыеРабочиеМеста", Результат[6].Выгрузить());

	//по регистру "Доступные условия работы сотрудника"
	ТаблицыДляДвижений.Вставить("ТаблицаДоступныеУсловияРаботыСотрудника", ТаблицаУсловийРаботы);
	
	//по регистру "Основное место работы сотрудника"
	ТаблицыДляДвижений.Вставить("ТаблицаОсновноеМестоРаботыСотрудника", Результат[7].Выгрузить());
	
	//готовим пустую таблицу движений по регистру "Выдача по потребности"
	ТаблицаВыдачаПоПотребности = ДокументСсылка.ПолучитьОбъект().Движения.ВыдачаПоПотребности.Выгрузить().Скопировать(,"Период,ВидДвижения,Организация,Сотрудник,НормаВыдачи,НоменклатураНормы,ДатаПотребности,ДатаВыдачи,ПроцентИзноса,Количество");
	
	//по регистру "ПотребностьВыдачиСИЗ"
	ТаблицыДляДвижений.Вставить("ТаблицаПотребностьВыдачиСИЗ", ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуДвиженийПоПотребности(ДокументСсылка,,ТаблицаВыдачаПоПотребности));
	
	//по регистру "ВыдачаПоПотребности"
	ТаблицыДляДвижений.Вставить("ТаблицаВыдачаПоПотребности", ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуДвиженийВыдачиПоПотребности(ДокументСсылка,ТаблицыДляДвижений.ТаблицаПотребностьВыдачиСИЗ,,ТаблицаВыдачаПоПотребности));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
    Результат.Добавить("ВходящийНомер");
	Результат.Добавить("ДатаВходящегоДокумента");
    Возврат Результат;
	
КонецФункции

#КонецЕсли