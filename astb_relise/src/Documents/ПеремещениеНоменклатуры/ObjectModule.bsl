#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Документы.ПеремещениеНоменклатуры.ЗаполнитьПоЗаявке(ДанныеЗаполнения,ЭтотОбъект);
	
КонецПроцедуры	
	
//Andrey_NSK***********************************************************************

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	СуммаДокумента = Товары.Итог("Сумма");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	УстановитьПривилегированныйРежим(Истина);
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	Документы.ПеремещениеНоменклатуры.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ЗапасыСерверПереопределяемый.ОтразитьОстаткиНоменклатуры(ДополнительныеСвойства, Движения, Отказ);
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		ЗапасыСерверПереопределяемый.ОтразитьЗаявкиНаВыдачу(ДополнительныеСвойства, Движения, Отказ);
		ЗапасыСерверПереопределяемый.ОтразитьСобственныеСИЗ(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;	
   	СформироватьСписокРегистровДляКонтроля();
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
    ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
КонецПроцедуры

Процедура СформироватьСписокРегистровДляКонтроля()

	Массив = Новый Массив;
	// Контроль выполняется при проведении\отмене проведения не нового документа.
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Если НЕ СкладОтправитель.РазрешитьОтрицательныеОстатки Тогда 
			Массив.Добавить(Движения.ОстаткиНоменклатуры);
		КонецЕсли;
		Если ЗначениеЗаполнено(ДокументОснование) Тогда
			Массив.Добавить(Движения.ЗаявкиНаВыдачу);
		КонецЕсли;	
	КонецЕсли;

	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		ПроведениеСервер.ПроверитьСоответствиеДатыПеремещенияПериодуЗаявки(ЭтотОбъект,Отказ);
	КонецЕсли;
	
	ПроведениеСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект,Отказ,"Товары");	
	
КонецПроцедуры

//Andrey_NSK***********************************************************************

#КонецЕсли

