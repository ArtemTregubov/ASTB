#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ОбменДанными.Загрузка и РежимПроведения = РежимПроведенияДокумента.Оперативный И Организация.КонтролироватьКоличествоСтавокПоШтатномуРасписанию Тогда
		
		ТаблицаРаботников = Работники.Выгрузить(,"ПодразделениеНовое,ДолжностьНовая,ЗанимаемыхСтавокНовое");
		ТаблицаРаботников.Свернуть("ПодразделениеНовое,ДолжностьНовая","ЗанимаемыхСтавокНовое");
		ТаблицаРаботников.Колонки.Добавить("Организация",Новый ОписаниеТипов("СправочникСсылка.Организации"));
		ТаблицаРаботников.ЗаполнитьЗначения(Организация,"Организация");
		
		Запрос = Новый Запрос;
		МВТ = Новый МенеджерВременныхТаблиц;
		Запрос.МенеджерВременныхТаблиц = МВТ;
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	ТЗ.ПодразделениеНовое КАК Подразделение,
		               |	ТЗ.ДолжностьНовая КАК Должность,
		               |	ТЗ.Организация КАК Организация,
		               |	ТЗ.ЗанимаемыхСтавокНовое КАК ЗанимаемыхСтавок
		               |ПОМЕСТИТЬ ТЗФайл
		               |ИЗ
		               |	&Таблица КАК ТЗ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	ТЗФайл.Организация,
		               |	ТЗФайл.Подразделение,
		               |	ТЗФайл.Должность,
		               |	СУММА((ЕСТЬNULL(ШтатноеРасписаниеСрезПоследних.КоличествоСтавок, 0) - ТЗФайл.ЗанимаемыхСтавок) * -1) КАК Ставка
		               |ИЗ
		               |	ТЗФайл КАК ТЗФайл
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтатноеРасписание.СрезПоследних КАК ШтатноеРасписаниеСрезПоследних
		               |		ПО (ШтатноеРасписаниеСрезПоследних.Организация = ТЗФайл.Организация)
		               |			И (ШтатноеРасписаниеСрезПоследних.Подразделение = ТЗФайл.Подразделение)
		               |			И (ШтатноеРасписаниеСрезПоследних.Должность = ТЗФайл.Должность)
		               |ГДЕ
		               |	ЕСТЬNULL(ШтатноеРасписаниеСрезПоследних.КоличествоСтавок, 0) - ТЗФайл.ЗанимаемыхСтавок < 0
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ТЗФайл.Должность,
		               |	ТЗФайл.Подразделение,
		               |	ТЗФайл.Организация";
		Запрос.УстановитьПараметр("Таблица",ТаблицаРаботников); 
		Результат = Запрос.Выполнить().Выгрузить();
		Если Результат.Количество() > 0 Тогда
			Отказ = Истина;
			Для каждого СтрокаРез из Результат Цикл
				Сообщить("В штатном расписании для организации " + Организация + ", подразделения " + СтрокаРез.Подразделение + ", должности "+ СтрокаРез.Должность 
				+ " указано меньшее количество ставок (на "+ СтрокаРез.Ставка + "шт.)");
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Если не Отказ Тогда
		ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
		Документы.КадровоеПеремещение.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
		ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
		ЗапасыСерверПереопределяемый.ОтразитьЗанятыеРабочиеМеста(ДополнительныеСвойства, Движения, Отказ);
		ЗапасыСерверПереопределяемый.ОтразитьДоступныеУсловияРаботыСотрудника(ДополнительныеСвойства, Движения, Отказ);
		ЗапасыСерверПереопределяемый.ОтразитьПотребностьВыдачиСИЗ(ДополнительныеСвойства, Движения, Отказ);
		ЗапасыСерверПереопределяемый.ОтразитьВыдачуПоПотребности(ДополнительныеСвойства, Движения, Отказ);
		ЗапасыСерверПереопределяемый.ОтразитьОсновноеМестоРаботыСотрудника(ДополнительныеСвойства, Движения, Отказ);
		СформироватьСписокРегистровДляКонтроля();
		ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
		ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
		ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	КонецЕсли;

КонецПроцедуры

Процедура СформироватьСписокРегистровДляКонтроля()

	Массив = Новый Массив;
	//Контроль выполняется при проведении\отмене проведения не нового документа.
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Массив.Добавить(Движения.ЗанятыеРабочиеМеста);
		Массив.Добавить(Движения.ПотребностьВыдачиСИЗ);
		Массив.Добавить(Движения.ВыдачаПоПотребности);
	КонецЕсли;

	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);

КонецПроцедуры

#КонецЕсли

