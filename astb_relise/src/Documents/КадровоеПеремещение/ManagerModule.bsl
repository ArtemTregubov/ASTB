#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(УстановленныеНормыВыдачиСИЗСрезПоследних.Период) КАК Период,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.Подразделение КАК Подразделение,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.Должность КАК Должность,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.РабочееМесто КАК РабочееМесто,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.УсловиеНормы КАК УсловиеНормы,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.НормаВыдачи КАК НормаВыдачи
	|ПОМЕСТИТЬ ВТ_МаксимальныеДатыНорм
	|ИЗ
	|	РегистрСведений.УстановленныеНормыВыдачиСИЗ.СрезПоследних(
	|			&ПериодРасчета,
	|			Организация = &Организация
	|				И Подразделение В (&МассивПодразделений)
	|				И Должность В (&МассивДолжностей)
	|				И (РабочееМесто В (&МассивРабочихМест)
	|					ИЛИ РабочееМесто = ЗНАЧЕНИЕ(Справочник.РабочиеМестаАСТБ.ПустаяСсылка))) КАК УстановленныеНормыВыдачиСИЗСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.Подразделение,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.Должность,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.РабочееМесто,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.УсловиеНормы,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.НормаВыдачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_МаксимальныеДатыНорм.Подразделение КАК Подразделение,
	|	ВТ_МаксимальныеДатыНорм.Должность КАК Должность,
	|	ВТ_МаксимальныеДатыНорм.РабочееМесто КАК РабочееМесто,
	|	ВТ_МаксимальныеДатыНорм.УсловиеНормы КАК УсловиеНормы,
	|	ВТ_МаксимальныеДатыНорм.НормаВыдачи КАК НормаВыдачи,
	|	УстановленныеНормыВыдачиСИЗСрезПоследних.Использовать КАК Использовать
	|ПОМЕСТИТЬ ВТ_ПоследниеНормы
	|ИЗ
	|	ВТ_МаксимальныеДатыНорм КАК ВТ_МаксимальныеДатыНорм
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УстановленныеНормыВыдачиСИЗ.СрезПоследних(
	|				&ПериодРасчета,
	|				Организация = &Организация
	|					И Подразделение В (&МассивПодразделений)
	|					И Должность В (&МассивДолжностей)
	|					И (РабочееМесто В (&МассивРабочихМест)
	|						ИЛИ РабочееМесто = ЗНАЧЕНИЕ(Справочник.РабочиеМестаАСТБ.ПустаяСсылка))) КАК УстановленныеНормыВыдачиСИЗСрезПоследних
	|		ПО ВТ_МаксимальныеДатыНорм.Период = УстановленныеНормыВыдачиСИЗСрезПоследних.Период
	|			И ВТ_МаксимальныеДатыНорм.Подразделение = УстановленныеНормыВыдачиСИЗСрезПоследних.Подразделение
	|			И ВТ_МаксимальныеДатыНорм.Должность = УстановленныеНормыВыдачиСИЗСрезПоследних.Должность
	|			И ВТ_МаксимальныеДатыНорм.РабочееМесто = УстановленныеНормыВыдачиСИЗСрезПоследних.РабочееМесто
	|			И ВТ_МаксимальныеДатыНорм.УсловиеНормы = УстановленныеНормыВыдачиСИЗСрезПоследних.УсловиеНормы
	|			И ВТ_МаксимальныеДатыНорм.НормаВыдачи = УстановленныеНормыВыдачиСИЗСрезПоследних.НормаВыдачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ВТ_ПоследниеНормы.Подразделение КАК Подразделение,
	|	ВТ_ПоследниеНормы.Должность КАК Должность,
	|	ВТ_ПоследниеНормы.РабочееМесто КАК РабочееМесто,
	|	ВТ_ПоследниеНормы.УсловиеНормы КАК УсловиеНормы,
	|	ВТ_ПоследниеНормы.Использовать КАК Использовать
	|ПОМЕСТИТЬ ВТ_НормыСПериодом
	|ИЗ
	|	ВТ_ПоследниеНормы КАК ВТ_ПоследниеНормы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НормыСПериодом.Период КАК Период,
	|	ВТ_НормыСПериодом.Подразделение КАК Подразделение,
	|	ВТ_НормыСПериодом.Должность КАК Должность,
	|	ВТ_НормыСПериодом.РабочееМесто КАК РабочееМесто,
	|	ВТ_НормыСПериодом.УсловиеНормы КАК УсловиеНормы,
	|	МАКСИМУМ(ВТ_НормыСПериодом.Использовать) КАК Использовать
	|ИЗ
	|	ВТ_НормыСПериодом КАК ВТ_НормыСПериодом
	|ГДЕ
	|	ВТ_НормыСПериодом.Использовать
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_НормыСПериодом.Период,
	|	ВТ_НормыСПериодом.Подразделение,
	|	ВТ_НормыСПериодом.Должность,
	|	ВТ_НормыСПериодом.РабочееМесто,
	|	ВТ_НормыСПериодом.УсловиеНормы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КадровоеПеремещениеРаботники.Сотрудник КАК Сотрудник,
	|	КадровоеПеремещениеРаботники.ПодразделениеНовое КАК Подразделение,
	|	КадровоеПеремещениеРаботники.ДолжностьНовая КАК Должность,
	|	КадровоеПеремещениеРаботники.РабочееМестоНовое КАК РабочееМесто
	|ИЗ
	|	Документ.КадровоеПеремещение.Работники КАК КадровоеПеремещениеРаботники
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
	|				&ПериодРасчета,
	|				Организация = &Организация
	|					И Сотрудник В (&МассивСотрудников)) КАК ЗанятыеРабочиеМестаОстатки
	|		ПО КадровоеПеремещениеРаботники.Сотрудник = ЗанятыеРабочиеМестаОстатки.Сотрудник
	|			И КадровоеПеремещениеРаботники.ПодразделениеНовое = ЗанятыеРабочиеМестаОстатки.Подразделение
	|			И КадровоеПеремещениеРаботники.ДолжностьНовая = ЗанятыеРабочиеМестаОстатки.Должность
	|			И КадровоеПеремещениеРаботники.РабочееМестоНовое = ЗанятыеРабочиеМестаОстатки.РабочееМесто
	|ГДЕ
	|	ЗанятыеРабочиеМестаОстатки.КоличествоОстаток ЕСТЬ NULL
	|	И КадровоеПеремещениеРаботники.Ссылка = &Ссылка
	|	И НЕ(КадровоеПеремещениеРаботники.ПодразделениеСтарое = КадровоеПеремещениеРаботники.ПодразделениеНовое
	|				И КадровоеПеремещениеРаботники.ДолжностьСтарая = КадровоеПеремещениеРаботники.ДолжностьНовая
	|				И КадровоеПеремещениеРаботники.РабочееМестоСтарое = КадровоеПеремещениеРаботники.РабочееМестоНовое)";
	
	Запрос.УстановитьПараметр("Ссылка",				ДокументСсылка);
	Запрос.УстановитьПараметр("Период",				ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("ПериодРасчета",		ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ДокументСсылка));
	Запрос.УстановитьПараметр("Организация",		ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("МассивСотрудников", 	ДокументСсылка.Работники.ВЫгрузитьКолонку("Сотрудник"));
	Запрос.УстановитьПараметр("МассивДолжностей",	ДокументСсылка.Работники.ВЫгрузитьКолонку("ДолжностьНовая"));
	Запрос.УстановитьПараметр("МассивПодразделений",ДокументСсылка.Работники.ВЫгрузитьКолонку("ПодразделениеНовое"));
	Запрос.УстановитьПараметр("МассивРабочихМест",	ДокументСсылка.Работники.ВЫгрузитьКолонку("РабочееМестоНовое"));
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаУсловийРаботы = ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуУсловийРаботыСотрудников(Результат[3].Выгрузить(),Результат[4].Выгрузить());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КадровоеПеремещениеРаботники.Ссылка.Дата КАК Период,
	|	КадровоеПеремещениеРаботники.НомерСтроки КАК НомерСтроки,
	|	КадровоеПеремещениеРаботники.Ссылка.Организация КАК Организация,
	|	КадровоеПеремещениеРаботники.Сотрудник КАК Сотрудник,
	|	КадровоеПеремещениеРаботники.ПодразделениеСтарое КАК ПодразделениеСтарое,
	|	КадровоеПеремещениеРаботники.ДолжностьСтарая КАК ДолжностьСтарая,
	|	КадровоеПеремещениеРаботники.РабочееМестоСтарое КАК РабочееМестоСтарое,
	|	КадровоеПеремещениеРаботники.ЗанимаемыхСтавокСтарое КАК ЗанимаемыхСтавокСтарое,
	|	КадровоеПеремещениеРаботники.ПодразделениеНовое КАК ПодразделениеНовое,
	|	КадровоеПеремещениеРаботники.ДолжностьНовая КАК ДолжностьНовая,
	|	КадровоеПеремещениеРаботники.РабочееМестоНовое КАК РабочееМестоНовое,
	|	КадровоеПеремещениеРаботники.ЗанимаемыхСтавокНовое КАК ЗанимаемыхСтавокНовое,
	|	ВЫБОР
	|		КОГДА КадровоеПеремещениеРаботники.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА КадровоеПеремещениеРаботники.Ссылка.ДатаОкончания
	|		ИНАЧЕ КадровоеПеремещениеРаботники.ДатаОкончания
	|	КОНЕЦ КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ТаблицаДокумента
	|ИЗ
	|	Документ.КадровоеПеремещение.Работники КАК КадровоеПеремещениеРаботники
	|ГДЕ
	|	КадровоеПеремещениеРаботники.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗанятыеРабочиеМестаОстатки.Подразделение КАК Подразделение,
	|	ЗанятыеРабочиеМестаОстатки.Должность КАК Должность,
	|	ЗанятыеРабочиеМестаОстатки.РабочееМесто КАК РабочееМесто,
	|	ЗанятыеРабочиеМестаОстатки.Сотрудник КАК Сотрудник,
	|	ЗанятыеРабочиеМестаОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ВТ_ЗанятыеРабочиеМеста
	|ИЗ
	|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
	|			&ПериодРасчета,
	|			Организация = &Организация
	|				И Сотрудник В (&МассивСотрудников)) КАК ЗанятыеРабочиеМестаОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗанятыеРабочиеМестаПослеПеремещения.Подразделение КАК Подразделение,
	|	ЗанятыеРабочиеМестаПослеПеремещения.Должность КАК Должность,
	|	ЗанятыеРабочиеМестаПослеПеремещения.РабочееМесто КАК РабочееМесто,
	|	ЗанятыеРабочиеМестаПослеПеремещения.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТ_ЗанятыеРабочиеМестаПослеПеремещения
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_ЗанятыеРабочиеМеста.Подразделение КАК Подразделение,
	|		ВТ_ЗанятыеРабочиеМеста.Должность КАК Должность,
	|		ВТ_ЗанятыеРабочиеМеста.РабочееМесто КАК РабочееМесто,
	|		ВТ_ЗанятыеРабочиеМеста.Сотрудник КАК Сотрудник
	|	ИЗ
	|		ВТ_ЗанятыеРабочиеМеста КАК ВТ_ЗанятыеРабочиеМеста
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|			ПО ВТ_ЗанятыеРабочиеМеста.Подразделение = ВТ_ТаблицаДокумента.ПодразделениеСтарое
	|				И ВТ_ЗанятыеРабочиеМеста.Должность = ВТ_ТаблицаДокумента.ДолжностьСтарая
	|				И ВТ_ЗанятыеРабочиеМеста.РабочееМесто = ВТ_ТаблицаДокумента.РабочееМестоСтарое
	|				И ВТ_ЗанятыеРабочиеМеста.Сотрудник = ВТ_ТаблицаДокумента.Сотрудник
	|	ГДЕ
	|		ВТ_ЗанятыеРабочиеМеста.КоличествоОстаток - ЕСТЬNULL(ВТ_ТаблицаДокумента.ЗанимаемыхСтавокСтарое, 0) > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_ТаблицаДокумента.ПодразделениеНовое,
	|		ВТ_ТаблицаДокумента.ДолжностьНовая,
	|		ВТ_ТаблицаДокумента.РабочееМестоНовое,
	|		ВТ_ТаблицаДокумента.Сотрудник
	|	ИЗ
	|		ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента) КАК ЗанятыеРабочиеМестаПослеПеремещения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Сотрудник КАК Сотрудник,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Подразделение КАК Подразделение,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Должность КАК Должность,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.РабочееМесто КАК РабочееМесто,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.ОсновноеМестоРаботы КАК ОсновноеМестоРаботы,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ОсновноеМестоРаботы
	|ИЗ
	|	РегистрСведений.ОсновноеМестоРаботыСотрудника.СрезПоследних(
	|			&ПериодРасчета,
	|			Организация = &Организация
	|				И Сотрудник В (&МассивСотрудников)) КАК ОсновноеМестоРаботыСотрудникаСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаУсловийРаботы.Период КАК Период,
	|	ТаблицаУсловийРаботы.Сотрудник КАК Сотрудник,
	|	ТаблицаУсловийРаботы.Подразделение КАК Подразделение,
	|	ТаблицаУсловийРаботы.Должность КАК Должность,
	|	ТаблицаУсловийРаботы.РабочееМесто КАК РабочееМесто,
	|	ТаблицаУсловийРаботы.Условие КАК Условие,
	|	ТаблицаУсловийРаботы.Использовать КАК Использовать
	|ПОМЕСТИТЬ ВТ_ТаблицаУсловийРаботы
	|ИЗ
	|	&ТаблицаУсловийРаботы КАК ТаблицаУсловийРаботы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВТ_ТаблицаДокумента.Период КАК Период,
	|	ВТ_ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ВТ_ТаблицаДокумента.Организация КАК Организация,
	|	ВТ_ТаблицаДокумента.Сотрудник КАК Сотрудник,
	|	ВТ_ТаблицаДокумента.ПодразделениеНовое КАК Подразделение,
	|	ВТ_ТаблицаДокумента.ДолжностьНовая КАК Должность,
	|	ВТ_ТаблицаДокумента.РабочееМестоНовое КАК РабочееМесто,
	|	ВТ_ТаблицаДокумента.ЗанимаемыхСтавокНовое КАК Количество
	|ИЗ
	|	ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|ГДЕ
	|	НЕ (ВТ_ТаблицаДокумента.ПодразделениеНовое = ВТ_ТаблицаДокумента.ПодразделениеСтарое
	|	И ВТ_ТаблицаДокумента.ДолжностьНовая = ВТ_ТаблицаДокумента.ДолжностьСтарая
	|	И ВТ_ТаблицаДокумента.РабочееМестоНовое = ВТ_ТаблицаДокумента.РабочееМестоСтарое
	|	И ВТ_ТаблицаДокумента.ЗанимаемыхСтавокНовое = ВТ_ТаблицаДокумента.ЗанимаемыхСтавокСтарое)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ВТ_ТаблицаДокумента.Период,
	|	ВТ_ТаблицаДокумента.НомерСтроки,
	|	ВТ_ТаблицаДокумента.Организация,
	|	ВТ_ТаблицаДокумента.Сотрудник,
	|	ВТ_ТаблицаДокумента.ПодразделениеСтарое,
	|	ВТ_ТаблицаДокумента.ДолжностьСтарая,
	|	ВТ_ТаблицаДокумента.РабочееМестоСтарое,
	|	ВТ_ТаблицаДокумента.ЗанимаемыхСтавокСтарое
	|ИЗ
	|	ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|ГДЕ
	|	НЕ (ВТ_ТаблицаДокумента.ПодразделениеНовое = ВТ_ТаблицаДокумента.ПодразделениеСтарое
	|	И ВТ_ТаблицаДокумента.ДолжностьНовая = ВТ_ТаблицаДокумента.ДолжностьСтарая
	|	И ВТ_ТаблицаДокумента.РабочееМестоНовое = ВТ_ТаблицаДокумента.РабочееМестоСтарое
	|	И ВТ_ТаблицаДокумента.ЗанимаемыхСтавокНовое = ВТ_ТаблицаДокумента.ЗанимаемыхСтавокСтарое)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Период КАК Период,
	|	ТаблицаДокумента.Сотрудник КАК Сотрудник,
	|	ТаблицаДокумента.ПодразделениеСтарое КАК Подразделение,
	|	ТаблицаДокумента.ДолжностьСтарая КАК Должность,
	|	ТаблицаДокумента.РабочееМестоСтарое КАК РабочееМесто,
	|	ДоступныеУсловияРаботыСотрудникаСрезПоследних.Условие КАК Условие,
	|	ЛОЖЬ КАК Использовать
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_ТаблицаДокумента.Период КАК Период,
	|		ВТ_ТаблицаДокумента.Сотрудник КАК Сотрудник,
	|		ВТ_ТаблицаДокумента.ПодразделениеСтарое КАК ПодразделениеСтарое,
	|		ВТ_ТаблицаДокумента.ДолжностьСтарая КАК ДолжностьСтарая,
	|		ВТ_ТаблицаДокумента.РабочееМестоСтарое КАК РабочееМестоСтарое,
	|		СУММА(ВТ_ТаблицаДокумента.ЗанимаемыхСтавокСтарое) КАК ЗанимаемыхСтавокСтарое
	|	ИЗ
	|		ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|	ГДЕ
	|		НЕ(ВТ_ТаблицаДокумента.ПодразделениеСтарое = ВТ_ТаблицаДокумента.ПодразделениеНовое
	|					И ВТ_ТаблицаДокумента.ДолжностьСтарая = ВТ_ТаблицаДокумента.ДолжностьНовая
	|					И ВТ_ТаблицаДокумента.РабочееМестоСтарое = ВТ_ТаблицаДокумента.РабочееМестоНовое)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВТ_ТаблицаДокумента.Период,
	|		ВТ_ТаблицаДокумента.Сотрудник,
	|		ВТ_ТаблицаДокумента.ПодразделениеСтарое,
	|		ВТ_ТаблицаДокумента.ДолжностьСтарая,
	|		ВТ_ТаблицаДокумента.РабочееМестоСтарое) КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗанятыеРабочиеМеста КАК ВТ_ЗанятыеРабочиеМеста
	|		ПО ТаблицаДокумента.Сотрудник = ВТ_ЗанятыеРабочиеМеста.Сотрудник
	|			И ТаблицаДокумента.ПодразделениеСтарое = ВТ_ЗанятыеРабочиеМеста.Подразделение
	|			И ТаблицаДокумента.ДолжностьСтарая = ВТ_ЗанятыеРабочиеМеста.Должность
	|			И ТаблицаДокумента.РабочееМестоСтарое = ВТ_ЗанятыеРабочиеМеста.РабочееМесто
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДоступныеУсловияРаботыСотрудника.СрезПоследних(&ПериодРасчета, Сотрудник В (&МассивСотрудников)) КАК ДоступныеУсловияРаботыСотрудникаСрезПоследних
	|		ПО ТаблицаДокумента.Сотрудник = ДоступныеУсловияРаботыСотрудникаСрезПоследних.Сотрудник
	|			И ТаблицаДокумента.ДолжностьСтарая = ДоступныеУсловияРаботыСотрудникаСрезПоследних.Должность
	|			И ТаблицаДокумента.ПодразделениеСтарое = ДоступныеУсловияРаботыСотрудникаСрезПоследних.Подразделение
	|ГДЕ
	|	НЕ ДоступныеУсловияРаботыСотрудникаСрезПоследних.Условие ЕСТЬ NULL
	|	И ТаблицаДокумента.ЗанимаемыхСтавокСтарое - ЕСТЬNULL(ВТ_ЗанятыеРабочиеМеста.КоличествоОстаток, 0) = 0
	|	И ДоступныеУсловияРаботыСотрудникаСрезПоследних.Использовать
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ТаблицаУсловийРаботы.Период,
	|	ВТ_ТаблицаУсловийРаботы.Сотрудник,
	|	ВТ_ТаблицаУсловийРаботы.Подразделение,
	|	ВТ_ТаблицаУсловийРаботы.Должность,
	|	ВТ_ТаблицаУсловийРаботы.РабочееМесто,
	|	ВТ_ТаблицаУсловийРаботы.Условие,
	|	ВТ_ТаблицаУсловийРаботы.Использовать
	|ИЗ
	|	ВТ_ТаблицаУсловийРаботы КАК ВТ_ТаблицаУсловийРаботы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ВТ_ТаблицаДокумента.Организация КАК Организация,
	|	ВТ_ТаблицаДокумента.ПодразделениеНовое КАК Подразделение,
	|	ВТ_ТаблицаДокумента.ДолжностьНовая КАК Должность,
	|	ВТ_ТаблицаДокумента.РабочееМестоНовое КАК РабочееМесто,
	|	ИСТИНА КАК ОсновноеМестоРаботы,
	|	ВТ_ТаблицаДокумента.Сотрудник КАК Сотрудник,
	|	ВТ_ТаблицаДокумента.ДатаОкончания КАК ДатаОкончания
	|ИЗ
	|	ВТ_ОсновноеМестоРаботы КАК ВТ_ОсновноеМестоРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|		ПО ВТ_ОсновноеМестоРаботы.Сотрудник = ВТ_ТаблицаДокумента.Сотрудник
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ВТ_ТаблицаДокумента.ПодразделениеСтарое = ВТ_ОсновноеМестоРаботы.Подразделение
	|					И ВТ_ТаблицаДокумента.ДолжностьСтарая = ВТ_ОсновноеМестоРаботы.Должность
	|					И ВТ_ТаблицаДокумента.РабочееМестоСтарое = ВТ_ОсновноеМестоРаботы.РабочееМесто
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|	И НЕ (ВТ_ТаблицаДокумента.ПодразделениеНовое = ВТ_ТаблицаДокумента.ПодразделениеСтарое
	|	И ВТ_ТаблицаДокумента.ДолжностьНовая = ВТ_ТаблицаДокумента.ДолжностьСтарая
	|	И ВТ_ТаблицаДокумента.РабочееМестоНовое = ВТ_ТаблицаДокумента.РабочееМестоСтарое)";
	
	Запрос.УстановитьПараметр("Ссылка",					ДокументСсылка);
	Запрос.УстановитьПараметр("Период",					ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("ПериодРасчета",			ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ДокументСсылка));
	Запрос.УстановитьПараметр("МассивСотрудников", 		ДокументСсылка.Работники.ВЫгрузитьКолонку("Сотрудник"));
	Запрос.УстановитьПараметр("Организация",			ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("ТаблицаУсловийРаботы",	ТаблицаУсловийРаботы);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	//по регистру "Занятые рабочие места"
	ТаблицыДляДвижений.Вставить("ТаблицаЗанятыеРабочиеМеста", Результат[5].Выгрузить());
	
	//по регистру "Доступные условия работы сотрудника"
	ТаблицыДляДвижений.Вставить("ТаблицаДоступныеУсловияРаботыСотрудника", Результат[6].Выгрузить());
	
	//по регистру "Основное место работы сотрудника"
	ТаблицыДляДвижений.Вставить("ТаблицаОсновноеМестоРаботыСотрудника", Результат[7].Выгрузить());
	
	//готовим пустую таблицу движений по регистру "Выдача по потребности"
	ТаблицаВыдачаПоПотребности = ДокументСсылка.ПолучитьОбъект().Движения.ВыдачаПоПотребности.Выгрузить().Скопировать(,"Период,ВидДвижения,Организация,Сотрудник,НормаВыдачи,НоменклатураНормы,ДатаПотребности,ДатаВыдачи,ПроцентИзноса,Количество");
	
	//по регистру "ПотребностьВыдачиСИЗ"
	ТаблицыДляДвижений.Вставить("ТаблицаПотребностьВыдачиСИЗ", ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуДвиженийПоПотребности(ДокументСсылка,,ТаблицаВыдачаПоПотребности));
	
	//по регистру "ВыдачаПоПотребности"
	ТаблицыДляДвижений.Вставить("ТаблицаВыдачаПоПотребности", ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуДвиженийВыдачиПоПотребности(ДокументСсылка,ТаблицыДляДвижений.ТаблицаПотребностьВыдачиСИЗ,,ТаблицаВыдачаПоПотребности));
	
КонецПроцедуры	

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой ЗапретРедактированияРеквизитовОбъектов.

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//     Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//              где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы, связанного с
//              реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
    Результат.Добавить("ВходящийНомер");
	Результат.Добавить("ДатаВходящегоДокумента");
    Возврат Результат;
	
КонецФункции

#КонецЕсли