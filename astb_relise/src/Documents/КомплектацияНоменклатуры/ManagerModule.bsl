#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Комплекты.Номенклатура КАК Комплект,
		|	Комплекты.ХарактеристикаНоменклатуры.Метрика КАК МетрикаКомплекта,
		|	СУММА(Комплекты.Количество) КАК КоличествоКомплектов,
		|	НоменклатураКомплектующие.Номенклатура КАК Номенклатура,
		|	СУММА(НоменклатураКомплектующие.Количество * Комплекты.Количество) КАК Количество,
		|	Комплекты.Ссылка,
		|	Комплекты.ХарактеристикаНоменклатуры
		|ПОМЕСТИТЬ ТаблКомплектующих
		|ИЗ
		|	Справочник.Номенклатура.Комплектующие КАК НоменклатураКомплектующие
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КомплектацияНоменклатуры.Товары КАК Комплекты
		|		ПО НоменклатураКомплектующие.Ссылка = Комплекты.Номенклатура
		|ГДЕ
		|	Комплекты.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	Комплекты.Номенклатура,
		|	Комплекты.Ссылка,
		|	Комплекты.ХарактеристикаНоменклатуры.Метрика,
		|	НоменклатураКомплектующие.Номенклатура,
		|	Комплекты.ХарактеристикаНоменклатуры
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Комплект,
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	&ОрганизацияДляОстатков КАК Организация,
		|	ТаблКомплектующих.Ссылка.Склад КАК Склад,
		|	ТаблКомплектующих.Ссылка.Дата КАК Период,
		|	ВЫБОР
		|		КОГДА ТаблКомплектующих.Ссылка.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектации.Комплектация)
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ КАК ВидДвижения,
		|	ТаблКомплектующих.Комплект КАК Номенклатура,
		|	ТаблКомплектующих.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ТаблКомплектующих.КоличествоКомплектов КАК Количество
		|ИЗ
		|	ТаблКомплектующих КАК ТаблКомплектующих
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблКомплектующих.ХарактеристикаНоменклатуры,
		|	ТаблКомплектующих.Комплект,
		|	ТаблКомплектующих.Ссылка.Склад,
		|	ТаблКомплектующих.Ссылка.Дата,
		|	ТаблКомплектующих.КоличествоКомплектов,
		|	ВЫБОР
		|		КОГДА ТаблКомплектующих.Ссылка.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектации.Комплектация)
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	&ОрганизацияДляОстатков,
		|	ТаблКомплектующих.Ссылка.Склад,
		|	ТаблКомплектующих.Ссылка.Дата,
		|	ВЫБОР
		|		КОГДА ТаблКомплектующих.Ссылка.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектации.Комплектация)
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	КОНЕЦ,
		|	ТаблКомплектующих.Номенклатура,
		|	ХарактеристикиНоменклатуры.Ссылка,
		|	СУММА(ТаблКомплектующих.Количество)
		|ИЗ
		|	ТаблКомплектующих КАК ТаблКомплектующих
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
		|		ПО ТаблКомплектующих.Номенклатура = ХарактеристикиНоменклатуры.Владелец
		|			И ТаблКомплектующих.МетрикаКомплекта = ХарактеристикиНоменклатуры.Метрика
		|
		|СГРУППИРОВАТЬ ПО
		|	ХарактеристикиНоменклатуры.Ссылка,
		|	ТаблКомплектующих.Номенклатура,
		|	ТаблКомплектующих.Ссылка.Склад,
		|	ТаблКомплектующих.Ссылка.Дата,
		|	ВЫБОР
		|		КОГДА ТаблКомплектующих.Ссылка.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектации.Комплектация)
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидДвижения,
		|	Номенклатура,
		|	ХарактеристикаНоменклатуры
		|АВТОУПОРЯДОЧИВАНИЕ";

	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Запрос.УстановитьПараметр("ОрганизацияДляОстатков",?(ПолучитьФункциональнуюОпцию("НеВестиУчетОстатковНоменклатурыПоОрганизации"),Справочники.Организации.ПустаяСсылка(),ДокументСсылка.Организация));
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	ТаблицыДляДвижений.Вставить("ТаблицаОстаткиНоменклатуры", Результат);
		
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Комплектация") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"Комплектация",
			"Комплектация номенклатуры",
			СформироватьПечатнуюФормуКомплектация(МассивОбъектов, ОбъектыПечати),,"Документ.КомплектацияНоменклатуры.ПФ_MXL_Комплектация");
				
	КонецЕсли;		
		
КонецПроцедуры

Функция СформироватьПечатнуюФормуКомплектация(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КомплектацияНоменклатуры_КомплектацияНоменклатуры";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Документы.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(Документы.Операция) КАК Операция,
	|	Документы.Организация.НаименованиеПолное КАК ПредставлениеОрганизации,
	|	Документы.Номер КАК Номер,
	|	Документы.Дата КАК Дата,
	|	Документы.Склад.Наименование КАК ПредставлениеСклада,
	|	Документы.Организация.Префикс КАК Префикс,
	|	Документы.Ответственный.Наименование КАК ПредставлениеОтветственного
	|ИЗ
	|	Документ.КомплектацияНоменклатуры КАК Документы
	|ГДЕ
	|	Документы.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Комплекты.Ссылка КАК Ссылка,
	|	Комплекты.Номенклатура КАК Комплекты,
	|	Комплекты.ХарактеристикаНоменклатуры КАК ХарактеристикаКомплектов,
	|	СУММА(Комплекты.Количество) КАК КоличествоКомплектов,
	|	Комплекты.ХарактеристикаНоменклатуры.Метрика КАК Метрика,
	|	Комплектующие.Номенклатура КАК Номенклатура,
	|	СУММА(Комплектующие.Количество * Комплекты.Количество) КАК Количество
	|ПОМЕСТИТЬ ТаблКомплектующих
	|ИЗ
	|	Документ.КомплектацияНоменклатуры.Товары КАК Комплекты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура.Комплектующие КАК Комплектующие
	|		ПО Комплекты.Номенклатура = Комплектующие.Ссылка
	|ГДЕ
	|	Комплекты.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	Комплектующие.Номенклатура,
	|	Комплекты.ХарактеристикаНоменклатуры,
	|	Комплекты.Номенклатура,
	|	Комплекты.Ссылка,
	|	Комплекты.ХарактеристикаНоменклатуры.Метрика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Комплекты,
	|	Номенклатура,
	|	Метрика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблКомплектующих.Ссылка КАК Ссылка,
	|	ТаблКомплектующих.ХарактеристикаКомплектов КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблКомплектующих.Ссылка.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектации.Комплектация)
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ОперацияКомплекта,
	|	ВЫБОР
	|		КОГДА ТаблКомплектующих.ХарактеристикаКомплектов = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблКомплектующих.Комплекты.Наименование
	|		ИНАЧЕ ТаблКомплектующих.Комплекты.Наименование + "" ("" + ТаблКомплектующих.ХарактеристикаКомплектов.Наименование + "")""
	|	КОНЕЦ КАК НаименованиеКомплекта,
	|	ВЫБОР
	|		КОГДА ТаблКомплектующих.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Артикул)
	|			ТОГДА ТаблКомплектующих.Комплекты.Артикул
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблКомплектующих.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Код)
	|					ТОГДА ТаблКомплектующих.Комплекты.Код
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ТаблКомплектующих.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.КодСинхронизации)
	|							ТОГДА ТаблКомплектующих.Комплекты.КодСинхронизации
	|						ИНАЧЕ ВЫБОР
	|								КОГДА ТаблКомплектующих.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.НоменклатурныйНомерОрганизации)
	|									ТОГДА """"
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК НомерКомплекта,
	|	ТаблКомплектующих.КоличествоКомплектов КАК КоличествоКомплектов,
	|	ТаблКомплектующих.Комплекты.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияКомплектов,
	|	ВЫБОР
	|		КОГДА ТаблКомплектующих.Ссылка.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектации.Комплектация)
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ КАК ОперацияНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблКомплектующих.ХарактеристикаКомплектов = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблКомплектующих.Номенклатура.Наименование
	|		ИНАЧЕ ТаблКомплектующих.Номенклатура.Наименование + "" ("" + ТаблКомплектующих.ХарактеристикаКомплектов.Наименование + "")""
	|	КОНЕЦ КАК НаименованиеНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблКомплектующих.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Артикул)
	|			ТОГДА ТаблКомплектующих.Номенклатура.Артикул
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблКомплектующих.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Код)
	|					ТОГДА ТаблКомплектующих.Номенклатура.Код
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ТаблКомплектующих.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.КодСинхронизации)
	|							ТОГДА ТаблКомплектующих.Номенклатура.КодСинхронизации
	|						ИНАЧЕ ВЫБОР
	|								КОГДА ТаблКомплектующих.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.НоменклатурныйНомерОрганизации)
	|									ТОГДА ЕСТЬNULL(НоменклатурныеНомераОрганизаций.НоменклатурныйНомер, """")
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК НомерНоменклатуры,
	|	СУММА(ТаблКомплектующих.Количество) КАК КоличествоНоменклатуры,
	|	ТаблКомплектующих.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНоменклатуры
	|ИЗ
	|	ТаблКомплектующих КАК ТаблКомплектующих
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатурныеНомераОрганизаций КАК НоменклатурныеНомераОрганизаций
	|		ПО ТаблКомплектующих.Ссылка.Организация = НоменклатурныеНомераОрганизаций.Организация
	|			И ТаблКомплектующих.ХарактеристикаКомплектов = НоменклатурныеНомераОрганизаций.ХарактеристикаНоменклатуры
	|			И ТаблКомплектующих.Номенклатура = НоменклатурныеНомераОрганизаций.Номенклатура
	|ГДЕ
	|	ТаблКомплектующих.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблКомплектующих.ХарактеристикаКомплектов,
	|	ТаблКомплектующих.Комплекты.ЕдиницаИзмерения.Наименование,
	|	ВЫБОР
	|		КОГДА ТаблКомплектующих.ХарактеристикаКомплектов = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблКомплектующих.Номенклатура.Наименование
	|		ИНАЧЕ ТаблКомплектующих.Номенклатура.Наименование + "" ("" + ТаблКомплектующих.ХарактеристикаКомплектов.Наименование + "")""
	|	КОНЕЦ,
	|	ТаблКомплектующих.Ссылка,
	|	ВЫБОР
	|		КОГДА ТаблКомплектующих.ХарактеристикаКомплектов = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблКомплектующих.Комплекты.Наименование
	|		ИНАЧЕ ТаблКомплектующих.Комплекты.Наименование + "" ("" + ТаблКомплектующих.ХарактеристикаКомплектов.Наименование + "")""
	|	КОНЕЦ,
	|	ТаблКомплектующих.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	ТаблКомплектующих.КоличествоКомплектов,
	|	ВЫБОР
	|		КОГДА ТаблКомплектующих.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Артикул)
	|			ТОГДА ТаблКомплектующих.Номенклатура.Артикул
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблКомплектующих.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Код)
	|					ТОГДА ТаблКомплектующих.Номенклатура.Код
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ТаблКомплектующих.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.КодСинхронизации)
	|							ТОГДА ТаблКомплектующих.Номенклатура.КодСинхронизации
	|						ИНАЧЕ ВЫБОР
	|								КОГДА ТаблКомплектующих.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.НоменклатурныйНомерОрганизации)
	|									ТОГДА ЕСТЬNULL(НоменклатурныеНомераОрганизаций.НоменклатурныйНомер, """")
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблКомплектующих.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Артикул)
	|			ТОГДА ТаблКомплектующих.Комплекты.Артикул
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблКомплектующих.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Код)
	|					ТОГДА ТаблКомплектующих.Комплекты.Код
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ТаблКомплектующих.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.КодСинхронизации)
	|							ТОГДА ТаблКомплектующих.Комплекты.КодСинхронизации
	|						ИНАЧЕ ВЫБОР
	|								КОГДА ТаблКомплектующих.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.НоменклатурныйНомерОрганизации)
	|									ТОГДА """"
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблКомплектующих.Ссылка.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектации.Комплектация)
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблКомплектующих.Ссылка.Операция = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектации.Комплектация)
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ОперацияКомплекта,
	|	НаименованиеКомплекта,
	|	Характеристика,
	|	НаименованиеНоменклатуры
	|ИТОГИ ПО
	|	Ссылка");
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.КомплектацияНоменклатуры.ПФ_MXL_Комплектация");
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ДанныеПечати = ПакетЗапросов[0].Выбрать();
	ВыборкаПоДокументам = ПакетЗапросов[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если НЕ ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Ссылка", ДанныеПечати.Ссылка)) Тогда
			Продолжить;
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
			
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Заполнить(ДанныеПечати);
		//Область.Параметры.ТекстЗаголовка = ОбщегоНазначенияПереопределяемый.СформироватьЗаголовокДокумента(Новый Структура("Номер, Дата, Префикс", ДанныеПечати.Номер, ДанныеПечати.Дата, ДанныеПечати.Префикс), "Комплектация номенклатуры");
		Область.Параметры.ТекстЗаголовка = СокрЛП(ДанныеПечати.Операция)+"   №"+ДанныеПечати.Номер+"  от  "+Формат(ДанныеПечати.Дата, "ДЛФ=Д");
		ТабличныйДокумент.Вывести(Область);
			
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		Комплект = "";
		Пока ВыборкаПоТоварам.Следующий() Цикл
			Если ВыборкаПоТоварам.НаименованиеКомплекта <> Комплект Тогда  //при смене комплекта выводим комплект
				Область = Макет.ПолучитьОбласть("СтрокаТаблицыКомплект");
                Комплект = ВыборкаПоТоварам.НаименованиеКомплекта;
				Если ВыборкаПоТоварам.ОперацияКомплекта = ВидДвиженияНакопления.Приход Тогда
					Область.Параметры.ВидОперации = "товар оприходован";
				Иначе
					Область.Параметры.ВидОперации = "товар списан";
				Конецесли;
			    Область.Параметры.Заполнить(ВыборкаПоТоварам);
				ТабличныйДокумент.Вывести(Область);
			КонецЕсли;
			
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы");
			Если ВыборкаПоТоварам.ОперацияНоменклатуры = ВидДвиженияНакопления.Приход Тогда
				Область.Параметры.ВидОперации = "товар оприходован";
			Иначе
				Область.Параметры.ВидОперации = "товар списан";
			Конецесли;
            Область.Параметры.Заполнить(ВыборкаПоТоварам);
			Область.Параметры.НаименованиеНоменклатуры = "       "+СокрЛП(ВыборкаПоТоварам.НаименованиеНоменклатуры);    //отступ для "иерархии"
			ТабличныйДокумент.Вывести(Область);
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		Область.Параметры.Ответственный = ФизическиеЛица.ФамилияИнициалыФизЛица(ДанныеПечати.ПредставлениеОтветственного);
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//списание
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.КомплектацияНоменклатуры";
	КомандаПечати.Идентификатор = "Комплектация";
	КомандаПечати.Представление = НСтр("ru = 'Комплектация номенклатуры'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры


#КонецЕсли