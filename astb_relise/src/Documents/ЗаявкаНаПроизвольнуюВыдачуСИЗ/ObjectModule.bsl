#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроведениеСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект,Отказ,"Товары");	
	
	ПроверитьДублиВСтроках(Отказ);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(СоздательДокумента) Тогда
	    СоздательДокумента = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
	    Ответственный = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьДублиВСтроках(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокумента.НоменклатураНормы,
	|	ТаблицаДокумента.Номенклатура,
	|	ТаблицаДокумента.ХарактеристикаНоменклатуры,
	|	ТаблицаДокумента.НомерСтроки
	|ПОМЕСТИТЬ ВТ_ТаблицаДокумента
	|ИЗ
	|	&ТаблицаДокумента КАК ТаблицаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаДокумента.НоменклатураНормы,
	|	ВТ_ТаблицаДокумента.Номенклатура,
	|	ВТ_ТаблицаДокумента.ХарактеристикаНоменклатуры,
	|	1 КАК КоличествоДублей,
	|	ВТ_ТаблицаДокумента.НомерСтроки
	|ПОМЕСТИТЬ ВТ_ТаблицаДляПоискаДублей
	|ИЗ
	|	ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаДляПоискаДублей.НоменклатураНормы,
	|	СУММА(ВТ_ТаблицаДляПоискаДублей.КоличествоДублей) КАК КоличествоДублей,
	|	МАКСИМУМ(ВТ_ТаблицаДляПоискаДублей.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ВТ_ДублиНоменклатурыНормы
	|ИЗ
	|	ВТ_ТаблицаДляПоискаДублей КАК ВТ_ТаблицаДляПоискаДублей
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаДляПоискаДублей.НоменклатураНормы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаДляПоискаДублей.Номенклатура,
	|	СУММА(ВТ_ТаблицаДляПоискаДублей.КоличествоДублей) КАК КоличествоДублей,
	|	МАКСИМУМ(ВТ_ТаблицаДляПоискаДублей.НомерСтроки) КАК НомерСтроки,
	|	ВТ_ТаблицаДляПоискаДублей.ХарактеристикаНоменклатуры
	|ПОМЕСТИТЬ ВТ_ДублиНоменклатуры
	|ИЗ
	|	ВТ_ТаблицаДляПоискаДублей КАК ВТ_ТаблицаДляПоискаДублей
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаДляПоискаДублей.Номенклатура,
	|	ВТ_ТаблицаДляПоискаДублей.ХарактеристикаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДублиНоменклатурыНормы.НоменклатураНормы,
	|	ВТ_ДублиНоменклатурыНормы.КоличествоДублей,
	|	ВТ_ДублиНоменклатурыНормы.НомерСтроки
	|ИЗ
	|	ВТ_ДублиНоменклатурыНормы КАК ВТ_ДублиНоменклатурыНормы
	|ГДЕ
	|	НЕ ВТ_ДублиНоменклатурыНормы.НоменклатураНормы = ЗНАЧЕНИЕ(Справочник.НоменклатураНормОрганизации.ПустаяСсылка)
	|	И ВТ_ДублиНоменклатурыНормы.КоличествоДублей > 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДублиНоменклатуры.Номенклатура,
	|	ВТ_ДублиНоменклатуры.ХарактеристикаНоменклатуры,
	|	ВТ_ДублиНоменклатуры.КоличествоДублей,
	|	ВТ_ДублиНоменклатуры.НомерСтроки
	|ИЗ
	|	ВТ_ДублиНоменклатуры КАК ВТ_ДублиНоменклатуры
	|ГДЕ
	|	НЕ ВТ_ДублиНоменклатуры.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ВТ_ДублиНоменклатуры.КоличествоДублей > 1";
	
	Запрос.УстановитьПараметр("ТаблицаДокумента",ЭтотОбъект.Товары.Выгрузить());
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаДублейНоменклатурыНормы 			= Результат[4].Выгрузить();
	ТаблицаДублейНоменклатуры 				= Результат[5].Выгрузить();
	
	КлючДанных = ПроцедурыРаботыСНормамиСервер.КлючДанныхДляСообщенияПользователю(ЭтотОбъект);
	
	Если НЕ ТаблицаДублейНоменклатурыНормы.Количество() = 0 Тогда
		
		Для Каждого СтрокаТаблицыДублейНоменклатурыНормы Из ТаблицаДублейНоменклатурыНормы Цикл
			
			ШаблонСообщения = НСтр("ru='Найдены дубли номенклатуры нормы ""%НоменклатураНормы%"" в строке %НомерСтроки%.'");
		
			ТекстСообщения = СтрЗаменить(ШаблонСообщения, 	"%НоменклатураНормы%", 	СтрокаТаблицыДублейНоменклатурыНормы.НоменклатураНормы.Наименование);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, 	"%НомерСтроки%", 		СтрокаТаблицыДублейНоменклатурыНормы.НомерСтроки);
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаТаблицыДублейНоменклатурыНормы.НомерСтроки, "НоменклатураНормы");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,КлючДанных,Поле,"Объект",Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если НЕ ТаблицаДублейНоменклатуры.Количество() = 0 Тогда
		
		Для Каждого СтрокаТаблицыДублейНоменклатуры Из ТаблицаДублейНоменклатуры Цикл
			
			ШаблонСообщения = НСтр("ru='Найдены дубли номенклатуры ""%Номенклатура%"" и характеристики ""%ХарактеристикаНоменклатуры%"" в строке %НомерСтроки%.'");
		
			ТекстСообщения = СтрЗаменить(ШаблонСообщения, 	"%Номенклатура%", 				СтрокаТаблицыДублейНоменклатуры.Номенклатура.Наименование);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, 	"%ХарактеристикаНоменклатуры%", СтрокаТаблицыДублейНоменклатуры.ХарактеристикаНоменклатуры.Наименование);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, 	"%НомерСтроки%", 				СтрокаТаблицыДублейНоменклатуры.НомерСтроки);
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", СтрокаТаблицыДублейНоменклатуры.НомерСтроки, "Номенклатура");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,КлючДанных,Поле,"Объект",Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	СуммаДокумента = Товары.Итог("Сумма");
	
КонецПроцедуры
