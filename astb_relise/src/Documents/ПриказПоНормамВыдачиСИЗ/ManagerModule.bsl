#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ЗаполнитьНормыВыдачиСИЗ(Объект, МассивПодразделений = Неопределено, МассивДолжностей = Неопределено, ПерсональнаяВыдача = Истина, КоллективнаяВыдача = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ИзмененныеНормы.Подразделение, ОтсутствующиеНормы.Подразделение) КАК Подразделение,
	|	ЕСТЬNULL(ИзмененныеНормы.Должность, ОтсутствующиеНормы.Должность) КАК Должность,
	|	ЕСТЬNULL(ИзмененныеНормы.РабочееМесто, ОтсутствующиеНормы.РабочееМесто) КАК РабочееМесто,
	|	ЕСТЬNULL(ИзмененныеНормы.УсловиеНормы, ОтсутствующиеНормы.УсловиеНормы) КАК УсловиеНормы,
	|	ЕСТЬNULL(ИзмененныеНормы.НормаВыдачи, ОтсутствующиеНормы.НормаВыдачи) КАК НормаВыдачи,
	|	ЕСТЬNULL(ИзмененныеНормы.Использовать, ОтсутствующиеНормы.Использовать) КАК Использовать
	|ИЗ
	|	(ВЫБРАТЬ
	|		ИсходныеНормы.Подразделение КАК Подразделение,
	|		ИсходныеНормы.Должность КАК Должность,
	|		ИсходныеНормы.РабочееМесто КАК РабочееМесто,
	|		ИсходныеНормы.УсловиеНормы КАК УсловиеНормы,
	|		ИсходныеНормы.НормаВыдачи КАК НормаВыдачи,
	|		ИсходныеНормы.Использовать КАК Использовать
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ИсходныеНормыВыдачиСИЗ.Подразделение КАК Подразделение,
	|			ИсходныеНормыВыдачиСИЗ.Должность КАК Должность,
	|			ИсходныеНормыВыдачиСИЗ.РабочееМесто КАК РабочееМесто,
	|			ИсходныеНормыВыдачиСИЗ.УсловиеНормы КАК УсловиеНормы,
	|			ИсходныеНормыВыдачиСИЗ.НормаВыдачи КАК НормаВыдачи,
	|			ИсходныеНормыВыдачиСИЗ.Использовать КАК Использовать
	|		ИЗ
	|			РегистрСведений.ИсходныеНормыВыдачиСИЗ КАК ИсходныеНормыВыдачиСИЗ
	|		ГДЕ
	|			ИсходныеНормыВыдачиСИЗ.Организация = &Организация
	|			И (&НеФильтроватьПоПодразделениям
	|					ИЛИ ИсходныеНормыВыдачиСИЗ.Подразделение В (&МассивПодразделений))
	|			И (&НеФильтроватьПоДолжностям
	|					ИЛИ ИсходныеНормыВыдачиСИЗ.Должность В (&МассивДолжностей))
	|			И ВЫБОР
	|					КОГДА &ПерсональнаяВыдача
	|						ТОГДА ВЫБОР
	|								КОГДА &КоллективнаяВыдача
	|									ТОГДА ИСТИНА
	|								ИНАЧЕ ИсходныеНормыВыдачиСИЗ.НормаВыдачи.ВидВыдачиСИЗ = ЗНАЧЕНИЕ(Перечисление.ВидыВыдачиСИЗ.ПерсональнаяВыдача)
	|							КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|							КОГДА &КоллективнаяВыдача
	|								ТОГДА ИсходныеНормыВыдачиСИЗ.НормаВыдачи.ВидВыдачиСИЗ = ЗНАЧЕНИЕ(Перечисление.ВидыВыдачиСИЗ.КоллективнаяВыдача)
	|							ИНАЧЕ ЛОЖЬ
	|						КОНЕЦ
	|				КОНЕЦ) КАК ИсходныеНормы
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УстановленныеНормыВыдачиСИЗ.СрезПоследних(
	|					&Период,
	|					Организация = &Организация
	|						И (&НеФильтроватьПоПодразделениям
	|							ИЛИ Подразделение В (&МассивПодразделений))
	|						И (&НеФильтроватьПоДолжностям
	|							ИЛИ Должность В (&МассивДолжностей))
	|						И ВЫБОР
	|							КОГДА &ПерсональнаяВыдача
	|								ТОГДА ВЫБОР
	|										КОГДА &КоллективнаяВыдача
	|											ТОГДА ИСТИНА
	|										ИНАЧЕ НормаВыдачи.ВидВыдачиСИЗ = ЗНАЧЕНИЕ(Перечисление.ВидыВыдачиСИЗ.ПерсональнаяВыдача)
	|									КОНЕЦ
	|							ИНАЧЕ ВЫБОР
	|									КОГДА &КоллективнаяВыдача
	|										ТОГДА НормаВыдачи.ВидВыдачиСИЗ = ЗНАЧЕНИЕ(Перечисление.ВидыВыдачиСИЗ.КоллективнаяВыдача)
	|									ИНАЧЕ ЛОЖЬ
	|								КОНЕЦ
	|						КОНЕЦ) КАК УстановленныеНормыВыдачиСИЗСрезПоследних
	|			ПО ИсходныеНормы.Подразделение = УстановленныеНормыВыдачиСИЗСрезПоследних.Подразделение
	|				И ИсходныеНормы.Должность = УстановленныеНормыВыдачиСИЗСрезПоследних.Должность
	|				И ИсходныеНормы.РабочееМесто = УстановленныеНормыВыдачиСИЗСрезПоследних.РабочееМесто
	|				И ИсходныеНормы.УсловиеНормы = УстановленныеНормыВыдачиСИЗСрезПоследних.УсловиеНормы
	|				И ИсходныеНормы.НормаВыдачи = УстановленныеНормыВыдачиСИЗСрезПоследних.НормаВыдачи
	|	ГДЕ
	|		ВЫБОР
	|				КОГДА УстановленныеНормыВыдачиСИЗСрезПоследних.Использовать ЕСТЬ NULL
	|					ТОГДА ВЫБОР
	|							КОГДА ИсходныеНормы.Использовать
	|								ТОГДА ИСТИНА
	|							ИНАЧЕ ЛОЖЬ
	|						КОНЕЦ
	|				ИНАЧЕ УстановленныеНормыВыдачиСИЗСрезПоследних.Использовать <> ИсходныеНормы.Использовать
	|			КОНЕЦ) КАК ИзмененныеНормы
	|		ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			УстановленныеНормыВыдачиСИЗСрезПоследних.Подразделение КАК Подразделение,
	|			УстановленныеНормыВыдачиСИЗСрезПоследних.Должность КАК Должность,
	|			УстановленныеНормыВыдачиСИЗСрезПоследних.РабочееМесто КАК РабочееМесто,
	|			УстановленныеНормыВыдачиСИЗСрезПоследних.УсловиеНормы КАК УсловиеНормы,
	|			УстановленныеНормыВыдачиСИЗСрезПоследних.НормаВыдачи КАК НормаВыдачи,
	|			ЛОЖЬ КАК Использовать
	|		ИЗ
	|			РегистрСведений.УстановленныеНормыВыдачиСИЗ.СрезПоследних(
	|					&Период,
	|					Организация = &Организация
	|						И (&НеФильтроватьПоПодразделениям
	|							ИЛИ Подразделение В (&МассивПодразделений))
	|						И (&НеФильтроватьПоДолжностям
	|							ИЛИ Должность В (&МассивДолжностей))
	|						И ВЫБОР
	|							КОГДА &ПерсональнаяВыдача
	|								ТОГДА ВЫБОР
	|										КОГДА &КоллективнаяВыдача
	|											ТОГДА ИСТИНА
	|										ИНАЧЕ НормаВыдачи.ВидВыдачиСИЗ = ЗНАЧЕНИЕ(Перечисление.ВидыВыдачиСИЗ.ПерсональнаяВыдача)
	|									КОНЕЦ
	|							ИНАЧЕ ВЫБОР
	|									КОГДА &КоллективнаяВыдача
	|										ТОГДА НормаВыдачи.ВидВыдачиСИЗ = ЗНАЧЕНИЕ(Перечисление.ВидыВыдачиСИЗ.КоллективнаяВыдача)
	|									ИНАЧЕ ЛОЖЬ
	|								КОНЕЦ
	|						КОНЕЦ) КАК УстановленныеНормыВыдачиСИЗСрезПоследних
	|				ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|					ИсходныеНормыВыдачиСИЗ.Подразделение КАК Подразделение,
	|					ИсходныеНормыВыдачиСИЗ.Должность КАК Должность,
	|					ИсходныеНормыВыдачиСИЗ.РабочееМесто КАК РабочееМесто,
	|					ИсходныеНормыВыдачиСИЗ.УсловиеНормы КАК УсловиеНормы,
	|					ИсходныеНормыВыдачиСИЗ.НормаВыдачи КАК НормаВыдачи,
	|					ИсходныеНормыВыдачиСИЗ.Использовать КАК Использовать
	|				ИЗ
	|					РегистрСведений.ИсходныеНормыВыдачиСИЗ КАК ИсходныеНормыВыдачиСИЗ
	|				ГДЕ
	|					ИсходныеНормыВыдачиСИЗ.Организация = &Организация
	|					И (&НеФильтроватьПоПодразделениям
	|							ИЛИ ИсходныеНормыВыдачиСИЗ.Подразделение В (&МассивПодразделений))
	|					И (&НеФильтроватьПоДолжностям
	|							ИЛИ ИсходныеНормыВыдачиСИЗ.Должность В (&МассивДолжностей))
	|					И ВЫБОР
	|							КОГДА &ПерсональнаяВыдача
	|								ТОГДА ВЫБОР
	|										КОГДА &КоллективнаяВыдача
	|											ТОГДА ИСТИНА
	|										ИНАЧЕ ИсходныеНормыВыдачиСИЗ.НормаВыдачи.ВидВыдачиСИЗ = ЗНАЧЕНИЕ(Перечисление.ВидыВыдачиСИЗ.ПерсональнаяВыдача)
	|									КОНЕЦ
	|							ИНАЧЕ ВЫБОР
	|									КОГДА &КоллективнаяВыдача
	|										ТОГДА ИсходныеНормыВыдачиСИЗ.НормаВыдачи.ВидВыдачиСИЗ = ЗНАЧЕНИЕ(Перечисление.ВидыВыдачиСИЗ.КоллективнаяВыдача)
	|									ИНАЧЕ ЛОЖЬ
	|								КОНЕЦ
	|						КОНЕЦ) КАК ИсходныеНормы
	|				ПО (ИсходныеНормы.Подразделение = УстановленныеНормыВыдачиСИЗСрезПоследних.Подразделение)
	|					И (ИсходныеНормы.Должность = УстановленныеНормыВыдачиСИЗСрезПоследних.Должность)
	|					И (ИсходныеНормы.РабочееМесто = УстановленныеНормыВыдачиСИЗСрезПоследних.РабочееМесто)
	|					И (ИсходныеНормы.УсловиеНормы = УстановленныеНормыВыдачиСИЗСрезПоследних.УсловиеНормы)
	|					И (ИсходныеНормы.НормаВыдачи = УстановленныеНормыВыдачиСИЗСрезПоследних.НормаВыдачи)
	|		ГДЕ
	|			ВЫБОР
	|					КОГДА ИсходныеНормы.Использовать ЕСТЬ NULL
	|						ТОГДА УстановленныеНормыВыдачиСИЗСрезПоследних.Использовать
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ) КАК ОтсутствующиеНормы
	|		ПО ИзмененныеНормы.Подразделение = ОтсутствующиеНормы.Подразделение
	|			И ИзмененныеНормы.Должность = ОтсутствующиеНормы.Должность
	|			И ИзмененныеНормы.РабочееМесто = ОтсутствующиеНормы.РабочееМесто
	|			И ИзмененныеНормы.УсловиеНормы = ОтсутствующиеНормы.УсловиеНормы
	|			И ИзмененныеНормы.НормаВыдачи = ОтсутствующиеНормы.НормаВыдачи";
	
	Запрос.УстановитьПараметр("Период",							КонецДня(Объект.Дата) + 1);
	Запрос.УстановитьПараметр("Организация",					Объект.Организация);
	Запрос.УстановитьПараметр("МассивПодразделений",			МассивПодразделений);
	Запрос.УстановитьПараметр("МассивДолжностей",				МассивДолжностей);
	Запрос.УстановитьПараметр("НеФильтроватьПоПодразделениям",  МассивПодразделений = Неопределено);
	Запрос.УстановитьПараметр("НеФильтроватьПоДолжностям",		МассивДолжностей = Неопределено);
	Запрос.УстановитьПараметр("ПерсональнаяВыдача",  			ПерсональнаяВыдача);
	Запрос.УстановитьПараметр("КоллективнаяВыдача",				КоллективнаяВыдача);
	
	Объект.НормыВыдачиСИЗ.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Ссылка.Дата КАК Период,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Подразделение,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Должность,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.РабочееМесто,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.УсловиеНормы,
	|	МАКСИМУМ(ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Использовать) КАК Использовать
	|ИЗ
	|	Документ.ПриказПоНормамВыдачиСИЗ.НормыВыдачиСИЗ КАК ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ
	|ГДЕ
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Подразделение,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Должность,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.РабочееМесто,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.УсловиеНормы,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Ссылка.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗанятыеРабочиеМестаОстатки.Подразделение,
	|	ЗанятыеРабочиеМестаОстатки.Должность,
	|	ЗанятыеРабочиеМестаОстатки.РабочееМесто,
	|	ЗанятыеРабочиеМестаОстатки.Сотрудник
	|ИЗ
	|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(&ПериодРасчета, Организация = &Организация) КАК ЗанятыеРабочиеМестаОстатки";
	
	Запрос.УстановитьПараметр("Ссылка",			ДокументСсылка);
	Запрос.УстановитьПараметр("ПериодРасчета",	ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ДокументСсылка));
	Запрос.УстановитьПараметр("Организация",	ДокументСсылка.Организация);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаУсловийРаботы = ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуУсловийРаботыСотрудников(Результат[0].Выгрузить(),Результат[1].Выгрузить());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Ссылка.Дата КАК Период,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Ссылка.Организация КАК Организация,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Подразделение КАК Подразделение,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Должность КАК Должность,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.РабочееМесто КАК РабочееМесто,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.УсловиеНормы КАК УсловиеНормы,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.НормаВыдачи КАК НормаВыдачи,
	|	МАКСИМУМ(ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Использовать) КАК Использовать
	|ПОМЕСТИТЬ ВТ_ТаблицаДокумента
	|ИЗ
	|	Документ.ПриказПоНормамВыдачиСИЗ.НормыВыдачиСИЗ КАК ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ
	|ГДЕ
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Ссылка.Дата,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Ссылка.Организация,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Подразделение,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.Должность,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.РабочееМесто,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.УсловиеНормы,
	|	ПриказПоНормамВыдачиСИЗНормыВыдачиСИЗ.НормаВыдачи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДоступныеУсловияРаботыСотрудникаСрезПоследних.Сотрудник КАК Сотрудник,
	|	ДоступныеУсловияРаботыСотрудникаСрезПоследних.Подразделение КАК Подразделение,
	|	ДоступныеУсловияРаботыСотрудникаСрезПоследних.Должность КАК Должность,
	|	ДоступныеУсловияРаботыСотрудникаСрезПоследних.РабочееМесто КАК РабочееМесто,
	|	ДоступныеУсловияРаботыСотрудникаСрезПоследних.Условие КАК Условие,
	|	ДоступныеУсловияРаботыСотрудникаСрезПоследних.Использовать КАК Использовать
	|ПОМЕСТИТЬ ВТ_ДоступныеУсловияРаботы
	|ИЗ
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(ДоступныеУсловияРаботыСотрудникаСрезПоследних.Период) КАК Период,
	|		ДоступныеУсловияРаботыСотрудникаСрезПоследних.Сотрудник КАК Сотрудник,
	|		ДоступныеУсловияРаботыСотрудникаСрезПоследних.Подразделение КАК Подразделение,
	|		ДоступныеУсловияРаботыСотрудникаСрезПоследних.Должность КАК Должность,
	|		ДоступныеУсловияРаботыСотрудникаСрезПоследних.РабочееМесто КАК РабочееМесто,
	|		ДоступныеУсловияРаботыСотрудникаСрезПоследних.Условие КАК Условие
	|	ИЗ
	|		РегистрСведений.ДоступныеУсловияРаботыСотрудника.СрезПоследних(&ПериодРасчета, ) КАК ДоступныеУсловияРаботыСотрудникаСрезПоследних
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДоступныеУсловияРаботыСотрудникаСрезПоследних.Сотрудник,
	|		ДоступныеУсловияРаботыСотрудникаСрезПоследних.Подразделение,
	|		ДоступныеУсловияРаботыСотрудникаСрезПоследних.Должность,
	|		ДоступныеУсловияРаботыСотрудникаСрезПоследних.РабочееМесто,
	|		ДоступныеУсловияРаботыСотрудникаСрезПоследних.Условие) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДоступныеУсловияРаботыСотрудника.СрезПоследних(&ПериодРасчета, ) КАК ДоступныеУсловияРаботыСотрудникаСрезПоследних
	|		ПО ВложенныйЗапрос.Период = ДоступныеУсловияРаботыСотрудникаСрезПоследних.Период
	|			И ВложенныйЗапрос.Сотрудник = ДоступныеУсловияРаботыСотрудникаСрезПоследних.Сотрудник
	|			И ВложенныйЗапрос.Подразделение = ДоступныеУсловияРаботыСотрудникаСрезПоследних.Подразделение
	|			И ВложенныйЗапрос.Должность = ДоступныеУсловияРаботыСотрудникаСрезПоследних.Должность
	|			И ВложенныйЗапрос.РабочееМесто = ДоступныеУсловияРаботыСотрудникаСрезПоследних.РабочееМесто
	|			И ВложенныйЗапрос.Условие = ДоступныеУсловияРаботыСотрудникаСрезПоследних.Условие
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаУсловийРаботы.Сотрудник КАК Сотрудник,
	|	ТаблицаУсловийРаботы.Подразделение КАК Подразделение,
	|	ТаблицаУсловийРаботы.Должность КАК Должность,
	|	ТаблицаУсловийРаботы.РабочееМесто КАК РабочееМесто,
	|	ТаблицаУсловийРаботы.Условие КАК Условие,
	|	ТаблицаУсловийРаботы.Использовать КАК Использовать,
	|	ТаблицаУсловийРаботы.Период КАК Период
	|ПОМЕСТИТЬ ВТ_ТаблицаУсловийРаботы
	|ИЗ
	|	&ТаблицаУсловийРаботы КАК ТаблицаУсловийРаботы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаДокумента.Период КАК Период,
	|	ВТ_ТаблицаДокумента.Организация КАК Организация,
	|	ВТ_ТаблицаДокумента.Подразделение КАК Подразделение,
	|	ВТ_ТаблицаДокумента.Должность КАК Должность,
	|	ВТ_ТаблицаДокумента.РабочееМесто КАК РабочееМесто,
	|	ВТ_ТаблицаДокумента.УсловиеНормы КАК УсловиеНормы,
	|	ВТ_ТаблицаДокумента.НормаВыдачи КАК НормаВыдачи,
	|	ВТ_ТаблицаДокумента.Использовать КАК Использовать
	|ИЗ
	|	ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ТаблицаУсловийРаботы.Период КАК Период,
	|	ВТ_ТаблицаУсловийРаботы.Сотрудник КАК Сотрудник,
	|	ВТ_ТаблицаУсловийРаботы.Подразделение КАК Подразделение,
	|	ВТ_ТаблицаУсловийРаботы.Должность КАК Должность,
	|	ВТ_ТаблицаУсловийРаботы.РабочееМесто КАК РабочееМесто,
	|	ВТ_ТаблицаУсловийРаботы.Условие КАК Условие,
	|	ВТ_ТаблицаУсловийРаботы.Использовать КАК Использовать
	|ИЗ
	|	ВТ_ТаблицаУсловийРаботы КАК ВТ_ТаблицаУсловийРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДоступныеУсловияРаботы КАК ВТ_ДоступныеУсловияРаботы
	|		ПО ВТ_ТаблицаУсловийРаботы.Должность = ВТ_ДоступныеУсловияРаботы.Должность
	|			И ВТ_ТаблицаУсловийРаботы.Сотрудник = ВТ_ДоступныеУсловияРаботы.Сотрудник
	|			И ВТ_ТаблицаУсловийРаботы.РабочееМесто = ВТ_ДоступныеУсловияРаботы.РабочееМесто
	|			И ВТ_ТаблицаУсловийРаботы.Подразделение = ВТ_ДоступныеУсловияРаботы.Подразделение
	|			И ВТ_ТаблицаУсловийРаботы.Условие = ВТ_ДоступныеУсловияРаботы.Условие
	|ГДЕ
	|	ВТ_ДоступныеУсловияРаботы.Условие ЕСТЬ NULL
	|	И ВТ_ТаблицаУсловийРаботы.Использовать";
	
	Запрос.УстановитьПараметр("Ссылка",					ДокументСсылка);
	Запрос.УстановитьПараметр("ПериодРасчета",			ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ДокументСсылка));
	Запрос.УстановитьПараметр("Организация",			ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("ТаблицаУсловийРаботы",	ТаблицаУсловийРаботы);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	ДвиженияПоДоступнымУсловиямРаботы = Результат[4].Выгрузить();
	
	//по регистру "УстановленныеНормыВыдачиСИЗ"
	ТаблицыДляДвижений.Вставить("ТаблицаУстановленныеНормыВыдачиСИЗ", Результат[3].Выгрузить());
	
	//по регистру "Доступные условия работы сотрудника"
	ТаблицыДляДвижений.Вставить("ТаблицаДоступныеУсловияРаботыСотрудника", ДвиженияПоДоступнымУсловиямРаботы);
	
	//готовим пустую таблицу движений по регистру "Выдача по потребности"
	ТаблицаВыдачаПоПотребности = ДокументСсылка.ПолучитьОбъект().Движения.ВыдачаПоПотребности.Выгрузить().Скопировать(,"Период,ВидДвижения,Организация,Сотрудник,НормаВыдачи,НоменклатураНормы,ДатаПотребности,ДатаВыдачи,ПроцентИзноса,Количество");
	
	//по регистру "ПотребностьВыдачиСИЗ"
	ТаблицыДляДвижений.Вставить("ТаблицаПотребностьВыдачиСИЗ", ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуДвиженийПоПотребности(ДокументСсылка,ДвиженияПоДоступнымУсловиямРаботы,ТаблицаВыдачаПоПотребности));
	
	//по регистру "ВыдачаПоПотребности"
	ТаблицыДляДвижений.Вставить("ТаблицаВыдачаПоПотребности", ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуДвиженийВыдачиПоПотребности(ДокументСсылка,ТаблицыДляДвижений.ТаблицаПотребностьВыдачиСИЗ,,ТаблицаВыдачаПоПотребности));
	
КонецПроцедуры

#КонецЕсли