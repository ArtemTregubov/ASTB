#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//+++АСТБ_Горюшин_Алексей_20659
	Если НЕ (Организация.ИспользоватьАлгоритм_0_0_0_3
		ИЛИ Организация.ИспользоватьАлгоритм_0_0_0_4
		ИЛИ Организация.ИспользоватьАлгоритм_0_0_1_2
		ИЛИ Организация.ИспользоватьАлгоритм_1_0_1_1)
		Тогда
		
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Создание документа невозможно! Не выбран алгоритм формирования потребности в организации.");
		
	КонецЕсли;
	//---АСТБ_Горюшин_Алексей_20659

	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	УстановитьПривилегированныйРежим(Истина);	
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, Режим);
	Документы.ПриказПоНормамВыдачиСИЗ.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ЗапасыСерверПереопределяемый.ОтразитьУстановленныеНормыВыдачиСИЗ(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСерверПереопределяемый.ОтразитьДоступныеУсловияРаботыСотрудника(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСерверПереопределяемый.ОтразитьПотребностьВыдачиСИЗ(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСерверПереопределяемый.ОтразитьВыдачуПоПотребности(ДополнительныеСвойства, Движения, Отказ);
	
	СформироватьСписокРегистровДляКонтроля();
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура СформироватьСписокРегистровДляКонтроля()

	Массив = Новый Массив;
	//Контроль выполняется при проведении\отмене проведения не нового документа.
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Массив.Добавить(Движения.ПотребностьВыдачиСИЗ);
		Массив.Добавить(Движения.ВыдачаПоПотребности);
	КонецЕсли;

	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроведениеСервер.ПроверитьНормы(ЭтотОбъект,Отказ,"НормыВыдачиСИЗ");
	
КонецПроцедуры

//+++АСТБ_Горюшин_Алексей_19737
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗачетВыданныхСредствЗащиты") Тогда
		
		Организация 		= ДанныеЗаполнения.Организация;
		Комментарий 		= "Сформирован из документа " + ДанныеЗаполнения;
		ДокументОснование 	= ДанныеЗаполнения;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ЗачетВыданныхСредствЗащитыТовары.Сотрудник КАК Сотрудник
			|ПОМЕСТИТЬ ВТ_Сотрудники
			|ИЗ
			|	Документ.ЗачетВыданныхСредствЗащиты.Товары КАК ЗачетВыданныхСредствЗащитыТовары
			|ГДЕ
			|	ЗачетВыданныхСредствЗащитыТовары.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ЗачетВыданныхСредствЗащитыТовары.НормаВыдачи КАК НормаВыдачи
			|ПОМЕСТИТЬ ВТ_НормыВыдачи
			|ИЗ
			|	Документ.ЗачетВыданныхСредствЗащиты.Товары КАК ЗачетВыданныхСредствЗащитыТовары
			|ГДЕ
			|	ЗачетВыданныхСредствЗащитыТовары.Ссылка = &Ссылка
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЗачетВыданныхСредствЗащиты.НормаВыдачи
			|ИЗ
			|	Документ.ЗачетВыданныхСредствЗащиты КАК ЗачетВыданныхСредствЗащиты
			|ГДЕ
			|	ЗачетВыданныхСредствЗащиты.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЗанятыеРабочиеМестаОстатки.Организация КАК Организация,
			|	ЗанятыеРабочиеМестаОстатки.Подразделение КАК Подразделение,
			|	ЗанятыеРабочиеМестаОстатки.Должность КАК Должность,
			|	ЗанятыеРабочиеМестаОстатки.РабочееМесто КАК РабочееМесто
			|ПОМЕСТИТЬ ВТ_ЗанятыеРабочиеМестаОстатки
			|ИЗ
			|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
			|			,
			|			Организация = &Организация
			|				И Сотрудник В
			|					(ВЫБРАТЬ
			|						ВТ_Сотрудники.Сотрудник
			|					ИЗ
			|						ВТ_Сотрудники)) КАК ЗанятыеРабочиеМестаОстатки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	УстановленныеНормыВыдачиСИЗСрезПоследних.Организация КАК Организация,
			|	УстановленныеНормыВыдачиСИЗСрезПоследних.Подразделение КАК Подразделение,
			|	УстановленныеНормыВыдачиСИЗСрезПоследних.Должность КАК Должность,
			|	УстановленныеНормыВыдачиСИЗСрезПоследних.РабочееМесто КАК РабочееМесто,
			|	УстановленныеНормыВыдачиСИЗСрезПоследних.УсловиеНормы КАК УсловиеНормы,
			|	УстановленныеНормыВыдачиСИЗСрезПоследних.НормаВыдачи КАК НормаВыдачи
			|ИЗ
			|	ВТ_ЗанятыеРабочиеМестаОстатки КАК ВТ_ЗанятыеРабочиеМестаОстатки
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УстановленныеНормыВыдачиСИЗ.СрезПоследних(
			|				,
			|				Организация = &Организация
			|					И Подразделение В
			|						(ВЫБРАТЬ
			|							ВТ_ЗанятыеРабочиеМестаОстатки.Подразделение
			|						ИЗ
			|							ВТ_ЗанятыеРабочиеМестаОстатки)
			|					И Должность В
			|						(ВЫБРАТЬ
			|							ВТ_ЗанятыеРабочиеМестаОстатки.Должность
			|						ИЗ
			|							ВТ_ЗанятыеРабочиеМестаОстатки)
			|					И РабочееМесто В
			|						(ВЫБРАТЬ
			|							ВТ_ЗанятыеРабочиеМестаОстатки.РабочееМесто
			|						ИЗ
			|							ВТ_ЗанятыеРабочиеМестаОстатки)) КАК УстановленныеНормыВыдачиСИЗСрезПоследних
			|		ПО (УстановленныеНормыВыдачиСИЗСрезПоследних.Подразделение = ВТ_ЗанятыеРабочиеМестаОстатки.Подразделение)
			|			И (УстановленныеНормыВыдачиСИЗСрезПоследних.Должность = ВТ_ЗанятыеРабочиеМестаОстатки.Должность)
			|			И (УстановленныеНормыВыдачиСИЗСрезПоследних.РабочееМесто = ВТ_ЗанятыеРабочиеМестаОстатки.РабочееМесто)
			|ГДЕ
			|	УстановленныеНормыВыдачиСИЗСрезПоследних.Использовать = ИСТИНА
			|	И УстановленныеНормыВыдачиСИЗСрезПоследних.НормаВыдачи В
			|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|				ВТ_НормыВыдачи.НормаВыдачи
			|			ИЗ
			|				ВТ_НормыВыдачи)";
		
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			 НоваяСтрока = НормыВыдачиСИЗ.Добавить();
			 ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
			 НоваяСтрока.Использовать = Истина;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры //---АСТБ_Горюшин_Алексей_19737

//+++АСТБ_Горюшин_Алексей_19737
Процедура ПриКопировании(ОбъектКопирования)
	ДокументОснование = Неопределено;
	Комментарий = "";
КонецПроцедуры //---АСТБ_Горюшин_Алексей_19737

#КонецЕсли