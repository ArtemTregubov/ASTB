////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Свойства
 	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект,НОВЫЙ Структура("ИмяЭлементаДляРазмещения","ГруппаДополнительныеРеквизиты"));
	// Конец СтандартныеПодсистемы.Свойства
	
	ЭтаФорма.УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация", Объект.Организация));

	ЭтаФорма.Элементы.НормыВыдачиСИЗ.КоманднаяПанель.ПодчиненныеЭлементы.НормыВыдачиЗаполнить.Доступность = (НЕ Объект.Проведен);
	
	ЭтаФорма.ТолькоПросмотр = Объект.Проведен;
	Если Объект.Проведен Тогда
		ЭтаФорма.Элементы.ПолеТекстПредупреждения.Заголовок = "Проведенный документ доступен только на просмотр";
		ЭтаФорма.Элементы.ГруппаПредупреждение.Видимость = Истина;
	Иначе	
		ЭтаФорма.Элементы.ГруппаПредупреждение.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтаФорма, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
 	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЭтаФорма.Элементы.НормыВыдачиСИЗ.КоманднаяПанель.ПодчиненныеЭлементы.НормыВыдачиЗаполнить.Доступность = (НЕ Объект.Проведен);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтаФорма, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборПодразделений.Форма.Форма" Тогда
		
		ЗаполнитьПоПодразделениямНаСервере(ВыбранноеЗначение.АдресРезультатаПодбораВХранилище);
		
	КонецЕсли;
	
	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборДолжностей.Форма.Форма" Тогда
		
		ЗаполнитьПоДолжностямНаСервере(ВыбранноеЗначение.АдресРезультатаПодбораВХранилище);
		
	КонецЕсли;
	
	Если Окно <> Неопределено Тогда
		Окно.Активизировать();
	КонецЕсли;

	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЭтаФорма.УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация", Объект.Организация));

КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	ЗаполнитьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Документы.ПриказПоНормамВыдачиСИЗ.ЗаполнитьНормыВыдачиСИЗ(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоПодразделениям(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Документ",							Объект.Ссылка);
	ПараметрыФормы.Вставить("Организация",						Объект.Организация);
	
	ОткрытьФорму("Обработка.ПодборПодразделений.Форма.Форма",ПараметрыФормы,ЭтаФорма,УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоПодразделениямНаСервере(АдресРезультатаПодбораВХранилище)
	
	МассивПодразделений = ПолучитьИзВременногоХранилища(АдресРезультатаПодбораВХранилище);
	
	Документы.ПриказПоНормамВыдачиСИЗ.ЗаполнитьНормыВыдачиСИЗ(Объект,МассивПодразделений);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДолжностям(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Документ",							Объект.Ссылка);
	ПараметрыФормы.Вставить("Организация",						Объект.Организация);
	
	ОткрытьФорму("Обработка.ПодборДолжностей.Форма.Форма",ПараметрыФормы,ЭтаФорма,УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоДолжностямНаСервере(АдресРезультатаПодбораВХранилище)
	
	МассивДолжностей = ПолучитьИзВременногоХранилища(АдресРезультатаПодбораВХранилище);
	
	Документы.ПриказПоНормамВыдачиСИЗ.ЗаполнитьНормыВыдачиСИЗ(Объект,,МассивДолжностей);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоПерсональнойВыдачеНаСервере()
	
	Документы.ПриказПоНормамВыдачиСИЗ.ЗаполнитьНормыВыдачиСИЗ(Объект,,,Истина,Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоПерсональнойВыдаче(Команда)
	
	ЗаполнитьПоПерсональнойВыдачеНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоКоллективнойВыдачеНаСервере()
	
	Документы.ПриказПоНормамВыдачиСИЗ.ЗаполнитьНормыВыдачиСИЗ(Объект,,,Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоКоллективнойВыдаче(Команда)
	
	ЗаполнитьПоКоллективнойВыдачеНаСервере();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ПОДСИСТЕМЫ "СВОЙСТВА"

// СтандартныеПодсистемы.Свойства
 &НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтаФорма, Объект.Ссылка);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ...

// СтандартныеПодсистемы.Свойства
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НормыВыдачиСИЗПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)

	//документ заполняется из исходных норм
	Отказ = Истина;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства
