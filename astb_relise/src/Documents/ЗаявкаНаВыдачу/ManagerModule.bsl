#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
		
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЗаявкаНаВыдачуТовары.Ссылка.Организация КАК Организация,
	               |	ЗаявкаНаВыдачуТовары.Ссылка КАК Заявка,
	               |	ЗаявкаНаВыдачуТовары.МВЗ КАК МВЗ,
	               |	ЗаявкаНаВыдачуТовары.Номенклатура КАК Номенклатура,
	               |	ЗаявкаНаВыдачуТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	СУММА(ЗаявкаНаВыдачуТовары.Количество) КАК Количество,
	               |	ЗаявкаНаВыдачуТовары.Ссылка.Дата КАК Период
	               |ПОМЕСТИТЬ ВТ_ГруппировкаТаблицыДокумента
	               |ИЗ
	               |	Документ.ЗаявкаНаВыдачу.Товары КАК ЗаявкаНаВыдачуТовары
	               |ГДЕ
	               |	ЗаявкаНаВыдачуТовары.Ссылка = &Ссылка
	               |	И НЕ ЗаявкаНаВыдачуТовары.Отказ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЗаявкаНаВыдачуТовары.Ссылка.Организация,
	               |	ЗаявкаНаВыдачуТовары.Ссылка,
	               |	ЗаявкаНаВыдачуТовары.МВЗ,
	               |	ЗаявкаНаВыдачуТовары.Номенклатура,
	               |	ЗаявкаНаВыдачуТовары.ХарактеристикаНоменклатуры,
	               |	ЗаявкаНаВыдачуТовары.Ссылка.Дата
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	               |	ВТ_ГруппировкаТаблицыДокумента.Организация КАК Организация,
	               |	ВТ_ГруппировкаТаблицыДокумента.Заявка КАК Заявка,
	               |	ВТ_ГруппировкаТаблицыДокумента.МВЗ КАК МВЗ,
	               |	ВТ_ГруппировкаТаблицыДокумента.Номенклатура КАК Номенклатура,
	               |	ВТ_ГруппировкаТаблицыДокумента.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ВТ_ГруппировкаТаблицыДокумента.Количество КАК Количество,
	               |	ВТ_ГруппировкаТаблицыДокумента.Период КАК Период
	               |ИЗ
	               |	ВТ_ГруппировкаТаблицыДокумента КАК ВТ_ГруппировкаТаблицыДокумента";
	
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	ТаблицыДляДвижений.Вставить("ТаблицаЗаявкиНаВыдачу", Результат);
	
КонецПроцедуры	
	
// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//ведомость
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ЗаявкаНаВыдачу";
	КомандаПечати.Идентификатор = "ЗаявкаВыдачу_ВедомостьПоПотребности";
	КомандаПечати.Представление = НСтр("ru = 'Заявка на выдачу (ведомость по потребности)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	//накладная по номенклатуре
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ЗаявкаНаВыдачу";
	КомандаПечати.Идентификатор = "ЗаявкаВыдачу_Накладная";
	КомандаПечати.Представление = НСтр("ru = 'Заявка на выдачу (накладная)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	//накладная по номенклатуре
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ЗаявкаНаВыдачу";
	КомандаПечати.Идентификатор = "ЗаявкаВыдачу_НакладнаяПоМВЗ";
	КомандаПечати.Представление = НСтр("ru = 'Заявка на выдачу (накладная по МВЗ)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры	

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаявкаВыдачу_ВедомостьПоПотребности") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ЗаявкаВыдачу_ВедомостьПоПотребности",
			"Заявка на выдачу (ведомость по потребности)",
			СформироватьПечатнуюФормуЗаявкиНаВыдачу_ВедомостьПоПотребности(МассивОбъектов, ОбъектыПечати),,"Документ.ЗаявкаНаВыдачу.ПФ_MXL_ЗаявкаВыдачу_ВедомостьПоПотребности");
				
	КонецЕсли;	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаявкаВыдачу_Накладная") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ЗаявкаВыдачу_Накладная",
			"Заявка на выдачу (накладная)",
			СформироватьПечатнуюФормуЗаявкиНаВыдачу_Накладная(МассивОбъектов, ОбъектыПечати),,"Документ.ЗаявкаНаВыдачу.ПФ_MXL_ЗаявкаВыдачу_Накладная");
				
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаявкаВыдачу_НакладнаяПоМВЗ") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ЗаявкаВыдачу_НакладнаяПоМВЗ",
			"Заявка на выдачу (накладная по МВЗ)",
			СформироватьПечатнуюФормуЗаявкиНаВыдачу_НакладнаяПоМВЗ(МассивОбъектов, ОбъектыПечати),,"Документ.ЗаявкаНаВыдачу.ПФ_MXL_ЗаявкаВыдачу_НакладнаяПоМВЗ");
				
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуЗаявкиНаВыдачу_ВедомостьПоПотребности(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЗаявкаНаВыдачу_ЗаявкаНаВыдачу_ВедомостьПоПотребности";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Документы.Ссылка КАК Ссылка,
	|	Документы.Организация.НаименованиеПолное КАК ПредставлениеОрганизации,
	|	Документы.Номер КАК Номер,
	|	Документы.Дата КАК Дата,
	|	Документы.Склад.Наименование КАК ПредставлениеСклада,
	|	Документы.МОЛ.Наименование КАК ПредставлениеМОЛ,
	|	Документы.Организация.Префикс КАК Префикс,
	|	Документы.НачалоПериодаВыдачи КАК НачалоПериода,
	|	Документы.КонецПериодаВыдачи КАК КонецПериода
	|ИЗ
	|	Документ.ЗаявкаНаВыдачу КАК Документы
	|ГДЕ
	|	Документы.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Организация КАК Организация,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Сотрудник КАК Сотрудник,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Подразделение КАК Подразделение,
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.Должность КАК Должность
	|ПОМЕСТИТЬ ВТ_ОсновноеМестоРаботы
	|ИЗ
	|	РегистрСведений.ОсновноеМестоРаботыСотрудника.СрезПоследних(, ) КАК ОсновноеМестоРаботыСотрудникаСрезПоследних
	|ГДЕ
	|	ОсновноеМестоРаботыСотрудникаСрезПоследних.ОсновноеМестоРаботы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Сотрудник КАК Сотрудник,
	|	ВТ_ОсновноеМестоРаботы.Подразделение КАК Подразделение,
	|	ВТ_ОсновноеМестоРаботы.Должность КАК Должность,
	|	Товары.НоменклатураНормы КАК НоменклатураНормы,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры КАК Размер,
	|	Товары.ДатаПотребности КАК ДатаПотребности,
	|	Товары.Количество КАК Количество,
	|	Товары.Сотрудник.ТабельныйНомер КАК ТабельныйНомер
	|ИЗ
	|	Документ.ЗаявкаНаВыдачу.Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОсновноеМестоРаботы КАК ВТ_ОсновноеМестоРаботы
	|		ПО Товары.Ссылка.Организация = ВТ_ОсновноеМестоРаботы.Организация
	|			И Товары.Сотрудник = ВТ_ОсновноеМестоРаботы.Сотрудник
	|ГДЕ
	|	Товары.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Товары.НомерСтроки
	|ИТОГИ ПО
	|	Ссылка");
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗаявкаНаВыдачу.ПФ_MXL_ЗаявкаВыдачу_ВедомостьПоПотребности");
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ДанныеПечати = ПакетЗапросов[0].Выбрать();
	ВыборкаПоДокументам = ПакетЗапросов[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если НЕ ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Ссылка", ДанныеПечати.Ссылка)) Тогда
			Продолжить;
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
			
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Заполнить(ДанныеПечати);
		
		ТекстЗаголовка = ПроцедурыРаботыСНормамиСервер.СформироватьЗаголовокДокумента(Новый Структура("Номер, Дата, Префикс", ДанныеПечати.Номер, ДанныеПечати.Дата, ДанныеПечати.Префикс), "Предварительная ведомость на выдачу СИЗ");
		Область.Параметры.ТекстЗаголовка 					= ТекстЗаголовка;
		Область.Параметры.ПредставлениеМОЛ 					= ФизическиеЛица.ФамилияИнициалыФизЛица(ДанныеПечати.ПредставлениеМОЛ);
		Область.Параметры.ПредставлениеПериодаФормирования 	= ПредставлениеПериода(НачалоДня(ДанныеПечати.НачалоПериода),КонецДня(ДанныеПечати.КонецПериода));
		ТабличныйДокумент.Вывести(Область);
			
		СчетСтрок = 1;
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			Область = Макет.ПолучитьОбласть("Строка");
			Область.Параметры.Заполнить(ВыборкаПоТоварам);
			Область.Параметры.Сотрудник = ФизическиеЛица.ФамилияИнициалыФизЛица(ВыборкаПоТоварам.Сотрудник.Наименование);
			ТабличныйДокумент.Вывести(Область);
			
			СчетСтрок = СчетСтрок + 1;
			
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция СформироватьПечатнуюФормуЗаявкиНаВыдачу_Накладная(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЗаявкаНаВыдачу_ЗаявкаНаВыдачу_Накладная";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Документы.Ссылка КАК Ссылка,
	|	Документы.Организация.НаименованиеПолное КАК ПредставлениеОрганизации,
	|	Документы.Номер КАК Номер,
	|	Документы.Дата КАК Дата,
	|	Документы.Склад.Наименование КАК ПредставлениеСклада,
	|	Документы.МОЛ.Наименование КАК ПредставлениеМОЛ,
	|	Документы.Организация.Префикс КАК Префикс,
	|	Документы.НачалоПериодаВыдачи КАК НачалоПериода,
	|	Документы.КонецПериодаВыдачи КАК КонецПериода
	|ИЗ
	|	Документ.ЗаявкаНаВыдачу КАК Документы
	|ГДЕ
	|	Документы.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаявкаНаВыдачуТоварыСводная.Ссылка КАК Ссылка,
	|	ЗаявкаНаВыдачуТоварыСводная.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА ЗаявкаНаВыдачуТоварыСводная.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Артикул)
	|			ТОГДА ЗаявкаНаВыдачуТоварыСводная.Номенклатура.Артикул
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЗаявкаНаВыдачуТоварыСводная.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Код)
	|					ТОГДА ЗаявкаНаВыдачуТоварыСводная.Номенклатура.Код
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ЗаявкаНаВыдачуТоварыСводная.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.КодСинхронизации)
	|							ТОГДА ЗаявкаНаВыдачуТоварыСводная.Номенклатура.КодСинхронизации
	|						ИНАЧЕ ВЫБОР
	|								КОГДА ЗаявкаНаВыдачуТоварыСводная.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.НоменклатурныйНомерОрганизации)
	|									ТОГДА ЕСТЬNULL(НоменклатурныеНомераОрганизаций.НоменклатурныйНомер, """")
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК НоменклатурныйНомер,
	|	ВЫБОР
	|		КОГДА ЗаявкаНаВыдачуТоварыСводная.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ЗаявкаНаВыдачуТоварыСводная.Номенклатура.Наименование
	|		ИНАЧЕ ЗаявкаНаВыдачуТоварыСводная.Номенклатура.Наименование + "" ("" + ЗаявкаНаВыдачуТоварыСводная.ХарактеристикаНоменклатуры.Наименование + "")""
	|	КОНЕЦ КАК НаименованиеТовара,
	|	ЗаявкаНаВыдачуТоварыСводная.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	ЗаявкаНаВыдачуТоварыСводная.Количество КАК Количество,
	|	ЗаявкаНаВыдачуТоварыСводная.Цена КАК Цена,
	|	ЗаявкаНаВыдачуТоварыСводная.Сумма КАК Сумма
	|ИЗ
	|	Документ.ЗаявкаНаВыдачу.ТоварыСводная КАК ЗаявкаНаВыдачуТоварыСводная
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатурныеНомераОрганизаций КАК НоменклатурныеНомераОрганизаций
	|		ПО ЗаявкаНаВыдачуТоварыСводная.Ссылка.Организация = НоменклатурныеНомераОрганизаций.Организация
	|			И ЗаявкаНаВыдачуТоварыСводная.Номенклатура = НоменклатурныеНомераОрганизаций.Номенклатура
	|			И ЗаявкаНаВыдачуТоварыСводная.ХарактеристикаНоменклатуры = НоменклатурныеНомераОрганизаций.ХарактеристикаНоменклатуры
	|ГДЕ
	|	ЗаявкаНаВыдачуТоварыСводная.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ЗаявкаНаВыдачуТоварыСводная.НомерСтроки
	|ИТОГИ ПО
	|	Ссылка");
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗаявкаНаВыдачу.ПФ_MXL_ЗаявкаВыдачу_Накладная");
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ДанныеПечати = ПакетЗапросов[0].Выбрать();
	ВыборкаПоДокументам = ПакетЗапросов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если НЕ ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Ссылка", ДанныеПечати.Ссылка)) Тогда
			Продолжить;
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
			
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Заполнить(ДанныеПечати);
		
		ТекстЗаголовка = ПроцедурыРаботыСНормамиСервер.СформироватьЗаголовокДокумента(Новый Структура("Номер, Дата, Префикс", ДанныеПечати.Номер, ДанныеПечати.Дата, ДанныеПечати.Префикс), "Накладная по заявке");
		Область.Параметры.ТекстЗаголовка 					= ТекстЗаголовка;
		Область.Параметры.ПредставлениеМОЛ 					= ФизическиеЛица.ФамилияИнициалыФизЛица(ДанныеПечати.ПредставлениеМОЛ);
		Область.Параметры.ПредставлениеПериодаФормирования 	= ПредставлениеПериода(НачалоДня(ДанныеПечати.НачалоПериода),КонецДня(ДанныеПечати.КонецПериода));
		ТабличныйДокумент.Вывести(Область);
			
		СчетСтрок = 1;
		СуммаИтого = 0;
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы");
			Область.Параметры.Заполнить(ВыборкаПоТоварам);
			ТабличныйДокумент.Вывести(Область);
			
			СчетСтрок = СчетСтрок + 1;
			СуммаИтого = СуммаИтого + ВыборкаПоТоварам.Сумма;
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		ТекстИтоговойСтроки = НСтр("ru = 'Всего наименований %ВсегоНаименований% на сумму %СуммаИтого%'");
		ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%ВсегоНаименований%", СчетСтрок-1);
		ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%СуммаИтого%", СуммаИтого);
		Область.Параметры.ИтоговаяСтрока = ТекстИтоговойСтроки;
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция СформироватьПечатнуюФормуЗаявкиНаВыдачу_НакладнаяПоМВЗ(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЗаявкаНаВыдачу_ЗаявкаНаВыдачу_НакладнаяПоМВЗ";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Документы.Ссылка КАК Ссылка,
	|	Документы.Организация.НаименованиеПолное КАК ПредставлениеОрганизации,
	|	Документы.Номер КАК Номер,
	|	Документы.Дата КАК Дата,
	|	Документы.Склад.Наименование КАК ПредставлениеСклада,
	|	Документы.МОЛ.Наименование КАК ПредставлениеМОЛ,
	|	Документы.Организация.Префикс КАК Префикс,
	|	Документы.НачалоПериодаВыдачи КАК НачалоПериода,
	|	Документы.КонецПериодаВыдачи КАК КонецПериода
	|ИЗ
	|	Документ.ЗаявкаНаВыдачу КАК Документы
	|ГДЕ
	|	Документы.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаявкаНаВыдачуТоварыСводная.Ссылка КАК Ссылка,
	|	ЗаявкаНаВыдачуТоварыСводная.МВЗ.Наименование КАК МВЗ,
	|	ВЫБОР
	|		КОГДА ЗаявкаНаВыдачуТоварыСводная.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Артикул)
	|			ТОГДА ЗаявкаНаВыдачуТоварыСводная.Номенклатура.Артикул
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЗаявкаНаВыдачуТоварыСводная.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Код)
	|					ТОГДА ЗаявкаНаВыдачуТоварыСводная.Номенклатура.Код
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ЗаявкаНаВыдачуТоварыСводная.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.КодСинхронизации)
	|							ТОГДА ЗаявкаНаВыдачуТоварыСводная.Номенклатура.КодСинхронизации
	|						ИНАЧЕ ВЫБОР
	|								КОГДА ЗаявкаНаВыдачуТоварыСводная.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.НоменклатурныйНомерОрганизации)
	|									ТОГДА ЕСТЬNULL(НоменклатурныеНомераОрганизаций.НоменклатурныйНомер, """")
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК НоменклатурныйНомер,
	|	ВЫБОР
	|		КОГДА ЗаявкаНаВыдачуТоварыСводная.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ЗаявкаНаВыдачуТоварыСводная.Номенклатура.Наименование
	|		ИНАЧЕ ЗаявкаНаВыдачуТоварыСводная.Номенклатура.Наименование + "" ("" + ЗаявкаНаВыдачуТоварыСводная.ХарактеристикаНоменклатуры.Наименование + "")""
	|	КОНЕЦ КАК НаименованиеТовара,
	|	ЗаявкаНаВыдачуТоварыСводная.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	ЗаявкаНаВыдачуТоварыСводная.Количество КАК Количество,
	|	ЗаявкаНаВыдачуТоварыСводная.Цена КАК Цена,
	|	ЗаявкаНаВыдачуТоварыСводная.Сумма КАК Сумма
	|ИЗ
	|	Документ.ЗаявкаНаВыдачу.ТоварыСводная КАК ЗаявкаНаВыдачуТоварыСводная
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатурныеНомераОрганизаций КАК НоменклатурныеНомераОрганизаций
	|		ПО ЗаявкаНаВыдачуТоварыСводная.Ссылка.Организация = НоменклатурныеНомераОрганизаций.Организация
	|			И ЗаявкаНаВыдачуТоварыСводная.Номенклатура = НоменклатурныеНомераОрганизаций.Номенклатура
	|			И ЗаявкаНаВыдачуТоварыСводная.ХарактеристикаНоменклатуры = НоменклатурныеНомераОрганизаций.ХарактеристикаНоменклатуры
	|ГДЕ
	|	ЗаявкаНаВыдачуТоварыСводная.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ЗаявкаНаВыдачуТоварыСводная.МВЗ.Наименование,
	|	ЗаявкаНаВыдачуТоварыСводная.Номенклатура.Наименование
	|ИТОГИ ПО
	|	Ссылка");
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗаявкаНаВыдачу.ПФ_MXL_ЗаявкаВыдачу_НакладнаяПоМВЗ");
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ДанныеПечати = ПакетЗапросов[0].Выбрать();
	ВыборкаПоДокументам = ПакетЗапросов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если НЕ ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Ссылка", ДанныеПечати.Ссылка)) Тогда
			Продолжить;
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
			
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Заполнить(ДанныеПечати);
		
		ТекстЗаголовка = ПроцедурыРаботыСНормамиСервер.СформироватьЗаголовокДокумента(Новый Структура("Номер, Дата, Префикс", ДанныеПечати.Номер, ДанныеПечати.Дата, ДанныеПечати.Префикс), "Накладная с МВЗ по заявке");
		Область.Параметры.ТекстЗаголовка 					= ТекстЗаголовка;
		Область.Параметры.ПредставлениеМОЛ 					= ФизическиеЛица.ФамилияИнициалыФизЛица(ДанныеПечати.ПредставлениеМОЛ);
		Область.Параметры.ПредставлениеПериодаФормирования 	= ПредставлениеПериода(НачалоДня(ДанныеПечати.НачалоПериода),КонецДня(ДанныеПечати.КонецПериода));
		ТабличныйДокумент.Вывести(Область);
			
		СчетСтрок = 1;
		СуммаИтого = 0;
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы");
			Область.Параметры.Заполнить(ВыборкаПоТоварам);
			Область.Параметры.НомерСтроки = СчетСтрок;
			ТабличныйДокумент.Вывести(Область);
			
			СчетСтрок = СчетСтрок + 1;
			СуммаИтого = СуммаИтого + ВыборкаПоТоварам.Сумма;
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		ТекстИтоговойСтроки = НСтр("ru = 'Всего наименований %ВсегоНаименований% на сумму %СуммаИтого%'");
		ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%ВсегоНаименований%", СчетСтрок-1);
		ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%СуммаИтого%", СуммаИтого);
		Область.Параметры.ИтоговаяСтрока = ТекстИтоговойСтроки;
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ЗаполнитьТаблицуДокумента(ТекущийОбъект,УтивыватьПросроченнуюПотребность) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	АлгоритмФормированияПотребности = ПроцедурыРаботыСНормамиСервер.ПолучитьАлгоритмФормированияПотребности(ТекущийОбъект.Организация);
	
	Если АлгоритмФормированияПотребности = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Выполнить("Алгоритм_" + АлгоритмФормированияПотребности + "_Сервер.ЗаявкаНаВыдачу_ЗаполнитьТаблицуДокумента(ТекущийОбъект,УтивыватьПросроченнуюПотребность)");
	
КонецПроцедуры

#КонецЕсли	