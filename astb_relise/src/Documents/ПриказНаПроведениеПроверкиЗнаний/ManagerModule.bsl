#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриказНаПроведениеПроверкиЗнанийРаботники.Ссылка.Дата КАК Период,
	|	ПриказНаПроведениеПроверкиЗнанийРаботники.Ссылка.Организация КАК Организация,
	|	ПриказНаПроведениеПроверкиЗнанийРаботники.Проводить КАК Проводить,
	|	ПриказНаПроведениеПроверкиЗнанийРаботники.Сотрудник КАК Сотрудник,
	|	ПриказНаПроведениеПроверкиЗнанийРаботники.Ссылка.Тест КАК Тест,
	|	ЛОЖЬ КАК Выполнено
	|ИЗ
	|	Документ.ПриказНаПроведениеПроверкиЗнаний.Работники КАК ПриказНаПроведениеПроверкиЗнанийРаботники
	|ГДЕ
	|	ПриказНаПроведениеПроверкиЗнанийРаботники.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	
	ТаблицыДляДвижений.Вставить("ТаблицаПриказыНаПроведениеПроверкиЗнаний", Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры	

//Процедура заполняет табличную часть документа-объекта в зависимости от вида заполнения
//Параметры:
//ТекущийОбъект - документ-объект, который заполняется
//ВидЗаполнения - строка - может принимать значения: "ПоТекущимДанным", "ПоПодразделениям", "ПоДолжностям" и "ПоСотрудникам"
//ЗначениеПараметраЗаполнения - значение, по которому заполняется документ-объект типа "Сотрудники", "Подразделения" или "Должности"
//
Процедура ЗаполнитьТаблицуДокумента(ТекущийОбъект,ВидЗаполнения,ЗначениеПараметраЗаполнения = Неопределено) Экспорт
	
	ТекущийОбъект.Работники.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаАнализа",ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка));
	Запрос.УстановитьПараметр("Организация",ТекущийОбъект.Организация);
	Запрос.УстановитьПараметр("Тест",		ТекущийОбъект.Тест);
	
	Если ВидЗаполнения = "ПоТекущимДанным" Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗанятыеРабочиеМестаОстатки.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ВТ_ЗРМ
		|ИЗ
		|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(&ДатаАнализа, Организация = &Организация) КАК ЗанятыеРабочиеМестаОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриказыНаПроведениеПроверкиЗнанийСрезПоследних.Сотрудник КАК Сотрудник,
		|	ПриказыНаПроведениеПроверкиЗнанийСрезПоследних.Проводить КАК Проводить
		|ИЗ
		|	РегистрСведений.ПриказыНаПроведениеПроверкиЗнаний.СрезПоследних(
		|			&ДатаАнализа,
		|			Организация = &Организация
		|				И Тест = &Тест) КАК ПриказыНаПроведениеПроверкиЗнанийСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗРМ КАК ВТ_ЗРМ
		|		ПО ПриказыНаПроведениеПроверкиЗнанийСрезПоследних.Сотрудник = ВТ_ЗРМ.Сотрудник
		|ГДЕ
		|	НЕ ПриказыНаПроведениеПроверкиЗнанийСрезПоследних.Выполнено
		|	И НЕ ВТ_ЗРМ.Сотрудник ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПриказыНаПроведениеПроверкиЗнанийСрезПоследних.Сотрудник.Наименование";
		
	ИначеЕсли ВидЗаполнения = "ПоПодразделениям" Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПриказыНаПроведениеПроверкиЗнанийСрезПоследних.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ВТ_Приказы
		|ИЗ
		|	РегистрСведений.ПриказыНаПроведениеПроверкиЗнаний.СрезПоследних(
		|			&ДатаАнализа,
		|			Организация = &Организация
		|				И Тест = &Тест) КАК ПриказыНаПроведениеПроверкиЗнанийСрезПоследних
		|ГДЕ
		|	ПриказыНаПроведениеПроверкиЗнанийСрезПоследних.Проводить
		|	И НЕ ПриказыНаПроведениеПроверкиЗнанийСрезПоследних.Выполнено
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗанятыеРабочиеМестаОстатки.Сотрудник КАК Сотрудник,
		|	ИСТИНА КАК Проводить
		|ИЗ
		|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
		|			&ДатаАнализа,
		|			Организация = &Организация
		|				И Подразделение В (&МассивПодразделений)) КАК ЗанятыеРабочиеМестаОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Приказы КАК ВТ_Приказы
		|		ПО ЗанятыеРабочиеМестаОстатки.Сотрудник = ВТ_Приказы.Сотрудник
		|ГДЕ
		|	ВТ_Приказы.Сотрудник ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗанятыеРабочиеМестаОстатки.Сотрудник.Наименование";
		
		Запрос.УстановитьПараметр("МассивПодразделений",ЗначениеПараметраЗаполнения);
		
	ИначеЕсли ВидЗаполнения = "ПоДолжностям" Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПриказыНаПроведениеПроверкиЗнанийСрезПоследних.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ВТ_Приказы
		|ИЗ
		|	РегистрСведений.ПриказыНаПроведениеПроверкиЗнаний.СрезПоследних(
		|			&ДатаАнализа,
		|			Организация = &Организация
		|				И Тест = &Тест) КАК ПриказыНаПроведениеПроверкиЗнанийСрезПоследних
		|ГДЕ
		|	ПриказыНаПроведениеПроверкиЗнанийСрезПоследних.Проводить
		|	И НЕ ПриказыНаПроведениеПроверкиЗнанийСрезПоследних.Выполнено
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗанятыеРабочиеМестаОстатки.Сотрудник КАК Сотрудник,
		|	ИСТИНА КАК Проводить
		|ИЗ
		|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
		|			&ДатаАнализа,
		|			Организация = &Организация
		|				И Должность В (&МассивДолжностей)) КАК ЗанятыеРабочиеМестаОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Приказы КАК ВТ_Приказы
		|		ПО ЗанятыеРабочиеМестаОстатки.Сотрудник = ВТ_Приказы.Сотрудник
		|ГДЕ
		|	ВТ_Приказы.Сотрудник ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗанятыеРабочиеМестаОстатки.Сотрудник.Наименование";
		
		Запрос.УстановитьПараметр("МассивДолжностей",ЗначениеПараметраЗаполнения);
		
	ИначеЕсли ВидЗаполнения = "ПоСотрудникам" Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПриказыНаПроведениеПроверкиЗнанийСрезПоследних.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ВТ_Приказы
		|ИЗ
		|	РегистрСведений.ПриказыНаПроведениеПроверкиЗнаний.СрезПоследних(
		|			&ДатаАнализа,
		|			Организация = &Организация
		|				И Тест = &Тест) КАК ПриказыНаПроведениеПроверкиЗнанийСрезПоследних
		|ГДЕ
		|	ПриказыНаПроведениеПроверкиЗнанийСрезПоследних.Проводить
		|	И НЕ ПриказыНаПроведениеПроверкиЗнанийСрезПоследних.Выполнено
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗанятыеРабочиеМестаОстатки.Сотрудник КАК Сотрудник,
		|	ИСТИНА КАК Проводить
		|ИЗ
		|	РегистрНакопления.ЗанятыеРабочиеМеста.Остатки(
		|			&ДатаАнализа,
		|			Организация = &Организация
		|				И Сотрудник В (&МассивСотрудников)) КАК ЗанятыеРабочиеМестаОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Приказы КАК ВТ_Приказы
		|		ПО ЗанятыеРабочиеМестаОстатки.Сотрудник = ВТ_Приказы.Сотрудник
		|ГДЕ
		|	ВТ_Приказы.Сотрудник ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗанятыеРабочиеМестаОстатки.Сотрудник.Наименование";
		
		Запрос.УстановитьПараметр("МассивСотрудников",ЗначениеПараметраЗаполнения);		
		
	Иначе
		
		Возврат;
		
	КонецЕсли;	
	
	ТекущийОбъект.Работники.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры	

#КонецЕсли