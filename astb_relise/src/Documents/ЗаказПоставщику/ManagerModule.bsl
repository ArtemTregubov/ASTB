#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	               |	ЗаказПоставщикуТовары.Ссылка.Организация,
				   |	ЗаказПоставщикуТовары.Ссылка КАК ЗаказПоставщику,
	               |	ЗаказПоставщикуТовары.Ссылка.Склад КАК Склад,
	               |	ЗаказПоставщикуТовары.Номенклатура,
	               |	ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры,
				   |	ЗаказПоставщикуТовары.ДокументВыдачи,
	               |	ЗаказПоставщикуТовары.Количество,
	               |	ЗаказПоставщикуТовары.НомерСтроки,
	               |	ЗаказПоставщикуТовары.Ссылка.Дата КАК Период
	               |ИЗ
	               |	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	               |ГДЕ
	               |	ЗаказПоставщикуТовары.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	ТаблицыДляДвижений.Вставить("ТаблицаЗаказыПоставщику", Результат);
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаказПоставщику") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ЗаказПоставщику",
			"Заказ поставщику",
			СформироватьПечатнуюФормуЗаказаПоставщику(МассивОбъектов, ОбъектыПечати),,"Документ.ЗаказПоставщику.ПФ_MXL_ЗаказПоставщику");
				
	КонецЕсли;
		
	//+++АСТБ_Горюшин_Алексей_49316
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СверкаКоличества") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"СверкаКоличества",
			"Сверка количества",
			СформироватьПечатнуюФормуСверкиКоличества(МассивОбъектов, ОбъектыПечати),,"Документ.ЗаказПоставщику.МакетСверкаКоличества");
				
	КонецЕсли;
	//---АСТБ_Горюшин_Алексей_49316
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуЗаказаПоставщику(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЗаказПоставщику_ЗаказПоставщику";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Документы.Ссылка КАК Ссылка,
	|	Документы.Организация.НаименованиеПолное КАК ПредставлениеОрганизации,
	|	Документы.Номер КАК Номер,
	|	Документы.Дата КАК Дата,
	|	Документы.Склад.Наименование КАК ПредставлениеСклада,
	|	Документы.Организация.Префикс КАК Префикс,
	|	Документы.Поставщик.НаименованиеПолное КАК ПредставлениеПоставщика,
	|	Документы.НачалоПериодаВыдачи КАК ПериодВыдачи
	|ИЗ
	|	Документ.ЗаказПоставщику КАК Документы
	|ГДЕ
	|	Документы.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Количество КАК Количество,
	|	Товары.Цена КАК Цена,
	|	Товары.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА Товары.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|			ТОГДА Товары.Номенклатура.Наименование
	|		ИНАЧЕ Товары.Номенклатура.Наименование + "" ("" + Товары.ХарактеристикаНоменклатуры.Наименование + "")""
	|	КОНЕЦ КАК НаименованиеТовара,
	|	ВЫБОР
	|		КОГДА Товары.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Артикул)
	|			ТОГДА Товары.Номенклатура.Артикул
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Товары.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Код)
	|					ТОГДА Товары.Номенклатура.Код
	|				ИНАЧЕ ВЫБОР
	|						КОГДА Товары.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.КодСинхронизации)
	|							ТОГДА Товары.Номенклатура.КодСинхронизации
	|						ИНАЧЕ ВЫБОР
	|								КОГДА Товары.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.НоменклатурныйНомерОрганизации)
	|									ТОГДА ЕСТЬNULL(НоменклатурныеНомераОрганизаций.НоменклатурныйНомер, """")
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК НоменклатурныйНомер,
	|	Товары.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	Товары.НомерСтроки
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатурныеНомераОрганизаций КАК НоменклатурныеНомераОрганизаций
	|		ПО Товары.Ссылка.Организация = НоменклатурныеНомераОрганизаций.Организация
	|			И Товары.Номенклатура = НоменклатурныеНомераОрганизаций.Номенклатура
	|			И Товары.ХарактеристикаНоменклатуры = НоменклатурныеНомераОрганизаций.НоменклатурныйНомер
	|ГДЕ
	|	Товары.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Товары.НомерСтроки
	|ИТОГИ ПО
	|	Ссылка");
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗаказПоставщику.ПФ_MXL_ЗаказПоставщику");
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ДанныеПечати = ПакетЗапросов[0].Выбрать();
	ВыборкаПоДокументам = ПакетЗапросов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если НЕ ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Ссылка", ДанныеПечати.Ссылка)) Тогда
			Продолжить;
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
			
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Заполнить(ДанныеПечати);
		
		ТекстЗаголовка = ПроцедурыРаботыСНормамиСервер.СформироватьЗаголовокДокумента(Новый Структура("Номер, Дата, Префикс", ДанныеПечати.Номер, ДанныеПечати.Дата, ДанныеПечати.Префикс), "Заказ поставщику");
		Область.Параметры.ТекстЗаголовка = ТекстЗаголовка + ?(ЗначениеЗаполнено(ДанныеПечати.ПериодВыдачи)," Период выдачи: " + Формат(ДанныеПечати.ПериодВыдачи,"ДФ='ММММ гггг'"),"");
		ТабличныйДокумент.Вывести(Область);
			
		СчетСтрок = 1;
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы");
			Область.Параметры.Заполнить(ВыборкаПоТоварам);
			ТабличныйДокумент.Вывести(Область);
			
			СчетСтрок = СчетСтрок + 1;
			
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		ТекстИтоговойСтроки = НСтр("ru = 'Всего наименований %ВсегоНаименований%'");
		ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%ВсегоНаименований%", СчетСтрок-1);
		Область.Параметры.ИтоговаяСтрока = ТекстИтоговойСтроки;
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//заказ поставщику
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ЗаказПоставщику";
	КомандаПечати.Идентификатор = "ЗаказПоставщику";
	КомандаПечати.Представление = НСтр("ru = 'Заказ поставщику'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	//+++АСТБ_Горюшин_Алексей_49316
	//сверка количества
	//пока не нужно
	//КомандаПечати = КомандыПечати.Добавить();
	//КомандаПечати.МенеджерПечати = "Документ.ЗаказПоставщику";
	//КомандаПечати.Идентификатор = "СверкаКоличества";
	//КомандаПечати.Представление = НСтр("ru = 'Сверка количества'");
	//КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	//---АСТБ_Горюшин_Алексей_49316
	
КонецПроцедуры

Процедура ПересчитатьЦены(ТекущийОбъект) Экспорт
	
	ТаблицаДокумента = ТекущийОбъект.Товары.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ТаблицаДокумента.Количество КАК Количество,
	|	ТаблицаДокумента.Цена КАК Цена,
	|	ТаблицаДокумента.Сумма КАК Сумма,
	|	ТаблицаДокумента.ДокументВыдачи КАК ДокументВыдачи
	|ПОМЕСТИТЬ ВТ_ТаблицаДокумента
	|ИЗ
	|	&ТаблицаДокумента КАК ТаблицаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ЦеныНоменклатурыСрезПоследних.Цена) КАК Цена
	|ПОМЕСТИТЬ ВТ_Цены
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|			&ПериодРасчета,
	|			Организация = &Организация
	|				И Поставщик = &Поставщик) КАК ЦеныНоменклатурыСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицаДокумента.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_ТаблицаДокумента.Количество КАК Количество,
	|	ЕСТЬNULL(ВТ_Цены.Цена, 0) КАК Цена,
	|	ВТ_ТаблицаДокумента.Количество * ЕСТЬNULL(ВТ_Цены.Цена, 0) КАК Сумма,
	|	ВТ_ТаблицаДокумента.ДокументВыдачи КАК ДокументВыдачи
	|ИЗ
	|	ВТ_ТаблицаДокумента КАК ВТ_ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Цены КАК ВТ_Цены
	|		ПО ВТ_ТаблицаДокумента.Номенклатура = ВТ_Цены.Номенклатура";
	
	Запрос.УстановитьПараметр("ТаблицаДокумента",	ТаблицаДокумента);
	Запрос.УстановитьПараметр("ПериодРасчета",		ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка));
	Запрос.УстановитьПараметр("Организация",		ТекущийОбъект.Организация);
	Запрос.УстановитьПараметр("Поставщик",			ТекущийОбъект.Поставщик);
	
	ТекущийОбъект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	ТекущийОбъект.СуммаДокумента = ТекущийОбъект.Товары.Итог("Сумма");
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуДокументаПоДокументамВыдачи(ТекущийОбъект,ДокументыВыдачи) Экспорт
	
	ТекущийОбъект.Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВыдачаСредствЗащитыСотрудникуТовары.Ссылка КАК Ссылка,
	|	ВыдачаСредствЗащитыСотрудникуТовары.НеВыдано КАК НеВыдано,
	|	ВыдачаСредствЗащитыСотрудникуТовары.НеВыданоПоПричине КАК НеВыданоПоПричине,
	|	ВыдачаСредствЗащитыСотрудникуТовары.Сотрудник КАК Сотрудник,
	|	ВыдачаСредствЗащитыСотрудникуТовары.НормаВыдачи КАК НормаВыдачи,
	|	ВыдачаСредствЗащитыСотрудникуТовары.НоменклатураНормы КАК НоменклатураНормы,
	|	ВыдачаСредствЗащитыСотрудникуТовары.Номенклатура КАК Номенклатура,
	|	ВыдачаСредствЗащитыСотрудникуТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВыдачаСредствЗащитыСотрудникуТовары.Количество КАК Количество,
	|	ВыдачаСредствЗащитыСотрудникуТовары.Цена КАК Цена,
	|	ВыдачаСредствЗащитыСотрудникуТовары.Сумма КАК Сумма,
	|	ВыдачаСредствЗащитыСотрудникуТовары.ДатаВыдачи КАК ДатаВыдачи,
	|	ВыдачаСредствЗащитыСотрудникуТовары.КоличествоПотребность КАК КоличествоПотребность,
	|	ВыдачаСредствЗащитыСотрудникуТовары.ДобавленаКопированием КАК ДобавленаКопированием
	|ПОМЕСТИТЬ ВТ_ТаблицыДокументовВыдачи
	|ИЗ
	|	Документ.ВыдачаСредствЗащитыСотруднику.Товары КАК ВыдачаСредствЗащитыСотрудникуТовары
	|ГДЕ
	|	ВыдачаСредствЗащитыСотрудникуТовары.Ссылка В(&МассивДокументов)
	|	И НЕ ВыдачаСредствЗащитыСотрудникуТовары.Ссылка.ПредварительнаяСборка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицыДокументовВыдачи.Ссылка КАК Ссылка,
	|	ВТ_ТаблицыДокументовВыдачи.НормаВыдачи КАК НормаВыдачи,
	|	ВТ_ТаблицыДокументовВыдачи.НоменклатураНормы КАК НоменклатураНормы,
	|	ВТ_ТаблицыДокументовВыдачи.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицыДокументовВыдачи.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ВТ_ТаблицыДокументовВыдачи.Количество) КАК Количество,
	|	СУММА(ВТ_ТаблицыДокументовВыдачи.КоличествоПотребность) КАК КоличествоПотребность
	|ПОМЕСТИТЬ ВТ_ОтказДляОбработки
	|ИЗ
	|	ВТ_ТаблицыДокументовВыдачи КАК ВТ_ТаблицыДокументовВыдачи
	|ГДЕ
	|	ВТ_ТаблицыДокументовВыдачи.НеВыдано
	|	И НЕ ВТ_ТаблицыДокументовВыдачи.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицыДокументовВыдачи.Ссылка,
	|	ВТ_ТаблицыДокументовВыдачи.НормаВыдачи,
	|	ВТ_ТаблицыДокументовВыдачи.НоменклатураНормы,
	|	ВТ_ТаблицыДокументовВыдачи.Номенклатура,
	|	ВТ_ТаблицыДокументовВыдачи.ХарактеристикаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицыДокументовВыдачи.Ссылка КАК Ссылка,
	|	ВТ_ТаблицыДокументовВыдачи.НормаВыдачи КАК НормаВыдачи,
	|	ВТ_ТаблицыДокументовВыдачи.НоменклатураНормы КАК НоменклатураНормы,
	|	ВТ_ТаблицыДокументовВыдачи.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицыДокументовВыдачи.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ВТ_ТаблицыДокументовВыдачи.Количество) КАК Количество,
	|	СУММА(ВТ_ТаблицыДокументовВыдачи.КоличествоПотребность) КАК КоличествоПотребность
	|ПОМЕСТИТЬ ВТ_ЧастичнаяВыдача
	|ИЗ
	|	ВТ_ТаблицыДокументовВыдачи КАК ВТ_ТаблицыДокументовВыдачи
	|ГДЕ
	|	НЕ ВТ_ТаблицыДокументовВыдачи.НеВыдано
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицыДокументовВыдачи.Ссылка,
	|	ВТ_ТаблицыДокументовВыдачи.НормаВыдачи,
	|	ВТ_ТаблицыДокументовВыдачи.НоменклатураНормы,
	|	ВТ_ТаблицыДокументовВыдачи.Номенклатура,
	|	ВТ_ТаблицыДокументовВыдачи.ХарактеристикаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОтказДляОбработки.Ссылка КАК ДокументВыдачи,
	|	ВТ_ОтказДляОбработки.НормаВыдачи КАК НормаВыдачи,
	|	ВТ_ОтказДляОбработки.НоменклатураНормы КАК НоменклатураНормы,
	|	ВТ_ОтказДляОбработки.Номенклатура КАК Номенклатура,
	|	ВТ_ОтказДляОбработки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ВТ_ОтказДляОбработки.КоличествоПотребность = 0
	|			ТОГДА ВТ_ЧастичнаяВыдача.КоличествоПотребность - ВТ_ЧастичнаяВыдача.Количество
	|		ИНАЧЕ ВТ_ОтказДляОбработки.КоличествоПотребность
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ ВТ_Результат
	|ИЗ
	|	ВТ_ОтказДляОбработки КАК ВТ_ОтказДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЧастичнаяВыдача КАК ВТ_ЧастичнаяВыдача
	|		ПО ВТ_ОтказДляОбработки.Ссылка = ВТ_ЧастичнаяВыдача.Ссылка
	|			И ВТ_ОтказДляОбработки.НормаВыдачи = ВТ_ЧастичнаяВыдача.НормаВыдачи
	|			И ВТ_ОтказДляОбработки.НоменклатураНормы = ВТ_ЧастичнаяВыдача.НоменклатураНормы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Результат.ДокументВыдачи КАК ДокументВыдачи,
	|	ВТ_Результат.Номенклатура КАК Номенклатура,
	|	ВТ_Результат.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ВТ_Результат.Количество) КАК Количество
	|ИЗ
	|	ВТ_Результат КАК ВТ_Результат
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Результат.ДокументВыдачи,
	|	ВТ_Результат.Номенклатура,
	|	ВТ_Результат.ХарактеристикаНоменклатуры";
	
	Запрос.УстановитьПараметр("МассивДокументов",ДокументыВыдачи);
	
	ТекущийОбъект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры	

Процедура ЗаполнитьТаблицуДокументаПоКраснойЛинии(ТекущийОбъект) Экспорт
	
	ТекущийОбъект.Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &НеВестиУчетОстатковПоОрганизациям
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		ИНАЧЕ ОстаткиНоменклатурыОстатки.Организация
	|	КОНЕЦ КАК Организация,
	|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
	|	ОстаткиНоменклатурыОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ОстаткиНоменклатурыОстатки.КоличествоОстаток) КАК КоличествоОстаток
	|ПОМЕСТИТЬ ВТ_Остатки
	|ИЗ
	|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	|			&ПериодАнализа,
	|			Склад = &Склад
	|				И (Организация = &Организация
	|					ИЛИ Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))) КАК ОстаткиНоменклатурыОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА &НеВестиУчетОстатковПоОрганизациям
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		ИНАЧЕ ОстаткиНоменклатурыОстатки.Организация
	|	КОНЕЦ,
	|	ОстаткиНоменклатурыОстатки.Номенклатура,
	|	ОстаткиНоменклатурыОстатки.ХарактеристикаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &НеВестиУчетОстатковПоОрганизациям
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		ИНАЧЕ КраснаяЛиния.Организация
	|	КОНЕЦ КАК Организация,
	|	КраснаяЛиния.Номенклатура КАК Номенклатура,
	|	КраснаяЛиния.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	КраснаяЛиния.Количество КАК Количество
	|ПОМЕСТИТЬ ВТ_КраснаяЛиния
	|ИЗ
	|	РегистрСведений.КраснаяЛиния КАК КраснаяЛиния
	|ГДЕ
	|	(КраснаяЛиния.Организация = &Организация
	|			ИЛИ &НеВестиУчетОстатковПоОрганизациям)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_КраснаяЛиния.Организация КАК Организация,
	|	ВТ_КраснаяЛиния.Номенклатура КАК Номенклатура,
	|	ВТ_КраснаяЛиния.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_КраснаяЛиния.Количество - ЕСТЬNULL(ВТ_Остатки.КоличествоОстаток, 0) КАК Количество
	|ИЗ
	|	ВТ_КраснаяЛиния КАК ВТ_КраснаяЛиния
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Остатки КАК ВТ_Остатки
	|		ПО ВТ_КраснаяЛиния.Организация = ВТ_Остатки.Организация
	|			И ВТ_КраснаяЛиния.Номенклатура = ВТ_Остатки.Номенклатура
	|			И ВТ_КраснаяЛиния.ХарактеристикаНоменклатуры = ВТ_Остатки.ХарактеристикаНоменклатуры
	|ГДЕ
	|	ВТ_КраснаяЛиния.Количество - ЕСТЬNULL(ВТ_Остатки.КоличествоОстаток, 0) > 0";
	
	Запрос.УстановитьПараметр("ПериодАнализа", 						ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка));
	Запрос.УстановитьПараметр("Организация",						ТекущийОбъект.Организация);
	Запрос.УстановитьПараметр("Склад",								ТекущийОбъект.Склад);
	Запрос.УстановитьПараметр("НеВестиУчетОстатковПоОрганизациям",	ПолучитьФункциональнуюОпцию("НеВестиУчетОстатковНоменклатурыПоОрганизации"));
	
	ТекущийОбъект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры	

//+++АСТБ_Горюшин_Алексей_49316
Функция  СформироватьПечатнуюФормуСверкиКоличества(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЗаказПоставщику_СверкаКоличества";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ЗаказПоставщику.Ссылка КАК Ссылка,
	|	ЗаказПоставщику.Дата КАК Дата,
	|	ЗаказПоставщику.Номер КАК Номер,
	|	ЗаказПоставщику.Организация КАК Организация,
	|	ЗаказПоставщику.Поставщик КАК Поставщик
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	|ГДЕ
	|	ЗаказПоставщику.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказПоставщикуТовары.Ссылка КАК Ссылка,
	|	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
	|	ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	СУММА(ЗаказПоставщикуТовары.Количество) КАК Количество,
	|	ЗаказПоставщикуТовары.Цена КАК Цена,
	|	СУММА(ЗаказПоставщикуТовары.Сумма) КАК Сумма
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	|ГДЕ
	|	ЗаказПоставщикуТовары.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказПоставщикуТовары.Ссылка,
	|	ЗаказПоставщикуТовары.Номенклатура,
	|	ЗаказПоставщикуТовары.ХарактеристикаНоменклатуры,
	|	ЗаказПоставщикуТовары.Цена	
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Номенклатура,
	|	ХарактеристикаНоменклатуры,
	|	Цена
	|ИТОГИ ПО
	|	Ссылка
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	ДанныеПечати		= ПакетЗапросов[0].Выбрать();
	ВыборкаПоДокументам	= ПакетЗапросов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Макет = Документы.ЗаказПоставщику.ПолучитьМакет("МакетСверкаКоличества");
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если НЕ ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Ссылка", ДанныеПечати.Ссылка)) Тогда
			Продолжить;
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		ОбластьЗаголовок 	= Макет.ПолучитьОбласть("Заголовок");
		Шапка				= Макет.ПолучитьОбласть("Шапка");
		ОбластьТоварыШапка 	= Макет.ПолучитьОбласть("ТоварыШапка");
		ОбластьТовары 		= Макет.ПолучитьОбласть("Товары");
		
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		Шапка.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(Шапка);
		
		ТабличныйДокумент.Вывести(ОбластьТоварыШапка);
		
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		Пока ВыборкаПоТоварам.Следующий() Цикл
			ОбластьТовары.Параметры.Заполнить(ВыборкаПоТоварам);
			ТабличныйДокумент.Вывести(ОбластьТовары);
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции //---АСТБ_Горюшин_Алексей_49316

#КонецЕсли
