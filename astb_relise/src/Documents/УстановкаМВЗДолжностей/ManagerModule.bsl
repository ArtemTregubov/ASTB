#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УстановкаМВЗДолжностейМВЗДолжностей.Ссылка.Дата КАК Период,
	|	УстановкаМВЗДолжностейМВЗДолжностей.Ссылка.Организация КАК Организация,
	|	УстановкаМВЗДолжностейМВЗДолжностей.Должность,
	|	УстановкаМВЗДолжностейМВЗДолжностей.МВЗ,
	|	УстановкаМВЗДолжностейМВЗДолжностей.Использовать
	|ИЗ
	|	Документ.УстановкаМВЗДолжностей.МВЗДолжностей КАК УстановкаМВЗДолжностейМВЗДолжностей
	|ГДЕ
	|	УстановкаМВЗДолжностейМВЗДолжностей.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	//по регистру "МВЗ должностей"
	ТаблицыДляДвижений.Вставить("ТаблицаМВЗДолжностей", Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧасть(ТекущийОбъект) Экспорт	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МВЗДолжностейСрезПоследних.Организация КАК Организация,
	|	МВЗДолжностейСрезПоследних.Должность КАК Должность,
	|	МВЗДолжностейСрезПоследних.МВЗ КАК МВЗ,
	|	МВЗДолжностейСрезПоследних.Использовать КАК Использовать
	|ПОМЕСТИТЬ ВТ_МВЗДолжностей
	|ИЗ
	|	РегистрСведений.МВЗДолжностей.СрезПоследних(&Период, Организация = &Организация) КАК МВЗДолжностейСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДолжностиИПрофессии.Ссылка КАК Должность
	|ПОМЕСТИТЬ ВТ_Должности
	|ИЗ
	|	Справочник.ДолжностиИПрофессии КАК ДолжностиИПрофессии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Должности.Должность КАК Должность,
	|	ЕСТЬNULL(ВТ_МВЗДолжностей.МВЗ, ЗНАЧЕНИЕ(Справочник.МВЗ.ПустаяСсылка)) КАК МВЗ,
	|	ВЫБОР
	|		КОГДА ВТ_МВЗДолжностей.МВЗ ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ВТ_МВЗДолжностей.Использовать
	|	КОНЕЦ КАК Использовать
	|ИЗ
	|	ВТ_Должности КАК ВТ_Должности
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МВЗДолжностей КАК ВТ_МВЗДолжностей
	|		ПО ВТ_Должности.Должность = ВТ_МВЗДолжностей.Должность";
	
	Запрос.УстановитьПараметр("Период",		ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(ТекущийОбъект.Ссылка));
	Запрос.УстановитьПараметр("Организация",ТекущийОбъект.Организация);
	
	ТекущийОбъект.МВЗДолжностей.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ЗаполнитьПоВсемДолжностям(ТекущийОбъект,ВыбранноеМВЗ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДолжностиИПрофессии.Ссылка КАК Должность,
	|	&МВЗ КАК МВЗ,
	|	ИСТИНА КАК Использовать
	|ИЗ
	|	Справочник.ДолжностиИПрофессии КАК ДолжностиИПрофессии";
	
	Запрос.УстановитьПараметр("МВЗ",ВыбранноеМВЗ);
	
	ТекущийОбъект.МВЗДолжностей.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

#КонецЕсли