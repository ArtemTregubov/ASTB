#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ОбменСредствЗащитыСотрудникаТовары.НоменклатураДляВыдачи КАК Номенклатура,
	|	ОбменСредствЗащитыСотрудникаТовары.ХарактеристикаНоменклатурыДляВыдачи КАК ХарактеристикаНоменклатуры,
	|	ОбменСредствЗащитыСотрудникаТовары.Количество,
	|	ОбменСредствЗащитыСотрудникаТовары.НомерСтроки,
	|	ОбменСредствЗащитыСотрудникаТовары.Ссылка.Склад,
	|	&ОрганизацияДляОстатков КАК Организация,
	|	ОбменСредствЗащитыСотрудникаТовары.Ссылка.Дата КАК Период
	|ПОМЕСТИТЬ ВТ_РасходПоОстаткам
	|ИЗ
	|	Документ.ОбменСредствЗащитыСотрудника.Товары КАК ОбменСредствЗащитыСотрудникаТовары
	|ГДЕ
	|	ОбменСредствЗащитыСотрудникаТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ОбменСредствЗащитыСотрудникаТовары.НоменклатураДляВозврата КАК Номенклатура,
	|	ОбменСредствЗащитыСотрудникаТовары.ХарактеристикаНоменклатурыДляВозврата КАК ХарактеристикаНоменклатуры,
	|	ОбменСредствЗащитыСотрудникаТовары.Количество,
	|	ОбменСредствЗащитыСотрудникаТовары.НомерСтроки,
	|	ОбменСредствЗащитыСотрудникаТовары.Ссылка.Склад,
	|	&ОрганизацияДляОстатков КАК Организация,
	|	ОбменСредствЗащитыСотрудникаТовары.Ссылка.Дата КАК Период
	|ПОМЕСТИТЬ ВТ_ПриходПоОстаткам
	|ИЗ
	|	Документ.ОбменСредствЗащитыСотрудника.Товары КАК ОбменСредствЗащитыСотрудникаТовары
	|ГДЕ
	|	ОбменСредствЗащитыСотрудникаТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РасходПоОстаткам.НомерСтроки,
	|	ВТ_РасходПоОстаткам.Период,
	|	ВТ_РасходПоОстаткам.ВидДвижения КАК ВидДвижения,
	|	ВТ_РасходПоОстаткам.Организация КАК Организация,
	|	ВТ_РасходПоОстаткам.Склад КАК Склад,
	|	ВТ_РасходПоОстаткам.Номенклатура КАК Номенклатура,
	|	ВТ_РасходПоОстаткам.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	ВТ_РасходПоОстаткам.Количество
	|ИЗ
	|	ВТ_РасходПоОстаткам КАК ВТ_РасходПоОстаткам
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ПриходПоОстаткам.НомерСтроки,
	|	ВТ_ПриходПоОстаткам.Период,
	|	ВТ_ПриходПоОстаткам.ВидДвижения,
	|	ВТ_ПриходПоОстаткам.Организация,
	|	ВТ_ПриходПоОстаткам.Склад,
	|	ВТ_ПриходПоОстаткам.Номенклатура,
	|	ВТ_ПриходПоОстаткам.ХарактеристикаНоменклатуры,
	|	ВТ_ПриходПоОстаткам.Количество
	|ИЗ
	|	ВТ_ПриходПоОстаткам КАК ВТ_ПриходПоОстаткам
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидДвижения УБЫВ";
	
	
	Запрос.УстановитьПараметр("Ссылка",					ДокументСсылка);
	Запрос.УстановитьПараметр("Организация",			ДокументСсылка.Организация);
	Запрос.УстановитьПараметр("Сотрудник",				ДокументСсылка.Сотрудник);
	Запрос.УстановитьПараметр("Период",					ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("ОрганизацияДляОстатков",	?(ПолучитьФункциональнуюОпцию("НеВестиУчетОстатковНоменклатурыПоОрганизации"),Справочники.Организации.ПустаяСсылка(),ДокументСсылка.Организация));
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	//по регистру "Остатки номенклатуры"
	ТаблицыДляДвижений.Вставить("ТаблицаОстаткиНоменклатуры", Запрос.Выполнить().Выгрузить());
	
	//по регистру "Выданные средства защиты"
	ТаблицыДляДвижений.Вставить("ТаблицаВыданныеСредстваЗащиты", ПроцедурыРаботыСНормамиСервер.ПолучитьТаблицуДвиженийПоВыданнымСИЗ(ДокументСсылка));
	
	//по регистру "ПерсональнаяПотребность"
	//ДвиженияПриход = Результат[4].Выгрузить();
	//ДвиженияРасход = Результат[5].Выгрузить();
	//ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДвиженияРасход,ДвиженияПриход);
	//ТаблицыДляДвижений.Вставить("ТаблицаПерсональнаяПотребность", ДвиженияПриход);
	
КонецПроцедуры

Процедура ЗаполнитьДокумент(Объект) экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыданныеСредстваЗащитыОстатки.НормаВыдачи,
	|	ВыданныеСредстваЗащитыОстатки.НоменклатураНормы,
	|	ВыданныеСредстваЗащитыОстатки.Номенклатура,
	|	ВыданныеСредстваЗащитыОстатки.ХарактеристикаНоменклатуры,
	|	ВыданныеСредстваЗащитыОстатки.ДатаВыдачи,
	|	ВыданныеСредстваЗащитыОстатки.КоличествоОстаток
	|ПОМЕСТИТЬ ВТ_ВыданныеСИЗ
	|ИЗ
	|	РегистрНакопления.ВыданныеСредстваЗащиты.Остатки(
	|			&ПериодРасчета,
	|			Организация = &Организация
	|				И Сотрудник = &Сотрудник
	|				И НормаВыдачи.ВидВыдачиСИЗ = ЗНАЧЕНИЕ(Перечисление.ВидыВыдачиСИЗ.ПерсональнаяВыдача)) КАК ВыданныеСредстваЗащитыОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НормыВыдачиСИЗСоставНормы.Ссылка,
	|	НормыВыдачиСИЗСоставНормы.НоменклатураНормы,
	|	НормыВыдачиСИЗСоставНормы.ПериодичностьВыдачи
	|ПОМЕСТИТЬ ВТ_СоставНормыВыдачи
	|ИЗ
	|	Справочник.НормыВыдачиСИЗ.СоставНормы КАК НормыВыдачиСИЗСоставНормы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВыданныеСИЗ.НормаВыдачи,
	|	ВТ_ВыданныеСИЗ.НоменклатураНормы КАК НоменклатураНормыДляВыдачи,
	|	ВТ_ВыданныеСИЗ.НоменклатураНормы КАК НоменклатураНормыДляВозврата,
	|	ВТ_ВыданныеСИЗ.Номенклатура КАК НоменклатураДляВозврата,
	|	ВТ_ВыданныеСИЗ.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатурыДляВозврата,
	|	ВТ_ВыданныеСИЗ.ДатаВыдачи,
	|	ВТ_ВыданныеСИЗ.КоличествоОстаток КАК Количество
	|ИЗ
	|	ВТ_ВыданныеСИЗ КАК ВТ_ВыданныеСИЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СоставНормыВыдачи КАК ВТ_СоставНормыВыдачи
	|		ПО ВТ_ВыданныеСИЗ.НормаВыдачи = ВТ_СоставНормыВыдачи.Ссылка
	|			И ВТ_ВыданныеСИЗ.НоменклатураНормы = ВТ_СоставНормыВыдачи.НоменклатураНормы
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ВЫБОР
	|				КОГДА ВТ_СоставНормыВыдачи.ПериодичностьВыдачи.ТипПериода = ЗНАЧЕНИЕ(Перечисление.ДоступныеПериодыОтчета.Год)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ВТ_ВыданныеСИЗ.ДатаВыдачи, ГОД, ВТ_СоставНормыВыдачи.ПериодичностьВыдачи.КоличествоПериодов)
	|				ИНАЧЕ ДОБАВИТЬКДАТЕ(ВТ_ВыданныеСИЗ.ДатаВыдачи, МЕСЯЦ, ВТ_СоставНормыВыдачи.ПериодичностьВыдачи.КоличествоПериодов)
	|			КОНЕЦ, МЕСЯЦ) > НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)";
	
	
	Запрос.УстановитьПараметр("Сотрудник",		Объект.Сотрудник);
	Запрос.УстановитьПараметр("ПериодРасчета",	ПроцедурыРаботыСНормамиСервер.ПолучитьГраницуАнализаПоДокументу(Объект.Ссылка));
	Запрос.УстановитьПараметр("Организация",	Объект.Организация);
	Запрос.УстановитьПараметр("Период",			?(ЗначениеЗаполнено(Объект.Дата),Объект.Дата,ТекущаяДата()));
	
	Объект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма");
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОбменСИЗ") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ОбменСИЗ",
			"Обмен СИЗ",
			СформироватьПечатнуюФормуОбмен(МассивОбъектов, ОбъектыПечати),,"Документ.ОбменСредствЗащитыСотрудника.ПФ_MXL_ОбменСИЗ");
				
	КонецЕсли;		
		
КонецПроцедуры

Функция СформироватьПечатнуюФормуОбмен(МассивОбъектов, ОбъектыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОбменСредствЗащитыСотрудника_ОбменСИЗ";
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Документы.Ссылка КАК Ссылка,
	|	Документы.Организация.НаименованиеПолное КАК ПредставлениеОрганизации,
	|	Документы.Номер КАК Номер,
	|	Документы.Дата КАК Дата,
	|	Документы.Склад.Наименование КАК ПредставлениеСклада,
	|	Документы.Организация.Префикс КАК Префикс,
	|	Документы.Сотрудник.Наименование КАК ПредставлениеСотрудника
	|ИЗ
	|	Документ.ОбменСредствЗащитыСотрудника КАК Документы
	|ГДЕ
	|	Документы.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Товары.Ссылка КАК Ссылка,
	|	Товары.Количество КАК Количество,
	|	Товары.Цена КАК Цена,
	|	Товары.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА Товары.ХарактеристикаНоменклатурыДляВыдачи = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|			ТОГДА Товары.НоменклатураДляВыдачи.Наименование
	|		ИНАЧЕ Товары.НоменклатураДляВыдачи.Наименование + "" ("" + Товары.ХарактеристикаНоменклатурыДляВыдачи.Наименование + "")""
	|	КОНЕЦ КАК НаименованиеТовараВыдано,
	|	ВЫБОР
	|		КОГДА Товары.ХарактеристикаНоменклатурыДляВозврата = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|			ТОГДА Товары.НоменклатураДляВозврата.Наименование
	|		ИНАЧЕ Товары.НоменклатураДляВозврата.Наименование + "" ("" + Товары.ХарактеристикаНоменклатурыДляВозврата.Наименование + "")""
	|	КОНЕЦ КАК НаименованиеТовараВозвращено,
	|	ВЫБОР
	|		КОГДА Товары.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Артикул)
	|			ТОГДА Товары.НоменклатураДляВыдачи.Артикул
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Товары.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Код)
	|					ТОГДА Товары.НоменклатураДляВыдачи.Код
	|				ИНАЧЕ ВЫБОР
	|						КОГДА Товары.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.КодСинхронизации)
	|							ТОГДА Товары.НоменклатураДляВыдачи.КодСинхронизации
	|						ИНАЧЕ ВЫБОР
	|								КОГДА Товары.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.НоменклатурныйНомерОрганизации)
	|									ТОГДА ЕСТЬNULL(НоменклатурныеНомераОрганизаций.НоменклатурныйНомер, """")
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК НоменклатурныйНомерВыдано,
	|	Товары.НоменклатураДляВыдачи.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименованиеВыдано,
	|	ВЫБОР
	|		КОГДА Товары.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Артикул)
	|			ТОГДА Товары.НоменклатураДляВозврата.Артикул
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Товары.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.Код)
	|					ТОГДА Товары.НоменклатураДляВозврата.Код
	|				ИНАЧЕ ВЫБОР
	|						КОГДА Товары.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.КодСинхронизации)
	|							ТОГДА Товары.НоменклатураДляВозврата.КодСинхронизации
	|						ИНАЧЕ ВЫБОР
	|								КОГДА Товары.Ссылка.Организация.ДополнительнаяИнформацияПоНоменклатуреДляПечатныхФорм = ЗНАЧЕНИЕ(Перечисление.ВидДополнительнойИнформацииПоНоменклатуре.НоменклатурныйНомерОрганизации)
	|									ТОГДА ЕСТЬNULL(НоменклатурныеНомераОрганизаций1.НоменклатурныйНомер, """")
	|								ИНАЧЕ """"
	|							КОНЕЦ
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК НоменклатурныйНомерВозвращено,
	|	Товары.НоменклатураДляВозврата.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименованиеВозвращено,
	|	Товары.НомерСтроки
	|ИЗ
	|	Документ.ОбменСредствЗащитыСотрудника.Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатурныеНомераОрганизаций КАК НоменклатурныеНомераОрганизаций
	|		ПО Товары.Ссылка.Организация = НоменклатурныеНомераОрганизаций.Организация
	|			И Товары.НоменклатураДляВозврата = НоменклатурныеНомераОрганизаций.Номенклатура
	|			И Товары.ХарактеристикаНоменклатурыДляВозврата = НоменклатурныеНомераОрганизаций.ХарактеристикаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатурныеНомераОрганизаций КАК НоменклатурныеНомераОрганизаций1
	|		ПО Товары.ХарактеристикаНоменклатурыДляВозврата = НоменклатурныеНомераОрганизаций1.Организация
	|			И Товары.НоменклатураДляВыдачи = НоменклатурныеНомераОрганизаций1.Номенклатура
	|			И Товары.ХарактеристикаНоменклатурыДляВыдачи = НоменклатурныеНомераОрганизаций1.ХарактеристикаНоменклатуры
	|ГДЕ
	|	Товары.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Товары.НомерСтроки
	|ИТОГИ ПО
	|	Ссылка");
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ОбменСредствЗащитыСотрудника.ПФ_MXL_ОбменСИЗ");
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ДанныеПечати = ПакетЗапросов[0].Выбрать();
	ВыборкаПоДокументам = ПакетЗапросов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если НЕ ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Ссылка", ДанныеПечати.Ссылка)) Тогда
			Продолжить;
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
			
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Заполнить(ДанныеПечати);
		
		Область.Параметры.ТекстЗаголовка = ПроцедурыРаботыСНормамиСервер.СформироватьЗаголовокДокумента(Новый Структура("Номер, Дата, Префикс", ДанныеПечати.Номер, ДанныеПечати.Дата, ДанныеПечати.Префикс), "Обмен СИЗ");
		ТабличныйДокумент.Вывести(Область);
			
		СчетСтрок = 1;
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы");
			Область.Параметры.Заполнить(ВыборкаПоТоварам);
			ТабличныйДокумент.Вывести(Область);
			
			СчетСтрок = СчетСтрок + 1;
			
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		ТекстИтоговойСтроки = НСтр("ru = 'Всего наименований %ВсегоНаименований%'");
		ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%ВсегоНаименований%", СчетСтрок-1);
		Область.Параметры.ИтоговаяСтрока = ТекстИтоговойСтроки;
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//списание
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ОбменСредствЗащитыСотрудника";
	КомандаПечати.Идентификатор = "ОбменСИЗ";
	КомандаПечати.Представление = НСтр("ru = 'Обмен СИЗ'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

#КонецЕсли