#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция СформироватьМассивНаименованийНормыВыдачиСИЗ(ТекущийОбъект) Экспорт
	
	МассивНаименований = Новый Массив;
	
	ВспомогательнаяТаблица = Новый ТаблицаЗначений;
	ВспомогательнаяТаблица.Колонки.Добавить("ВариантНаименования");
	ВспомогательнаяТаблица.Колонки.Добавить("НомерСтрокиСостава");
	
	Для Каждого СтрокаСостава Из ТекущийОбъект.СоставНормы Цикл
		
		МассивСловНаименования 	= УправлениеКонтактнойИнформациейКлиентСервер.СловаТекста(СокрЛП(СтрокаСостава.НоменклатураНормы.Наименование)," ");
		
		ТекущаяПериодичность 	= ?(ЗначениеЗаполнено(СтрокаСостава.ПериодичностьВыдачи)," (" + СокрЛП(СтрокаСостава.ПериодичностьВыдачи.Наименование) + ") "," (" + ТекущийОбъект.ВидРасчета + ") ");
		
		ТекущееНаименование = "";
		
		Для Каждого ЭлементМассива Из МассивСловНаименования Цикл
			
			Если НЕ ЗначениеЗаполнено(ЭлементМассива) Тогда
				Продолжить;
			КонецЕсли;
			
		    ТекущееНаименование = ТекущееНаименование + " " + ЭлементМассива;
			
			Если СтрДлина(ТекущееНаименование) > 150 Тогда
				Продолжить;
			КонецЕсли;	
			
			НоваяСтрока 					= ВспомогательнаяТаблица.Добавить();
			НоваяСтрока.ВариантНаименования = СокрЛП(ТекущееНаименование + ТекущаяПериодичность);
			НоваяСтрока.НомерСтрокиСостава 	= СтрокаСостава.НомерСтроки;
			
		КонецЦикла;
		
	КонецЦикла;
	
	СтруктураПоиска = Новый Структура("НомерСтрокиСостава",1);
	
	НайденныеСтроки = ВспомогательнаяТаблица.НайтиСтроки(СтруктураПоиска);
	
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
		
		ТекущееНаименование = НайденнаяСтрока.ВариантНаименования;
		
		Если ТекущийОбъект.СоставНормы.Количество() <= 1 Тогда
		
		    МассивНаименований.Добавить(ТекущееНаименование);
			
		Иначе
			
			РекурсияПоНомеруСтроки(ТекущееНаименование + " или ",2,ВспомогательнаяТаблица,ТекущийОбъект.СоставНормы.Количество(),МассивНаименований);
			
		КонецЕсли;
		
	КонецЦикла;
	
	//если массив пустой, надо хоть что-нибудь добавить
	Если МассивНаименований.Количество() = 0 Тогда
		
		ТекущаяПериодичность = ?(ЗначениеЗаполнено(ТекущийОбъект.СоставНормы[0].ПериодичностьВыдачи)," (" + СокрЛП(ТекущийОбъект.СоставНормы[0].ПериодичностьВыдачи.Наименование) + ") "," (" + ТекущийОбъект.ВидРасчета + ") ");
		
		МассивНаименований.Добавить(СокрЛП(ТекущийОбъект.СоставНормы[0].НоменклатураНормы.Наименование) + ТекущаяПериодичность);
			
	КонецЕсли;
	
	Возврат МассивНаименований;
	
КонецФункции	
	
Процедура РекурсияПоНомеруСтроки(ВходящееНаименование,НомерСтроки,ВспомогательнаяТаблица,КоличествоСтрокСостава,МассивНаименований)	
	
	СтруктураПоиска = Новый Структура("НомерСтрокиСостава",НомерСтроки);
	
	НайденныеСтроки = ВспомогательнаяТаблица.НайтиСтроки(СтруктураПоиска);
	
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
		
		ТекущееНаименование = ВходящееНаименование + НайденнаяСтрока.ВариантНаименования;
		
		Если СтрДлина(ТекущееНаименование) > 150 Тогда
			Продолжить;
		КонецЕсли;
		
		Если КоличествоСтрокСостава <= НомерСтроки Тогда
		
		    МассивНаименований.Добавить(ТекущееНаименование);
			
		Иначе
			
			РекурсияПоНомеруСтроки(ТекущееНаименование + " или ",НомерСтроки + 1,ВспомогательнаяТаблица,КоличествоСтрокСостава,МассивНаименований);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
	
#КонецЕсли