#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати 					= КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати 	= "Справочник.ВидыРемонтаСИЗ";
	КомандаПечати.Идентификатор 	= "СписокВидовРемонтаСИЗ";
	КомандаПечати.Представление 	= НСтр("ru = 'Список видов ремонта СИЗ'");
	КомандаПечати.СписокФорм		= "ФормаСписка";
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СписокВидовРемонтаСИЗ") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"СписокВидовРемонтаСИЗ",
			"Список видов ремонта СИЗ",
			СформироватьПечатнуюФормуСпискаВидовРемонтаСИЗ(МассивОбъектов, ОбъектыПечати),,"Справочник.ВидыРемонтаСИЗ.ПФ_MXL_СписокВидовРемонтаСИЗ");
				
	КонецЕсли;	
	
КонецПроцедуры	

Функция СформироватьПечатнуюФормуСпискаВидовРемонтаСИЗ(МассивОбъектов, ОбъектыПечати) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	//если строки не выбраны, печатаем все
	Если НЕ МассивОбъектов.Количество() > 1 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыРемонтаСИЗ.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ВидыРемонтаСИЗ КАК ВидыРемонтаСИЗ
		|ГДЕ
		|	НЕ ВидыРемонтаСИЗ.Штрихкод = """"
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидыРемонтаСИЗ.Наименование";
		
		МассивОбъектов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
	КонецЕсли;
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СписокВидовРемонтаСИЗ";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.ВидыРемонтаСИЗ.ПФ_MXL_СписокВидовРемонтаСИЗ");
	
	Эталон = Обработки.ПечатьЭтикетокДляВыданныхСИЗ.ПолучитьМакет("Эталон");
	
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
	
	Для Каждого ЭлементМассиваОбъектов Из МассивОбъектов Цикл
		
		Если НЕ ЗначениеЗаполнено(ЭлементМассиваОбъектов.Штрихкод) Тогда
			Продолжить;
		КонецЕсли;
		
		Область = Макет.ПолучитьОбласть("Строка");
		
		Для каждого Рисунок Из Область.Рисунки Цикл
			
			ПараметрыШтрихкода = Новый Структура;
			ПараметрыШтрихкода.Вставить("Ширина",  Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
			ПараметрыШтрихкода.Вставить("Высота",  Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
			ПараметрыШтрихкода.Вставить("Штрихкод",СокрЛП(ЭлементМассиваОбъектов.Штрихкод));
			ПараметрыШтрихкода.Вставить("ТипКода", "1"); //EAN13
							
			Рисунок.Картинка = ПроцедурыРаботыСНормамиСервер.ПолучитьКартинкуШтрихкода(ПараметрыШтрихкода);
			
		КонецЦикла;
		
		Область.Параметры.Наименование 	= СокрЛП(ЭлементМассиваОбъектов.Наименование);
		
		ТабличныйДокумент.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("Разделитель");
		
		ТабличныйДокумент.Вывести(Область);
		
	КонецЦикла;	
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецЕсли	