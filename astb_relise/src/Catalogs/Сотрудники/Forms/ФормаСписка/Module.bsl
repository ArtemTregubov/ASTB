&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Элементы.ЕстьТелефон.Видимость 	= ПолучитьФункциональнуюОпцию("ИспользоватьПрочиеВзаимодействия");
	Элементы.ЕстьЭП.Видимость 		= ПолучитьФункциональнуюОпцию("ИспользоватьПочтовыйКлиент");
	Элементы.ЕстьАнкета.Видимость 	= ПолучитьФункциональнуюОпцию("ИспользоватьАнкетирование");
	
	ИспользоватьПодключаемоеОборудование = ПолучитьФункциональнуюОпцию("ИспользоватьПодключаемоеОборудование");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтаФорма, "СчитывательМагнитныхКарт");
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтаФорма);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование"
		И ВводДоступен() Тогда
		Если ИмяСобытия = "TracksData" Тогда
			ПолученКодИзСМК(Параметр);
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

// Процедура выбирает из массива параметров магнитный код карты, считанный СМК.
//
&НаКлиенте
Процедура ПолученКодИзСМК(Параметр)
	
	Если Параметр[1][3] <> Неопределено Тогда
		МКод = Параметр[1][3][0].ДанныеДорожек[0].ЗначениеПоля;
	Иначе
		МКод = Параметр[0];
	КонецЕсли;
	
	ПолучитьСотрудникаНаСервере(МКод);
	
КонецПроцедуры

// Процедура получает клиента из справочника "Клиенты" по магнитному коду карты.
//
&НаСервере
Процедура ПолучитьСотрудникаНаСервере(МКод)
	
	Сотрудник = ПроцедурыРаботыСНормамиСервер.НайтиСотрудникаПоМагнитнойКарте(МКод);
	Если Сотрудник <> Неопределено Тогда
		Элементы.Список.ТекущаяСтрока = Сотрудник;
	Иначе
		ТекстСообщения = НСтр("ru = 'Карта не найдена.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти