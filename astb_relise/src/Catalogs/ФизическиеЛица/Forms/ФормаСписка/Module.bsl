&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВнешниеПользователи.НастроитьОтображениеСпискаВнешнихПользователей(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВнешнихПользователейНаСервере(МассивФизическихЛиц)

	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого ФизическоеЛицо Из МассивФизическихЛиц Цикл
		
		Если ПроцедурыРаботыСНормамиСервер.ФизическоеЛицоМожетЯвлятьсяВнешнимПользователем(ФизическоеЛицо) Тогда
			
			//добавляем пользователя ИБ
			ПользовательИБ = ПользователиИнформационнойБазы.СоздатьПользователя();  
			Если Константы.СпособАвтоматическогоФормированияЛогиновВнешнихПользователей.Получить() = Перечисления.СпособыАвтоматическогоФормированияЛогиновВнешнихПользователей.Префикс_ТабельныйНомер Тогда
				Сотрудник = Справочники.Сотрудники.НайтиПоРеквизиту("ФизическоеЛицо",ФизическоеЛицо);
				ПользовательИБ.Имя = Сотрудник.Владелец.Префикс + "_" + Сотрудник.ТабельныйНомер;
			ИначеЕсли Константы.СпособАвтоматическогоФормированияЛогиновВнешнихПользователей.Получить() = Перечисления.СпособыАвтоматическогоФормированияЛогиновВнешнихПользователей.ТабельныйНомер Тогда
				Сотрудник = Справочники.Сотрудники.НайтиПоРеквизиту("ФизическоеЛицо",ФизическоеЛицо);
				ПользовательИБ.Имя = Сотрудник.ТабельныйНомер;
			Иначе 
				ПользовательИБ.Имя = ПользователиСлужебныйКлиентСервер.ПолучитьКраткоеИмяПользователяИБ(ФизическоеЛицо.Наименование);
			КонецЕсли;
			ПользовательИБ.АутентификацияСтандартная 	= Истина;
			ПользовательИБ.ПоказыватьВСпискеВыбора 		= Ложь;
			ПользовательИБ.ПолноеИмя 					= ФизическоеЛицо.Наименование;
			ПользовательИБ.РежимЗапуска 				= РежимЗапускаКлиентскогоПриложения.Авто;
			ПользовательИБ.Язык 						= Метаданные.Языки.Русский;
			ПользовательИБ.Роли.Добавить(Метаданные.Роли.БазовыеПраваВнешнегоПользователя);
			ПользовательИБ.Роли.Добавить(Метаданные.Роли.ВыводНаПринтерФайлБуферОбмена);
			ПользовательИБ.Роли.Добавить(Метаданные.Роли.ЗапускВебКлиента);
			ПользовательИБ.Роли.Добавить(Метаданные.Роли.ЗапускТонкогоКлиента);
			ПользовательИБ.Роли.Добавить(Метаданные.Роли.ИспользоватьРабочийСтолВнешнегоПользователя);
			//возможно, пользователь с таким имененм уже есть
			Попытка
				ПользовательИБ.Записать();
			Исключение
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = ОписаниеОшибки();
				Сообщение.Сообщить();
				Продолжить;
			КонецПопытки;
			
			//добавляем внешнего пользователя
			НовыйВнешнийПользователь = Справочники.ВнешниеПользователи.СоздатьЭлемент();
			НовыйВнешнийПользователь.ОбъектАвторизации 				= ФизическоеЛицо;
			НовыйВнешнийПользователь.Наименование 					= ФизическоеЛицо.Наименование;
			НовыйВнешнийПользователь.ИдентификаторПользователяИБ 	= ПользовательИБ.УникальныйИдентификатор;
			НовыйВнешнийПользователь.ОбменДанными.Загрузка			= Истина;
			НовыйВнешнийПользователь.Записать();
			
			//запись сведений в регистр
			НаборЗаписей = РегистрыСведений.СведенияОПользователях.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Пользователь.Установить(НовыйВнешнийПользователь.Ссылка);
			НаборЗаписей.Прочитать();
			Если НаборЗаписей.Количество() = 0 Тогда
				СведенияОПользователе = НаборЗаписей.Добавить();
				СведенияОПользователе.Пользователь = НовыйВнешнийПользователь.Ссылка;
			Иначе
				СведенияОПользователе = НаборЗаписей[0];
			КонецЕсли;
			СведенияОПользователе.ПотребоватьСменуПароляПриВходе = Истина;
		 	НаборЗаписей.Записать();
			
			//группа доступа
			ПрофильВнешнегоПользователя = Справочники.ПрофилиГруппДоступа.НайтиПоНаименованию("Внешние пользователи (физические лица)");
			ГруппаДоступаВнешнегоПользователя = Справочники.ГруппыДоступа.НайтиПоРеквизиту("Профиль",ПрофильВнешнегоПользователя);
			
			Если ЗначениеЗаполнено(ГруппаДоступаВнешнегоПользователя) Тогда
				ГруппаДоступаВнешнегоПользователяОбъект = ГруппаДоступаВнешнегоПользователя.ПолучитьОбъект();
				НоваяСтрока = ГруппаДоступаВнешнегоПользователяОбъект.Пользователи.Добавить();
				НоваяСтрока.Пользователь = НовыйВнешнийПользователь.Ссылка;
				ГруппаДоступаВнешнегоПользователяОбъект.Записать();
			КонецЕсли;
			
			//состав групп пользователей
			НаборЗаписей = РегистрыСведений.СоставыГруппПользователей.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ГруппаПользователей.Установить(НовыйВнешнийПользователь.Ссылка);
			НаборЗаписей.Отбор.Пользователь.Установить(НовыйВнешнийПользователь.Ссылка);
			НаборЗаписей.Прочитать();
			Если НаборЗаписей.Количество() = 0 Тогда
				СоставГруппПользователей = НаборЗаписей.Добавить();
				СоставГруппПользователей.ГруппаПользователей 	= НовыйВнешнийПользователь.Ссылка;
				СоставГруппПользователей.Пользователь 			= НовыйВнешнийПользователь.Ссылка;
			Иначе
				СоставГруппПользователей = НаборЗаписей[0];
			КонецЕсли;
			СоставГруппПользователей.Используется = Истина;
		 	НаборЗаписей.Записать();
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Физическое лицо " + ФизическоеЛицо + " добавлено как внешний пользователь.";
			Сообщение.Сообщить();
			
		Иначе
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Физическое лицо " + ФизическоеЛицо + " не может быть внешним пользователем.
							|Проверьте, что физическое лицо является сотрудником и организации разрешен доступ сотрудников к базе.";
			Сообщение.Сообщить(); 
			
		КонецЕсли;	
		
	КонецЦикла;	
	
	ЭтотОбъект.Элементы.Список.Обновить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВнешнихПользователей(Команда)
	
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	
	МассивФизическихЛиц = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		
		Если ТипЗнч(ВыделеннаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущаяСтрока = Элементы.Список.ДанныеСтроки(ВыделеннаяСтрока);
		
		Если ТекущаяСтрока.ВнешнийДоступ Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекущаяСтрока <> Неопределено Тогда
			МассивФизическихЛиц.Добавить(ТекущаяСтрока.Ссылка);
		КонецЕсли;
		
	КонецЦикла;
	
	ДобавитьВнешнихПользователейНаСервере(МассивФизическихЛиц);
	
КонецПроцедуры
